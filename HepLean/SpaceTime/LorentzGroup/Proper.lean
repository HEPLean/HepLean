/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.LorentzGroup.Basic
/-!
# The Proper Lorentz Group

The proper Lorentz group is the subgroup of the Lorentz group with determinant `1`.

We define the give a series of lemmas related to the determinant of the Lorentz group.

-/

noncomputable section

open Matrix
open Complex
open ComplexConjugate

namespace LorentzGroup

open minkowskiMatrix

variable {d : â„•}

/-- The determinant of a member of the Lorentz group is `1` or `-1`. -/
lemma det_eq_one_or_neg_one (Î› : ğ“› d) : Î›.1.det = 1 âˆ¨ Î›.1.det = -1 := by
  refine mul_self_eq_one_iff.mp ?_
  simpa only [det_mul, det_dual, det_one] using congrArg det ((mem_iff_self_mul_dual).mp Î›.2)

local notation "â„¤â‚‚" => Multiplicative (ZMod 2)

instance : TopologicalSpace â„¤â‚‚ := instTopologicalSpaceFin

instance : DiscreteTopology â„¤â‚‚ := by
  exact forall_open_iff_discrete.mp fun _ => trivial

instance : TopologicalGroup â„¤â‚‚ := TopologicalGroup.mk

/-- A continuous function from `({-1, 1} : Set â„)` to `â„¤â‚‚`. -/
@[simps!]
def coeForâ„¤â‚‚ : C(({-1, 1} : Set â„), â„¤â‚‚) where
  toFun x := if x = âŸ¨1, Set.mem_insert_of_mem (-1) rflâŸ©
    then Additive.toMul 0 else Additive.toMul (1 : ZMod 2)
  continuous_toFun := continuous_of_discreteTopology

/-- The continuous map taking a Lorentz matrix to its determinant. -/
def detContinuous : C(ğ“› d, â„¤â‚‚) :=
  ContinuousMap.comp coeForâ„¤â‚‚ {
    toFun := fun Î› => âŸ¨Î›.1.det, Or.symm (LorentzGroup.det_eq_one_or_neg_one _)âŸ©,
    continuous_toFun := by
      refine Continuous.subtype_mk ?_ _
      exact Continuous.matrix_det $
        Continuous.comp' (continuous_iff_le_induced.mpr fun U a => a) continuous_id'
      }

lemma detContinuous_eq_one (Î› : LorentzGroup d) :
    detContinuous Î› = Additive.toMul 0 â†” Î›.1.det = 1 := by
  simp only [detContinuous, ContinuousMap.comp_apply, ContinuousMap.coe_mk, coeForâ„¤â‚‚_apply,
    Subtype.mk.injEq, ite_eq_left_iff, toMul_eq_one]
  simp only [toMul_zero, ite_eq_left_iff, toMul_eq_one]
  refine Iff.intro (fun h => ?_) (fun h => ?_)
  Â· by_contra hn
    have h' := h hn
    change (1 : Fin 2) = (0 : Fin 2) at h'
    simp only [Fin.isValue, one_ne_zero] at h'
  Â· intro h'
    exact False.elim (h' h)

lemma detContinuous_eq_zero (Î› : LorentzGroup d) :
    detContinuous Î› = Additive.toMul (1 : ZMod 2) â†” Î›.1.det = - 1 := by
  simp only [detContinuous, ContinuousMap.comp_apply, ContinuousMap.coe_mk, coeForâ„¤â‚‚_apply,
    Subtype.mk.injEq, Nat.reduceAdd]
  refine Iff.intro (fun h => ?_) (fun h => ?_)
  Â· by_contra hn
    rw [if_pos] at h
    Â· change (0 : Fin 2) = (1 : Fin 2) at h
      simp only [Fin.isValue, zero_ne_one] at h
    Â· cases' det_eq_one_or_neg_one Î› with h2 h2
      Â· simp_all only [ite_true]
      Â· simp_all only [not_true_eq_false]
  Â· rw [if_neg]
    Â· rfl
    Â· cases' det_eq_one_or_neg_one Î› with h2 h2
      Â· rw [h]
        linarith
      Â· linarith

lemma detContinuous_eq_iff_det_eq (Î› Î›' : LorentzGroup d) :
    detContinuous Î› = detContinuous Î›' â†” Î›.1.det = Î›'.1.det := by
  cases' det_eq_one_or_neg_one Î› with h1 h1
  Â· rw [h1, (detContinuous_eq_one Î›).mpr h1]
    cases' det_eq_one_or_neg_one Î›' with h2 h2
    Â· rw [h2, (detContinuous_eq_one Î›').mpr h2]
      simp only [toMul_zero]
    Â· rw [h2, (detContinuous_eq_zero Î›').mpr h2]
      erw [Additive.toMul.apply_eq_iff_eq]
      change (0 : Fin 2) = (1 : Fin 2) â†” _
      simp only [Fin.isValue, zero_ne_one, false_iff]
      linarith
  Â· rw [h1, (detContinuous_eq_zero Î›).mpr h1]
    cases' det_eq_one_or_neg_one Î›' with h2 h2
    Â· rw [h2, (detContinuous_eq_one Î›').mpr h2]
      erw [Additive.toMul.apply_eq_iff_eq]
      change (1 : Fin 2) = (0 : Fin 2) â†” _
      simp only [Fin.isValue, one_ne_zero, false_iff]
      linarith
    Â· rw [h2, (detContinuous_eq_zero Î›').mpr h2]
      simp only [Nat.reduceAdd]

/-- The representation taking a Lorentz matrix to its determinant. -/
@[simps!]
def detRep : ğ“› d â†’* â„¤â‚‚ where
  toFun Î› := detContinuous Î›
  map_one' := by
    simp only [detContinuous, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
      lorentzGroupIsGroup_one_coe, det_one, coeForâ„¤â‚‚_apply, â†“reduceIte]
  map_mul' Î›1 Î›2 := by
    simp only [Submonoid.coe_mul, Subgroup.coe_toSubmonoid, Units.val_mul, det_mul, toMul_zero,
      mul_ite, mul_one, ite_mul, one_mul]
    cases' det_eq_one_or_neg_one Î›1 with h1 h1
    Â· rw [(detContinuous_eq_one Î›1).mpr h1]
      cases' det_eq_one_or_neg_one Î›2 with h2 h2
      Â· rw [(detContinuous_eq_one Î›2).mpr h2]
        apply (detContinuous_eq_one _).mpr
        simp only [lorentzGroupIsGroup_mul_coe, det_mul, h1, h2, mul_one]
      Â· rw [(detContinuous_eq_zero Î›2).mpr h2]
        apply (detContinuous_eq_zero _).mpr
        simp only [lorentzGroupIsGroup_mul_coe, det_mul, h1, h2, mul_neg, mul_one]
    Â· rw [(detContinuous_eq_zero Î›1).mpr h1]
      cases' det_eq_one_or_neg_one Î›2 with h2 h2
      Â· rw [(detContinuous_eq_one Î›2).mpr h2]
        apply (detContinuous_eq_zero _).mpr
        simp only [lorentzGroupIsGroup_mul_coe, det_mul, h1, h2, mul_one]
      Â· rw [(detContinuous_eq_zero Î›2).mpr h2]
        apply (detContinuous_eq_one _).mpr
        simp only [lorentzGroupIsGroup_mul_coe, det_mul, h1, h2, mul_neg, mul_one, neg_neg]

lemma detRep_continuous : Continuous (@detRep d) := detContinuous.2

lemma det_on_connected_component {Î› Î›' : LorentzGroup d} (h : Î›' âˆˆ connectedComponent Î›) :
    Î›.1.det = Î›'.1.det := by
  obtain âŸ¨s, hs, hÎ›'âŸ© := h
  let f : ContinuousMap s â„¤â‚‚ := ContinuousMap.restrict s detContinuous
  haveI : PreconnectedSpace s := isPreconnected_iff_preconnectedSpace.mp hs.1
  simpa [f, detContinuous_eq_iff_det_eq] using
    (@IsPreconnected.subsingleton â„¤â‚‚ _ _ _ (isPreconnected_range f.2))
    (Set.mem_range_self âŸ¨Î›, hs.2âŸ©) (Set.mem_range_self âŸ¨Î›', hÎ›'âŸ©)

lemma detRep_on_connected_component {Î› Î›' : LorentzGroup d} (h : Î›' âˆˆ connectedComponent Î›) :
    detRep Î› = detRep Î›' := by
  simp only [detRep_apply, detContinuous, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
    coeForâ„¤â‚‚_apply, Subtype.mk.injEq]
  rw [det_on_connected_component h]

lemma det_of_joined {Î› Î›' : LorentzGroup d} (h : Joined Î› Î›') : Î›.1.det = Î›'.1.det :=
  det_on_connected_component $ pathComponent_subset_component _ h

/-- A Lorentz Matrix is proper if its determinant is 1. -/
@[simp]
def IsProper (Î› : LorentzGroup d) : Prop := Î›.1.det = 1

instance : DecidablePred (@IsProper d) := by
  intro Î›
  apply Real.decidableEq

lemma IsProper_iff (Î› : LorentzGroup d) : IsProper Î› â†” detRep Î› = 1 := by
  rw [show 1 = detRep 1 from Eq.symm (MonoidHom.map_one detRep), detRep_apply, detRep_apply,
    detContinuous_eq_iff_det_eq]
  simp only [IsProper, lorentzGroupIsGroup_one_coe, det_one]

lemma id_IsProper : @IsProper d 1 := by
  simp [IsProper]

lemma isProper_on_connected_component {Î› Î›' : LorentzGroup d} (h : Î›' âˆˆ connectedComponent Î›) :
    IsProper Î› â†” IsProper Î›' := by
  simp only [IsProper]
  rw [det_on_connected_component h]

end LorentzGroup

end
