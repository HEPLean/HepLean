/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.LorentzVector.Real.Basic
/-!

# Contraction of Real Lorentz vectors

-/

noncomputable section

open Matrix
open MatrixGroups
open Complex
open TensorProduct
open SpaceTime
open CategoryTheory.MonoidalCategory
open minkowskiMatrix
namespace Lorentz

variable {d : â„•}

/-- The bi-linear map corresponding to contraction of a contravariant Lorentz vector with a
  covariant Lorentz vector. -/
def contrModCoModBi (d : â„•) : ContrMod d â†’â‚—[â„] CoMod d â†’â‚—[â„] â„ where
  toFun Ïˆ := {
    toFun := fun Ï† => Ïˆ.toFin1dâ„ â¬áµ¥ Ï†.toFin1dâ„,
    map_add' := by
      intro Ï† Ï†'
      simp only [map_add]
      rw [dotProduct_add]
    map_smul' := by
      intro r Ï†
      simp only [LinearEquiv.map_smul]
      rw [dotProduct_smul]
      rfl}
  map_add' Ïˆ Ïˆ':= by
    refine LinearMap.ext (fun Ï† => ?_)
    simp only [map_add, LinearMap.coe_mk, AddHom.coe_mk, LinearMap.add_apply]
    rw [add_dotProduct]
  map_smul' r Ïˆ := by
    refine LinearMap.ext (fun Ï† => ?_)
    simp only [LinearEquiv.map_smul, LinearMap.coe_mk, AddHom.coe_mk]
    rw [smul_dotProduct]
    rfl

/-- The bi-linear map corresponding to contraction of a covariant Lorentz vector with a
  contravariant Lorentz vector. -/
def coModContrModBi (d : â„•) : CoMod d â†’â‚—[â„] ContrMod d â†’â‚—[â„] â„ where
  toFun Ï† := {
    toFun := fun Ïˆ => Ï†.toFin1dâ„ â¬áµ¥ Ïˆ.toFin1dâ„,
    map_add' := by
      intro Ïˆ Ïˆ'
      simp only [map_add]
      rw [dotProduct_add]
    map_smul' := by
      intro r Ïˆ
      simp only [LinearEquiv.map_smul]
      rw [dotProduct_smul]
      rfl}
  map_add' Ï† Ï†' := by
    refine LinearMap.ext (fun Ïˆ => ?_)
    simp only [map_add, LinearMap.coe_mk, AddHom.coe_mk, LinearMap.add_apply]
    rw [add_dotProduct]
  map_smul' r Ï† := by
    refine LinearMap.ext (fun Ïˆ => ?_)
    simp only [LinearEquiv.map_smul, LinearMap.coe_mk, AddHom.coe_mk]
    rw [smul_dotProduct]
    rfl

/-- The linear map from Contr d âŠ— Co d to â„ given by
    summing over components of contravariant Lorentz vector and
    covariant Lorentz vector in the
    standard basis (i.e. the dot product).
    In terms of index notation this is the contraction is Ïˆâ± Ï†áµ¢. -/
def contrCoContract : Contr d âŠ— Co d âŸ¶ ğŸ™_ (Rep â„ (LorentzGroup d)) where
  hom := TensorProduct.lift (contrModCoModBi d)
  comm M := TensorProduct.ext' fun Ïˆ Ï† => by
    change (M.1 *áµ¥ Ïˆ.toFin1dâ„)  â¬áµ¥ ((LorentzGroup.transpose Mâ»Â¹).1 *áµ¥ Ï†.toFin1dâ„) = _
    rw [dotProduct_mulVec, LorentzGroup.transpose_val,
      vecMul_transpose, mulVec_mulVec, LorentzGroup.coe_inv, inv_mul_of_invertible M.1]
    simp only [one_mulVec, CategoryTheory.Equivalence.symm_inverse,
      Action.functorCategoryEquivalence_functor, Action.FunctorCategoryEquivalence.functor_obj_obj,
      Action.instMonoidalCategory_tensorUnit_V, Action.instMonoidalCategory_tensorObj_V,
      Action.tensorUnit_Ï', CategoryTheory.Category.comp_id, lift.tmul]
    rfl

/-- Notation for `contrCoContract` acting on a tmul. -/
scoped[Lorentz] notation "âŸª" Ïˆ "," Ï† "âŸ«â‚˜" => contrCoContract.hom (Ïˆ âŠ—â‚œ Ï†)

lemma contrCoContract_hom_tmul (Ïˆ : Contr d) (Ï† : Co d) : âŸªÏˆ, Ï†âŸ«â‚˜ = Ïˆ.toFin1dâ„ â¬áµ¥ Ï†.toFin1dâ„ := by
  rfl

/-- The linear map from Co d âŠ— Contr d to â„ given by
    summing over components of contravariant Lorentz vector and
    covariant Lorentz vector in the
    standard basis (i.e. the dot product).
    In terms of index notation this is the contraction is Ïˆâ± Ï†áµ¢. -/
def coContrContract : Co d âŠ— Contr d âŸ¶ ğŸ™_ (Rep â„ (LorentzGroup d)) where
  hom := TensorProduct.lift (coModContrModBi d)
  comm M := TensorProduct.ext' fun Ïˆ Ï† => by
    change ((LorentzGroup.transpose Mâ»Â¹).1 *áµ¥ Ïˆ.toFin1dâ„) â¬áµ¥ (M.1 *áµ¥ Ï†.toFin1dâ„)  = _
    rw [dotProduct_mulVec, LorentzGroup.transpose_val, mulVec_transpose, vecMul_vecMul,
      LorentzGroup.coe_inv, inv_mul_of_invertible M.1]
    simp only [vecMul_one, CategoryTheory.Equivalence.symm_inverse,
      Action.functorCategoryEquivalence_functor, Action.FunctorCategoryEquivalence.functor_obj_obj,
      Action.instMonoidalCategory_tensorUnit_V, Action.instMonoidalCategory_tensorObj_V,
      Action.tensorUnit_Ï', CategoryTheory.Category.comp_id, lift.tmul]
    rfl

/-- Notation for `coContrContract` acting on a tmul. -/
scoped[Lorentz] notation "âŸª" Ï† "," Ïˆ "âŸ«â‚˜" => coContrContract.hom (Ï† âŠ—â‚œ Ïˆ)

lemma coContrContract_hom_tmul (Ï† : Co d) (Ïˆ : Contr d) : âŸªÏ†, ÏˆâŸ«â‚˜ = Ï†.toFin1dâ„ â¬áµ¥ Ïˆ.toFin1dâ„ := by
  rfl

/-!

## Symmetry relations

-/

lemma contrCoContract_tmul_symm (Ï† : Contr d) (Ïˆ : Co d) : âŸªÏ†, ÏˆâŸ«â‚˜ = âŸªÏˆ, Ï†âŸ«â‚˜ := by
  rw [contrCoContract_hom_tmul, coContrContract_hom_tmul, dotProduct_comm]

lemma coContrContract_tmul_symm (Ï† : Co d) (Ïˆ : Contr d) : âŸªÏ†, ÏˆâŸ«â‚˜ = âŸªÏˆ, Ï†âŸ«â‚˜ := by
  rw [contrCoContract_tmul_symm]

/-!

## Contracting contr vectors with contr vectors etc.

-/
open CategoryTheory.MonoidalCategory
open CategoryTheory

/-- The linear map from Contr d âŠ— Contr d to â„ induced by the homomorphism
  `Contr.toCo` and the contraction `contrCoContract`. -/
def contrContrContract : Contr d âŠ— Contr d âŸ¶ ğŸ™_ (Rep â„ (LorentzGroup d)) :=
  (Contr d â— Contr.toCo d) â‰« contrCoContract

/-- Notation for `contrContrContract` acting on a tmul. -/
scoped[Lorentz] notation "âŸª" Ïˆ "," Ï† "âŸ«â‚˜" => contrContrContract.hom (Ïˆ âŠ—â‚œ Ï†)

lemma contrContrContract_hom_tmul (Ï† : Contr d) (Ïˆ : Contr d) :
    âŸªÏ†, ÏˆâŸ«â‚˜ = Ï†.toFin1dâ„ â¬áµ¥ Î· *áµ¥ Ïˆ.toFin1dâ„:= by
  simp only [Action.instMonoidalCategory_tensorUnit_V, Action.instMonoidalCategory_tensorObj_V,
    contrContrContract, Action.comp_hom, Action.instMonoidalCategory_whiskerLeft_hom,
    Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
    Action.FunctorCategoryEquivalence.functor_obj_obj, ModuleCat.coe_comp, Function.comp_apply,
    ModuleCat.MonoidalCategory.whiskerLeft_apply]
  erw [contrCoContract_hom_tmul]
  rfl

/-- The linear map from Co d âŠ— Co d to â„ induced by the homomorphism
  `Co.toContr` and the contraction `coContrContract`. -/
def coCoContract : Co d âŠ— Co d âŸ¶ ğŸ™_ (Rep â„ (LorentzGroup d)) :=
  (Co d â— Co.toContr d) â‰« coContrContract

/-- Notation for `coCoContract` acting on a tmul. -/
scoped[Lorentz] notation "âŸª" Ïˆ "," Ï† "âŸ«â‚˜" => coCoContract.hom (Ïˆ âŠ—â‚œ Ï†)

lemma coCoContract_hom_tmul (Ï† : Co d) (Ïˆ : Co d) :
    âŸªÏ†, ÏˆâŸ«â‚˜ = Ï†.toFin1dâ„ â¬áµ¥ Î· *áµ¥ Ïˆ.toFin1dâ„:= by
  simp only [Action.instMonoidalCategory_tensorUnit_V, Action.instMonoidalCategory_tensorObj_V,
    contrContrContract, Action.comp_hom, Action.instMonoidalCategory_whiskerLeft_hom,
    Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
    Action.FunctorCategoryEquivalence.functor_obj_obj, ModuleCat.coe_comp, Function.comp_apply,
    ModuleCat.MonoidalCategory.whiskerLeft_apply]
  erw [coContrContract_hom_tmul]
  rfl

/-!

## Lemmas related to contraction.

We derive the lemmas in main for `contrContrContract`.

-/
namespace contrContrContract

variable (x y : Contr d)
open minkowskiMetric

lemma as_sum : âŸªx, yâŸ«â‚˜ = x.val (Sum.inl 0) * y.val (Sum.inl 0) -
      âˆ‘ i, x.val (Sum.inr i) * y.val (Sum.inr i)  := by
  rw [contrContrContract_hom_tmul]
  simp only [dotProduct, minkowskiMatrix, LieAlgebra.Orthogonal.indefiniteDiagonal, mulVec_diagonal,
    Fintype.sum_sum_type, Finset.univ_unique, Fin.default_eq_zero, Fin.isValue, Sum.elim_inl,
    one_mul, Finset.sum_singleton, Sum.elim_inr, neg_mul, mul_neg, Finset.sum_neg_distrib]
  rfl

lemma symm : âŸªx, yâŸ«â‚˜ = âŸªy, xâŸ«â‚˜ := by
  rw [as_sum, as_sum]
  congr 1
  rw [mul_comm]
  congr
  funext i
  rw [mul_comm]

lemma dual_mulVec_right : âŸªx, dual Î› *áµ¥ yâŸ«â‚˜ = âŸªÎ› *áµ¥ x, yâŸ«â‚˜ := by
  rw [contrContrContract_hom_tmul, contrContrContract_hom_tmul]
  simp only [Action.instMonoidalCategory_tensorUnit_V, ContrMod.mulVec_toFin1dâ„, mulVec_mulVec]
  simp only [dual, â† mul_assoc, minkowskiMatrix.sq, one_mul]
  rw [â† mulVec_mulVec, dotProduct_mulVec, vecMul_transpose]

lemma dual_mulVec_left : âŸªdual Î› *áµ¥ x, yâŸ«â‚˜ = âŸªx, Î› *áµ¥ yâŸ«â‚˜ := by
  rw [symm, dual_mulVec_right, symm]

end contrContrContract

end Lorentz
end
