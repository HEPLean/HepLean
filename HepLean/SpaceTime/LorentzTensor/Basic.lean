/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.LinearAlgebra.PiTensorProduct
import Mathlib.RepresentationTheory.Basic
/-!

# Structure of Lorentz Tensors

In this file we set up the basic structures we will use to define Lorentz tensors.

## References

-- For modular operads see: [Raynor][raynor2021graphical]

-/

noncomputable section

open TensorProduct

variable {R : Type} [CommSemiring R]

/-- An initial structure specifying a tensor system (e.g. a system in which you can
  define real Lorentz tensors). -/
structure PreTensorStructure (R : Type) [CommSemiring R] where
  /-- The allowed colors of indices.
    For example for a real Lorentz tensor these are `{up, down}`. -/
  Color : Type
  /-- To each color we associate a module. -/
  ColorModule : Color â†’ Type
  /-- A map taking every color to its dual color. -/
  Ï„ : Color â†’ Color
  /-- The map `Ï„` is an involution. -/
  Ï„_involutive : Function.Involutive Ï„
  /-- Each `ColorModule` has the structure of an additive commutative monoid. -/
  colorModule_addCommMonoid : âˆ€ Î¼, AddCommMonoid (ColorModule Î¼)
  /-- Each `ColorModule` has the structure of a module over `R`. -/
  colorModule_module : âˆ€ Î¼, Module R (ColorModule Î¼)
  /-- The contraction of a vector with a vector with dual color. -/
  contrDual : âˆ€ Î¼, ColorModule Î¼ âŠ—[R] ColorModule (Ï„ Î¼) â†’â‚—[R] R

namespace PreTensorStructure

variable (ğ“£ : PreTensorStructure R)

variable {d : â„•} {X Y Y' Z W : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z] [Fintype W] [DecidableEq W]
  {cX cX2 : X â†’ ğ“£.Color} {cY : Y â†’ ğ“£.Color} {cZ : Z â†’ ğ“£.Color}
  {cW : W â†’ ğ“£.Color} {cY' : Y' â†’ ğ“£.Color} {Î¼ Î½: ğ“£.Color}

instance : AddCommMonoid (ğ“£.ColorModule Î¼) := ğ“£.colorModule_addCommMonoid Î¼

instance : Module R (ğ“£.ColorModule Î¼) := ğ“£.colorModule_module Î¼

/-- The type of tensors given a map from an indexing set `X` to the type of colors,
  specifying the color of that index. -/
def Tensor (c : X â†’ ğ“£.Color) : Type := â¨‚[R] x, ğ“£.ColorModule (c x)

instance : AddCommMonoid (ğ“£.Tensor cX) :=
  PiTensorProduct.instAddCommMonoid fun i => ğ“£.ColorModule (cX i)

instance : Module R (ğ“£.Tensor cX) := PiTensorProduct.instModule

/-- Equivalence of `ColorModule` given an equality of colors. -/
def colorModuleCast (h : Î¼ = Î½) : ğ“£.ColorModule Î¼ â‰ƒâ‚—[R] ğ“£.ColorModule Î½ where
  toFun x := Equiv.cast (congrArg ğ“£.ColorModule h) x
  invFun x := (Equiv.cast (congrArg ğ“£.ColorModule h)).symm x
  map_add' x y := by
    subst h
    rfl
  map_smul' x y := by
    subst h
    rfl
  left_inv x := Equiv.symm_apply_apply (Equiv.cast (congrArg ğ“£.ColorModule h)) x
  right_inv x := Equiv.apply_symm_apply (Equiv.cast (congrArg ğ“£.ColorModule h)) x

lemma tensorProd_piTensorProd_ext {M : Type} [AddCommMonoid M] [Module R M]
    {f g : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY â†’â‚—[R] M}
    (h : âˆ€ p q, f (PiTensorProduct.tprod R p âŠ—â‚œ[R] PiTensorProduct.tprod R q)
    = g (PiTensorProduct.tprod R p âŠ—â‚œ[R] PiTensorProduct.tprod R q)) : f = g := by
  apply TensorProduct.ext'
  refine fun x â†¦
  PiTensorProduct.induction_on' x ?_ (by
    intro a b hx hy y
    simp [map_add, add_tmul, hx, hy])
  intro rx fx
  refine fun y â†¦
    PiTensorProduct.induction_on' y ?_ (by
      intro a b hx hy
      simp at hx hy
      simp [map_add, tmul_add, hx, hy])
  intro ry fy
  simp only [PiTensorProduct.tprodCoeff_eq_smul_tprod, tmul_smul, LinearMapClass.map_smul]
  apply congrArg
  simp only [smul_tmul, tmul_smul, LinearMapClass.map_smul]
  exact congrArg (HSMul.hSMul rx) (h fx fy)

/-!

## Mapping isomorphisms

-/

/-- An linear equivalence of tensor spaces given a color-preserving equivalence of indexing sets. -/
def mapIso {c : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color} (e : X â‰ƒ Y) (h : c = d âˆ˜ e) :
    ğ“£.Tensor c â‰ƒâ‚—[R] ğ“£.Tensor d :=
  (PiTensorProduct.reindex R _ e) â‰ªâ‰«â‚—
  (PiTensorProduct.congr (fun y => ğ“£.colorModuleCast (by rw [h]; simp)))

lemma mapIso_trans_cond {e : X â‰ƒ Y} {e' : Y â‰ƒ Z} (h : cX = cY âˆ˜ e) (h' : cY = cZ âˆ˜ e') :
    cX = cZ âˆ˜ (e.trans e') := by
  funext a
  subst h h'
  simp

@[simp]
lemma mapIso_trans (e : X â‰ƒ Y) (e' : Y â‰ƒ Z) (h : cX = cY âˆ˜ e) (h' : cY = cZ âˆ˜ e') :
    (ğ“£.mapIso e h â‰ªâ‰«â‚— ğ“£.mapIso e' h') = ğ“£.mapIso (e.trans e') (ğ“£.mapIso_trans_cond h h') := by
  refine LinearEquiv.toLinearMap_inj.mp ?_
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [mapIso, LinearMap.compMultilinearMap_apply, LinearEquiv.coe_coe,
    LinearEquiv.trans_apply, PiTensorProduct.reindex_tprod, Equiv.symm_trans_apply]
  change (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cY x)) e')
    ((PiTensorProduct.congr fun y => ğ“£.colorModuleCast _) _)) =
    (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cX x)) (e.trans e')) _)
  rw [PiTensorProduct.congr_tprod, PiTensorProduct.reindex_tprod,
    PiTensorProduct.congr_tprod, PiTensorProduct.reindex_tprod, PiTensorProduct.congr]
  simp [colorModuleCast]

@[simp]
lemma mapIso_mapIso (e : X â‰ƒ Y) (e' : Y â‰ƒ Z) (h : cX = cY âˆ˜ e) (h' : cY = cZ âˆ˜ e')
    (T : ğ“£.Tensor cX) :
    (ğ“£.mapIso e' h') (ğ“£.mapIso e h T) = ğ“£.mapIso (e.trans e') (ğ“£.mapIso_trans_cond h h') T := by
  rw [â† LinearEquiv.trans_apply, mapIso_trans]

@[simp]
lemma mapIso_symm (e : X â‰ƒ Y) (h : cX = cY âˆ˜ e) :
    (ğ“£.mapIso e h).symm = ğ“£.mapIso e.symm ((Equiv.eq_comp_symm e cY cX).mpr h.symm) := by
  refine LinearEquiv.toLinearMap_inj.mp ?_
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp [mapIso, LinearMap.compMultilinearMap_apply, LinearEquiv.coe_coe,
    LinearEquiv.symm_apply_apply, PiTensorProduct.reindex_tprod]
  change (PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cX x)) e).symm
    ((PiTensorProduct.congr fun y => ğ“£.colorModuleCast _).symm ((PiTensorProduct.tprod R) x)) =
    (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cY x)) e.symm)
    ((PiTensorProduct.tprod R) x))
  rw [PiTensorProduct.reindex_tprod, PiTensorProduct.congr_tprod, PiTensorProduct.congr_symm_tprod,
    LinearEquiv.symm_apply_eq, PiTensorProduct.reindex_tprod]
  apply congrArg
  funext i
  simp only [colorModuleCast, Equiv.cast_symm, LinearEquiv.coe_symm_mk,
    Equiv.symm_symm_apply, LinearEquiv.coe_mk]
  rw [â† Equiv.symm_apply_eq]
  simp only [Equiv.cast_symm, Equiv.cast_apply, cast_cast]
  apply cast_eq_iff_heq.mpr
  rw [Equiv.apply_symm_apply]

@[simp]
lemma mapIso_refl : ğ“£.mapIso (Equiv.refl X) (rfl : cX = cX) = LinearEquiv.refl R _ := by
  refine LinearEquiv.toLinearMap_inj.mp ?_
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [mapIso, Equiv.refl_symm, Equiv.refl_apply, PiTensorProduct.reindex_refl,
    LinearMap.compMultilinearMap_apply, LinearEquiv.coe_coe, LinearEquiv.trans_apply,
    LinearEquiv.refl_apply, LinearEquiv.refl_toLinearMap, LinearMap.id, LinearMap.coe_mk,
    AddHom.coe_mk, id_eq]
  change (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _) ((PiTensorProduct.tprod R) x) = _
  rw [PiTensorProduct.congr_tprod]
  rfl

@[simp]
lemma mapIso_tprod {c : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color} (e : X â‰ƒ Y) (h : c = d âˆ˜ e)
    (f : (i : X) â†’ ğ“£.ColorModule (c i)) : (ğ“£.mapIso e h) (PiTensorProduct.tprod R f) =
    (PiTensorProduct.tprod R (fun i => ğ“£.colorModuleCast (by rw [h]; simp) (f (e.symm i)))) := by
  simp [mapIso]
  change (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R _ e) ((PiTensorProduct.tprod R) f)) = _
  rw [PiTensorProduct.reindex_tprod]
  exact PiTensorProduct.congr_tprod (fun y => ğ“£.colorModuleCast _) fun i => f (e.symm i)

/-!

## Pure tensors

This section is needed since: `PiTensorProduct.tmulEquiv` is not defined for dependent types.
Hence we need to construct a version of it here.

-/

/-- The type of pure tensors, i.e. of the form `v1 âŠ— v2 âŠ— v3 âŠ— ...`. -/
abbrev PureTensor (c : X â†’ ğ“£.Color) := (x : X) â†’ ğ“£.ColorModule (c x)

/-- A pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` constructed from a pure tensor
  in `ğ“£.PureTensor cX` and a pure tensor in `ğ“£.PureTensor cY`. -/
def elimPureTensor (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY) : ğ“£.PureTensor (Sum.elim cX cY) :=
  fun x =>
    match x with
    | Sum.inl x => p x
    | Sum.inr x => q x

@[simp]
lemma elimPureTensor_update_right (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY)
    (y : Y) (r : ğ“£.ColorModule (cY y)) : ğ“£.elimPureTensor p (Function.update q y r) =
    Function.update (ğ“£.elimPureTensor p q) (Sum.inr y) r := by
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x =>
    change Function.update q y r x = _
    simp only [Function.update, Sum.inr.injEq, Sum.elim_inr]
    split_ifs
    rename_i h
    subst h
    simp_all only
    rfl

@[simp]
lemma elimPureTensor_update_left (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY)
    (x : X) (r : ğ“£.ColorModule (cX x)) : ğ“£.elimPureTensor (Function.update p x r) q =
    Function.update (ğ“£.elimPureTensor p q) (Sum.inl x) r := by
  funext y
  match y with
  | Sum.inl y =>
    change (Function.update p x r) y = _
    simp only [Function.update, Sum.inl.injEq, Sum.elim_inl]
    split_ifs
    rename_i h
    subst h
    simp_all only
    rfl
  | Sum.inr y => rfl

/-- The projection of a pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` onto a pure tensor in
  `ğ“£.PureTensor cX`. -/
def inlPureTensor (p : ğ“£.PureTensor (Sum.elim cX cY)) : ğ“£.PureTensor cX := fun x => p (Sum.inl x)

/-- The projection of a pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` onto a pure tensor in
  `ğ“£.PureTensor cY`. -/
def inrPureTensor (p : ğ“£.PureTensor (Sum.elim cX cY)) : ğ“£.PureTensor cY := fun y => p (Sum.inr y)

@[simp]
lemma inlPureTensor_update_left [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (x : X)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inl x))) :
    ğ“£.inlPureTensor (Function.update f (Sum.inl x) v1) =
    Function.update (ğ“£.inlPureTensor f) x v1 := by
  funext y
  simp [inlPureTensor, Function.update, Sum.inl.injEq, Sum.elim_inl]
  split
  next h =>
    subst h
    simp_all only
  rfl

@[simp]
lemma inrPureTensor_update_left [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (x : X)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inl x))) :
    ğ“£.inrPureTensor (Function.update f (Sum.inl x) v1) = (ğ“£.inrPureTensor f) := by
  funext x
  simp [inrPureTensor, Function.update]

@[simp]
lemma inrPureTensor_update_right [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (y : Y)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inr y))) :
    ğ“£.inrPureTensor (Function.update f (Sum.inr y) v1) =
    Function.update (ğ“£.inrPureTensor f) y v1 := by
  funext y
  simp [inrPureTensor, Function.update, Sum.inl.injEq, Sum.elim_inl]
  split
  next h =>
    subst h
    simp_all only
  rfl

@[simp]
lemma inlPureTensor_update_right [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (y : Y)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inr y))) :
    ğ“£.inlPureTensor (Function.update f (Sum.inr y) v1) = (ğ“£.inlPureTensor f) := by
  funext x
  simp [inlPureTensor, Function.update]

/-- The multilinear map taking pure tensors a `ğ“£.PureTensor cX` and a pure tensor in
  `ğ“£.PureTensor cY`, and constructing a tensor in `ğ“£.Tensor (Sum.elim cX cY))`. -/
def elimPureTensorMulLin : MultilinearMap R (fun i => ğ“£.ColorModule (cX i))
    (MultilinearMap R (fun x => ğ“£.ColorModule (cY x)) (ğ“£.Tensor (Sum.elim cX cY))) where
  toFun p := {
    toFun := fun q => PiTensorProduct.tprod R (ğ“£.elimPureTensor p q)
    map_add' := fun m x v1 v2 => by
      simp [Sum.elim_inl, Sum.elim_inr]
    map_smul' := fun m x r v => by
      simp [Sum.elim_inl, Sum.elim_inr]}
  map_add' p x v1 v2 := by
    apply MultilinearMap.ext
    intro y
    simp
  map_smul' p x r v := by
    apply MultilinearMap.ext
    intro y
    simp

/-!

## tensorator

-/

/-! TODO: Replace with dependent type version of `MultilinearMap.domCoprod` when in Mathlib. -/
/-- The multi-linear map taking a pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` and constructing
  a vector in `ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY`. -/
def domCoprod : MultilinearMap R (fun x => ğ“£.ColorModule (Sum.elim cX cY x))
    (ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY) where
  toFun f := (PiTensorProduct.tprod R (ğ“£.inlPureTensor f)) âŠ—â‚œ
    (PiTensorProduct.tprod R (ğ“£.inrPureTensor f))
  map_add' f xy v1 v2:= by
    match xy with
    | Sum.inl x => simp [â† TensorProduct.add_tmul]
    | Sum.inr y => simp [â† TensorProduct.tmul_add]
  map_smul' f xy r p := by
    match xy with
    | Sum.inl x => simp [TensorProduct.tmul_smul, TensorProduct.smul_tmul]
    | Sum.inr y => simp [TensorProduct.tmul_smul, TensorProduct.smul_tmul]

/-- The linear map combining two tensors into a single tensor
  via the tensor product i.e. `v1 v2 â†¦ v1 âŠ— v2`. -/
def tensoratorSymm : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY â†’â‚—[R] ğ“£.Tensor (Sum.elim cX cY) := by
  refine TensorProduct.lift {
    toFun := fun a â†¦
      PiTensorProduct.lift <|
          PiTensorProduct.lift (ğ“£.elimPureTensorMulLin) a,
    map_add' := fun a b â†¦ by simp
    map_smul' := fun r a â†¦ by simp}

/-! TODO: Replace with dependent type version of `PiTensorProduct.tmulEquiv` when in Mathlib. -/
/-- Splitting a tensor in `ğ“£.Tensor (Sum.elim cX cY)` into the tensor product of two tensors. -/
def tensorator : ğ“£.Tensor (Sum.elim cX cY) â†’â‚—[R] ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY :=
  PiTensorProduct.lift ğ“£.domCoprod

/-- An equivalence formed by taking the tensor product of tensors. -/
def tensoratorEquiv (c : X â†’ ğ“£.Color) (d : Y â†’ ğ“£.Color) :
    ğ“£.Tensor c âŠ—[R] ğ“£.Tensor d â‰ƒâ‚—[R] ğ“£.Tensor (Sum.elim c d) :=
  LinearEquiv.ofLinear (ğ“£.tensoratorSymm) (ğ“£.tensorator)
  (by
    apply PiTensorProduct.ext
    apply MultilinearMap.ext
    intro p
    simp [tensorator, tensoratorSymm, domCoprod]
    change (PiTensorProduct.lift _) ((PiTensorProduct.tprod R) _) =
      LinearMap.id ((PiTensorProduct.tprod R) p)
    rw [PiTensorProduct.lift.tprod]
    simp [elimPureTensorMulLin, elimPureTensor]
    change (PiTensorProduct.tprod R) _ = _
    apply congrArg
    funext x
    match x with
    | Sum.inl x => rfl
    | Sum.inr x => rfl)
  (by
    apply tensorProd_piTensorProd_ext
    intro p q
    simp [tensorator, tensoratorSymm]
    change (PiTensorProduct.lift ğ“£.domCoprod)
      ((PiTensorProduct.lift (ğ“£.elimPureTensorMulLin p)) ((PiTensorProduct.tprod R) q)) =_
    rw [PiTensorProduct.lift.tprod]
    simp [elimPureTensorMulLin]
    rfl)

@[simp]
lemma tensoratorEquiv_tmul_tprod (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY) :
    (ğ“£.tensoratorEquiv cX cY) ((PiTensorProduct.tprod R) p âŠ—â‚œ[R] (PiTensorProduct.tprod R) q) =
    (PiTensorProduct.tprod R) (ğ“£.elimPureTensor p q) := by
  simp only [tensoratorEquiv, tensoratorSymm, tensorator, domCoprod, LinearEquiv.ofLinear_apply,
    lift.tmul, LinearMap.coe_mk, AddHom.coe_mk, PiTensorProduct.lift.tprod]
  exact PiTensorProduct.lift.tprod q

lemma tensoratorEquiv_mapIso_cond {e : X â‰ƒ Y} {e' : Z â‰ƒ Y} {e'' : W â‰ƒ X}
    (h : cX = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') (h'' : bW = cX âˆ˜ e'') :
    Sum.elim bW cZ = Sum.elim cX cY âˆ˜ â‡‘(e''.sumCongr e') := by
  subst h h' h''
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x => rfl

@[simp]
lemma tensoratorEquiv_mapIso (e : X â‰ƒ Y) (e' : Z â‰ƒ Y) (e'' : W â‰ƒ X)
    (h : cX = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') (h'' : bW = cX âˆ˜ e'') :
    (TensorProduct.congr (ğ“£.mapIso e'' h'') (ğ“£.mapIso e' h')) â‰ªâ‰«â‚— (ğ“£.tensoratorEquiv cX cY)
    = (ğ“£.tensoratorEquiv bW cZ)
    â‰ªâ‰«â‚— (ğ“£.mapIso (Equiv.sumCongr e'' e') (ğ“£.tensoratorEquiv_mapIso_cond h h' h'')) := by
  apply LinearEquiv.toLinearMap_inj.mp
  apply tensorProd_piTensorProd_ext
  intro p q
  simp only [LinearEquiv.coe_coe, LinearEquiv.trans_apply, congr_tmul, mapIso_tprod,
    tensoratorEquiv_tmul_tprod, Equiv.sumCongr_symm, Equiv.sumCongr_apply]
  apply congrArg
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x => rfl

@[simp]
lemma tensoratorEquiv_mapIso_apply (e : X â‰ƒ Y) (e' : Z â‰ƒ Y) (e'' : W â‰ƒ X)
    (h : cX = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') (h'' : cW = cX âˆ˜ e'')
    (x : ğ“£.Tensor cW âŠ—[R] ğ“£.Tensor cZ) :
    (ğ“£.tensoratorEquiv cX cY) ((TensorProduct.congr (ğ“£.mapIso e'' h'') (ğ“£.mapIso e' h')) x) =
    (ğ“£.mapIso (Equiv.sumCongr e'' e') (ğ“£.tensoratorEquiv_mapIso_cond h h' h''))
    ((ğ“£.tensoratorEquiv cW cZ) x) := by
  trans ((TensorProduct.congr (ğ“£.mapIso e'' h'') (ğ“£.mapIso e' h')) â‰ªâ‰«â‚—
    (ğ“£.tensoratorEquiv cX cY)) x
  rfl
  rw [tensoratorEquiv_mapIso]
  rfl
  exact e
  exact h

lemma tensoratorEquiv_mapIso_tmul (e : X â‰ƒ Y) (e' : Z â‰ƒ Y) (e'' : W â‰ƒ X)
    (h : cX = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') (h'' : cW = cX âˆ˜ e'')
    (x : ğ“£.Tensor cW) (y : ğ“£.Tensor cZ) :
    (ğ“£.tensoratorEquiv cX cY) ((ğ“£.mapIso e'' h'' x) âŠ—â‚œ[R] (ğ“£.mapIso e' h' y)) =
    (ğ“£.mapIso (Equiv.sumCongr e'' e') (ğ“£.tensoratorEquiv_mapIso_cond h h' h''))
    ((ğ“£.tensoratorEquiv cW cZ) (x âŠ—â‚œ y)) := by
  rw [â† tensoratorEquiv_mapIso_apply]
  rfl
  exact e
  exact h

/-!

## Splitting tensors into tensor products

-/

/-- The decomposition of a set into a direct sum based on the image of an injection. -/
def decompEmbedSet (f : Y â†ª X) :
    X â‰ƒ {x // x âˆˆ (Finset.image f Finset.univ)á¶œ} âŠ• Y :=
  (Equiv.Set.sumCompl (Set.range â‡‘f)).symm.trans <|
  (Equiv.sumComm _ _).trans <|
  Equiv.sumCongr ((Equiv.subtypeEquivRight (by simp))) <|
  (Function.Embedding.toEquivRange f).symm

/-- The restriction of a map from an indexing set to the space to the complement of the image
  of an embedding. -/
def decompEmbedColorLeft (c : X â†’ ğ“£.Color) (f : Y â†ª X) :
    {x // x âˆˆ (Finset.image f Finset.univ)á¶œ} â†’ ğ“£.Color :=
  (c âˆ˜ (decompEmbedSet f).symm) âˆ˜ Sum.inl

/-- The restriction of a map from an indexing set to the space to the image
  of an embedding. -/
def decompEmbedColorRight (c : X â†’ ğ“£.Color) (f : Y â†ª X) : Y â†’ ğ“£.Color :=
  (c âˆ˜ (decompEmbedSet f).symm) âˆ˜ Sum.inr

lemma decompEmbed_cond (c : X â†’ ğ“£.Color) (f : Y â†ª X) : c =
    (Sum.elim (ğ“£.decompEmbedColorLeft c f) (ğ“£.decompEmbedColorRight c f)) âˆ˜ decompEmbedSet f := by
  simpa [decompEmbedColorLeft, decompEmbedColorRight] using (Equiv.comp_symm_eq _ _ _).mp rfl

/-- Decomposes a tensor into a tensor product of two tensors
  one which has indices in the image of the given embedding, and the other has indices in
  the complement of that image. -/
def decompEmbed (f : Y â†ª X) :
    ğ“£.Tensor cX â‰ƒâ‚—[R] ğ“£.Tensor (ğ“£.decompEmbedColorLeft cX f) âŠ—[R] ğ“£.Tensor (cX âˆ˜ f) :=
  (ğ“£.mapIso (decompEmbedSet f) (ğ“£.decompEmbed_cond cX f)) â‰ªâ‰«â‚—
  (ğ“£.tensoratorEquiv (ğ“£.decompEmbedColorLeft cX f) (ğ“£.decompEmbedColorRight cX f)).symm

/-!

## Contraction

-/

/-- A linear map taking tensors mapped with the same index set to the product of paired tensors. -/
def pairProd : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cX2 â†’â‚—[R]
    â¨‚[R] x, ğ“£.ColorModule (cX x) âŠ—[R] ğ“£.ColorModule (cX2 x) :=
  TensorProduct.lift (
    PiTensorProduct.mapâ‚‚ (fun x =>
      TensorProduct.mk R (ğ“£.ColorModule (cX x)) (ğ“£.ColorModule (cX2 x))))

lemma mkPiAlgebra_equiv (e : X â‰ƒ Y) :
    (PiTensorProduct.lift (MultilinearMap.mkPiAlgebra R X R)) =
    (PiTensorProduct.lift (MultilinearMap.mkPiAlgebra R Y R)) âˆ˜â‚—
    (PiTensorProduct.reindex R _ e).toLinearMap := by
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [LinearMap.compMultilinearMap_apply, PiTensorProduct.lift.tprod,
    MultilinearMap.mkPiAlgebra_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
    PiTensorProduct.reindex_tprod, Equiv.prod_comp]

/-- Given a tensor in `ğ“£.Tensor cX` and a tensor in `ğ“£.Tensor (ğ“£.Ï„ âˆ˜ cX)`, the element of
  `R` formed by contracting all of their indices. -/
def contrAll' : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor (ğ“£.Ï„ âˆ˜ cX) â†’â‚—[R] R :=
  (PiTensorProduct.lift (MultilinearMap.mkPiAlgebra R X R)) âˆ˜â‚—
  (PiTensorProduct.map (fun x => ğ“£.contrDual (cX x))) âˆ˜â‚—
  (ğ“£.pairProd)

lemma contrAll'_mapIso_cond {e : X â‰ƒ Y} (h : cX = cY âˆ˜ e) :
    ğ“£.Ï„ âˆ˜ cY = (ğ“£.Ï„ âˆ˜ cX) âˆ˜ â‡‘e.symm := by
  subst h
  exact (Equiv.eq_comp_symm e (ğ“£.Ï„ âˆ˜ cY) (ğ“£.Ï„ âˆ˜ cY âˆ˜ â‡‘e)).mpr rfl

@[simp]
lemma contrAll'_mapIso (e : X â‰ƒ Y) (h : c = cY âˆ˜ e) :
    ğ“£.contrAll' âˆ˜â‚—
      (TensorProduct.congr (ğ“£.mapIso e h) (LinearEquiv.refl R _)).toLinearMap =
    ğ“£.contrAll' âˆ˜â‚— (TensorProduct.congr (LinearEquiv.refl R _)
      (ğ“£.mapIso e.symm (ğ“£.contrAll'_mapIso_cond h))).toLinearMap := by
  apply TensorProduct.ext'
  refine fun x â†¦
    PiTensorProduct.induction_on' x ?_ (by
      intro a b hx hy y
      simp [map_add, add_tmul, hx, hy])
  intro rx fx
  refine fun y â†¦
    PiTensorProduct.induction_on' y ?_ (by
      intro a b hx hy
      simp at hx hy
      simp [map_add, tmul_add, hx, hy])
  intro ry fy
  simp [contrAll']
  rw [mkPiAlgebra_equiv e]
  apply congrArg
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply]
  apply congrArg
  rw [â† LinearEquiv.symm_apply_eq]
  rw [PiTensorProduct.reindex_symm]
  rw [â† PiTensorProduct.map_reindex]
  subst h
  simp only [Equiv.symm_symm_apply, Function.comp_apply]
  apply congrArg
  rw [pairProd, pairProd]
  simp only [Function.comp_apply, lift.tmul, LinearMapClass.map_smul, LinearMap.smul_apply]
  apply congrArg
  change _ = ((PiTensorProduct.mapâ‚‚ fun x => TensorProduct.mk R (ğ“£.ColorModule (cY (e x)))
      (ğ“£.ColorModule (ğ“£.Ï„ (cY (e x)))))
      ((PiTensorProduct.tprod R) fx))
    ((ğ“£.mapIso e.symm _) ((PiTensorProduct.tprod R) fy))
  rw [mapIso_tprod]
  simp only [Equiv.symm_symm_apply, Function.comp_apply]
  rw [PiTensorProduct.mapâ‚‚_tprod_tprod]
  change PiTensorProduct.reindex R _ e.symm
    ((PiTensorProduct.mapâ‚‚ _
        ((PiTensorProduct.tprod R) fun i => (ğ“£.colorModuleCast _) (fx (e.symm i))))
      ((PiTensorProduct.tprod R) fy)) = _
  rw [PiTensorProduct.mapâ‚‚_tprod_tprod]
  simp only [Equiv.symm_symm_apply, Function.comp_apply, mk_apply]
  erw [PiTensorProduct.reindex_tprod]
  apply congrArg
  funext i
  simp only [Equiv.symm_symm_apply]
  congr
  simp [colorModuleCast]
  apply cast_eq_iff_heq.mpr
  rw [Equiv.symm_apply_apply]

@[simp]
lemma contrAll'_mapIso_tmul (e : X â‰ƒ Y) (h : c = cY âˆ˜ e) (x : ğ“£.Tensor c)
    (y : ğ“£.Tensor (ğ“£.Ï„ âˆ˜ cY)) : ğ“£.contrAll' ((ğ“£.mapIso e h) x âŠ—â‚œ[R] y) =
    ğ“£.contrAll' (x âŠ—â‚œ[R] (ğ“£.mapIso e.symm (ğ“£.contrAll'_mapIso_cond h) y)) := by
  change (ğ“£.contrAll' âˆ˜â‚—
    (TensorProduct.congr (ğ“£.mapIso e h) (LinearEquiv.refl R _)).toLinearMap) (x âŠ—â‚œ[R] y) = _
  rw [contrAll'_mapIso]
  rfl

/-- The contraction of all the indices of two tensors with dual colors. -/
def contrAll {c : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color}
    (e : X â‰ƒ Y) (h : c = ğ“£.Ï„ âˆ˜ d âˆ˜ e) : ğ“£.Tensor c âŠ—[R] ğ“£.Tensor d â†’â‚—[R] R :=
  ğ“£.contrAll' âˆ˜â‚— (TensorProduct.congr (LinearEquiv.refl _ _)
    (ğ“£.mapIso e.symm (by subst h; funext a; simp; rw [ğ“£.Ï„_involutive]))).toLinearMap

lemma contrAll_symm_cond {e : X â‰ƒ Y} (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) :
    cY = ğ“£.Ï„ âˆ˜ c âˆ˜ â‡‘e.symm := by
  subst h
  ext1 x
  simp only [Function.comp_apply, Equiv.apply_symm_apply]
  rw [ğ“£.Ï„_involutive]

lemma contrAll_mapIso_right_cond {e : X â‰ƒ Y} {e' : Z â‰ƒ Y}
    (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') : c = ğ“£.Ï„ âˆ˜ cZ âˆ˜ â‡‘(e.trans e'.symm) := by
  subst h h'
  ext1 x
  simp only [Function.comp_apply, Equiv.coe_trans, Equiv.apply_symm_apply]

@[simp]
lemma contrAll_mapIso_right_tmul (e : X â‰ƒ Y) (e' : Z â‰ƒ Y)
    (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') (x : ğ“£.Tensor c) (z : ğ“£.Tensor cZ) :
    ğ“£.contrAll e h (x âŠ—â‚œ[R] (ğ“£.mapIso e' h' z)) =
    ğ“£.contrAll (e.trans e'.symm) (ğ“£.contrAll_mapIso_right_cond h h') (x âŠ—â‚œ[R] z) := by
  rw [contrAll, contrAll]
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, congr_tmul,
    LinearEquiv.refl_apply, mapIso_mapIso]
  congr

@[simp]
lemma contrAll_comp_mapIso_right (e : X â‰ƒ Y) (e' : Z â‰ƒ Y)
    (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = cY âˆ˜ e') : ğ“£.contrAll e h âˆ˜â‚—
    (TensorProduct.congr (LinearEquiv.refl R (ğ“£.Tensor c)) (ğ“£.mapIso e' h')).toLinearMap
    = ğ“£.contrAll (e.trans e'.symm) (ğ“£.contrAll_mapIso_right_cond h h') := by
  apply TensorProduct.ext'
  intro x y
  exact ğ“£.contrAll_mapIso_right_tmul e e' h h' x y

lemma contrAll_mapIso_left_cond {e : X â‰ƒ Y} {e' : Z â‰ƒ X}
    (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = c âˆ˜ e') : cZ = ğ“£.Ï„ âˆ˜ cY âˆ˜ â‡‘(e'.trans e) := by
  subst h h'
  ext1 x
  simp only [Function.comp_apply, Equiv.coe_trans, Equiv.apply_symm_apply]

@[simp]
lemma contrAll_mapIso_left_tmul {e : X â‰ƒ Y} {e' : Z â‰ƒ X}
    (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = c âˆ˜ e') (x : ğ“£.Tensor cZ) (y : ğ“£.Tensor cY) :
    ğ“£.contrAll e h ((ğ“£.mapIso e' h' x) âŠ—â‚œ[R] y) =
    ğ“£.contrAll (e'.trans e) (ğ“£.contrAll_mapIso_left_cond h h') (x âŠ—â‚œ[R] y) := by
  rw [contrAll, contrAll]
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, congr_tmul,
    LinearEquiv.refl_apply, contrAll'_mapIso_tmul, mapIso_mapIso]
  congr

@[simp]
lemma contrAll_mapIso_left {e : X â‰ƒ Y} {e' : Z â‰ƒ X}
    (h : c = ğ“£.Ï„ âˆ˜ cY âˆ˜ e) (h' : cZ = c âˆ˜ e') :
    ğ“£.contrAll e h âˆ˜â‚—
    (TensorProduct.congr (ğ“£.mapIso e' h') (LinearEquiv.refl R (ğ“£.Tensor cY))).toLinearMap
    = ğ“£.contrAll (e'.trans e) (ğ“£.contrAll_mapIso_left_cond h h') := by
  apply TensorProduct.ext'
  intro x y
  exact ğ“£.contrAll_mapIso_left_tmul h h' x y

end PreTensorStructure

/-! TODO: Add unit here. -/
/-- A `PreTensorStructure` with the additional constraint that `contrDua` is symmetric. -/
structure TensorStructure (R : Type) [CommSemiring R] extends PreTensorStructure R where
  /-- The symmetry condition on `contrDua`. -/
  contrDual_symm : âˆ€ Î¼,
    (contrDual Î¼) âˆ˜â‚— (TensorProduct.comm R (ColorModule (Ï„ Î¼)) (ColorModule Î¼)).toLinearMap =
    (contrDual (Ï„ Î¼)) âˆ˜â‚— (TensorProduct.congr (LinearEquiv.refl _ _)
    (toPreTensorStructure.colorModuleCast (by rw[toPreTensorStructure.Ï„_involutive]))).toLinearMap

namespace TensorStructure

open PreTensorStructure

variable (ğ“£ : TensorStructure R)

variable {d : â„•} {X Y Y' Z : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z]
  {c câ‚‚ : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color} {b : Z â†’ ğ“£.Color} {d' : Y' â†’ ğ“£.Color} {Î¼ Î½: ğ“£.Color}

end TensorStructure

/-- A `TensorStructure` with a group action. -/
structure GroupTensorStructure (R : Type) [CommSemiring R]
    (G : Type) [Group G] extends TensorStructure R where
  /-- For each color `Î¼` a representation of `G` on `ColorModule Î¼`. -/
  repColorModule : (Î¼ : Color) â†’ Representation R G (ColorModule Î¼)
  /-- The contraction of a vector with its dual is invariant under the group action. -/
  contrDual_inv : âˆ€ Î¼ g, contrDual Î¼ âˆ˜â‚—
    TensorProduct.map (repColorModule Î¼ g) (repColorModule (Ï„ Î¼) g) = contrDual Î¼

namespace GroupTensorStructure
open TensorStructure
open PreTensorStructure

variable {G : Type} [Group G]

variable (ğ“£ : GroupTensorStructure R G)

variable {d : â„•} {X Y Y' Z : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z]
  {cX cX2 : X â†’ ğ“£.Color} {cY : Y â†’ ğ“£.Color} {cZ : Z â†’ ğ“£.Color} {cY' : Y' â†’ ğ“£.Color} {Î¼ Î½: ğ“£.Color}

/-- The representation of the group `G` on the vector space of tensors. -/
def rep : Representation R G (ğ“£.Tensor cX) where
  toFun g := PiTensorProduct.map (fun x => ğ“£.repColorModule (cX x) g)
  map_one' := by
    simp_all only [_root_.map_one, PiTensorProduct.map_one]
  map_mul' g g' := by
    simp_all only [_root_.map_mul]
    exact PiTensorProduct.map_mul _ _

local infixl:78 " â€¢ " => ğ“£.rep

lemma repColorModule_colorModuleCast_apply (h : Î¼ = Î½) (g : G) (x : ğ“£.ColorModule Î¼) :
    (ğ“£.repColorModule Î½ g) ((ğ“£.colorModuleCast h) x) =
    (ğ“£.colorModuleCast h) ((ğ“£.repColorModule Î¼ g) x) := by
  subst h
  simp [colorModuleCast]

@[simp]
lemma repColorModule_colorModuleCast (h : Î¼ = Î½) (g : G) :
    (ğ“£.repColorModule Î½ g) âˆ˜â‚— (ğ“£.colorModuleCast h).toLinearMap =
    (ğ“£.colorModuleCast h).toLinearMap âˆ˜â‚— (ğ“£.repColorModule Î¼ g) := by
  apply LinearMap.ext
  intro x
  simp [repColorModule_colorModuleCast_apply]

@[simp]
lemma rep_mapIso (e : X â‰ƒ Y) (h : cX = cY âˆ˜ e) (g : G) :
    (ğ“£.rep g) âˆ˜â‚— (ğ“£.mapIso e h).toLinearMap = (ğ“£.mapIso e h).toLinearMap âˆ˜â‚— ğ“£.rep g := by
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [LinearMap.compMultilinearMap_apply, LinearMap.coe_comp, LinearEquiv.coe_coe,
    Function.comp_apply]
  erw [mapIso_tprod]
  simp [rep, repColorModule_colorModuleCast_apply]
  change (PiTensorProduct.map fun x => (ğ“£.repColorModule (cY x)) g)
    ((PiTensorProduct.tprod R) fun i => (ğ“£.colorModuleCast _) (x (e.symm i))) =
    (ğ“£.mapIso e h) ((PiTensorProduct.map _) ((PiTensorProduct.tprod R) x))
  rw [PiTensorProduct.map_tprod, PiTensorProduct.map_tprod]
  rw [mapIso_tprod]
  apply congrArg
  funext i
  subst h
  simp [repColorModule_colorModuleCast_apply]

@[simp]
lemma rep_mapIso_apply (e : X â‰ƒ Y) (h : cX = cY âˆ˜ e) (g : G) (x : ğ“£.Tensor cX) :
    g â€¢ (ğ“£.mapIso e h x) = (ğ“£.mapIso e h) (g â€¢ x) := by
  trans ((ğ“£.rep g) âˆ˜â‚— (ğ“£.mapIso e h).toLinearMap) x
  rfl
  simp

@[simp]
lemma rep_tprod (g : G) (f : (i : X) â†’ ğ“£.ColorModule (cX i)) :
    g â€¢ (PiTensorProduct.tprod R f) = PiTensorProduct.tprod R (fun x =>
      ğ“£.repColorModule (cX x) g (f x)) := by
  simp [rep]
  change (PiTensorProduct.map _) ((PiTensorProduct.tprod R) f) = _
  rw [PiTensorProduct.map_tprod]

/-!

## Group acting on tensor products

-/

lemma rep_tensoratorEquiv (g : G) :
    (ğ“£.tensoratorEquiv cX cY) âˆ˜â‚— (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) = ğ“£.rep g âˆ˜â‚—
    (ğ“£.tensoratorEquiv cX cY).toLinearMap := by
  apply tensorProd_piTensorProd_ext
  intro p q
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, map_tmul, rep_tprod,
    tensoratorEquiv_tmul_tprod]
  apply congrArg
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x => rfl

lemma rep_tensoratorEquiv_apply (g : G) (x : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY) :
    (ğ“£.tensoratorEquiv cX cY) ((TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) x)
    = (ğ“£.rep g) ((ğ“£.tensoratorEquiv cX cY) x) := by
  trans ((ğ“£.tensoratorEquiv cX cY) âˆ˜â‚— (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g))) x
  rfl
  rw [rep_tensoratorEquiv]
  rfl

lemma rep_tensoratorEquiv_tmul (g : G) (x : ğ“£.Tensor cX) (y : ğ“£.Tensor cY) :
    (ğ“£.tensoratorEquiv cX cY) ((g â€¢ x) âŠ—â‚œ[R] (g â€¢ y)) =
    g â€¢ ((ğ“£.tensoratorEquiv cX cY) (x âŠ—â‚œ[R] y)) := by
  nth_rewrite 1 [â† rep_tensoratorEquiv_apply]
  rfl

/-!

## Group acting on contraction

-/

@[simp]
lemma contrAll_rep {c : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color} (e : X â‰ƒ Y) (h : c = ğ“£.Ï„ âˆ˜ d âˆ˜ e) (g : G) :
    ğ“£.contrAll e h âˆ˜â‚— (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) = ğ“£.contrAll e h := by
  apply TensorProduct.ext'
  refine fun x â†¦ PiTensorProduct.induction_on' x ?_ (by
      intro a b hx hy y
      simp [map_add, add_tmul, hx, hy])
  intro rx fx
  refine fun y â†¦ PiTensorProduct.induction_on' y ?_ (by
      intro a b hx hy
      simp at hx hy
      simp [map_add, tmul_add, hx, hy])
  intro ry fy
  simp [contrAll, TensorProduct.smul_tmul]
  apply congrArg
  apply congrArg
  simp [contrAll']
  apply congrArg
  simp [pairProd]
  change (PiTensorProduct.map _) ((PiTensorProduct.mapâ‚‚ _ _) _) =
    (PiTensorProduct.map _) ((PiTensorProduct.mapâ‚‚ _ _) _)
  rw [PiTensorProduct.mapâ‚‚_tprod_tprod, PiTensorProduct.mapâ‚‚_tprod_tprod, PiTensorProduct.map_tprod,
  PiTensorProduct.map_tprod]
  simp only [mk_apply]
  apply congrArg
  funext x
  rw [â† repColorModule_colorModuleCast_apply]
  nth_rewrite 2 [â† ğ“£.contrDual_inv (c x) g]
  rfl

@[simp]
lemma contrAll_rep_apply {c : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color} (e : X â‰ƒ Y) (h : c = ğ“£.Ï„ âˆ˜ d âˆ˜ e)
    (g : G) (x : ğ“£.Tensor c âŠ— ğ“£.Tensor d) :
    ğ“£.contrAll e h (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g) x) = ğ“£.contrAll e h x := by
  change (ğ“£.contrAll e h âˆ˜â‚— (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g))) x = _
  rw [contrAll_rep]

@[simp]
lemma contrAll_rep_tmul {c : X â†’ ğ“£.Color} {d : Y â†’ ğ“£.Color} (e : X â‰ƒ Y) (h : c = ğ“£.Ï„ âˆ˜ d âˆ˜ e)
    (g : G) (x : ğ“£.Tensor c) (y : ğ“£.Tensor d) :
    ğ“£.contrAll e h ((g â€¢ x) âŠ—â‚œ[R] (g â€¢ y)) = ğ“£.contrAll e h (x âŠ—â‚œ[R] y) := by
  nth_rewrite 2 [â† contrAll_rep_apply]
  rfl

end GroupTensorStructure

end
