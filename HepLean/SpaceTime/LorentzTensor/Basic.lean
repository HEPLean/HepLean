/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.LinearAlgebra.PiTensorProduct
import Mathlib.LinearAlgebra.DFinsupp
/-!

# Structure of Tensors

This file sets up the structure `TensorStructure` which contains
data of types (or `colors`) of indices, the dual of colors, the associated module,
contraction between color modules, and the unit of contraction.

This structure is extended to `DualizeTensorStructure` which adds a metric to the tensor structure,
allowing a vector to be taken to its dual vector by contraction with a specified metric.
The definition of `DualizeTensorStructure` can be found in
`HepLean.SpaceTime.LorentzTensor.RisingLowering`.

The structure `DualizeTensorStructure` is further extended in
`HepLean.SpaceTime.LorentzTensor.LorentzTensorStruct` to add a group action on the tensor space,
under which contraction and rising and lowering etc, are invariant.

## References

-- For modular operads see: [Raynor][raynor2021graphical]

-/

open TensorProduct

variable {R : Type} [CommSemiring R]

/-- The index color data associated with a tensor structure.
    This corresponds to a type with an involution. -/
structure TensorColor where
  /-- The allowed colors of indices.
    For example for a real Lorentz tensor these are `{up, down}`. -/
  Color : Type
  /-- A map taking every color to its dual color. -/
  Ï„ : Color â†’ Color
  /-- The map `Ï„` is an involution. -/
  Ï„_involutive : Function.Involutive Ï„

namespace TensorColor

variable (ğ“’ : TensorColor) [Fintype ğ“’.Color] [DecidableEq ğ“’.Color]
variable {d : â„•} {X X' Y Y' Z W : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z] [Fintype W] [DecidableEq W]

/-- A relation on colors which is true if the two colors are equal or are duals. -/
def colorRel (Î¼ Î½ : ğ“’.Color) : Prop := Î¼ = Î½ âˆ¨ Î¼ = ğ“’.Ï„ Î½

instance : Decidable (colorRel ğ“’ Î¼ Î½) :=
  Or.decidable

/-- An equivalence relation on colors which is true if the two colors are equal or are duals. -/
lemma colorRel_equivalence : Equivalence ğ“’.colorRel where
  refl := by
    intro x
    left
    rfl
  symm := by
    intro x y h
    rcases h with h | h
    Â· left
      exact h.symm
    Â· right
      subst h
      exact (ğ“’.Ï„_involutive y).symm
  trans := by
    intro x y z hxy hyz
    rcases hxy with hxy | hxy <;>
      rcases hyz with hyz | hyz <;>
      subst hxy hyz
    Â· left
      rfl
    Â· right
      rfl
    Â· right
      rfl
    Â· left
      exact ğ“’.Ï„_involutive z

/-- The structure of a setoid on colors, two colors are related if they are equal,
  or dual. -/
instance colorSetoid : Setoid ğ“’.Color := âŸ¨ğ“’.colorRel, ğ“’.colorRel_equivalenceâŸ©

/-- A map taking a color to its equivalence class in `colorSetoid`. -/
def colorQuot (Î¼ : ğ“’.Color) : Quotient ğ“’.colorSetoid :=
  Quotient.mk ğ“’.colorSetoid Î¼

instance (Î¼ Î½ : ğ“’.Color) : Decidable (Î¼ â‰ˆ Î½) :=
  Or.decidable

instance : DecidableEq (Quotient ğ“’.colorSetoid) :=
  instDecidableEqQuotientOfDecidableEquiv

/-- The types of maps from an `X` to `ğ“’.Color`. -/
def ColorMap (X : Type) := X â†’ ğ“’.Color

namespace ColorMap

variable {ğ“’ : TensorColor} [Fintype ğ“’.Color] [DecidableEq ğ“’.Color]

variable (cX : ColorMap ğ“’ X) (cY : ColorMap ğ“’ Y) (cZ : ColorMap ğ“’ Z)

/-- A relation, given an equivalence of types, between ColorMap which is true
  if related by composition of the equivalence. -/
def MapIso (e : X â‰ƒ Y) (cX : ColorMap ğ“’ X) (cY : ColorMap ğ“’ Y) : Prop := cX = cY âˆ˜ e

/-- The sum of two color maps, formed by `Sum.elim`. -/
def sum (cX : ColorMap ğ“’ X) (cY : ColorMap ğ“’ Y) : ColorMap ğ“’ (Sum X Y) :=
  Sum.elim cX cY

/-- The dual of a color map, formed by composition with `ğ“’.Ï„`. -/
def dual (cX : ColorMap ğ“’ X) : ColorMap ğ“’ X := ğ“’.Ï„ âˆ˜ cX

namespace MapIso

variable {e : X â‰ƒ Y} {e' : Y â‰ƒ Z} {cX : ColorMap ğ“’ X} {cY : ColorMap ğ“’ Y} {cZ : ColorMap ğ“’ Z}
variable {cX' : ColorMap ğ“’ X'} {cY' : ColorMap ğ“’ Y'}

lemma symm (h : cX.MapIso e cY) : cY.MapIso e.symm cX := by
  rw [MapIso] at h
  exact (Equiv.eq_comp_symm e cY cX).mpr h.symm

lemma symm' : cX.MapIso e cY â†” cY.MapIso e.symm cX := by
  refine âŸ¨symm, symmâŸ©

lemma trans (h : cX.MapIso e cY) (h' : cY.MapIso e' cZ) :
    cX.MapIso (e.trans e') cZ:= by
  funext a
  subst h h'
  rfl

lemma sum {eX : X â‰ƒ X'} {eY : Y â‰ƒ Y'} (hX : cX.MapIso eX cX') (hY : cY.MapIso eY cY') :
    (cX.sum cY).MapIso (eX.sumCongr eY) (cX'.sum cY') := by
  funext x
  subst hX hY
  match x with
  | Sum.inl x => rfl
  | Sum.inr x => rfl

lemma dual {e : X â‰ƒ Y} (h : cX.MapIso e cY) :
    cX.dual.MapIso e cY.dual := by
  subst h
  rfl

end MapIso

end ColorMap

end TensorColor

noncomputable section
namespace TensorStructure

/-- An auxillary function to contract the vector space `V1` and `V2` in `V1 âŠ—[R] V2 âŠ—[R] V3`. -/
def contrLeftAux {V1 V2 V3 : Type} [AddCommMonoid V1] [AddCommMonoid V2] [AddCommMonoid V3]
    [Module R V1] [Module R V2] [Module R V3] (f : V1 âŠ—[R] V2 â†’â‚—[R] R) :
    V1 âŠ—[R] V2 âŠ—[R] V3 â†’â‚—[R] V3 :=
  (TensorProduct.lid R _).toLinearMap âˆ˜â‚—
  TensorProduct.map (f) (LinearEquiv.refl R V3).toLinearMap
  âˆ˜â‚— (TensorProduct.assoc R _ _ _).symm.toLinearMap

/-- An auxillary function to contract the vector space `V1` and `V2` in `(V3 âŠ—[R] V1) âŠ—[R] V2`. -/
def contrRightAux {V1 V2 V3 : Type} [AddCommMonoid V1] [AddCommMonoid V2] [AddCommMonoid V3]
    [Module R V1] [Module R V2] [Module R V3] (f : V1 âŠ—[R] V2 â†’â‚—[R] R) :
    (V3 âŠ—[R] V1) âŠ—[R] V2 â†’â‚—[R] V3 :=
  (TensorProduct.rid R _).toLinearMap âˆ˜â‚—
  TensorProduct.map (LinearEquiv.refl R V3).toLinearMap f âˆ˜â‚—
  (TensorProduct.assoc R _ _ _).toLinearMap

/-- An auxillary function to contract the vector space `V1` and `V2` in
  `V4 âŠ—[R] V1 âŠ—[R] V2 âŠ—[R] V3`. -/
def contrMidAux {V1 V2 V3 V4 : Type} [AddCommMonoid V1] [AddCommMonoid V2] [AddCommMonoid V3]
    [AddCommMonoid V4] [Module R V1] [Module R V2] [Module R V3] [Module R V4]
    (f : V1 âŠ—[R] V2 â†’â‚—[R] R) : (V4 âŠ—[R] V1) âŠ—[R] (V2 âŠ—[R] V3) â†’â‚—[R] V4 âŠ—[R] V3 :=
  (TensorProduct.map (LinearEquiv.refl R V4).toLinearMap (contrLeftAux f)) âˆ˜â‚—
  (TensorProduct.assoc R _ _ _).toLinearMap

lemma contrRightAux_comp {V1 V2 V3 V4 V5 : Type} [AddCommMonoid V1] [AddCommMonoid V2]
    [AddCommMonoid V3] [AddCommMonoid V4] [AddCommMonoid V5] [Module R V1] [Module R V3]
    [Module R V2] [Module R V4] [Module R V5] (f : V2 âŠ—[R] V3 â†’â‚—[R] R) (g : V4 âŠ—[R] V5 â†’â‚—[R] R) :
    (contrRightAux f âˆ˜â‚— TensorProduct.map (LinearMap.id : V1 âŠ—[R] V2 â†’â‚—[R] V1 âŠ—[R] V2)
      (contrRightAux g)) =
    (contrRightAux g) âˆ˜â‚— TensorProduct.map (contrMidAux f) LinearMap.id
    âˆ˜â‚— (TensorProduct.assoc R _ _ _).symm.toLinearMap := by
  apply TensorProduct.ext'
  intro x y
  refine TensorProduct.induction_on x (by simp) ?_ (fun x z h1 h2 =>
    by simp [add_tmul, LinearMap.map_add, h1, h2])
  intro x1 x2
  refine TensorProduct.induction_on y (by simp) ?_ (fun x z h1 h2 =>
    by simp [add_tmul, tmul_add, LinearMap.map_add, h1, h2])
  intro y x5
  refine TensorProduct.induction_on y (by simp) ?_ (fun x z h1 h2 =>
    by simp [add_tmul, tmul_add, LinearMap.map_add, h1, h2])
  intro x3 x4
  simp [contrRightAux, contrMidAux, contrLeftAux]
  rfl

end TensorStructure

/-- An initial structure specifying a tensor system (e.g. a system in which you can
  define real Lorentz tensors or Einstein notation convention). -/
structure TensorStructure (R : Type) [CommSemiring R] extends TensorColor where
  /-- To each color we associate a module. -/
  ColorModule : Color â†’ Type
  /-- Each `ColorModule` has the structure of an additive commutative monoid. -/
  colorModule_addCommMonoid : âˆ€ Î¼, AddCommMonoid (ColorModule Î¼)
  /-- Each `ColorModule` has the structure of a module over `R`. -/
  colorModule_module : âˆ€ Î¼, Module R (ColorModule Î¼)
  /-- The contraction of a vector with a vector with dual color. -/
  contrDual : âˆ€ Î¼, ColorModule Î¼ âŠ—[R] ColorModule (Ï„ Î¼) â†’â‚—[R] R
  /-- The contraction is symmetric. -/
  contrDual_symm : âˆ€ Î¼ x y, (contrDual Î¼) (x âŠ—â‚œ[R] y) =
    (contrDual (Ï„ Î¼)) (y âŠ—â‚œ[R] (Equiv.cast (congrArg ColorModule (Ï„_involutive Î¼).symm) x))
  /-- The unit of the contraction. -/
  unit : (Î¼ : Color) â†’ ColorModule (Ï„ Î¼) âŠ—[R] ColorModule Î¼
  /-- The unit is a right identity. -/
  unit_rid : âˆ€ Î¼ (x : ColorModule Î¼),
    TensorStructure.contrLeftAux (contrDual Î¼) (x âŠ—â‚œ[R] unit Î¼) = x
  /-- The metric for a given color. -/
  metric : (Î¼ : Color) â†’ ColorModule Î¼ âŠ—[R] ColorModule Î¼
  /-- The metric contracted with its dual is the unit. -/
  metric_dual : âˆ€ (Î¼ : Color), (TensorStructure.contrMidAux (contrDual Î¼)
    (metric Î¼ âŠ—â‚œ[R] metric (Ï„ Î¼))) = TensorProduct.comm _ _ _ (unit Î¼)

namespace TensorStructure

variable (ğ“£ : TensorStructure R)

variable {d : â„•} {X Y Y' Z W : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z] [Fintype W] [DecidableEq W]
  {cX cX2 : ğ“£.ColorMap X} {cY : ğ“£.ColorMap Y} {cZ : ğ“£.ColorMap Z}
  {cW : ğ“£.ColorMap W} {cY' : ğ“£.ColorMap Y'} {Î¼ Î½ Î· : ğ“£.Color}

instance : AddCommMonoid (ğ“£.ColorModule Î¼) := ğ“£.colorModule_addCommMonoid Î¼

instance : Module R (ğ“£.ColorModule Î¼) := ğ“£.colorModule_module Î¼

/-- The type of tensors given a map from an indexing set `X` to the type of colors,
  specifying the color of that index. -/
def Tensor (c : ğ“£.ColorMap X) : Type := â¨‚[R] x, ğ“£.ColorModule (c x)

instance : AddCommMonoid (ğ“£.Tensor cX) :=
  PiTensorProduct.instAddCommMonoid fun i => ğ“£.ColorModule (cX i)

instance : Module R (ğ“£.Tensor cX) := PiTensorProduct.instModule

/-!

## Color

Recall the `color` of an index describes the type of the index.

For example, in a real Lorentz tensor the colors are `{up, down}`.

-/

/-- Equivalence of `ColorModule` given an equality of colors. -/
def colorModuleCast (h : Î¼ = Î½) : ğ“£.ColorModule Î¼ â‰ƒâ‚—[R] ğ“£.ColorModule Î½ where
  toFun := Equiv.cast (congrArg ğ“£.ColorModule h)
  invFun := (Equiv.cast (congrArg ğ“£.ColorModule h)).symm
  map_add' x y := by
    subst h
    rfl
  map_smul' x y := by
    subst h
    rfl
  left_inv x := Equiv.symm_apply_apply (Equiv.cast (congrArg ğ“£.ColorModule h)) x
  right_inv x := Equiv.apply_symm_apply (Equiv.cast (congrArg ğ“£.ColorModule h)) x

lemma tensorProd_piTensorProd_ext {M : Type} [AddCommMonoid M] [Module R M]
    {f g : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY â†’â‚—[R] M}
    (h : âˆ€ p q, f (PiTensorProduct.tprod R p âŠ—â‚œ[R] PiTensorProduct.tprod R q)
    = g (PiTensorProduct.tprod R p âŠ—â‚œ[R] PiTensorProduct.tprod R q)) : f = g := by
  apply TensorProduct.ext'
  refine fun x â†¦
  PiTensorProduct.induction_on' x ?_ (by
    intro a b hx hy y
    simp [map_add, add_tmul, hx, hy])
  intro rx fx
  refine fun y â†¦
    PiTensorProduct.induction_on' y ?_ (by
      intro a b hx hy
      simp at hx hy
      simp [map_add, tmul_add, hx, hy])
  intro ry fy
  simp only [PiTensorProduct.tprodCoeff_eq_smul_tprod, tmul_smul, LinearMapClass.map_smul]
  apply congrArg
  simp only [smul_tmul, tmul_smul, LinearMapClass.map_smul]
  exact congrArg (HSMul.hSMul rx) (h fx fy)

/-!

## Mapping isomorphisms

-/

/-- An linear equivalence of tensor spaces given a color-preserving equivalence of indexing sets. -/
def mapIso {c : ğ“£.ColorMap X} {d : ğ“£.ColorMap Y} (e : X â‰ƒ Y) (h : c.MapIso e d) :
    ğ“£.Tensor c â‰ƒâ‚—[R] ğ“£.Tensor d :=
  (PiTensorProduct.reindex R _ e) â‰ªâ‰«â‚—
  (PiTensorProduct.congr (fun y => ğ“£.colorModuleCast (by rw [h]; simp)))

lemma mapIso_ext {c : ğ“£.ColorMap X} {d : ğ“£.ColorMap Y} (e e' : X â‰ƒ Y) (h : c.MapIso e d)
    (h' : c.MapIso e' d) (he : e = e') : ğ“£.mapIso e h = ğ“£.mapIso e' h' := by
  simp [he]

@[simp]
lemma mapIso_trans (e : X â‰ƒ Y) (e' : Y â‰ƒ Z) (h : cX.MapIso e cY) (h' : cY.MapIso e' cZ) :
    (ğ“£.mapIso e h â‰ªâ‰«â‚— ğ“£.mapIso e' h') = ğ“£.mapIso (e.trans e') (h.trans h') := by
  refine LinearEquiv.toLinearMap_inj.mp ?_
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [mapIso, LinearMap.compMultilinearMap_apply, LinearEquiv.coe_coe,
    LinearEquiv.trans_apply, PiTensorProduct.reindex_tprod, Equiv.symm_trans_apply]
  change (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cY x)) e')
    ((PiTensorProduct.congr fun y => ğ“£.colorModuleCast _) _)) =
    (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cX x)) (e.trans e')) _)
  rw [PiTensorProduct.congr_tprod, PiTensorProduct.reindex_tprod,
    PiTensorProduct.congr_tprod, PiTensorProduct.reindex_tprod, PiTensorProduct.congr]
  simp [colorModuleCast]

@[simp]
lemma mapIso_mapIso (e : X â‰ƒ Y) (e' : Y â‰ƒ Z) (h : cX.MapIso e cY) (h' : cY.MapIso e' cZ)
    (T : ğ“£.Tensor cX) :
    (ğ“£.mapIso e' h') (ğ“£.mapIso e h T) = ğ“£.mapIso (e.trans e') (h.trans h') T := by
  rw [â† LinearEquiv.trans_apply, mapIso_trans]

@[simp]
lemma mapIso_symm (e : X â‰ƒ Y) (h : cX.MapIso e cY) :
    (ğ“£.mapIso e h).symm = ğ“£.mapIso e.symm (h.symm) := by
  refine LinearEquiv.toLinearMap_inj.mp ?_
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp [mapIso, LinearMap.compMultilinearMap_apply, LinearEquiv.coe_coe,
    LinearEquiv.symm_apply_apply, PiTensorProduct.reindex_tprod]
  change (PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cX x)) e).symm
    ((PiTensorProduct.congr fun y => ğ“£.colorModuleCast _).symm ((PiTensorProduct.tprod R) x)) =
    (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R (fun x => ğ“£.ColorModule (cY x)) e.symm)
    ((PiTensorProduct.tprod R) x))
  rw [PiTensorProduct.reindex_tprod, PiTensorProduct.congr_tprod, PiTensorProduct.congr_symm_tprod,
    LinearEquiv.symm_apply_eq, PiTensorProduct.reindex_tprod]
  apply congrArg
  funext i
  simp only [colorModuleCast, Equiv.cast_symm, LinearEquiv.coe_symm_mk,
    Equiv.symm_symm_apply, LinearEquiv.coe_mk]
  rw [â† Equiv.symm_apply_eq]
  simp only [Equiv.cast_symm, Equiv.cast_apply, cast_cast]
  apply cast_eq_iff_heq.mpr
  rw [Equiv.apply_symm_apply]

@[simp]
lemma mapIso_refl : ğ“£.mapIso (Equiv.refl X) (rfl : cX = cX) = LinearEquiv.refl R _ := by
  refine LinearEquiv.toLinearMap_inj.mp ?_
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [mapIso, Equiv.refl_symm, Equiv.refl_apply, PiTensorProduct.reindex_refl,
    LinearMap.compMultilinearMap_apply, LinearEquiv.coe_coe, LinearEquiv.trans_apply,
    LinearEquiv.refl_apply, LinearEquiv.refl_toLinearMap, LinearMap.id, LinearMap.coe_mk,
    AddHom.coe_mk, id_eq]
  change (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _) ((PiTensorProduct.tprod R) x) = _
  rw [PiTensorProduct.congr_tprod]
  rfl

@[simp]
lemma mapIso_tprod {c : ğ“£.ColorMap X} {d : ğ“£.ColorMap Y} (e : X â‰ƒ Y) (h : c.MapIso e d)
    (f : (i : X) â†’ ğ“£.ColorModule (c i)) : (ğ“£.mapIso e h) (PiTensorProduct.tprod R f) =
    (PiTensorProduct.tprod R (fun i => ğ“£.colorModuleCast (by rw [h]; simp) (f (e.symm i)))) := by
  simp only [mapIso, LinearEquiv.trans_apply]
  change (PiTensorProduct.congr fun y => ğ“£.colorModuleCast _)
    ((PiTensorProduct.reindex R _ e) ((PiTensorProduct.tprod R) f)) = _
  rw [PiTensorProduct.reindex_tprod]
  exact PiTensorProduct.congr_tprod (fun y => ğ“£.colorModuleCast _) fun i => f (e.symm i)

/-!

## Pure tensors

This section is needed since: `PiTensorProduct.tmulEquiv` is not defined for dependent types.
Hence we need to construct a version of it here.

-/

/-- The type of pure tensors, i.e. of the form `v1 âŠ— v2 âŠ— v3 âŠ— ...`. -/
abbrev PureTensor (c : X â†’ ğ“£.Color) := (x : X) â†’ ğ“£.ColorModule (c x)

/-- A pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` constructed from a pure tensor
  in `ğ“£.PureTensor cX` and a pure tensor in `ğ“£.PureTensor cY`. -/
def elimPureTensor (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY) : ğ“£.PureTensor (Sum.elim cX cY) :=
  fun x =>
    match x with
    | Sum.inl x => p x
    | Sum.inr x => q x

@[simp]
lemma elimPureTensor_update_right (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY)
    (y : Y) (r : ğ“£.ColorModule (cY y)) : ğ“£.elimPureTensor p (Function.update q y r) =
    Function.update (ğ“£.elimPureTensor p q) (Sum.inr y) r := by
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x =>
    change Function.update q y r x = _
    simp only [Function.update, Sum.inr.injEq, Sum.elim_inr]
    split_ifs
    Â· rename_i h
      subst h
      simp_all only
    Â· rfl

@[simp]
lemma elimPureTensor_update_left (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY)
    (x : X) (r : ğ“£.ColorModule (cX x)) : ğ“£.elimPureTensor (Function.update p x r) q =
    Function.update (ğ“£.elimPureTensor p q) (Sum.inl x) r := by
  funext y
  match y with
  | Sum.inl y =>
    change (Function.update p x r) y = _
    simp only [Function.update, Sum.inl.injEq, Sum.elim_inl]
    split_ifs
    Â· rename_i h
      subst h
      rfl
    Â· rfl
  | Sum.inr y => rfl

/-- The projection of a pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` onto a pure tensor in
  `ğ“£.PureTensor cX`. -/
def inlPureTensor (p : ğ“£.PureTensor (Sum.elim cX cY)) : ğ“£.PureTensor cX := fun x => p (Sum.inl x)

/-- The projection of a pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` onto a pure tensor in
  `ğ“£.PureTensor cY`. -/
def inrPureTensor (p : ğ“£.PureTensor (Sum.elim cX cY)) : ğ“£.PureTensor cY := fun y => p (Sum.inr y)

@[simp]
lemma inlPureTensor_update_left [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (x : X)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inl x))) :
    ğ“£.inlPureTensor (Function.update f (Sum.inl x) v1) =
    Function.update (ğ“£.inlPureTensor f) x v1 := by
  funext y
  simp [inlPureTensor, Function.update, Sum.inl.injEq, Sum.elim_inl]
  split
  Â· rename_i h
    subst h
    rfl
  Â· rfl

@[simp]
lemma inrPureTensor_update_left [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (x : X)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inl x))) :
    ğ“£.inrPureTensor (Function.update f (Sum.inl x) v1) = (ğ“£.inrPureTensor f) := by
  funext x
  simp [inrPureTensor, Function.update]

@[simp]
lemma inrPureTensor_update_right [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (y : Y)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inr y))) :
    ğ“£.inrPureTensor (Function.update f (Sum.inr y) v1) =
    Function.update (ğ“£.inrPureTensor f) y v1 := by
  funext y
  simp [inrPureTensor, Function.update, Sum.inl.injEq, Sum.elim_inl]
  split
  Â· rename_i h
    subst h
    rfl
  Â· rfl

@[simp]
lemma inlPureTensor_update_right [DecidableEq (X âŠ• Y)] (f : ğ“£.PureTensor (Sum.elim cX cY)) (y : Y)
    (v1 : ğ“£.ColorModule (Sum.elim cX cY (Sum.inr y))) :
    ğ“£.inlPureTensor (Function.update f (Sum.inr y) v1) = (ğ“£.inlPureTensor f) := by
  funext x
  simp [inlPureTensor, Function.update]

/-- The multilinear map taking pure tensors a `ğ“£.PureTensor cX` and a pure tensor in
  `ğ“£.PureTensor cY`, and constructing a tensor in `ğ“£.Tensor (Sum.elim cX cY))`. -/
def elimPureTensorMulLin : MultilinearMap R (fun i => ğ“£.ColorModule (cX i))
    (MultilinearMap R (fun x => ğ“£.ColorModule (cY x)) (ğ“£.Tensor (Sum.elim cX cY))) where
  toFun p := {
    toFun := fun q => PiTensorProduct.tprod R (ğ“£.elimPureTensor p q)
    map_add' := fun m x v1 v2 => by
      simp [Sum.elim_inl, Sum.elim_inr]
    map_smul' := fun m x r v => by
      simp [Sum.elim_inl, Sum.elim_inr]}
  map_add' p x v1 v2 := by
    apply MultilinearMap.ext
    intro y
    simp
  map_smul' p x r v := by
    apply MultilinearMap.ext
    intro y
    simp

/-!

## tensorator

-/

/-! TODO: Replace with dependent type version of `MultilinearMap.domCoprod` when in Mathlib. -/
/-- The multi-linear map taking a pure tensor in `ğ“£.PureTensor (Sum.elim cX cY)` and constructing
  a vector in `ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY`. -/
def domCoprod : MultilinearMap R (fun x => ğ“£.ColorModule (Sum.elim cX cY x))
    (ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY) where
  toFun f := (PiTensorProduct.tprod R (ğ“£.inlPureTensor f)) âŠ—â‚œ
    (PiTensorProduct.tprod R (ğ“£.inrPureTensor f))
  map_add' f xy v1 v2:= by
    match xy with
    | Sum.inl x => simp [â† TensorProduct.add_tmul]
    | Sum.inr y => simp [â† TensorProduct.tmul_add]
  map_smul' f xy r p := by
    match xy with
    | Sum.inl x => simp [TensorProduct.tmul_smul, TensorProduct.smul_tmul]
    | Sum.inr y => simp [TensorProduct.tmul_smul, TensorProduct.smul_tmul]

/-- The linear map combining two tensors into a single tensor
  via the tensor product i.e. `v1 v2 â†¦ v1 âŠ— v2`. -/
def tensoratorSymm : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY â†’â‚—[R] ğ“£.Tensor (Sum.elim cX cY) := by
  refine TensorProduct.lift {
    toFun := fun a â†¦
      PiTensorProduct.lift <|
          PiTensorProduct.lift (ğ“£.elimPureTensorMulLin) a,
    map_add' := fun a b â†¦ by simp
    map_smul' := fun r a â†¦ by simp}

/-! TODO: Replace with dependent type version of `PiTensorProduct.tmulEquiv` when in Mathlib. -/
/-- Splitting a tensor in `ğ“£.Tensor (Sum.elim cX cY)` into the tensor product of two tensors. -/
def tensorator : ğ“£.Tensor (Sum.elim cX cY) â†’â‚—[R] ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY :=
  PiTensorProduct.lift ğ“£.domCoprod

/-- An equivalence formed by taking the tensor product of tensors. -/
def tensoratorEquiv (c : X â†’ ğ“£.Color) (d : Y â†’ ğ“£.Color) :
    ğ“£.Tensor c âŠ—[R] ğ“£.Tensor d â‰ƒâ‚—[R] ğ“£.Tensor (Sum.elim c d) :=
  LinearEquiv.ofLinear (ğ“£.tensoratorSymm) (ğ“£.tensorator)
  (by
    apply PiTensorProduct.ext
    apply MultilinearMap.ext
    intro p
    simp [tensorator, tensoratorSymm, domCoprod]
    change (PiTensorProduct.lift _) ((PiTensorProduct.tprod R) _) =
      LinearMap.id ((PiTensorProduct.tprod R) p)
    rw [PiTensorProduct.lift.tprod]
    simp [elimPureTensorMulLin, elimPureTensor]
    change (PiTensorProduct.tprod R) _ = _
    apply congrArg
    funext x
    match x with
    | Sum.inl x => rfl
    | Sum.inr x => rfl)
  (by
    apply tensorProd_piTensorProd_ext
    intro p q
    simp [tensorator, tensoratorSymm]
    change (PiTensorProduct.lift ğ“£.domCoprod)
      ((PiTensorProduct.lift (ğ“£.elimPureTensorMulLin p)) ((PiTensorProduct.tprod R) q)) =_
    rw [PiTensorProduct.lift.tprod]
    simp [elimPureTensorMulLin]
    rfl)

@[simp]
lemma tensoratorEquiv_tmul_tprod (p : ğ“£.PureTensor cX) (q : ğ“£.PureTensor cY) :
    (ğ“£.tensoratorEquiv cX cY) ((PiTensorProduct.tprod R) p âŠ—â‚œ[R] (PiTensorProduct.tprod R) q) =
    (PiTensorProduct.tprod R) (ğ“£.elimPureTensor p q) := by
  simp only [tensoratorEquiv, tensoratorSymm, tensorator, domCoprod, LinearEquiv.ofLinear_apply,
    lift.tmul, LinearMap.coe_mk, AddHom.coe_mk, PiTensorProduct.lift.tprod]
  exact PiTensorProduct.lift.tprod q

@[simp]
lemma tensoratorEquiv_symm_tprod (f : ğ“£.PureTensor (Sum.elim cX cY)) :
    (ğ“£.tensoratorEquiv cX cY).symm ((PiTensorProduct.tprod R) f) =
    (PiTensorProduct.tprod R) (ğ“£.inlPureTensor f) âŠ—â‚œ[R]
    (PiTensorProduct.tprod R) (ğ“£.inrPureTensor f) := by
  simp [tensoratorEquiv, tensorator]
  change (PiTensorProduct.lift ğ“£.domCoprod) ((PiTensorProduct.tprod R) f) = _
  simp [domCoprod]

@[simp]
lemma tensoratorEquiv_mapIso (e' : Z â‰ƒ Y) (e'' : W â‰ƒ X)
    (h' : cZ.MapIso e' cY) (h'' : cW.MapIso e'' cX) :
    (TensorProduct.congr (ğ“£.mapIso e'' h'') (ğ“£.mapIso e' h')) â‰ªâ‰«â‚— (ğ“£.tensoratorEquiv cX cY)
    = (ğ“£.tensoratorEquiv cW cZ) â‰ªâ‰«â‚— (ğ“£.mapIso (Equiv.sumCongr e'' e') (h''.sum h')) := by
  apply LinearEquiv.toLinearMap_inj.mp
  apply tensorProd_piTensorProd_ext
  intro p q
  simp only [LinearEquiv.coe_coe, LinearEquiv.trans_apply, congr_tmul, mapIso_tprod,
    tensoratorEquiv_tmul_tprod]
  erw [LinearEquiv.trans_apply]
  simp only [tensoratorEquiv_tmul_tprod, mapIso_tprod, Equiv.sumCongr_symm, Equiv.sumCongr_apply]
  apply congrArg
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x => rfl

@[simp]
lemma tensoratorEquiv_mapIso_apply (e' : Z â‰ƒ Y) (e'' : W â‰ƒ X)
    (h' : cZ.MapIso e' cY) (h'' : cW.MapIso e'' cX)
    (x : ğ“£.Tensor cW âŠ—[R] ğ“£.Tensor cZ) :
    (ğ“£.tensoratorEquiv cX cY) ((TensorProduct.congr (ğ“£.mapIso e'' h'') (ğ“£.mapIso e' h')) x) =
    (ğ“£.mapIso (Equiv.sumCongr e'' e') (h''.sum h'))
    ((ğ“£.tensoratorEquiv cW cZ) x) := by
  trans ((TensorProduct.congr (ğ“£.mapIso e'' h'') (ğ“£.mapIso e' h')) â‰ªâ‰«â‚—
    (ğ“£.tensoratorEquiv cX cY)) x
  Â· rfl
  Â· rw [tensoratorEquiv_mapIso]
    rfl

lemma tensoratorEquiv_mapIso_tmul (e' : Z â‰ƒ Y) (e'' : W â‰ƒ X)
    (h' : cZ.MapIso e' cY) (h'' : cW.MapIso e'' cX)
    (x : ğ“£.Tensor cW) (y : ğ“£.Tensor cZ) :
    (ğ“£.tensoratorEquiv cX cY) ((ğ“£.mapIso e'' h'' x) âŠ—â‚œ[R] (ğ“£.mapIso e' h' y)) =
    (ğ“£.mapIso (Equiv.sumCongr e'' e') (h''.sum h'))
    ((ğ“£.tensoratorEquiv cW cZ) (x âŠ—â‚œ y)) := by
  rw [â† tensoratorEquiv_mapIso_apply]
  rfl

/-!

## contrDual properties

-/

lemma contrDual_cast (h : Î¼ = Î½) (x : ğ“£.ColorModule Î¼) (y : ğ“£.ColorModule (ğ“£.Ï„ Î¼)) :
    ğ“£.contrDual Î¼ (x âŠ—â‚œ[R] y) = ğ“£.contrDual Î½ (ğ“£.colorModuleCast h x âŠ—â‚œ[R]
      ğ“£.colorModuleCast (congrArg ğ“£.Ï„ h) y) := by
  subst h
  rfl

/-- `ğ“£.contrDual (ğ“£.Ï„ Î¼)` in terms of `ğ“£.contrDual Î¼`. -/
@[simp]
lemma contrDual_symm' (Î¼ : ğ“£.Color) (x : ğ“£.ColorModule (ğ“£.Ï„ Î¼))
    (y : ğ“£.ColorModule (ğ“£.Ï„ (ğ“£.Ï„ Î¼))) : ğ“£.contrDual (ğ“£.Ï„ Î¼) (x âŠ—â‚œ[R] y) =
    (ğ“£.contrDual Î¼) ((ğ“£.colorModuleCast (ğ“£.Ï„_involutive Î¼) y) âŠ—â‚œ[R] x) := by
  rw [ğ“£.contrDual_symm, ğ“£.contrDual_cast (ğ“£.Ï„_involutive Î¼)]
  congr
  exact (LinearEquiv.eq_symm_apply (ğ“£.colorModuleCast (congrArg ğ“£.Ï„ (ğ“£.Ï„_involutive Î¼)))).mp rfl

lemma contrDual_symm_contrRightAux (h : Î½ = Î·) :
    (ğ“£.colorModuleCast h) âˆ˜â‚— contrRightAux (ğ“£.contrDual Î¼) =
    contrRightAux (ğ“£.contrDual (ğ“£.Ï„ (ğ“£.Ï„ Î¼))) âˆ˜â‚—
    (TensorProduct.congr (
      TensorProduct.congr (ğ“£.colorModuleCast h) (ğ“£.colorModuleCast (ğ“£.Ï„_involutive Î¼).symm))
    (ğ“£.colorModuleCast ((ğ“£.Ï„_involutive (ğ“£.Ï„ Î¼)).symm))).toLinearMap := by
  apply TensorProduct.ext'
  intro x y
  refine TensorProduct.induction_on x (by simp) ?_ ?_
  Â· intro x z
    simp [contrRightAux]
    congr
    Â· exact (LinearEquiv.symm_apply_eq (ğ“£.colorModuleCast (ğ“£.Ï„_involutive Î¼))).mp rfl
    Â· exact (LinearEquiv.symm_apply_eq (ğ“£.colorModuleCast (ğ“£.Ï„_involutive (ğ“£.Ï„ Î¼)))).mp rfl
  Â· intro x z h1 h2
    simp [add_tmul, LinearMap.map_add, h1, h2]

lemma contrDual_symm_contrRightAux_apply_tmul (h : Î½ = Î·)
    (m : ğ“£.ColorModule Î½ âŠ—[R] ğ“£.ColorModule Î¼) (x : ğ“£.ColorModule (ğ“£.Ï„ Î¼)) :
    ğ“£.colorModuleCast h (contrRightAux (ğ“£.contrDual Î¼) (m âŠ—â‚œ[R] x)) =
    contrRightAux (ğ“£.contrDual (ğ“£.Ï„ (ğ“£.Ï„ Î¼))) ((TensorProduct.congr
    (ğ“£.colorModuleCast h) (ğ“£.colorModuleCast (ğ“£.Ï„_involutive Î¼).symm) (m)) âŠ—â‚œ
    (ğ“£.colorModuleCast (ğ“£.Ï„_involutive (ğ“£.Ï„ Î¼)).symm x)) := by
  trans ((ğ“£.colorModuleCast h) âˆ˜â‚— contrRightAux (ğ“£.contrDual Î¼)) (m âŠ—â‚œ[R] x)
  Â· rfl
  Â· rw [contrDual_symm_contrRightAux]
    rfl

/-!

## Of empty

-/

/-- The equivalence between `ğ“£.Tensor cX` and `R` when the indexing set `X` is empty. -/
def isEmptyEquiv [IsEmpty X] : ğ“£.Tensor cX â‰ƒâ‚—[R] R :=
  PiTensorProduct.isEmptyEquiv X

@[simp]
lemma isEmptyEquiv_tprod [IsEmpty X] (f : ğ“£.PureTensor cX) :
    ğ“£.isEmptyEquiv (PiTensorProduct.tprod R f) = 1 := by
  simp only [isEmptyEquiv]
  erw [PiTensorProduct.isEmptyEquiv_apply_tprod]
/-!

## Splitting tensors into tensor products

-/
/-! TODO: Delete the content of this section. -/

/-- The decomposition of a set into a direct sum based on the image of an injection. -/
def decompEmbedSet (f : Y â†ª X) :
    X â‰ƒ {x // x âˆˆ (Finset.image f Finset.univ)á¶œ} âŠ• Y :=
  (Equiv.Set.sumCompl (Set.range â‡‘f)).symm.trans <|
  (Equiv.sumComm _ _).trans <|
  Equiv.sumCongr ((Equiv.subtypeEquivRight (by simp))) <|
  (Function.Embedding.toEquivRange f).symm

/-- The restriction of a map from an indexing set to the space to the complement of the image
  of an embedding. -/
def decompEmbedColorLeft (c : X â†’ ğ“£.Color) (f : Y â†ª X) :
    {x // x âˆˆ (Finset.image f Finset.univ)á¶œ} â†’ ğ“£.Color :=
  (c âˆ˜ (decompEmbedSet f).symm) âˆ˜ Sum.inl

/-- The restriction of a map from an indexing set to the space to the image
  of an embedding. -/
def decompEmbedColorRight (c : X â†’ ğ“£.Color) (f : Y â†ª X) : Y â†’ ğ“£.Color :=
  (c âˆ˜ (decompEmbedSet f).symm) âˆ˜ Sum.inr

lemma decompEmbed_cond (c : X â†’ ğ“£.Color) (f : Y â†ª X) : c =
    (Sum.elim (ğ“£.decompEmbedColorLeft c f) (ğ“£.decompEmbedColorRight c f)) âˆ˜ decompEmbedSet f := by
  simpa [decompEmbedColorLeft, decompEmbedColorRight] using (Equiv.comp_symm_eq _ _ _).mp rfl

/-- Decomposes a tensor into a tensor product of two tensors
  one which has indices in the image of the given embedding, and the other has indices in
  the complement of that image. -/
def decompEmbed (f : Y â†ª X) :
    ğ“£.Tensor cX â‰ƒâ‚—[R] ğ“£.Tensor (ğ“£.decompEmbedColorLeft cX f) âŠ—[R] ğ“£.Tensor (cX âˆ˜ f) :=
  (ğ“£.mapIso (decompEmbedSet f) (ğ“£.decompEmbed_cond cX f)) â‰ªâ‰«â‚—
  (ğ“£.tensoratorEquiv (ğ“£.decompEmbedColorLeft cX f) (ğ“£.decompEmbedColorRight cX f)).symm

end TensorStructure

end
