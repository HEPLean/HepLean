/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.LorentzTensor.Basic
import Mathlib.RepresentationTheory.Basic
/-!

# Group actions on tensor structures

-/

noncomputable section

open TensorProduct

variable {R : Type} [CommSemiring R]

/-! TODO: Add preservation of the unit, and the metric. -/

/-- A multiplicative action on a tensor structure (e.g. the action of the Lorentz
  group on real Lorentz tensors). -/
class MulActionTensor (G : Type) [Monoid G] (ğ“£ : TensorStructure R) where
  /-- For each color `Î¼` a representation of `G` on `ColorModule Î¼`. -/
  repColorModule : (Î¼ : ğ“£.Color) â†’ Representation R G (ğ“£.ColorModule Î¼)
  /-- The contraction of a vector with its dual is invariant under the group action. -/
  contrDual_inv : âˆ€ Î¼ g, ğ“£.contrDual Î¼ âˆ˜â‚—
    TensorProduct.map (repColorModule Î¼ g) (repColorModule (ğ“£.Ï„ Î¼) g) = ğ“£.contrDual Î¼
  /-- The invariance of the metric under the group action. -/
  metric_inv : âˆ€ Î¼ g, (TensorProduct.map (repColorModule Î¼ g) (repColorModule Î¼ g)) (ğ“£.metric Î¼) =
    ğ“£.metric Î¼

namespace MulActionTensor

variable {G H : Type} [Group G] [Group H]

variable (ğ“£ : TensorStructure R) [MulActionTensor G ğ“£]

variable {d : â„•} {X Y Y' Z : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z]
  {cX cX2 : X â†’ ğ“£.Color} {cY : Y â†’ ğ“£.Color} {cZ : Z â†’ ğ“£.Color} {cY' : Y' â†’ ğ“£.Color} {Î¼ Î½: ğ“£.Color}

/-!

# Instances of `MulActionTensor`

-/

/-- The `MulActionTensor` defined by restriction along a group homomorphism. -/
def compHom (f : H â†’* G) : MulActionTensor H ğ“£ where
  repColorModule Î¼ := MonoidHom.comp (repColorModule Î¼) f
  contrDual_inv Î¼ h := by
    simp only [MonoidHom.coe_comp, Function.comp_apply]
    rw [contrDual_inv]
  metric_inv Î¼ h := by
    simp only [MonoidHom.coe_comp, Function.comp_apply]
    rw [metric_inv]

/-- The trivial `MulActionTensor` defined via trivial representations. -/
def trivial : MulActionTensor G ğ“£ where
  repColorModule Î¼ := Representation.trivial R
  contrDual_inv Î¼ g := by
    simp only [Representation.trivial, MonoidHom.one_apply, TensorProduct.map_one]
    rfl
  metric_inv Î¼ g := by
    simp only [Representation.trivial, MonoidHom.one_apply, TensorProduct.map_one]
    rfl

end MulActionTensor

namespace TensorStructure
open TensorStructure
open MulActionTensor

variable {G : Type} [Group G]

variable (ğ“£ : TensorStructure R) [MulActionTensor G ğ“£]

variable {d : â„•} {X Y Y' Z : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z]
  {cX cX2 : X â†’ ğ“£.Color} {cY : Y â†’ ğ“£.Color} {cZ : Z â†’ ğ“£.Color} {cY' : Y' â†’ ğ“£.Color} {Î¼ Î½: ğ“£.Color}

/-!

# Equivariance properties involving modules

-/

@[simp]
lemma contrDual_equivariant_tmul (g : G) (x : ğ“£.ColorModule Î¼) (y : ğ“£.ColorModule (ğ“£.Ï„ Î¼)) :
    (ğ“£.contrDual Î¼ ((repColorModule Î¼ g x) âŠ—â‚œ[R] (repColorModule (ğ“£.Ï„ Î¼) g y))) =
    ğ“£.contrDual Î¼ (x âŠ—â‚œ[R] y) := by
  trans (ğ“£.contrDual Î¼ âˆ˜â‚—
      TensorProduct.map (repColorModule Î¼ g) (repColorModule (ğ“£.Ï„ Î¼) g)) (x âŠ—â‚œ[R] y)
  Â· rfl
  Â· rw [contrDual_inv]

@[simp]
lemma colorModuleCast_equivariant_apply (h : Î¼ = Î½) (g : G) (x : ğ“£.ColorModule Î¼) :
    (ğ“£.colorModuleCast h) (repColorModule Î¼ g x) =
    (repColorModule Î½ g) (ğ“£.colorModuleCast h x) := by
  subst h
  simp [colorModuleCast]

@[simp]
lemma contrRightAux_contrDual_equivariant_tmul (g : G) (m : ğ“£.ColorModule Î½ âŠ—[R] ğ“£.ColorModule Î¼)
    (x : ğ“£.ColorModule (ğ“£.Ï„ Î¼)) : (contrRightAux (ğ“£.contrDual Î¼))
    ((TensorProduct.map (repColorModule Î½ g) (repColorModule Î¼ g) m) âŠ—â‚œ[R]
    (repColorModule (ğ“£.Ï„ Î¼) g x)) =
    repColorModule Î½ g ((contrRightAux (ğ“£.contrDual Î¼)) (m âŠ—â‚œ[R] x)) := by
  refine TensorProduct.induction_on m (by simp) ?_ ?_
  Â· intro y z
    simp [contrRightAux]
  Â· intro x z h1 h2
    simp [add_tmul, LinearMap.map_add, h1, h2]

/-!

## Representation of tensor products

-/

/-- The representation of the group `G` on the vector space of tensors. -/
def rep : Representation R G (ğ“£.Tensor cX) where
  toFun g := PiTensorProduct.map (fun x => repColorModule (cX x) g)
  map_one' := by
    simp_all only [_root_.map_one, PiTensorProduct.map_one]
  map_mul' g g' := by
    simp_all only [_root_.map_mul]
    exact PiTensorProduct.map_mul _ _

local infixl:101 " â€¢ " => ğ“£.rep

@[simp]
lemma repColorModule_colorModuleCast (h : Î¼ = Î½) (g : G) :
    (repColorModule Î½ g) âˆ˜â‚— (ğ“£.colorModuleCast h).toLinearMap =
    (ğ“£.colorModuleCast h).toLinearMap âˆ˜â‚— (repColorModule Î¼ g) := by
  apply LinearMap.ext
  intro x
  simp [colorModuleCast_equivariant_apply]

@[simp]
lemma rep_mapIso (e : X â‰ƒ Y) (h : cX = cY âˆ˜ e) (g : G) :
    (ğ“£.rep g) âˆ˜â‚— (ğ“£.mapIso e h).toLinearMap = (ğ“£.mapIso e h).toLinearMap âˆ˜â‚— ğ“£.rep g := by
  apply PiTensorProduct.ext
  apply MultilinearMap.ext
  intro x
  simp only [LinearMap.compMultilinearMap_apply, LinearMap.coe_comp, LinearEquiv.coe_coe,
    Function.comp_apply]
  erw [mapIso_tprod]
  simp [rep, colorModuleCast_equivariant_apply]
  change (PiTensorProduct.map fun x => (repColorModule (cY x)) g)
    ((PiTensorProduct.tprod R) fun i => (ğ“£.colorModuleCast _) (x (e.symm i))) =
    (ğ“£.mapIso e h) ((PiTensorProduct.map _) ((PiTensorProduct.tprod R) x))
  rw [PiTensorProduct.map_tprod, PiTensorProduct.map_tprod, mapIso_tprod]
  apply congrArg
  funext i
  subst h
  simp [colorModuleCast_equivariant_apply]

@[simp]
lemma rep_mapIso_apply (e : X â‰ƒ Y) (h : cX = cY âˆ˜ e) (g : G) (x : ğ“£.Tensor cX) :
    (ğ“£.mapIso e h) (g â€¢ x) = g â€¢ (ğ“£.mapIso e h x) := by
  trans ((ğ“£.rep g) âˆ˜â‚— (ğ“£.mapIso e h).toLinearMap) x
  Â· simp
  Â· rfl

@[simp]
lemma rep_tprod (g : G) (f : (i : X) â†’ ğ“£.ColorModule (cX i)) :
    g â€¢ (PiTensorProduct.tprod R f) = PiTensorProduct.tprod R (fun x =>
    repColorModule (cX x) g (f x)) := by
  simp [rep]
  change (PiTensorProduct.map _) ((PiTensorProduct.tprod R) f) = _
  rw [PiTensorProduct.map_tprod]

/-!

## Group acting on tensor products

-/

lemma tensoratorEquiv_equivariant (g : G) :
    (ğ“£.tensoratorEquiv cX cY) âˆ˜â‚— (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) = ğ“£.rep g âˆ˜â‚—
    (ğ“£.tensoratorEquiv cX cY).toLinearMap := by
  apply tensorProd_piTensorProd_ext
  intro p q
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, map_tmul, rep_tprod,
    tensoratorEquiv_tmul_tprod]
  apply congrArg
  funext x
  match x with
  | Sum.inl x => rfl
  | Sum.inr x => rfl

@[simp]
lemma tensoratorEquiv_equivariant_apply (g : G) (x : ğ“£.Tensor cX âŠ—[R] ğ“£.Tensor cY) :
    (ğ“£.tensoratorEquiv cX cY) ((TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) x)
    = (ğ“£.rep g) ((ğ“£.tensoratorEquiv cX cY) x) := by
  trans ((ğ“£.tensoratorEquiv cX cY) âˆ˜â‚— (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g))) x
  Â· rfl
  Â· rw [tensoratorEquiv_equivariant]
    rfl

lemma rep_tensoratorEquiv_tmul (g : G) (x : ğ“£.Tensor cX) (y : ğ“£.Tensor cY) :
    (ğ“£.tensoratorEquiv cX cY) ((g â€¢ x) âŠ—â‚œ[R] (g â€¢ y)) =
    g â€¢ ((ğ“£.tensoratorEquiv cX cY) (x âŠ—â‚œ[R] y)) := by
  nth_rewrite 1 [â† tensoratorEquiv_equivariant_apply]
  rfl

lemma rep_tensoratorEquiv_symm (g : G) :
    (ğ“£.tensoratorEquiv cX cY).symm âˆ˜â‚— ğ“£.rep g = (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) âˆ˜â‚—
    (ğ“£.tensoratorEquiv cX cY).symm.toLinearMap := by
  rw [LinearEquiv.eq_comp_toLinearMap_symm, LinearMap.comp_assoc,
    LinearEquiv.toLinearMap_symm_comp_eq]
  exact Eq.symm (tensoratorEquiv_equivariant ğ“£ g)

@[simp]
lemma rep_tensoratorEquiv_symm_apply (g : G) (x : ğ“£.Tensor (Sum.elim cX cY)) :
    (ğ“£.tensoratorEquiv cX cY).symm ((ğ“£.rep g) x) =
    (TensorProduct.map (ğ“£.rep g) (ğ“£.rep g)) ((ğ“£.tensoratorEquiv cX cY).symm x) := by
  trans ((ğ“£.tensoratorEquiv cX cY).symm âˆ˜â‚— ğ“£.rep g) x
  Â· rfl
  Â· rw [rep_tensoratorEquiv_symm]
    rfl

@[simp]
lemma rep_lid (g : G) : TensorProduct.lid R (ğ“£.Tensor cX) âˆ˜â‚—
    (TensorProduct.map (LinearMap.id) (ğ“£.rep g)) = (ğ“£.rep g) âˆ˜â‚—
    (TensorProduct.lid R (ğ“£.Tensor cX)).toLinearMap := by
  apply TensorProduct.ext'
  intro r y
  simp

@[simp]
lemma rep_lid_apply (g : G) (x : R âŠ—[R] ğ“£.Tensor cX) :
    (TensorProduct.lid R (ğ“£.Tensor cX)) ((TensorProduct.map (LinearMap.id) (ğ“£.rep g)) x) =
    (ğ“£.rep g) ((TensorProduct.lid R (ğ“£.Tensor cX)).toLinearMap x) := by
  trans ((TensorProduct.lid R (ğ“£.Tensor cX)) âˆ˜â‚— (TensorProduct.map (LinearMap.id) (ğ“£.rep g))) x
  Â· rfl
  Â· rw [rep_lid]
    rfl

end TensorStructure

end
