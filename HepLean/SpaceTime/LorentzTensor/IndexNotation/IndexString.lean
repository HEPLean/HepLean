/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.LorentzTensor.IndexNotation.TensorIndex
/-!

# Strings of indices

A string of indices e.g. `áµ˜Â¹Â²áµ¤â‚„â‚ƒ` is the structure we usually see
following a tensor symbol in index notation.

This file defines such an index string, and from it constructs a list of indices.

-/

open Lean
open String

namespace IndexNotation

variable (X : Type) [IndexNotation X] [Fintype X] [DecidableEq X]

/-!

## The definition of an index string

-/

/-- Takes in a list of characters and splits it into a list of characters at those characters
  which are notation characters e.g. `'áµ˜'`. -/
def stringToPreIndexList (l : List Char) : List (List Char) :=
  let indexHeads := l.filter (fun c => isNotationChar X c)
  let indexTails := l.splitOnP (fun c => isNotationChar X c)
  let l' := List.zip indexHeads indexTails.tail
  l'.map fun x => x.1 :: x.2

/-- A bool which is true given a list of characters if and only if everl element
  of the corresponding `stringToPreIndexList` correspondings to an index. -/
def listCharIsIndexString (l : List Char) : Bool :=
  (stringToPreIndexList X l).all fun l => (listCharIndex X l && l â‰  [])

/-- A string of indices to be associated with a tensor. For example, `áµ˜â°áµ¤â‚‚â‚†â‚€áµ˜Â³`. -/
def IndexString : Type := {s : String // listCharIsIndexString X s.toList = true}

namespace IndexString

/-!

## Constructing a list of indices from an index string

-/

variable {X : Type} [IndexNotation X] [Fintype X] [DecidableEq X]

/-- The character list associated with a index string. -/
def toCharList (s : IndexString X) : List Char := s.val.toList

/-! TODO: Move. -/
/-- Takes a list of characters to the correpsonding index if it exists else to `none`. -/
def charListToOptionIndex (l : List Char) : Option (Index X) :=
  if h : listCharIndex X l && l â‰  [] then
    some (Index.ofCharList l (by simpa using h))
  else
    none

/-- The indices associated to an index string. -/
def toIndexList (s : IndexString X) : IndexList X :=
  {val :=
    (stringToPreIndexList X s.toCharList).filterMap fun l => charListToOptionIndex l}

/-- The formation of an index list from a string `s` statisfying `listCharIndexStringBool`. -/
def toIndexList' (s : String) (hs : listCharIsIndexString X s.toList = true) : IndexList X :=
  toIndexList âŸ¨s, hsâŸ©

end IndexString

end IndexNotation
namespace TensorStructure

/-!

## Making a tensor index from an index string

-/

namespace TensorIndex
variable {R : Type} [CommSemiring R] (ğ“£ : TensorStructure R)
variable {ğ“£ : TensorStructure R} [IndexNotation ğ“£.Color] [Fintype ğ“£.Color] [DecidableEq ğ“£.Color]
variable {n m : â„•} {cn : Fin n â†’ ğ“£.Color} {cm : Fin m â†’ ğ“£.Color}

open IndexNotation ColorIndexList IndexString

/-- The construction of a tensor index from a tensor and a string satisfing conditions which are
  easy to check automatically. -/
noncomputable def fromIndexString (T : ğ“£.Tensor cn) (s : String)
    (hs : listCharIsIndexString ğ“£.toTensorColor.Color s.toList = true)
    (hn : n = (toIndexList' s hs).length)
    (hD : (toIndexList' s hs).withDual = (toIndexList' s hs).withUniqueDual)
    (hC : IndexList.ColorCond (toIndexList' s hs))
    (hd : TensorColor.ColorMap.DualMap (toIndexList' s hs).colorMap
      (cn âˆ˜ Fin.cast hn.symm)) : ğ“£.TensorIndex :=
  TensorStructure.TensorIndex.mkDualMap T âŸ¨(toIndexList' s hs), hD, hCâŸ© hn hd

end TensorIndex

end TensorStructure
