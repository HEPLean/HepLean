/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.LorentzTensor.IndexNotation.IndexListColor
import HepLean.SpaceTime.LorentzTensor.Basic
import HepLean.SpaceTime.LorentzTensor.RisingLowering
import HepLean.SpaceTime.LorentzTensor.Contraction
/-!

# The structure of a tensor with a string of indices

-/

/-! TODO: Introduce a way to change an index from e.g. `áµ˜Â¹` to `áµ˜Â²`.
  Would be nice to have a tactic that did this automatically. -/

namespace TensorStructure
noncomputable section

open TensorColor
open IndexNotation

variable {R : Type} [CommSemiring R] (ğ“£ : TensorStructure R)

variable {d : â„•} {X Y Y' Z W : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z] [Fintype W] [DecidableEq W]
  {cX cX2 : X â†’ ğ“£.Color} {cY : Y â†’ ğ“£.Color} {cZ : Z â†’ ğ“£.Color}
  {cW : W â†’ ğ“£.Color} {cY' : Y' â†’ ğ“£.Color} {Î¼ Î½ Î· : ğ“£.Color}

variable [IndexNotation ğ“£.Color] [Fintype ğ“£.Color] [DecidableEq ğ“£.Color]

/-- The structure an tensor with a index specification e.g. `áµ˜Â¹áµ¤â‚‚`. -/
structure TensorIndex where
  /-- The list of indices. -/
  index : IndexListColor ğ“£.toTensorColor
  /-- The underlying tensor. -/
  tensor : ğ“£.Tensor index.1.colorMap

namespace TensorIndex
open TensorColor IndexListColor
variable {ğ“£ : TensorStructure R} [IndexNotation ğ“£.Color] [Fintype ğ“£.Color] [DecidableEq ğ“£.Color]
variable {n m : â„•} {cn : Fin n â†’ ğ“£.Color} {cm : Fin m â†’ ğ“£.Color}

lemma index_eq_colorMap_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (hi : Tâ‚.index = Tâ‚‚.index) :
    (Tâ‚‚.index).1.colorMap = (Tâ‚.index).1.colorMap âˆ˜ (Fin.castOrderIso (by rw [hi])).toEquiv := by
  funext i
  congr 1
  rw [hi]
  simp only [RelIso.coe_fn_toEquiv, Fin.castOrderIso_apply]
  exact
    (Fin.heq_ext_iff (congrArg IndexList.numIndices (congrArg Subtype.val (id (Eq.symm hi))))).mpr
      rfl

lemma ext (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (hi : Tâ‚.index = Tâ‚‚.index)
    (h : Tâ‚.tensor = ğ“£.mapIso (Fin.castOrderIso (by rw [hi])).toEquiv
    (index_eq_colorMap_eq hi) Tâ‚‚.tensor) : Tâ‚ = Tâ‚‚ := by
  cases Tâ‚; cases Tâ‚‚
  simp at hi
  subst hi
  simp_all

lemma index_eq_of_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ = Tâ‚‚) : Tâ‚.index = Tâ‚‚.index := by
  cases h
  rfl

@[simp]
lemma tensor_eq_of_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ = Tâ‚‚) : Tâ‚.tensor =
    ğ“£.mapIso (Fin.castOrderIso (by rw [index_eq_of_eq h])).toEquiv
    (index_eq_colorMap_eq (index_eq_of_eq h)) Tâ‚‚.tensor := by
  have hi := index_eq_of_eq h
  cases Tâ‚
  cases Tâ‚‚
  simp at hi
  subst hi
  simpa using h

/-- The construction of a `TensorIndex` from a tensor, a IndexListColor, and a condition
  on the dual maps. -/
def mkDualMap (T : ğ“£.Tensor cn) (l : IndexListColor ğ“£.toTensorColor) (hn : n = l.1.length)
    (hd : ColorMap.DualMap l.1.colorMap (cn âˆ˜ Fin.cast hn.symm)) :
    ğ“£.TensorIndex where
  index := l
  tensor :=
      ğ“£.mapIso (Equiv.refl _) (ColorMap.DualMap.split_dual' (by simp [hd])) <|
      ğ“£.dualize (ColorMap.DualMap.split l.1.colorMap (cn âˆ˜ Fin.cast hn.symm)) <|
      (ğ“£.mapIso (Fin.castOrderIso hn).toEquiv rfl T : ğ“£.Tensor (cn âˆ˜ Fin.cast hn.symm))

/-!

## The contraction of indices

-/

/-- The contraction of indices in a `TensorIndex`. -/
def contr (T : ğ“£.TensorIndex) : ğ“£.TensorIndex where
  index := T.index.contr
  tensor :=
      ğ“£.mapIso (Fin.castOrderIso T.index.contr_numIndices.symm).toEquiv
      T.index.contr_colorMap <|
      ğ“£.contr (T.index.splitContr).symm T.index.splitContr_map T.tensor

/-- Applying contr to a tensor whose indices has no contracts does not do anything. -/
@[simp]
lemma contr_of_hasNoContr (T : ğ“£.TensorIndex) (h : T.index.1.HasNoContr) :
    T.contr = T := by
  refine ext _ _ ?_ ?_
  exact Subtype.eq (T.index.1.contr_of_hasNoContr h)
  simp only [contr]
  have h1 : IsEmpty T.index.1.contrPairSet := T.index.1.contrPairSet_isEmpty_of_hasNoContr h
  cases T
  rename_i i T
  simp only
  refine PiTensorProduct.induction_on' T ?_ (by
    intro a b hx hy
    simp [map_add, add_mul, hx, hy])
  intro r f
  simp only [PiTensorProduct.tprodCoeff_eq_smul_tprod, LinearMapClass.map_smul, mapIso_tprod, id_eq,
    eq_mpr_eq_cast, OrderIso.toEquiv_symm, RelIso.coe_fn_toEquiv]
  apply congrArg
  erw [TensorStructure.contr_tprod_isEmpty]
  erw [mapIso_tprod]
  apply congrArg
  funext l
  rw [â† LinearEquiv.symm_apply_eq]
  simp only [colorModuleCast, Equiv.cast_symm, OrderIso.toEquiv_symm, RelIso.coe_fn_toEquiv,
    Function.comp_apply, LinearEquiv.coe_mk, Equiv.cast_apply, LinearEquiv.coe_symm_mk, cast_cast]
  apply cast_eq_iff_heq.mpr
  rw [splitContr_symm_apply_of_hasNoContr _ h]
  rfl

@[simp]
lemma contr_contr (T : ğ“£.TensorIndex) : T.contr.contr = T.contr :=
  T.contr.contr_of_hasNoContr T.index.1.contr_hasNoContr

@[simp]
lemma contr_index (T : ğ“£.TensorIndex) : T.contr.index = T.index.contr := rfl


/-!

## Scalar multiplication of

-/

/-- The scalar multiplication of a `TensorIndex` by an element of `R`. -/
instance : SMul R ğ“£.TensorIndex where
  smul := fun r T => {
    index := T.index
    tensor := r â€¢ T.tensor}

@[simp]
lemma smul_index (r : R) (T : ğ“£.TensorIndex) : (r â€¢ T).index = T.index := rfl

@[simp]
lemma smul_tensor (r : R) (T : ğ“£.TensorIndex) : (r â€¢ T).tensor = r â€¢ T.tensor := rfl

@[simp]
lemma smul_contr (r : R) (T : ğ“£.TensorIndex) : (r â€¢ T).contr = r â€¢ T.contr := by
  refine ext _ _ rfl ?_
  simp only [contr, smul_index, smul_tensor, LinearMapClass.map_smul, Fin.castOrderIso_refl,
    OrderIso.refl_toEquiv, mapIso_refl, LinearEquiv.refl_apply]

/-!

## Equivalence relation on `TensorIndex`

-/

/-- An (equivalence) relation on two `TensorIndex`.
  The point in this equivalence relation is that certain things (like the
  permutation of indices, the contraction of indices, or rising or lowering indices) can be placed
  in the indices or moved to the tensor itself. These two descriptions are equivalent. -/
def Rel (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) : Prop :=
  Tâ‚.index.PermContr Tâ‚‚.index âˆ§ âˆ€ (h : Tâ‚.index.PermContr Tâ‚‚.index),
  Tâ‚.contr.tensor = ğ“£.mapIso h.toEquiv.symm h.toEquiv_colorMap Tâ‚‚.contr.tensor

namespace Rel

/-- Rel is reflexive. -/
lemma refl (T : ğ“£.TensorIndex) : Rel T T := by
  apply And.intro
  exact IndexListColor.PermContr.refl T.index
  intro h
  simp [PermContr.toEquiv_refl']

/-- Rel is symmetric. -/
lemma symm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Rel Tâ‚ Tâ‚‚) : Rel Tâ‚‚ Tâ‚ := by
  apply And.intro h.1.symm
  intro h'
  rw [â† mapIso_symm]
  symm
  erw [LinearEquiv.symm_apply_eq]
  rw [h.2]
  apply congrFun
  congr
  exact h'.symm

/-- Rel is transitive. -/
lemma trans {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h1 : Rel Tâ‚ Tâ‚‚) (h2 : Rel Tâ‚‚ Tâ‚ƒ) : Rel Tâ‚ Tâ‚ƒ := by
  apply And.intro (h1.1.trans h2.1)
  intro h
  change _ = (ğ“£.mapIso (h1.1.trans h2.1).toEquiv.symm _) Tâ‚ƒ.contr.tensor
  trans (ğ“£.mapIso ((h1.1).toEquiv.trans (h2.1).toEquiv).symm (by
    rw [PermContr.toEquiv_trans]
    exact proof_2 Tâ‚ Tâ‚ƒ h)) Tâ‚ƒ.contr.tensor
  swap
  congr
  rw [PermContr.toEquiv_trans]
  erw [â† mapIso_trans]
  simp only [LinearEquiv.trans_apply]
  apply (h1.2 h1.1).trans
  apply congrArg
  exact h2.2 h2.1

/-- Rel forms an equivalence relation. -/
lemma isEquivalence : Equivalence (@Rel _ _ ğ“£ _) where
  refl := Rel.refl
  symm := Rel.symm
  trans := Rel.trans

/-- The equality of tensors corresponding to related tensor indices. -/
lemma to_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Rel Tâ‚ Tâ‚‚) :
    Tâ‚.contr.tensor = ğ“£.mapIso h.1.toEquiv.symm h.1.toEquiv_colorMap Tâ‚‚.contr.tensor := h.2 h.1

end Rel

/-- The structure of a Setoid on `ğ“£.TensorIndex` induced by `Rel`. -/
instance asSetoid : Setoid ğ“£.TensorIndex := âŸ¨Rel, Rel.isEquivalenceâŸ©

/-- A tensor index is equivalent to its contraction. -/
lemma rel_contr (T : ğ“£.TensorIndex) : T â‰ˆ T.contr := by
  apply And.intro
  simp only [PermContr, contr_index, IndexListColor.contr_contr, List.Perm.refl, true_and]
  rw [IndexListColor.contr_contr]
  exact T.index.contr.1.hasNoContr_color_eq_of_id_eq T.index.1.contr_hasNoContr
  intro h
  rw [tensor_eq_of_eq T.contr_contr]
  simp only [contr_index, mapIso_mapIso]
  trans ğ“£.mapIso (Equiv.refl _) (by rfl) T.contr.tensor
  simp only [contr_index, mapIso_refl, LinearEquiv.refl_apply]
  congr
  apply Equiv.ext
  intro x
  rw [PermContr.toEquiv_contr_eq T.index.contr_contr.symm]
  rfl

lemma smul_equiv {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ â‰ˆ Tâ‚‚) (r : R) : r â€¢ Tâ‚ â‰ˆ r â€¢ Tâ‚‚ := by
  apply And.intro h.1
  intro h1
  rw [tensor_eq_of_eq (smul_contr r Tâ‚), tensor_eq_of_eq (smul_contr r Tâ‚‚)]
  simp only [contr_index, smul_index, Fin.castOrderIso_refl, OrderIso.refl_toEquiv, mapIso_refl,
    smul_tensor, LinearMapClass.map_smul, LinearEquiv.refl_apply]
  apply congrArg
  exact h.2 h1

/-!

## Addition of allowed `TensorIndex`

-/

/-- The condition on tensors with indices for their addition to exists.
  This condition states that the the indices of one tensor are exact permutations of indices
  of another after contraction. This includes the id of the index and the color.

  This condition is general enough to allow addition of e.g. `Ïˆáµ¤â‚áµ¤â‚‚ + Ï†áµ¤â‚‚áµ¤â‚`, but
  will NOT allow e.g. `Ïˆáµ¤â‚áµ¤â‚‚ + Ï†áµ˜Â²áµ¤â‚`. -/
def AddCond (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) : Prop :=
  Tâ‚.index.PermContr Tâ‚‚.index

namespace AddCond

lemma to_PermContr {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) : Tâ‚.index.PermContr Tâ‚‚.index := h

@[symm]
lemma symm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) : AddCond Tâ‚‚ Tâ‚ := by
  rw [AddCond] at h
  exact h.symm

lemma refl (T : ğ“£.TensorIndex) : AddCond T T := by
  exact PermContr.refl _

lemma trans {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h1 : AddCond Tâ‚ Tâ‚‚) (h2 : AddCond Tâ‚‚ Tâ‚ƒ) :
    AddCond Tâ‚ Tâ‚ƒ := by
  rw [AddCond] at h1 h2
  exact h1.trans h2

lemma rel_left {Tâ‚ Tâ‚' Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚ â‰ˆ Tâ‚') :
    AddCond Tâ‚' Tâ‚‚ := h'.1.symm.trans h

lemma rel_right {Tâ‚ Tâ‚‚ Tâ‚‚' : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚‚ â‰ˆ Tâ‚‚') :
    AddCond Tâ‚ Tâ‚‚' := h.trans h'.1

/-- The equivalence between indices after contraction given a `AddCond`. -/
@[simp]
def toEquiv {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    Fin Tâ‚.contr.index.1.length â‰ƒ Fin Tâ‚‚.contr.index.1.length := h.to_PermContr.toEquiv

lemma toEquiv_colorMap {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    ColorMap.MapIso h.toEquiv (Tâ‚.contr.index).1.colorMap (Tâ‚‚.contr.index).1.colorMap :=
    h.to_PermContr.toEquiv_colorMap'

end AddCond

/-- The addition of two `TensorIndex` given the condition that, after contraction,
  their index lists are the same. -/
def add (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    ğ“£.TensorIndex where
  index := Tâ‚‚.index.contr
  tensor := (ğ“£.mapIso h.toEquiv h.toEquiv_colorMap Tâ‚.contr.tensor) + Tâ‚‚.contr.tensor

/-- Notation for addition of tensor indices. -/
notation:71 Tâ‚ "+["h"]" Tâ‚‚:72 => add Tâ‚ Tâ‚‚ h

namespace AddCond

lemma add_right {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚ƒ) (h' : AddCond Tâ‚‚ Tâ‚ƒ) :
    AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ) := by
  simpa only [AddCond, add, contr_index] using h.rel_right Tâ‚ƒ.rel_contr

lemma add_left {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : AddCond Tâ‚‚ Tâ‚ƒ) :
    AddCond (Tâ‚ +[h] Tâ‚‚) Tâ‚ƒ :=
  (add_right h'.symm h).symm

lemma of_add_right' {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ} (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) :
    AddCond Tâ‚ Tâ‚ƒ := by
  change Tâ‚.AddCond Tâ‚ƒ.contr at h
  exact h.rel_right Tâ‚ƒ.rel_contr.symm

lemma of_add_right {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ} (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) :
    AddCond Tâ‚ Tâ‚‚ := h.of_add_right'.trans h'.symm

lemma of_add_left {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚}
    (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) : AddCond Tâ‚‚ Tâ‚ƒ :=
  (of_add_right' h.symm).symm

lemma of_add_left' {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚}
    (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) : AddCond Tâ‚ Tâ‚ƒ :=
  (of_add_right h.symm).symm

lemma add_left_of_add_right {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ}
    (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) : AddCond (Tâ‚ +[of_add_right h] Tâ‚‚) Tâ‚ƒ := by
  have h0 := ((of_add_right' h).trans h'.symm)
  exact (h'.symm.add_right h0).symm

lemma add_right_of_add_left {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚}
    (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) : AddCond Tâ‚ (Tâ‚‚ +[of_add_left h] Tâ‚ƒ) :=
  (add_left (of_add_left h) (of_add_left' h).symm).symm

lemma add_comm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    AddCond (Tâ‚ +[h] Tâ‚‚) (Tâ‚‚ +[h.symm] Tâ‚) := by
  apply add_right
  apply add_left
  exact h.symm

end AddCond

@[simp]
lemma add_index (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (add Tâ‚ Tâ‚‚ h).index = Tâ‚‚.index.contr := rfl

@[simp]
lemma add_tensor (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (add Tâ‚ Tâ‚‚ h).tensor =
    (ğ“£.mapIso h.toEquiv h.toEquiv_colorMap Tâ‚.contr.tensor) + Tâ‚‚.contr.tensor := by rfl

/-- Scalar multiplication commutes with addition. -/
lemma smul_add (r : R) (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    r â€¢ (Tâ‚ +[h] Tâ‚‚) = r â€¢ Tâ‚ +[h] r â€¢ Tâ‚‚ := by
  refine ext _ _ rfl ?_
  simp [add]
  rw [tensor_eq_of_eq (smul_contr r Tâ‚), tensor_eq_of_eq (smul_contr r Tâ‚‚)]
  simp only [smul_index, contr_index, Fin.castOrderIso_refl, OrderIso.refl_toEquiv, mapIso_refl,
    smul_tensor, AddCond.toEquiv, LinearMapClass.map_smul, LinearEquiv.refl_apply]

lemma add_hasNoContr (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (Tâ‚ +[h] Tâ‚‚).index.1.HasNoContr := by
  simpa using Tâ‚‚.index.1.contr_hasNoContr

@[simp]
lemma contr_add (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (Tâ‚ +[h] Tâ‚‚).contr = Tâ‚ +[h] Tâ‚‚ :=
  contr_of_hasNoContr (Tâ‚ +[h] Tâ‚‚) (add_hasNoContr Tâ‚ Tâ‚‚ h)

@[simp]
lemma contr_add_tensor (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (Tâ‚ +[h] Tâ‚‚).contr.tensor =
    ğ“£.mapIso (Fin.castOrderIso (by rw [index_eq_of_eq (contr_add Tâ‚ Tâ‚‚ h)])).toEquiv
    (index_eq_colorMap_eq (index_eq_of_eq (contr_add Tâ‚ Tâ‚‚ h))) (Tâ‚ +[h] Tâ‚‚).tensor :=
  tensor_eq_of_eq (contr_add Tâ‚ Tâ‚‚ h)

lemma add_comm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) : Tâ‚ +[h] Tâ‚‚ â‰ˆ Tâ‚‚ +[h.symm] Tâ‚ := by
  apply And.intro h.add_comm
  intro h
  simp only [contr_index, add_index, contr_add_tensor, add_tensor, AddCond.toEquiv, map_add,
    mapIso_mapIso]
  rw [_root_.add_comm]
  congr 1
  all_goals
    apply congrFun
    apply congrArg
    congr 1
    rw [â† PermContr.toEquiv_contr_eq, â† PermContr.toEquiv_contr_eq,
      PermContr.toEquiv_trans, PermContr.toEquiv_symm, PermContr.toEquiv_trans]
    simp only [IndexListColor.contr_contr]
    simp only [IndexListColor.contr_contr]

open AddCond in
lemma add_rel_left {Tâ‚ Tâ‚' Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚ â‰ˆ Tâ‚') :
    Tâ‚ +[h] Tâ‚‚ â‰ˆ Tâ‚' +[h.rel_left h'] Tâ‚‚ := by
  apply And.intro (PermContr.refl _)
  intro h
  simp only [contr_index, add_index, contr_add_tensor, add_tensor, toEquiv, map_add, mapIso_mapIso,
    PermContr.toEquiv_refl, Equiv.refl_symm, mapIso_refl, LinearEquiv.refl_apply]
  congr 1
  rw [h'.to_eq]
  simp only [mapIso_mapIso]
  congr 1
  congr 1
  rw [PermContr.toEquiv_symm, â† PermContr.toEquiv_contr_eq, PermContr.toEquiv_trans,
    PermContr.toEquiv_trans, PermContr.toEquiv_trans]
  simp only [IndexListColor.contr_contr]

open AddCond in
lemma add_rel_right {Tâ‚ Tâ‚‚ Tâ‚‚' : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚‚ â‰ˆ Tâ‚‚') :
    Tâ‚ +[h] Tâ‚‚ â‰ˆ Tâ‚ +[h.rel_right h'] Tâ‚‚' :=
  (add_comm _).trans ((add_rel_left _ h').trans (add_comm _))

open AddCond in
lemma add_assoc' {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ} (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) :
    Tâ‚ +[h] (Tâ‚‚ +[h'] Tâ‚ƒ) = Tâ‚ +[h'.of_add_right h] Tâ‚‚ +[h'.add_left_of_add_right h] Tâ‚ƒ := by
  refine ext _ _ ?_ ?_
  simp only [add_index, IndexListColor.contr_contr]
  simp only [add_index, add_tensor, contr_index, toEquiv, contr_add_tensor, map_add, mapIso_mapIso]
  rw [_root_.add_assoc]
  congr
  rw [â† PermContr.toEquiv_contr_eq, â† PermContr.toEquiv_contr_eq]
  rw [PermContr.toEquiv_trans, PermContr.toEquiv_trans, PermContr.toEquiv_trans]
  simp only [IndexListColor.contr_contr]
  simp only [IndexListColor.contr_contr]
  rw [â† PermContr.toEquiv_contr_eq, PermContr.toEquiv_trans]
  simp only [IndexListColor.contr_contr]

open AddCond in
lemma add_assoc {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚} (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) :
    Tâ‚ +[h'] Tâ‚‚ +[h] Tâ‚ƒ = Tâ‚ +[h'.add_right_of_add_left h] (Tâ‚‚ +[h'.of_add_left h] Tâ‚ƒ) := by
  rw [add_assoc']

/-! TODO: Show that the product is well defined with respect to Rel. -/

/-!

## Product of `TensorIndex` allowed

-/

/-- The condition on two tensors with indices determining if it possible to
  take their product.

  This condition says that the indices of the two tensors can contract nicely,
  after the contraction of indivdual indices has taken place. Note that
  it is required to take the contraction of indivdual tensors before taking the product
  to ensure that the product is well-defined under the `Rel` equivalence relation.

  For example, indices with the same id have dual colors, and no more then two indices
  have the same id (after contraction). For example, the product of `Ïˆáµ˜Â¹áµ¤â‚‚áµ˜Â²` could be taken with
  `Ï†áµ¤â‚áµ¤â‚ƒáµ˜Â³` or `Ï†áµ¤â‚„áµ¤â‚ƒáµ˜Â³` or `Ï†áµ¤â‚áµ¤â‚‚áµ˜Â²` or `Ï†áµ¤â‚‚áµ¤â‚áµ˜Â¹`
  (since contraction is done before taking the product)
   but not with `Ï†áµ¤â‚áµ¤â‚ƒáµ˜Â³` or `Ï†áµ¤â‚áµ¤â‚‚áµ˜Â²` or  `Ï†áµ¤â‚ƒáµ¤â‚‚áµ˜Â²`. -/
def ProdCond (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) : Prop :=
  IndexListColorProp ğ“£.toTensorColor (Tâ‚.contr.index.1 ++ Tâ‚‚.contr.index.1)

namespace ProdCond

lemma to_indexListColorProp {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : ProdCond Tâ‚ Tâ‚‚) :
    IndexListColorProp ğ“£.toTensorColor (Tâ‚.contr.index.1 ++ Tâ‚‚.contr.index.1) := h

end ProdCond

/-- The tensor product of two `TensorIndex`. -/
def prod (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex)
    (h : ProdCond Tâ‚ Tâ‚‚) : ğ“£.TensorIndex where
  index := Tâ‚.contr.index.prod Tâ‚‚.contr.index h.to_indexListColorProp
  tensor :=
      ğ“£.mapIso ((Fin.castOrderIso (IndexListColor.prod_numIndices)).toEquiv.trans
        (finSumFinEquiv.symm)).symm
      (IndexListColor.prod_colorMap h) <|
      ğ“£.tensoratorEquiv _ _ (Tâ‚.contr.tensor âŠ—â‚œ[R] Tâ‚‚.contr.tensor)

@[simp]
lemma prod_index (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : ProdCond Tâ‚ Tâ‚‚) :
    (prod Tâ‚ Tâ‚‚ h).index = Tâ‚.contr.index.prod Tâ‚‚.contr.index h.to_indexListColorProp := rfl


end TensorIndex
end
end TensorStructure
