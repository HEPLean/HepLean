/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.Tensors.IndexNotation.IndexList.Color
import HepLean.Tensors.IndexNotation.ColorIndexList.ContrPerm
import HepLean.Tensors.IndexNotation.ColorIndexList.Append
import HepLean.Tensors.Basic
import HepLean.Tensors.RisingLowering
import HepLean.Tensors.Contraction
/-!

# The structure of a tensor with a string of indices

-/

/-! TODO: Introduce a way to change an index from e.g. `áµ˜Â¹` to `áµ˜Â²`.
  Would be nice to have a tactic that did this automatically. -/

namespace TensorStructure
noncomputable section

open TensorColor
open IndexNotation

variable {R : Type} [CommSemiring R] (ğ“£ : TensorStructure R)

variable {d : â„•} {X Y Y' Z W : Type} [Fintype X] [DecidableEq X] [Fintype Y] [DecidableEq Y]
  [Fintype Y'] [DecidableEq Y'] [Fintype Z] [DecidableEq Z] [Fintype W] [DecidableEq W]
  {cX cX2 : X â†’ ğ“£.Color} {cY : Y â†’ ğ“£.Color} {cZ : Z â†’ ğ“£.Color}
  {cW : W â†’ ğ“£.Color} {cY' : Y' â†’ ğ“£.Color} {Î¼ Î½ Î· : ğ“£.Color}

variable [IndexNotation ğ“£.Color]

/-- The structure an tensor with a index specification e.g. `áµ˜Â¹áµ¤â‚‚`. -/
structure TensorIndex extends ColorIndexList ğ“£.toTensorColor where
  /-- The underlying tensor. -/
  tensor : ğ“£.Tensor toColorIndexList.colorMap'

namespace TensorIndex

open TensorColor ColorIndexList

variable {ğ“£ : TensorStructure R} [IndexNotation ğ“£.Color] [DecidableEq ğ“£.Color]
variable {n m : â„•} {cn : Fin n â†’ ğ“£.Color} {cm : Fin m â†’ ğ“£.Color}

instance : Coe ğ“£.TensorIndex (ColorIndexList ğ“£.toTensorColor) where
  coe T := T.toColorIndexList

omit [DecidableEq ğ“£.Color] in
lemma colormap_mapIso {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (hi : Tâ‚.toColorIndexList = Tâ‚‚.toColorIndexList) :
    ColorMap.MapIso (Fin.castOrderIso (congrArg IndexList.length (congrArg toIndexList hi))).toEquiv
    Tâ‚.colorMap' Tâ‚‚.colorMap' := by
  cases Tâ‚; cases Tâ‚‚
  simp [ColorMap.MapIso]
  simp at hi
  rename_i a b c d
  cases a
  cases c
  rename_i a1 a2 a3 a4 a5 a6
  cases a1
  cases a4
  simp_all
  simp at hi
  subst hi
  rfl

omit [DecidableEq ğ“£.Color] in
lemma ext {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (hi : Tâ‚.toColorIndexList = Tâ‚‚.toColorIndexList)
    (h : Tâ‚.tensor = ğ“£.mapIso (Fin.castOrderIso (by simp [IndexList.length, hi])).toEquiv
    (colormap_mapIso hi.symm) Tâ‚‚.tensor) : Tâ‚ = Tâ‚‚ := by
  cases Tâ‚; cases Tâ‚‚
  simp at h
  simp_all
  simp at hi
  subst hi
  simp_all

omit [DecidableEq ğ“£.Color] in
lemma index_eq_of_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ = Tâ‚‚) :
    Tâ‚.toColorIndexList = Tâ‚‚.toColorIndexList := by
  cases h
  rfl

/-- Given an equality of `TensorIndex`, the isomorphism taking one underlying
  tensor to the other. -/
@[simp]
def tensorIso {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ = Tâ‚‚) :
    ğ“£.Tensor Tâ‚‚.colorMap' â‰ƒâ‚—[R] ğ“£.Tensor Tâ‚.colorMap' :=
  ğ“£.mapIso (Fin.castOrderIso (by rw [index_eq_of_eq h])).toEquiv
    (colormap_mapIso (index_eq_of_eq h).symm)

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma tensor_eq_of_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ = Tâ‚‚) :
    Tâ‚.tensor = tensorIso h Tâ‚‚.tensor := by
  have hi := index_eq_of_eq h
  cases Tâ‚
  cases Tâ‚‚
  simp at hi
  subst hi
  simpa using h

/-- The construction of a `TensorIndex` from a tensor, a IndexListColor, and a condition
  on the dual maps. -/
def mkDualMap (T : ğ“£.Tensor cn) (l : ColorIndexList ğ“£.toTensorColor) (hn : n = l.1.length)
    (hd : ColorMap.DualMap l.colorMap' (cn âˆ˜ Fin.cast hn.symm)) :
    ğ“£.TensorIndex where
  toColorIndexList := l
  tensor :=
      ğ“£.mapIso (Equiv.refl _) (ColorMap.DualMap.split_dual' (by simpa using hd)) <|
      ğ“£.dualize (ColorMap.DualMap.split l.colorMap' (cn âˆ˜ Fin.cast hn.symm)) <|
      (ğ“£.mapIso (Fin.castOrderIso hn).toEquiv rfl T : ğ“£.Tensor (cn âˆ˜ Fin.cast hn.symm))

/-!

## The contraction of indices

-/

/-- The contraction of indices in a `TensorIndex`. -/
def contr (T : ğ“£.TensorIndex) : ğ“£.TensorIndex where
  toColorIndexList := T.toColorIndexList.contr
  tensor := ğ“£.mapIso (Equiv.refl _) T.contrEquiv_colorMapIso <|
      ğ“£.contr T.toColorIndexList.contrEquiv T.contrEquiv_contrCond T.tensor

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma contr_tensor (T : ğ“£.TensorIndex) :
    T.contr.tensor = ((ğ“£.mapIso (Equiv.refl _) T.contrEquiv_colorMapIso <|
      ğ“£.contr T.toColorIndexList.contrEquiv T.contrEquiv_contrCond T.tensor)) := by
  rfl

omit [DecidableEq ğ“£.Color] in
/-- Applying contr to a tensor whose indices has no contracts does not do anything. -/
@[simp]
lemma contr_of_withDual_empty (T : ğ“£.TensorIndex) (h : T.withDual = âˆ…) :
    T.contr = T := by
  refine ext ?_ ?_
  Â· simp only [contr, ColorIndexList.contr]
    have hx := T.contrIndexList_of_withDual_empty h
    apply ColorIndexList.ext
    simp only [hx]
  Â· simp only [contr]
    cases T
    rename_i i T
    simp only
    refine PiTensorProduct.induction_on' T ?_ (by
      intro a b hx hy
      simp [map_add, add_mul, hx, hy])
    intro r f
    simp only [PiTensorProduct.tprodCoeff_eq_smul_tprod, LinearMapClass.map_smul, mapIso_tprod,
      id_eq, eq_mpr_eq_cast, OrderIso.toEquiv_symm, RelIso.coe_fn_toEquiv]
    apply congrArg
    have hEm : IsEmpty { x // x âˆˆ i.withUniqueDualLT } := by
      rw [Finset.isEmpty_coe_sort]
      rw [Finset.eq_empty_iff_forall_not_mem]
      intro x hx
      have hx' : x âˆˆ i.withUniqueDual := by
        exact Finset.mem_of_mem_filter x hx
      rw [â† i.unique_duals] at h
      rw [h] at hx'
      simp_all only [Finset.not_mem_empty]
    erw [TensorStructure.contr_tprod_isEmpty]
    erw [mapIso_tprod]
    simp only [Equiv.refl_symm, Equiv.refl_apply, colorMap', mapIso_tprod, id_eq,
      OrderIso.toEquiv_symm, RelIso.coe_fn_toEquiv]
    apply congrArg
    funext l
    rw [â† LinearEquiv.symm_apply_eq]
    simp only [colorModuleCast, Equiv.cast_symm, OrderIso.toEquiv_symm, RelIso.coe_fn_toEquiv,
      Function.comp_apply, LinearEquiv.coe_mk, Equiv.cast_apply, LinearEquiv.coe_symm_mk, cast_cast]
    apply cast_eq_iff_heq.mpr
    let hl := i.contrEquiv_on_withDual_empty l h
    exact let_value_heq f hl

omit [DecidableEq ğ“£.Color] in
lemma contr_tensor_of_withDual_empty (T : ğ“£.TensorIndex) (h : T.withDual = âˆ…) :
    T.contr.tensor = tensorIso (contr_of_withDual_empty T h) T.tensor := by
  exact tensor_eq_of_eq (contr_of_withDual_empty T h)

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma contr_contr (T : ğ“£.TensorIndex) : T.contr.contr = T.contr :=
  T.contr.contr_of_withDual_empty (by simp [contr, ColorIndexList.contr])

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma contr_toColorIndexList (T : ğ“£.TensorIndex) :
    T.contr.toColorIndexList = T.toColorIndexList.contr := rfl

omit [DecidableEq ğ“£.Color] in
lemma contr_toIndexList (T : ğ“£.TensorIndex) :
    T.contr.toIndexList = T.toIndexList.contrIndexList := rfl

/-!

## Equivalence relation on `TensorIndex`

-/

/-- An (equivalence) relation on two `TensorIndex`.
  The point in this equivalence relation is that certain things (like the
  permutation of indices, the contraction of indices, or rising or lowering indices) can be placed
  in the indices or moved to the tensor itself. These two descriptions are equivalent. -/
def Rel (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) : Prop :=
  Tâ‚.ContrPerm Tâ‚‚ âˆ§ âˆ€ (h : Tâ‚.ContrPerm Tâ‚‚),
  Tâ‚.contr.tensor = ğ“£.mapIso (contrPermEquiv h).symm (contrPermEquiv_colorMap_iso h) Tâ‚‚.contr.tensor

namespace Rel

/-- Rel is reflexive. -/
lemma refl (T : ğ“£.TensorIndex) : Rel T T := by
  apply And.intro
  Â· simp only [ContrPerm.refl]
  Â· simp only [ContrPerm.refl, contr_toColorIndexList, contr_tensor, contrPermEquiv_refl,
    Equiv.refl_symm, mapIso_refl, LinearEquiv.refl_apply, imp_self]

/-- Rel is symmetric. -/
lemma symm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Rel Tâ‚ Tâ‚‚) : Rel Tâ‚‚ Tâ‚ := by
  apply And.intro h.1.symm
  intro h'
  rw [â† mapIso_symm]
  Â· symm
    erw [LinearEquiv.symm_apply_eq]
    rw [h.2]
    Â· rfl
    exact h'.symm

/-- Rel is transitive. -/
lemma trans {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h1 : Rel Tâ‚ Tâ‚‚) (h2 : Rel Tâ‚‚ Tâ‚ƒ) : Rel Tâ‚ Tâ‚ƒ := by
  apply And.intro ((h1.1.trans h2.1))
  intro h
  change _ = (ğ“£.mapIso (contrPermEquiv (h1.1.trans h2.1)).symm _) Tâ‚ƒ.contr.tensor
  trans (ğ“£.mapIso ((contrPermEquiv h1.1).trans (contrPermEquiv h2.1)).symm (by
    simp only [contrPermEquiv_trans, contrPermEquiv_symm, contr_toColorIndexList]
    have h1 := contrPermEquiv_colorMap_iso (ContrPerm.symm (ContrPerm.trans h1.left h2.left))
    rw [â† ColorMap.MapIso.symm'] at h1
    exact h1)) Tâ‚ƒ.contr.tensor
  Â· erw [â† mapIso_trans]
    Â· simp only [LinearEquiv.trans_apply]
      apply (h1.2 h1.1).trans
      Â· apply congrArg
        exact h2.2 h2.1
  Â· congr 1
    simp only [contrPermEquiv_trans, contrPermEquiv_symm]

/-- Rel forms an equivalence relation. -/
lemma isEquivalence : Equivalence (@Rel _ _ ğ“£ _ _) where
  refl := Rel.refl
  symm := Rel.symm
  trans := Rel.trans

/-- The equality of tensors corresponding to related tensor indices. -/
lemma to_eq {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Rel Tâ‚ Tâ‚‚) :
    Tâ‚.contr.tensor = ğ“£.mapIso (contrPermEquiv h.1).symm
    (contrPermEquiv_colorMap_iso h.1) Tâ‚‚.contr.tensor := h.2 h.1

lemma of_withDual_empty {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚.ContrPerm Tâ‚‚)
    (h1 : Tâ‚.withDual = âˆ…) (h2 : Tâ‚‚.withDual = âˆ…)
    (hT : Tâ‚.tensor =
    ğ“£.mapIso (permEquiv h h1 h2).symm (permEquiv_colorMap_iso h h1 h2) Tâ‚‚.tensor) : Rel Tâ‚ Tâ‚‚ := by
  apply And.intro h
  intro h'
  rw [contr_tensor_of_withDual_empty Tâ‚ h1, contr_tensor_of_withDual_empty Tâ‚‚ h2, hT]
  simp only [contr_toColorIndexList, tensorIso, mapIso_mapIso, contrPermEquiv_symm]
  apply congrFun
  apply congrArg
  apply mapIso_ext
  ext i
  simp [permEquiv, contrPermEquiv]
  have hn := congrArg (fun x => x.toIndexList) (contr_of_withDual_empty Tâ‚ h1)
  have hn2 := congrArg (fun x => x.toIndexList) (contr_of_withDual_empty Tâ‚‚ h2)
  simp only [contr_toColorIndexList] at hn hn2
  rw [IndexList.getDualInOtherEquiv_cast hn2 hn]
  rfl

end Rel

/-- The structure of a Setoid on `ğ“£.TensorIndex` induced by `Rel`. -/
instance asSetoid : Setoid ğ“£.TensorIndex := âŸ¨Rel, Rel.isEquivalenceâŸ©

/-- A tensor index is equivalent to its contraction. -/
lemma rel_contr (T : ğ“£.TensorIndex) : T â‰ˆ T.contr := by
  apply And.intro
  Â· simp only [contr_toColorIndexList, ContrPerm.contr_self]
  Â· intro h
    rw [tensor_eq_of_eq T.contr_contr]
    simp only [contr_toColorIndexList, colorMap', contrPermEquiv_self_contr, OrderIso.toEquiv_symm,
      Fin.symm_castOrderIso, mapIso_mapIso, tensorIso]
    trans ğ“£.mapIso (Equiv.refl _) (by rfl) T.contr.tensor
    Â· simp only [contr_toColorIndexList, mapIso_refl, LinearEquiv.refl_apply]
    Â· rfl

/-!

## Scalar multiplication of

-/

/-- The scalar multiplication of a `TensorIndex` by an element of `R`. -/
instance : SMul R ğ“£.TensorIndex where
  smul := fun r T => {
    toColorIndexList := T.toColorIndexList
    tensor := r â€¢ T.tensor}

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma smul_index (r : R) (T : ğ“£.TensorIndex) : (r â€¢ T).toColorIndexList = T.toColorIndexList := rfl

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma smul_tensor (r : R) (T : ğ“£.TensorIndex) : (r â€¢ T).tensor = r â€¢ T.tensor := rfl

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma smul_contr (r : R) (T : ğ“£.TensorIndex) : (r â€¢ T).contr = r â€¢ T.contr := by
  refine ext rfl ?_
  simp only [contr, smul_index, smul_tensor, LinearMapClass.map_smul, Fin.castOrderIso_refl,
    OrderIso.refl_toEquiv, mapIso_refl, LinearEquiv.refl_apply]

lemma smul_rel {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : Tâ‚ â‰ˆ Tâ‚‚) (r : R) : r â€¢ Tâ‚ â‰ˆ r â€¢ Tâ‚‚ := by
  apply And.intro h.1
  intro h1
  rw [tensor_eq_of_eq (smul_contr r Tâ‚), tensor_eq_of_eq (smul_contr r Tâ‚‚)]
  simp only [contr_toColorIndexList, smul_index, Fin.castOrderIso_refl, OrderIso.refl_toEquiv,
    mapIso_refl, smul_tensor, map_smul, LinearEquiv.refl_apply, contrPermEquiv_symm, tensorIso]
  apply congrArg
  exact h.2 h1

/-!

## Addition of allowed `TensorIndex`

-/

/-- The condition on tensors with indices for their addition to exists.
  This condition states that the the indices of one tensor are exact permutations of indices
  of another after contraction. This includes the id of the index and the color.

  This condition is general enough to allow addition of e.g. `Ïˆáµ¤â‚áµ¤â‚‚ + Ï†áµ¤â‚‚áµ¤â‚`, but
  will NOT allow e.g. `Ïˆáµ¤â‚áµ¤â‚‚ + Ï†áµ˜Â²áµ¤â‚`. -/
def AddCond (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) : Prop := Tâ‚.ContrPerm Tâ‚‚

namespace AddCond

lemma to_PermContr {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    Tâ‚.toColorIndexList.ContrPerm Tâ‚‚.toColorIndexList := h

@[symm]
lemma symm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) : AddCond Tâ‚‚ Tâ‚ := by
  rw [AddCond] at h
  exact h.symm

lemma refl (T : ğ“£.TensorIndex) : AddCond T T := ContrPerm.refl T.toColorIndexList

lemma trans {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h1 : AddCond Tâ‚ Tâ‚‚) (h2 : AddCond Tâ‚‚ Tâ‚ƒ) :
    AddCond Tâ‚ Tâ‚ƒ := by
  rw [AddCond] at h1 h2
  exact h1.trans h2

lemma rel_left {Tâ‚ Tâ‚' Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚ â‰ˆ Tâ‚') :
    AddCond Tâ‚' Tâ‚‚ := h'.1.symm.trans h

lemma rel_right {Tâ‚ Tâ‚‚ Tâ‚‚' : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚‚ â‰ˆ Tâ‚‚') :
    AddCond Tâ‚ Tâ‚‚' := h.trans h'.1

end AddCond

/-- The equivalence between indices after contraction given a `AddCond`. -/
@[simp]
def addCondEquiv {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    Fin Tâ‚.contr.length â‰ƒ Fin Tâ‚‚.contr.length := contrPermEquiv h

lemma addCondEquiv_colorMap {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    ColorMap.MapIso (addCondEquiv h) Tâ‚.contr.colorMap' Tâ‚‚.contr.colorMap' :=
    contrPermEquiv_colorMap_iso' h

/-- The addition of two `TensorIndex` given the condition that, after contraction,
  their index lists are the same. -/
def add (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    ğ“£.TensorIndex where
  toColorIndexList := Tâ‚‚.toColorIndexList.contr
  tensor := (ğ“£.mapIso (addCondEquiv h) (addCondEquiv_colorMap h) Tâ‚.contr.tensor) + Tâ‚‚.contr.tensor

/-- Notation for addition of tensor indices. -/
notation:71 Tâ‚ "+["h"]" Tâ‚‚:72 => add Tâ‚ Tâ‚‚ h

namespace AddCond

lemma add_right {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚ƒ) (h' : AddCond Tâ‚‚ Tâ‚ƒ) :
    AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ) := by
  simpa only [AddCond, add] using h.rel_right Tâ‚ƒ.rel_contr

lemma add_left {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : AddCond Tâ‚‚ Tâ‚ƒ) :
    AddCond (Tâ‚ +[h] Tâ‚‚) Tâ‚ƒ :=
  (add_right h'.symm h).symm

lemma of_add_right' {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ} (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) :
    AddCond Tâ‚ Tâ‚ƒ := by
  change Tâ‚.AddCond Tâ‚ƒ.contr at h
  exact h.rel_right Tâ‚ƒ.rel_contr.symm

lemma of_add_right {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ} (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) :
    AddCond Tâ‚ Tâ‚‚ := h.of_add_right'.trans h'.symm

lemma of_add_left {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚}
    (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) : AddCond Tâ‚‚ Tâ‚ƒ :=
  (of_add_right' h.symm).symm

lemma of_add_left' {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚}
    (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) : AddCond Tâ‚ Tâ‚ƒ :=
  (of_add_right h.symm).symm

lemma add_left_of_add_right {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ}
    (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) : AddCond (Tâ‚ +[of_add_right h] Tâ‚‚) Tâ‚ƒ := by
  have h0 := ((of_add_right' h).trans h'.symm)
  exact (h'.symm.add_right h0).symm

lemma add_right_of_add_left {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚}
    (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) : AddCond Tâ‚ (Tâ‚‚ +[of_add_left h] Tâ‚ƒ) :=
  (add_left (of_add_left h) (of_add_left' h).symm).symm

lemma add_comm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) :
    AddCond (Tâ‚ +[h] Tâ‚‚) (Tâ‚‚ +[h.symm] Tâ‚) := by
  apply add_right
  apply add_left
  exact h.symm

end AddCond

@[simp]
lemma add_toColorIndexList (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (add Tâ‚ Tâ‚‚ h).toColorIndexList = Tâ‚‚.toColorIndexList.contr := rfl

@[simp]
lemma add_tensor (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (add Tâ‚ Tâ‚‚ h).tensor =
    (ğ“£.mapIso (addCondEquiv h) (addCondEquiv_colorMap h) Tâ‚.contr.tensor) + Tâ‚‚.contr.tensor := rfl

/-- Scalar multiplication commutes with addition. -/
lemma smul_add (r : R) (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    r â€¢ (Tâ‚ +[h] Tâ‚‚) = r â€¢ Tâ‚ +[h] r â€¢ Tâ‚‚ := by
  refine ext rfl ?_
  simp only [add, contr_toColorIndexList, addCondEquiv, smul_index, smul_tensor, _root_.smul_add,
    Fin.castOrderIso_refl, OrderIso.refl_toEquiv, mapIso_refl, map_add, LinearEquiv.refl_apply,
    tensorIso]
  rw [tensor_eq_of_eq (smul_contr r Tâ‚), tensor_eq_of_eq (smul_contr r Tâ‚‚)]
  simp only [smul_index, contr_toColorIndexList, Fin.castOrderIso_refl, OrderIso.refl_toEquiv,
    mapIso_refl, smul_tensor, map_smul, LinearEquiv.refl_apply, tensorIso]

lemma add_withDual_empty (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (Tâ‚ +[h] Tâ‚‚).withDual = âˆ… := by
  simp only [add_toColorIndexList]
  change Tâ‚‚.toColorIndexList.contr.withDual = âˆ…
  simp only [ColorIndexList.contr, IndexList.contrIndexList_withDual]

@[simp]
lemma contr_add (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (Tâ‚ +[h] Tâ‚‚).contr = Tâ‚ +[h] Tâ‚‚ :=
  contr_of_withDual_empty (Tâ‚ +[h] Tâ‚‚) (add_withDual_empty Tâ‚ Tâ‚‚ h)

lemma contr_add_tensor (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : AddCond Tâ‚ Tâ‚‚) :
    (Tâ‚ +[h] Tâ‚‚).contr.tensor =
    ğ“£.mapIso (Fin.castOrderIso (by rw [index_eq_of_eq (contr_add Tâ‚ Tâ‚‚ h)])).toEquiv
    (colormap_mapIso (by simp)) (Tâ‚ +[h] Tâ‚‚).tensor :=
  tensor_eq_of_eq (contr_add Tâ‚ Tâ‚‚ h)

lemma add_comm {Tâ‚ Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) : Tâ‚ +[h] Tâ‚‚ â‰ˆ Tâ‚‚ +[h.symm] Tâ‚ := by
  apply And.intro h.add_comm
  intro h
  simp only [contr_toColorIndexList, add_toColorIndexList, contr_add_tensor, add_tensor,
    addCondEquiv, map_add, mapIso_mapIso, colorMap', contrPermEquiv_symm]
  rw [_root_.add_comm]
  apply Mathlib.Tactic.LinearCombination.add_pf
  Â· apply congrFun
    apply congrArg
    apply mapIso_ext
    rw [â† contrPermEquiv_self_contr, â† contrPermEquiv_self_contr, contrPermEquiv_trans,
      contrPermEquiv_trans]
  Â· apply congrFun
    apply congrArg
    apply mapIso_ext
    rw [â† contrPermEquiv_self_contr, â† contrPermEquiv_self_contr, contrPermEquiv_trans,
      contrPermEquiv_trans]

open AddCond in
lemma add_rel_left {Tâ‚ Tâ‚' Tâ‚‚ : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚ â‰ˆ Tâ‚') :
    Tâ‚ +[h] Tâ‚‚ â‰ˆ Tâ‚' +[h.rel_left h'] Tâ‚‚ := by
  apply And.intro (ContrPerm.refl _)
  intro h
  simp only [contr_add_tensor, add_tensor, map_add]
  apply Mathlib.Tactic.LinearCombination.add_pf
  Â· rw [h'.to_eq]
    simp only [contr_toColorIndexList, add_toColorIndexList, colorMap', addCondEquiv,
      contrPermEquiv_symm, mapIso_mapIso, contrPermEquiv_trans, contrPermEquiv_refl,
      Equiv.refl_symm, mapIso_refl, LinearEquiv.refl_apply]
  Â· simp only [contr_toColorIndexList, add_toColorIndexList, colorMap', contrPermEquiv_refl,
      Equiv.refl_symm, mapIso_refl, LinearEquiv.refl_apply]

open AddCond in
lemma add_rel_right {Tâ‚ Tâ‚‚ Tâ‚‚' : ğ“£.TensorIndex} (h : AddCond Tâ‚ Tâ‚‚) (h' : Tâ‚‚ â‰ˆ Tâ‚‚') :
    Tâ‚ +[h] Tâ‚‚ â‰ˆ Tâ‚ +[h.rel_right h'] Tâ‚‚' :=
  (add_comm _).trans ((add_rel_left _ h').trans (add_comm _))

open AddCond in
lemma add_assoc' {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚‚ Tâ‚ƒ} (h : AddCond Tâ‚ (Tâ‚‚ +[h'] Tâ‚ƒ)) :
    Tâ‚ +[h] (Tâ‚‚ +[h'] Tâ‚ƒ) = Tâ‚ +[h'.of_add_right h] Tâ‚‚ +[h'.add_left_of_add_right h] Tâ‚ƒ := by
  refine ext ?_ ?_
  Â· simp only [add_toColorIndexList, ColorIndexList.contr_contr]
  Â· simp only [add_toColorIndexList, add_tensor, contr_toColorIndexList, addCondEquiv,
      contr_add_tensor, map_add, mapIso_mapIso]
    rw [_root_.add_assoc]
    apply Mathlib.Tactic.LinearCombination.add_pf
    Â· apply congrFun
      apply congrArg
      apply mapIso_ext
      rw [â† contrPermEquiv_self_contr, â† contrPermEquiv_self_contr]
      rw [contrPermEquiv_trans, contrPermEquiv_trans, contrPermEquiv_trans]
    Â· apply Mathlib.Tactic.LinearCombination.add_pf _ rfl
      apply congrFun
      apply congrArg
      apply mapIso_ext
      rw [â† contrPermEquiv_self_contr, contrPermEquiv_trans, â† contrPermEquiv_self_contr,
        contrPermEquiv_trans, contrPermEquiv_trans]

open AddCond in
lemma add_assoc {Tâ‚ Tâ‚‚ Tâ‚ƒ : ğ“£.TensorIndex} {h' : AddCond Tâ‚ Tâ‚‚} (h : AddCond (Tâ‚ +[h'] Tâ‚‚) Tâ‚ƒ) :
    Tâ‚ +[h'] Tâ‚‚ +[h] Tâ‚ƒ = Tâ‚ +[h'.add_right_of_add_left h] (Tâ‚‚ +[h'.of_add_left h] Tâ‚ƒ) := by
  rw [add_assoc']

/-!

## Product of `TensorIndex` when allowed

-/

/-! TODO: Show that the product is well defined with respect to Rel. -/

/-- The condition on two `TensorIndex` which is true if and only if their `ColorIndexList`s
  are related by the condition `AppendCond`. That is, they can be appended to form a
  `ColorIndexList`. -/
def ProdCond (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) : Prop :=
  AppendCond Tâ‚.toColorIndexList Tâ‚‚.toColorIndexList

namespace ProdCond

variable {Tâ‚ Tâ‚' Tâ‚‚ Tâ‚‚' : ğ“£.TensorIndex}

omit [DecidableEq ğ“£.Color] in
lemma to_AppendCond (h : ProdCond Tâ‚ Tâ‚‚) :
    Tâ‚.AppendCond Tâ‚‚ := h

@[symm]
lemma symm (h : ProdCond Tâ‚ Tâ‚‚) : ProdCond Tâ‚‚ Tâ‚ := h.to_AppendCond.symm

/-! TODO: Prove properties regarding the interaction of `ProdCond` and `Rel`. -/

end ProdCond

/-- The tensor product of two `TensorIndex`.

  Note: By defualt contraction is NOT done before taking the products. -/
def prod (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : ProdCond Tâ‚ Tâ‚‚) : ğ“£.TensorIndex where
  toColorIndexList := Tâ‚ ++[h] Tâ‚‚
  tensor := ğ“£.mapIso IndexList.appendEquiv (Tâ‚.colorMap_sumELim Tâ‚‚) <|
      ğ“£.tensoratorEquiv _ _ (Tâ‚.tensor âŠ—â‚œ[R] Tâ‚‚.tensor)

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma prod_toColorIndexList (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : ProdCond Tâ‚ Tâ‚‚) :
    (prod Tâ‚ Tâ‚‚ h).toColorIndexList = Tâ‚.toColorIndexList ++[h] Tâ‚‚.toColorIndexList := rfl

omit [DecidableEq ğ“£.Color] in
@[simp]
lemma prod_toIndexList (Tâ‚ Tâ‚‚ : ğ“£.TensorIndex) (h : ProdCond Tâ‚ Tâ‚‚) :
    (prod Tâ‚ Tâ‚‚ h).toIndexList = Tâ‚.toIndexList ++ Tâ‚‚.toIndexList := rfl

end TensorIndex
end
end TensorStructure
