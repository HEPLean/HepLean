/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.Tensors.ComplexLorentz.Basic
import HepLean.Tensors.OverColor.Basic
import HepLean.Mathematics.PiTensorProduct
/-!

## Monodial functor from color cat.

-/
namespace Fermion

noncomputable section

open Matrix
open MatrixGroups
open Complex
open TensorProduct
open IndexNotation
open CategoryTheory

/-- The linear equivalence between `colorToRep c1` and `colorToRep c2` when `c1 = c2`. -/
def colorToRepCongr {c1 c2 : Color} (h : c1 = c2) : colorToRep c1 ‚âÉ‚Çó[‚ÑÇ] colorToRep c2 where
  toFun := Equiv.cast (congrArg (CoeSort.coe ‚àò colorToRep) h)
  invFun := (Equiv.cast (congrArg (CoeSort.coe ‚àò colorToRep) h)).symm
  map_add' x y := by
    subst h
    rfl
  map_smul' x y := by
    subst h
    rfl
  left_inv x := Equiv.symm_apply_apply (Equiv.cast (congrArg (CoeSort.coe ‚àò colorToRep) h)) x
  right_inv x := Equiv.apply_symm_apply (Equiv.cast (congrArg (CoeSort.coe ‚àò colorToRep) h)) x

lemma colorToRepCongr_comm_œÅ {c1 c2 : Color} (h : c1 = c2) (M : SL(2, ‚ÑÇ)) (x : (colorToRep c1)) :
    (colorToRepCongr h) ((colorToRep c1).œÅ M x) = (colorToRep c2).œÅ M ((colorToRepCongr h) x) := by
  subst h
  rfl

namespace colorFun

/-- Given a object in `OverColor Color` the correpsonding tensor product of representations. -/
def obj' (f : OverColor Color) : Rep ‚ÑÇ SL(2, ‚ÑÇ) := Rep.of {
  toFun := fun M => PiTensorProduct.map (fun x => (colorToRep (f.hom x)).œÅ M),
  map_one' := by
    simp
  map_mul' := fun M N => by
    simp only [CategoryTheory.Functor.id_obj, _root_.map_mul]
    ext x : 2
    simp only [LinearMap.compMultilinearMap_apply, PiTensorProduct.map_tprod, LinearMap.mul_apply]}

lemma obj'_œÅ (f : OverColor Color) (M : SL(2, ‚ÑÇ)) : (obj' f).œÅ M =
    PiTensorProduct.map (fun x => (colorToRep (f.hom x)).œÅ M) := rfl

lemma obj'_œÅ_tprod (f : OverColor Color) (M : SL(2, ‚ÑÇ))
    (x : (i : f.left) ‚Üí CoeSort.coe (colorToRep (f.hom i))) :
    (obj' f).œÅ M ((PiTensorProduct.tprod ‚ÑÇ) x) =
    PiTensorProduct.tprod ‚ÑÇ (fun i => (colorToRep (f.hom i)).œÅ M (x i)) := by
  rw [obj'_œÅ]
  change (PiTensorProduct.map fun x => (colorToRep (f.hom x)).œÅ M) ((PiTensorProduct.tprod ‚ÑÇ) x) =
    (PiTensorProduct.tprod ‚ÑÇ) fun i => ((colorToRep (f.hom i)).œÅ M) (x i)
  rw [PiTensorProduct.map_tprod]

/-- Given a morphism in `OverColor Color` the corresopnding linear equivalence between `obj' _`
  induced by reindexing. -/
def mapToLinearEquiv' {f g : OverColor Color} (m : f ‚ü∂ g) : (obj' f).V ‚âÉ‚Çó[‚ÑÇ] (obj' g).V :=
  (PiTensorProduct.reindex ‚ÑÇ (fun x => colorToRep (f.hom x)) (OverColor.Hom.toEquiv m)).trans
  (PiTensorProduct.congr (fun i => colorToRepCongr (OverColor.Hom.toEquiv_symm_apply m i)))

lemma mapToLinearEquiv'_tprod {f g : OverColor Color} (m : f ‚ü∂ g)
    (x : (i : f.left) ‚Üí CoeSort.coe (colorToRep (f.hom i))) :
    mapToLinearEquiv' m (PiTensorProduct.tprod ‚ÑÇ x) =
    PiTensorProduct.tprod ‚ÑÇ (fun i => (colorToRepCongr (OverColor.Hom.toEquiv_symm_apply m i))
    (x ((OverColor.Hom.toEquiv m).symm i))) := by
  rw [mapToLinearEquiv']
  simp only [CategoryTheory.Functor.id_obj, LinearEquiv.trans_apply]
  change (PiTensorProduct.congr fun i => colorToRepCongr _)
    ((PiTensorProduct.reindex ‚ÑÇ (fun x => CoeSort.coe (colorToRep (f.hom x)))
    (OverColor.Hom.toEquiv m)) ((PiTensorProduct.tprod ‚ÑÇ) x)) = _
  rw [PiTensorProduct.reindex_tprod, PiTensorProduct.congr_tprod]
  rfl

/-- Given a morphism in `OverColor Color` the corresopnding map of representations induced by
  reindexing. -/
def map' {f g : OverColor Color} (m : f ‚ü∂ g) : obj' f ‚ü∂ obj' g where
  hom := (mapToLinearEquiv' m).toLinearMap
  comm M := by
    ext x : 2
    refine PiTensorProduct.induction_on' x ?_ (by
      intro x y hx hy
      simp only [CategoryTheory.Functor.id_obj, map_add, hx, ModuleCat.coe_comp,
        Function.comp_apply, hy])
    intro r x
    simp only [CategoryTheory.Functor.id_obj, PiTensorProduct.tprodCoeff_eq_smul_tprod,
      _root_.map_smul, ModuleCat.coe_comp, Function.comp_apply]
    apply congrArg
    change (mapToLinearEquiv' m) (((obj' f).œÅ M) ((PiTensorProduct.tprod ‚ÑÇ) x)) =
      ((obj' g).œÅ M) ((mapToLinearEquiv' m) ((PiTensorProduct.tprod ‚ÑÇ) x))
    rw [mapToLinearEquiv'_tprod, obj'_œÅ_tprod]
    erw [mapToLinearEquiv'_tprod, obj'_œÅ_tprod]
    apply congrArg
    funext i
    rw [colorToRepCongr_comm_œÅ]

end colorFun

/-- The functor between `OverColor Color` and `Rep ‚ÑÇ SL(2, ‚ÑÇ)` taking a map of colors
  to the corresponding tensor product representation. -/
@[simps! obj_V_carrier]
def colorFun : OverColor Color ‚•§ Rep ‚ÑÇ SL(2, ‚ÑÇ) where
  obj := colorFun.obj'
  map := colorFun.map'
  map_id f := by
    ext x
    refine PiTensorProduct.induction_on' x (fun r x => ?_) (fun x y hx hy => by
      simp only [CategoryTheory.Functor.id_obj, map_add, hx, ModuleCat.coe_comp,
        Function.comp_apply, hy])
    simp only [CategoryTheory.Functor.id_obj, PiTensorProduct.tprodCoeff_eq_smul_tprod,
      _root_.map_smul, Action.id_hom, ModuleCat.id_apply]
    apply congrArg
    erw [colorFun.mapToLinearEquiv'_tprod]
    exact congrArg _ (funext (fun i => rfl))
  map_comp {X Y Z} f g := by
    ext x
    refine PiTensorProduct.induction_on' x (fun r x => ?_) (fun x y hx hy => by
      simp only [CategoryTheory.Functor.id_obj, map_add, hx, ModuleCat.coe_comp,
        Function.comp_apply, hy])
    simp only [Functor.id_obj, PiTensorProduct.tprodCoeff_eq_smul_tprod, _root_.map_smul,
      Action.comp_hom, ModuleCat.coe_comp, Function.comp_apply]
    apply congrArg
    rw [colorFun.map', colorFun.map', colorFun.map']
    change (colorFun.mapToLinearEquiv' (CategoryTheory.CategoryStruct.comp f g))
      ((PiTensorProduct.tprod ‚ÑÇ) x) =
      (colorFun.mapToLinearEquiv' g) ((colorFun.mapToLinearEquiv' f) ((PiTensorProduct.tprod ‚ÑÇ) x))
    rw [colorFun.mapToLinearEquiv'_tprod, colorFun.mapToLinearEquiv'_tprod]
    erw [colorFun.mapToLinearEquiv'_tprod]
    refine congrArg _ (funext (fun i => ?_))
    simp only [colorToRepCongr, Function.comp_apply, Equiv.cast_symm, LinearEquiv.coe_mk,
      Equiv.cast_apply, cast_cast, cast_inj]
    rfl

namespace colorFun

open CategoryTheory
open MonoidalCategory

lemma map_tprod {X Y : OverColor Color} (p : (i : X.left) ‚Üí (colorToRep (X.hom i)))
    (f : X ‚ü∂ Y) : (colorFun.map f).hom (PiTensorProduct.tprod ‚ÑÇ p) =
    PiTensorProduct.tprod ‚ÑÇ fun (i : Y.left) => colorToRepCongr
    (OverColor.Hom.toEquiv_comp_inv_apply f i) (p ((OverColor.Hom.toEquiv f).symm i)) := by
  change (colorFun.map' f).hom ((PiTensorProduct.tprod ‚ÑÇ) p) = _
  simp [colorFun.map', mapToLinearEquiv']
  erw [LinearEquiv.trans_apply]
  change (PiTensorProduct.congr fun i => colorToRepCongr _)
    ((PiTensorProduct.reindex ‚ÑÇ (fun x => _) (OverColor.Hom.toEquiv f))
      ((PiTensorProduct.tprod ‚ÑÇ) p)) = _
  rw [PiTensorProduct.reindex_tprod, PiTensorProduct.congr_tprod]

lemma obj_œÅ_tprod (f : OverColor Color) (M : SL(2, ‚ÑÇ))
    (x : (i : f.left) ‚Üí CoeSort.coe (colorToRep (f.hom i))) :
    (colorFun.obj f).œÅ M ((PiTensorProduct.tprod ‚ÑÇ) x) =
    PiTensorProduct.tprod ‚ÑÇ (fun i => (colorToRep (f.hom i)).œÅ M (x i)) := by
  exact obj'_œÅ_tprod _ _ _

@[simp]
lemma obj_œÅ_empty (g : SL(2, ‚ÑÇ)) : (colorFun.obj (ùüô_ (OverColor Color))).œÅ g = LinearMap.id := by
  erw [colorFun.obj'_œÅ]
  ext x
  refine PiTensorProduct.induction_on' x (fun r x => ?_) <| fun x y hx hy => by
    simp only [CategoryTheory.Functor.id_obj, map_add, hx, ModuleCat.coe_comp,
      Function.comp_apply, hy]
    erw [hx, hy]
    rfl
  simp only [OverColor.instMonoidalCategoryStruct_tensorUnit_left, Functor.id_obj,
    OverColor.instMonoidalCategoryStruct_tensorUnit_hom, PiTensorProduct.tprodCoeff_eq_smul_tprod,
    _root_.map_smul, PiTensorProduct.map_tprod, LinearMap.id_coe, id_eq]
  apply congrArg
  apply congrArg
  funext i
  exact Empty.elim i

/-- The unit natural isomorphism. -/
def Œµ : ùüô_ (Rep ‚ÑÇ SL(2, ‚ÑÇ)) ‚âÖ colorFun.obj (ùüô_ (OverColor Color)) where
  hom := {
    hom := (PiTensorProduct.isEmptyEquiv Empty).symm.toLinearMap
    comm := fun M => by
      refine LinearMap.ext (fun x => ?_)
      simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorUnit_left,
        OverColor.instMonoidalCategoryStruct_tensorUnit_hom,
        Action.instMonoidalCategory_tensorUnit_V, Action.tensorUnit_œÅ', Functor.id_obj,
        Category.id_comp, LinearEquiv.coe_coe]
      erw [obj_œÅ_empty M]
      rfl}
  inv := {
    hom := (PiTensorProduct.isEmptyEquiv Empty).toLinearMap
    comm := fun M => by
      refine LinearMap.ext (fun x => ?_)
      simp only [Action.instMonoidalCategory_tensorUnit_V, colorFun_obj_V_carrier,
        OverColor.instMonoidalCategoryStruct_tensorUnit_left,
        OverColor.instMonoidalCategoryStruct_tensorUnit_hom, Functor.id_obj, Action.tensorUnit_œÅ']
      erw [obj_œÅ_empty M]
      rfl}
  hom_inv_id := by
    ext1
    simp only [Action.instMonoidalCategory_tensorUnit_V, CategoryStruct.comp,
      OverColor.instMonoidalCategoryStruct_tensorUnit_hom,
      OverColor.instMonoidalCategoryStruct_tensorUnit_left, Functor.id_obj, Action.Hom.comp_hom,
      colorFun_obj_V_carrier, LinearEquiv.comp_coe, LinearEquiv.symm_trans_self,
      LinearEquiv.refl_toLinearMap, Action.id_hom]
    rfl
  inv_hom_id := by
    ext1
    simp only [CategoryStruct.comp, OverColor.instMonoidalCategoryStruct_tensorUnit_hom,
      OverColor.instMonoidalCategoryStruct_tensorUnit_left, Functor.id_obj, Action.Hom.comp_hom,
      colorFun_obj_V_carrier, Action.instMonoidalCategory_tensorUnit_V, LinearEquiv.comp_coe,
      LinearEquiv.self_trans_symm, LinearEquiv.refl_toLinearMap, Action.id_hom]
    rfl

/-- An auxillary equivalence, and trivial, of modules needed to define `ŒºModEquiv`. -/
def colorToRepSumEquiv {X Y : OverColor Color} (i : X.left ‚äï Y.left) :
    Sum.elim (fun i => colorToRep (X.hom i)) (fun i => colorToRep (Y.hom i)) i ‚âÉ‚Çó[‚ÑÇ]
    colorToRep (Sum.elim X.hom Y.hom i) :=
  match i with
  | Sum.inl _ => LinearEquiv.refl _ _
  | Sum.inr _ => LinearEquiv.refl _ _

/-- The equivalence of modules corresonding to the tensorate. -/
def ŒºModEquiv (X Y : OverColor Color) :
    (colorFun.obj X ‚äó colorFun.obj Y).V ‚âÉ‚Çó[‚ÑÇ] colorFun.obj (X ‚äó Y) :=
  HepLean.PiTensorProduct.tmulEquiv ‚â™‚â´‚Çó PiTensorProduct.congr colorToRepSumEquiv

lemma ŒºModEquiv_tmul_tprod {X Y : OverColor Color}(p : (i : X.left) ‚Üí (colorToRep (X.hom i)))
    (q : (i : Y.left) ‚Üí (colorToRep (Y.hom i))) :
    (ŒºModEquiv X Y) ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q) =
    (PiTensorProduct.tprod ‚ÑÇ) fun i =>
    (colorToRepSumEquiv i) (HepLean.PiTensorProduct.elimPureTensor p q i) := by
  rw [ŒºModEquiv]
  simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom, Action.instMonoidalCategory_tensorObj_V,
    Functor.id_obj, Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
    Action.FunctorCategoryEquivalence.functor_obj_obj]
  rw [LinearEquiv.trans_apply]
  erw [HepLean.PiTensorProduct.tmulEquiv_tmul_tprod]
  change (PiTensorProduct.congr colorToRepSumEquiv) ((PiTensorProduct.tprod ‚ÑÇ)
    (HepLean.PiTensorProduct.elimPureTensor p q)) = _
  rw [PiTensorProduct.congr_tprod]
  rfl

/-- The natural isomorphism corresponding to the tensorate. -/
def Œº (X Y : OverColor Color) : colorFun.obj X ‚äó colorFun.obj Y ‚âÖ colorFun.obj (X ‚äó Y) where
  hom := {
    hom := (ŒºModEquiv X Y).toLinearMap
    comm := fun M => by
      refine HepLean.PiTensorProduct.induction_tmul (fun p q => ?_)
      simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
        OverColor.instMonoidalCategoryStruct_tensorObj_hom, Functor.id_obj, CategoryStruct.comp,
        Action.instMonoidalCategory_tensorObj_V, Action.tensor_œÅ', LinearMap.coe_comp,
        Function.comp_apply]
      change (ŒºModEquiv X Y) (((((colorFun.obj X).œÅ M) (PiTensorProduct.tprod ‚ÑÇ p)) ‚äó‚Çú[‚ÑÇ]
        (((colorFun.obj Y).œÅ M) (PiTensorProduct.tprod ‚ÑÇ q)))) = ((colorFun.obj (X ‚äó Y)).œÅ M)
        ((ŒºModEquiv X Y) ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q))
      rw [ŒºModEquiv_tmul_tprod]
      erw [obj'_œÅ_tprod, obj'_œÅ_tprod, obj'_œÅ_tprod]
      rw [ŒºModEquiv_tmul_tprod]
      apply congrArg
      funext i
      match i with
      | Sum.inl i =>
        rfl
      | Sum.inr i =>
        rfl
  }
  inv := {
    hom := (ŒºModEquiv X Y).symm.toLinearMap
    comm := fun M => by
      simp [CategoryStruct.comp]
      erw [LinearEquiv.eq_comp_toLinearMap_symm,LinearMap.comp_assoc,
      LinearEquiv.toLinearMap_symm_comp_eq]
      refine HepLean.PiTensorProduct.induction_tmul (fun p q => ?_)
      simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
        OverColor.instMonoidalCategoryStruct_tensorObj_hom, Functor.id_obj, CategoryStruct.comp,
        Action.instMonoidalCategory_tensorObj_V, Action.tensor_œÅ', LinearMap.coe_comp,
        Function.comp_apply]
      symm
      change (ŒºModEquiv X Y) (((((colorFun.obj X).œÅ M) (PiTensorProduct.tprod ‚ÑÇ p)) ‚äó‚Çú[‚ÑÇ]
        (((colorFun.obj Y).œÅ M) (PiTensorProduct.tprod ‚ÑÇ q)))) = ((colorFun.obj (X ‚äó Y)).œÅ M)
        ((ŒºModEquiv X Y) ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q))
      rw [ŒºModEquiv_tmul_tprod]
      erw [obj'_œÅ_tprod, obj'_œÅ_tprod, obj'_œÅ_tprod]
      rw [ŒºModEquiv_tmul_tprod]
      apply congrArg
      funext i
      match i with
      | Sum.inl i =>
        rfl
      | Sum.inr i =>
        rfl}
  hom_inv_id := by
    ext1
    simp only [Action.instMonoidalCategory_tensorObj_V, CategoryStruct.comp, Action.Hom.comp_hom,
      colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
      OverColor.instMonoidalCategoryStruct_tensorObj_hom, LinearEquiv.comp_coe,
      LinearEquiv.self_trans_symm, LinearEquiv.refl_toLinearMap, Action.id_hom]
    rfl
  inv_hom_id := by
    ext1
    simp only [CategoryStruct.comp, Action.instMonoidalCategory_tensorObj_V, Action.Hom.comp_hom,
      colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
      OverColor.instMonoidalCategoryStruct_tensorObj_hom, LinearEquiv.comp_coe,
      LinearEquiv.symm_trans_self, LinearEquiv.refl_toLinearMap, Action.id_hom]
    rfl

lemma Œº_tmul_tprod {X Y : OverColor Color} (p : (i : X.left) ‚Üí (colorToRep (X.hom i)))
    (q : (i : Y.left) ‚Üí (colorToRep (Y.hom i))) :
    (Œº X Y).hom.hom ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q) =
    (PiTensorProduct.tprod ‚ÑÇ) fun i =>
    (colorToRepSumEquiv i) (HepLean.PiTensorProduct.elimPureTensor p q i) := by
  exact ŒºModEquiv_tmul_tprod p q

lemma Œº_natural_left {X Y : OverColor Color} (f : X ‚ü∂ Y) (Z : OverColor Color) :
    MonoidalCategory.whiskerRight (colorFun.map f) (colorFun.obj Z) ‚â´ (Œº Y Z).hom =
    (Œº X Z).hom ‚â´ colorFun.map (MonoidalCategory.whiskerRight f Z) := by
  ext1
  refine HepLean.PiTensorProduct.induction_tmul (fun p q => ?_)
  simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom, Functor.id_obj, CategoryStruct.comp,
    Action.Hom.comp_hom, Action.instMonoidalCategory_tensorObj_V,
    Action.instMonoidalCategory_whiskerRight_hom, LinearMap.coe_comp, Function.comp_apply]
  change _ = (colorFun.map (MonoidalCategory.whiskerRight f Z)).hom
    ((Œº X Z).hom.hom ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q))
  rw [Œº_tmul_tprod]
  change _ = (colorFun.map (f ‚ñ∑ Z)).hom
    ((PiTensorProduct.tprod ‚ÑÇ) fun i => (colorToRepSumEquiv i)
    (HepLean.PiTensorProduct.elimPureTensor p q i))
  rw [colorFun.map_tprod]
  have h1 : (((colorFun.map f).hom ‚ñ∑ (colorFun.obj Z).V) ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ]
      (PiTensorProduct.tprod ‚ÑÇ) q)) = ((colorFun.map f).hom
      ((PiTensorProduct.tprod ‚ÑÇ) p) ‚äó‚Çú[‚ÑÇ] ((PiTensorProduct.tprod ‚ÑÇ) q)) := by rfl
  erw [h1]
  rw [colorFun.map_tprod]
  change (Œº Y Z).hom.hom (((PiTensorProduct.tprod ‚ÑÇ) fun i => (colorToRepCongr _)
    (p ((OverColor.Hom.toEquiv f).symm i))) ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q) = _
  rw [Œº_tmul_tprod]
  apply congrArg
  funext i
  match i with
  | Sum.inl i => rfl
  | Sum.inr i => rfl

lemma Œº_natural_right {X Y : OverColor Color} (X' : OverColor Color) (f : X ‚ü∂ Y) :
    MonoidalCategory.whiskerLeft (colorFun.obj X') (colorFun.map f) ‚â´ (Œº X' Y).hom =
    (Œº X' X).hom ‚â´ colorFun.map (MonoidalCategory.whiskerLeft X' f) := by
  ext1
  refine HepLean.PiTensorProduct.induction_tmul (fun p q => ?_)
  simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom, Functor.id_obj, CategoryStruct.comp,
    Action.Hom.comp_hom, Action.instMonoidalCategory_tensorObj_V,
    Action.instMonoidalCategory_whiskerLeft_hom, LinearMap.coe_comp, Function.comp_apply]
  change _ = (colorFun.map (X' ‚óÅ f)).hom ((Œº X' X).hom.hom
    ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q))
  rw [Œº_tmul_tprod]
  change _ = (colorFun.map (X' ‚óÅ f)).hom ((PiTensorProduct.tprod ‚ÑÇ) fun i =>
    (colorToRepSumEquiv i) (HepLean.PiTensorProduct.elimPureTensor p q i))
  rw [map_tprod]
  have h1 : (((colorFun.obj X').V ‚óÅ (colorFun.map f).hom)
      ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q))
      = ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (colorFun.map f).hom ((PiTensorProduct.tprod ‚ÑÇ) q)) := by
    rfl
  erw [h1]
  rw [map_tprod]
  change (Œº X' Y).hom.hom ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) fun i =>
    (colorToRepCongr _) (q ((OverColor.Hom.toEquiv f).symm i))) = _
  rw [Œº_tmul_tprod]
  apply congrArg
  funext i
  match i with
  | Sum.inl i => rfl
  | Sum.inr i => rfl

lemma associativity (X Y Z : OverColor Color) :
    whiskerRight (Œº X Y).hom (colorFun.obj Z) ‚â´
    (Œº (X ‚äó Y) Z).hom ‚â´ colorFun.map (associator X Y Z).hom =
    (associator (colorFun.obj X) (colorFun.obj Y) (colorFun.obj Z)).hom ‚â´
    whiskerLeft (colorFun.obj X) (Œº Y Z).hom ‚â´ (Œº X (Y ‚äó Z)).hom := by
  ext1
  refine HepLean.PiTensorProduct.induction_assoc' (fun p q m => ?_)
  simp only [colorFun_obj_V_carrier, OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom, Functor.id_obj, CategoryStruct.comp,
    Action.Hom.comp_hom, Action.instMonoidalCategory_tensorObj_V,
    Action.instMonoidalCategory_whiskerRight_hom, LinearMap.coe_comp, Function.comp_apply,
    Action.instMonoidalCategory_whiskerLeft_hom, Action.instMonoidalCategory_associator_hom_hom]
  change (colorFun.map (Œ±_ X Y Z).hom).hom ((Œº (X ‚äó Y) Z).hom.hom
    ((((Œº X Y).hom.hom ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ]
    (PiTensorProduct.tprod ‚ÑÇ) q)) ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) m))) =
    (Œº X (Y ‚äó Z)).hom.hom ((((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] ((Œº Y Z).hom.hom
    ((PiTensorProduct.tprod ‚ÑÇ) q ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) m)))))
  rw [Œº_tmul_tprod, Œº_tmul_tprod]
  change (colorFun.map (Œ±_ X Y Z).hom).hom ((Œº (X ‚äó Y) Z).hom.hom
    (((PiTensorProduct.tprod ‚ÑÇ) fun i => (colorToRepSumEquiv i)
    (HepLean.PiTensorProduct.elimPureTensor p q i)) ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) m)) =
    (Œº X (Y ‚äó Z)).hom.hom ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) fun i =>
    (colorToRepSumEquiv i) (HepLean.PiTensorProduct.elimPureTensor q m i))
  rw [Œº_tmul_tprod, Œº_tmul_tprod]
  erw [map_tprod]
  apply congrArg
  funext i
  match i with
  | Sum.inl i => rfl
  | Sum.inr (Sum.inl i) => rfl
  | Sum.inr (Sum.inr i) => rfl

lemma left_unitality (X : OverColor Color) : (leftUnitor (colorFun.obj X)).hom =
    whiskerRight colorFun.Œµ.hom (colorFun.obj X) ‚â´
    (Œº (ùüô_ (OverColor Color)) X).hom ‚â´ colorFun.map (leftUnitor X).hom := by
  ext1
  apply HepLean.PiTensorProduct.induction_mod_tmul (fun x q => ?_)
  simp only [colorFun_obj_V_carrier, Equivalence.symm_inverse,
    Action.functorCategoryEquivalence_functor, Action.FunctorCategoryEquivalence.functor_obj_obj,
    Action.instMonoidalCategory_tensorUnit_V, Functor.id_obj,
    Action.instMonoidalCategory_leftUnitor_hom_hom, CategoryStruct.comp, Action.Hom.comp_hom,
    Action.instMonoidalCategory_tensorObj_V, OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorUnit_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom,
    Action.instMonoidalCategory_whiskerRight_hom, LinearMap.coe_comp, Function.comp_apply]
  change TensorProduct.lid ‚ÑÇ (colorFun.obj X) (x ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q) =
    (colorFun.map (Œª_ X).hom).hom ((Œº (ùüô_ (OverColor Color)) X).hom.hom
    ((((PiTensorProduct.isEmptyEquiv Empty).symm x) ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q)))
  simp [PiTensorProduct.isEmptyEquiv]
  rw [TensorProduct.smul_tmul, TensorProduct.tmul_smul]
  erw [LinearMap.map_smul, LinearMap.map_smul]
  apply congrArg
  change _ = (colorFun.map (Œª_ X).hom).hom ((Œº (ùüô_ (OverColor Color)) X).hom.hom
    ((PiTensorProduct.tprod ‚ÑÇ) _ ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) q))
  rw [Œº_tmul_tprod]
  erw [map_tprod]
  rfl

lemma right_unitality (X : OverColor Color) : (MonoidalCategory.rightUnitor (colorFun.obj X)).hom =
    whiskerLeft (colorFun.obj X) Œµ.hom ‚â´
    (Œº X (ùüô_ (OverColor Color))).hom ‚â´ colorFun.map (MonoidalCategory.rightUnitor X).hom := by
  ext1
  apply HepLean.PiTensorProduct.induction_tmul_mod (fun p x => ?_)
  simp only [colorFun_obj_V_carrier, Functor.id_obj, Equivalence.symm_inverse,
    Action.functorCategoryEquivalence_functor, Action.FunctorCategoryEquivalence.functor_obj_obj,
    Action.instMonoidalCategory_tensorUnit_V, Action.instMonoidalCategory_rightUnitor_hom_hom,
    CategoryStruct.comp, Action.Hom.comp_hom, Action.instMonoidalCategory_tensorObj_V,
    OverColor.instMonoidalCategoryStruct_tensorObj_left,
    OverColor.instMonoidalCategoryStruct_tensorUnit_left,
    OverColor.instMonoidalCategoryStruct_tensorObj_hom, Action.instMonoidalCategory_whiskerLeft_hom,
    LinearMap.coe_comp, Function.comp_apply]
  change TensorProduct.rid ‚ÑÇ (colorFun.obj X) ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] x) =
    (colorFun.map (œÅ_ X).hom).hom ((Œº X (ùüô_ (OverColor Color))).hom.hom
    ((((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] ((PiTensorProduct.isEmptyEquiv Empty).symm x)))))
  simp [PiTensorProduct.isEmptyEquiv]
  erw [LinearMap.map_smul, LinearMap.map_smul]
  apply congrArg
  change _ = (colorFun.map (œÅ_ X).hom).hom ((Œº X (ùüô_ (OverColor Color))).hom.hom
    ((PiTensorProduct.tprod ‚ÑÇ) p ‚äó‚Çú[‚ÑÇ] (PiTensorProduct.tprod ‚ÑÇ) _))
  rw [Œº_tmul_tprod]
  erw [map_tprod]
  rfl

end colorFun

/-- The monoidal functor between `OverColor Color` and `Rep ‚ÑÇ SL(2, ‚ÑÇ)` taking a map of colors
  to the corresponding tensor product representation. -/
def colorFunMon : MonoidalFunctor (OverColor Color) (Rep ‚ÑÇ SL(2, ‚ÑÇ)) where
  toFunctor := colorFun
  Œµ := colorFun.Œµ.hom
  Œº X Y := (colorFun.Œº X Y).hom
  Œº_natural_left := colorFun.Œº_natural_left
  Œº_natural_right := colorFun.Œº_natural_right
  associativity := colorFun.associativity
  left_unitality := colorFun.left_unitality
  right_unitality := colorFun.right_unitality

end
end Fermion
