/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license.
Authors: Joseph Tooby-Smith
-/
import HepLean.StandardModel.Basic
import Mathlib.Data.Complex.Exponential
import Mathlib.Tactic.Polyrith
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.RepresentationTheory.Basic
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Geometry.Manifold.ContMDiff.Product
import Mathlib.Analysis.Complex.RealDeriv
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Pow
import Mathlib.Algebra.QuadraticDiscriminant
/-!
# The Higgs field

This file defines the basic properties for the higgs field in the standard model.

## References

- We use conventions given in: https://pdg.lbl.gov/2019/reviews/rpp2019-rev-higgs-boson.pdf

-/
universe v u
namespace StandardModel
noncomputable section

open Manifold
open Matrix
open Complex
open ComplexConjugate

/-- The complex vector space in which the Higgs field takes values. -/
abbrev higgsVec := EuclideanSpace â„‚ (Fin 2)

/-- The trivial vector bundle ğ“¡Â² Ã— â„‚Â². (TODO: Make associated bundle.) -/
abbrev higgsBundle := Bundle.Trivial spaceTime higgsVec

instance : SmoothVectorBundle higgsVec higgsBundle (ğ“¡ 4)  :=
  Bundle.Trivial.smoothVectorBundle higgsVec ğ“˜(â„, spaceTime)

/-- A higgs field is a smooth section of the higgs bundle. -/
abbrev higgsField : Type := SmoothSection (ğ“¡ 4) higgsVec higgsBundle

instance : NormedAddCommGroup (Fin 2 â†’ â„‚) := by
  exact Pi.normedAddCommGroup



section higgsVec

/-- The continous linear map from the vector space `higgsVec` to `(Fin 2 â†’ â„‚)` acheived by
casting vectors. -/
def higgsVecToFin2â„‚ : higgsVec â†’L[â„] (Fin 2 â†’ â„‚) where
  toFun x := x
  map_add' x y := by
    simp
  map_smul' a x := by
    simp

lemma smooth_higgsVecToFin2â„‚ : Smooth ğ“˜(â„, higgsVec) ğ“˜(â„, Fin 2 â†’ â„‚) higgsVecToFin2â„‚ :=
  ContinuousLinearMap.smooth higgsVecToFin2â„‚

/-- Given an element of `gaugeGroup` the linear automorphism of `higgsVec` it gets taken to. -/
@[simps!]
noncomputable def higgsRepMap (g : guageGroup) : higgsVec â†’â‚—[â„‚] higgsVec where
  toFun S :=  (g.2.2 ^ 3) â€¢ (g.2.1.1 *áµ¥ S)
  map_add' S T := by
    simp [Matrix.mulVec_add, smul_add]
    rw [Matrix.mulVec_add, smul_add]
  map_smul' a S := by
    simp [Matrix.mulVec_smul]
    rw [Matrix.mulVec_smul]
    exact smul_comm  _ _ _


/-- The representation of the SM guage group acting on `â„‚Â²`. -/
noncomputable def higgsRep : Representation â„‚ guageGroup higgsVec where
  toFun := higgsRepMap
  map_mul' U V := by
    apply LinearMap.ext
    intro S
    simp only [higgsRepMap, Prod.snd_mul, Submonoid.coe_inf, Prod.fst_mul, Submonoid.coe_mul,
      LinearMap.coe_mk, AddHom.coe_mk, LinearMap.mul_apply, LinearMap.map_smul_of_tower,
      mulVec_mulVec]
    simp  [mul_pow, smul_smul, mul_comm]
  map_one' := by
    apply LinearMap.ext
    intro S
    simp only [higgsRepMap, LinearMap.mul_apply, AddHom.coe_mk, LinearMap.coe_mk]
    change 1 ^ 3 â€¢ (1 *áµ¥ _) = _
    rw [one_pow, Matrix.one_mulVec]
    simp only [one_smul, LinearMap.one_apply]

namespace higgsVec

/-- Given a vector `â„‚Â²` the constant higgs field with value equal to that
section. -/
noncomputable def toField (Ï† : higgsVec) : higgsField where
  toFun := fun _ => Ï†
  contMDiff_toFun := by
    intro x
    rw [Bundle.contMDiffAt_section]
    exact smoothAt_const

/-- The higgs potential for `higgsVec`, i.e. for constant higgs fields. -/
def potential (Î¼Sq lambda : â„) (Ï† : higgsVec) : â„ := - Î¼Sq  * â€–Ï†â€– ^ 2  +
  lambda * â€–Ï†â€– ^ 4

lemma potential_snd_term_nonneg {lambda : â„} (hLam : 0 < lambda) (Ï† : higgsVec) :
    0 â‰¤ lambda * â€–Ï†â€– ^ 4 := by
  rw [mul_nonneg_iff]
  apply Or.inl
  simp_all only [ge_iff_le, norm_nonneg, pow_nonneg, and_true]
  exact le_of_lt hLam

lemma potential_as_quad (Î¼Sq lambda : â„) (Ï† : higgsVec) :
    lambda  * â€–Ï†â€– ^ 2 * â€–Ï†â€– ^ 2 + (- Î¼Sq ) * â€–Ï†â€– ^ 2 + (- potential Î¼Sq lambda Ï† ) = 0 := by
  simp [potential]
  ring

lemma zero_le_potential_discrim (Î¼Sq lambda : â„) (Ï† : higgsVec) (hLam : 0 < lambda) :
    0 â‰¤ discrim (lambda ) (- Î¼Sq ) (- potential Î¼Sq lambda Ï†) := by
  have h1 := potential_as_quad Î¼Sq lambda Ï†
  rw [quadratic_eq_zero_iff_discrim_eq_sq] at h1
  rw [h1]
  exact sq_nonneg (2 * (lambda ) * â€–Ï†â€– ^ 2 + -Î¼Sq)
  simp only [ne_eq, div_eq_zero_iff, OfNat.ofNat_ne_zero, or_false]
  exact ne_of_gt hLam


lemma potential_eq_zero_sol (Î¼Sq lambda : â„) (hLam : 0 < lambda)(Ï† : higgsVec)
    (hV : potential Î¼Sq lambda Ï† = 0) : Ï† = 0 âˆ¨ â€–Ï†â€– ^ 2 = Î¼Sq / lambda := by
  have h1 := potential_as_quad Î¼Sq lambda Ï†
  rw [hV] at h1
  have h2 : â€–Ï†â€– ^ 2 * (lambda  * â€–Ï†â€– ^ 2  + -Î¼Sq ) = 0 := by
    linear_combination h1
  simp at h2
  cases' h2 with h2 h2
  simp_all
  apply Or.inr
  field_simp at h2 âŠ¢
  ring_nf
  linear_combination h2

lemma potential_eq_zero_sol_of_Î¼Sq_nonpos {Î¼Sq lambda : â„} (hLam : 0 < lambda) (hÎ¼Sq : Î¼Sq â‰¤ 0)
    (Ï† : higgsVec)  (hV : potential Î¼Sq lambda Ï† = 0) : Ï† = 0 := by
  cases' (potential_eq_zero_sol Î¼Sq lambda hLam Ï† hV) with h1 h1
  exact h1
  by_cases hÎ¼SqZ : Î¼Sq = 0
  simpa [hÎ¼SqZ] using h1
  refine ((?_ : Â¬ 0 â‰¤  Î¼Sq / lambda) (?_)).elim
  Â· simp_all [div_nonneg_iff]
    intro h
    exact lt_imp_lt_of_le_imp_le (fun _ => h) (lt_of_le_of_ne hÎ¼Sq hÎ¼SqZ)
  Â· rw [â† h1]
    exact sq_nonneg â€–Ï†â€–

lemma potential_bounded_below (Î¼Sq lambda : â„) (hLam : 0 < lambda) (Ï† : higgsVec) :
    - Î¼Sq ^ 2 / (4 * lambda) â‰¤ potential Î¼Sq lambda Ï†  := by
  have h1 := zero_le_potential_discrim Î¼Sq lambda Ï† hLam
  simp [discrim] at h1
  ring_nf at h1
  rw [â† neg_le_iff_add_nonneg'] at h1
  have h3 : lambda * potential Î¼Sq lambda Ï† * 4 = (4 * lambda) * potential Î¼Sq lambda Ï† := by
    ring
  rw [h3] at h1
  have h2 :=  (div_le_iff' (by simp [hLam] : 0 < 4 * lambda )).mpr h1
  ring_nf at h2 âŠ¢
  exact h2

lemma potential_bounded_below_of_Î¼Sq_nonpos {Î¼Sq lambda : â„} (hLam : 0 < lambda)
    (hÎ¼Sq : Î¼Sq â‰¤ 0) (Ï† : higgsVec) : 0 â‰¤ potential Î¼Sq lambda Ï† := by
  simp only [potential, neg_mul, add_zero]
  refine add_nonneg ?_ (potential_snd_term_nonneg hLam Ï†)
  field_simp
  rw [@mul_nonpos_iff]
  simp_all only [ge_iff_le, norm_nonneg, pow_nonneg, and_self, or_true]


lemma potential_eq_bound_discrim_zero (Î¼Sq lambda : â„) (hLam : 0 < lambda)(Ï† : higgsVec)
    (hV : potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda)) :
    discrim (lambda) (- Î¼Sq) (- potential Î¼Sq lambda Ï†) = 0 := by
  simp [discrim, hV]
  field_simp
  ring

lemma potential_eq_bound_higgsVec_sq (Î¼Sq lambda : â„) (hLam : 0 < lambda)(Ï† : higgsVec)
    (hV : potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda)) :
    â€–Ï†â€– ^ 2 = Î¼Sq / (2 * lambda) := by
  have h1 := potential_as_quad Î¼Sq lambda Ï†
  rw [quadratic_eq_zero_iff_of_discrim_eq_zero _
    (potential_eq_bound_discrim_zero Î¼Sq lambda hLam Ï† hV)] at h1
  rw [h1]
  field_simp
  ring_nf
  simp only [ne_eq, div_eq_zero_iff, OfNat.ofNat_ne_zero, or_false]
  exact ne_of_gt hLam

lemma potential_eq_bound_iff (Î¼Sq lambda : â„) (hLam : 0 < lambda)(Ï† : higgsVec) :
    potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda) â†” â€–Ï†â€– ^ 2 = Î¼Sq / (2 * lambda) := by
  apply Iff.intro
  Â· intro h
    exact potential_eq_bound_higgsVec_sq Î¼Sq lambda hLam Ï† h
  Â· intro h
    have hv : â€–Ï†â€–  ^ 4 = â€–Ï†â€– ^ 2 * â€–Ï†â€– ^ 2 := by
      ring_nf
    field_simp [potential, hv, h]
    ring

lemma potential_eq_bound_iff_of_Î¼Sq_nonpos {Î¼Sq lambda : â„} (hLam : 0 < lambda)
    (hÎ¼Sq : Î¼Sq â‰¤ 0) (Ï† : higgsVec) : potential Î¼Sq lambda Ï† = 0 â†” Ï† = 0 := by
  apply Iff.intro
  Â· intro h
    exact potential_eq_zero_sol_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï† h
  Â· intro h
    simp [potential, h]

lemma potential_eq_bound_IsMinOn (Î¼Sq lambda : â„) (hLam : 0 < lambda)  (Ï† : higgsVec)
    (hv : potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda)) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† := by
  rw [isMinOn_univ_iff]
  intro x
  rw [hv]
  exact potential_bounded_below Î¼Sq lambda hLam x

lemma potential_eq_bound_IsMinOn_of_Î¼Sq_nonpos {Î¼Sq lambda : â„} (hLam : 0 < lambda)
    (hÎ¼Sq : Î¼Sq â‰¤ 0) (Ï† : higgsVec) (hv : potential Î¼Sq lambda Ï† = 0) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† := by
  rw [isMinOn_univ_iff]
  intro x
  rw [hv]
  exact potential_bounded_below_of_Î¼Sq_nonpos hLam hÎ¼Sq x

lemma potential_bound_reached_of_Î¼Sq_nonneg {Î¼Sq lambda : â„} (hLam : 0 < lambda) (hÎ¼Sq : 0 â‰¤ Î¼Sq) :
    âˆƒ (Ï† : higgsVec), potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda) := by
  use ![âˆš(Î¼Sq/(2 * lambda)), 0]
  refine (potential_eq_bound_iff Î¼Sq lambda hLam _).mpr ?_
  simp [@PiLp.norm_sq_eq_of_L2, Fin.sum_univ_two]
  field_simp [mul_pow]

lemma IsMinOn_potential_iff_of_Î¼Sq_nonneg {Î¼Sq lambda : â„} (hLam : 0 < lambda) (hÎ¼Sq : 0 â‰¤ Î¼Sq) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† â†” â€–Ï†â€– ^ 2 = Î¼Sq /(2 * lambda) := by
  apply Iff.intro
  Â· intro h
    obtain âŸ¨Ï†m, hÏ†âŸ© := potential_bound_reached_of_Î¼Sq_nonneg hLam hÎ¼Sq
    have hm := isMinOn_univ_iff.mp h Ï†m
    rw [hÏ†] at hm
    have h1 := potential_bounded_below Î¼Sq lambda hLam Ï†
    rw [â† potential_eq_bound_iff Î¼Sq lambda hLam Ï†]
    exact (Real.partialOrder.le_antisymm _ _ h1 hm).symm
  Â· intro h
    rw [â† potential_eq_bound_iff Î¼Sq lambda hLam Ï†] at h
    exact potential_eq_bound_IsMinOn Î¼Sq lambda hLam Ï† h


lemma IsMinOn_potential_iff_of_Î¼Sq_nonpos {Î¼Sq lambda : â„} (hLam : 0 < lambda) (hÎ¼Sq : Î¼Sq â‰¤ 0) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† â†” Ï† = 0 := by
  apply Iff.intro
  Â· intro h
    have h0 := isMinOn_univ_iff.mp h 0
    rw [(potential_eq_bound_iff_of_Î¼Sq_nonpos hLam hÎ¼Sq 0).mpr (by rfl)] at h0
    have h1 := potential_bounded_below_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï†
    rw [â† (potential_eq_bound_iff_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï†)]
    exact (Real.partialOrder.le_antisymm _ _ h1 h0).symm
  Â· intro h
    rw [â† potential_eq_bound_iff_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï†] at h
    exact potential_eq_bound_IsMinOn_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï† h

/-- Given a Higgs vector, a rotation matrix which puts the fst component of the
vector to zero, and the snd componenet to a real -/
def rotateMatrix (Ï† : higgsVec) : Matrix (Fin 2) (Fin 2) â„‚ :=
  ![![Ï† 1 /â€–Ï†â€– , - Ï† 0 /â€–Ï†â€–], ![conj (Ï† 0) / â€–Ï†â€– , conj (Ï† 1) / â€–Ï†â€–] ]

lemma rotateMatrix_star (Ï† : higgsVec) :
    star Ï†.rotateMatrix =
    ![![conj (Ï† 1) /â€–Ï†â€– ,  Ï† 0 /â€–Ï†â€–], ![- conj (Ï† 0) / â€–Ï†â€– , Ï† 1 / â€–Ï†â€–] ] := by
  simp [star]
  rw [rotateMatrix, conjTranspose]
  ext i j
  fin_cases i <;> fin_cases j <;> simp [conj_ofReal]


lemma rotateMatrix_det {Ï† : higgsVec} (hÏ† : Ï† â‰  0) : (rotateMatrix Ï†).det = 1 := by
  simp [rotateMatrix, det_fin_two]
  have h1 : (â€–Ï†â€– : â„‚)  â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
  field_simp
  rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
  simp only [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
    Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm, add_comm]
  rfl

lemma rotateMatrix_unitary {Ï† : higgsVec} (hÏ† : Ï† â‰  0) :
    (rotateMatrix Ï†) âˆˆ unitaryGroup (Fin 2) â„‚ := by
  rw [mem_unitaryGroup_iff', rotateMatrix_star, rotateMatrix]
  erw [mul_fin_two, one_fin_two]
  have : (â€–Ï†â€– : â„‚)  â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
  congr
  field_simp
  ext i j
  fin_cases i <;> fin_cases j <;> field_simp
  Â· rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
    simp only [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
      Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm, add_comm]
    rfl
  Â· ring_nf
  Â· ring_nf
  Â· rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
    simp only [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
      Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm]
    rfl

lemma rotateMatrix_specialUnitary {Ï† : higgsVec} (hÏ† : Ï† â‰  0) :
    (rotateMatrix Ï†) âˆˆ specialUnitaryGroup (Fin 2) â„‚ :=
  mem_specialUnitaryGroup_iff.mpr âŸ¨rotateMatrix_unitary hÏ†, rotateMatrix_det hÏ†âŸ©

/-- Given a Higgs vector, an element of the gauge group which puts the fst component of the
vector to zero, and the snd componenet to a real -/
def rotateGuageGroup {Ï† : higgsVec} (hÏ† : Ï† â‰  0) : guageGroup :=
    âŸ¨1, âŸ¨(rotateMatrix Ï†), rotateMatrix_specialUnitary hÏ†âŸ©, 1âŸ©

lemma rotateGuageGroup_apply {Ï† : higgsVec} (hÏ† : Ï† â‰  0) :
    higgsRep (rotateGuageGroup hÏ†) Ï† = ![0, ofReal â€–Ï†â€–] := by
  simp [higgsRep, higgsRepMap, rotateGuageGroup, rotateMatrix, higgsRepMap]
  ext i
  fin_cases i
  simp [mulVec, vecHead, vecTail]
  ring_nf
  simp only [Fin.mk_one, Fin.isValue, cons_val_one, head_cons]
  simp [mulVec, vecHead, vecTail]
  have : (â€–Ï†â€– : â„‚)  â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
  field_simp
  rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
  simp only [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
      Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm]
  rfl

theorem rotate_fst_zero_snd_real (Ï† : higgsVec) :
    âˆƒ (g : guageGroup), higgsRep g Ï† = ![0, ofReal â€–Ï†â€–] := by
  by_cases h : Ï† = 0
  Â· use âŸ¨1, 1, 1âŸ©
    simp [h, higgsRep, higgsRepMap]
    ext i
    fin_cases i <;> rfl
  Â· use rotateGuageGroup h
    exact rotateGuageGroup_apply h

end higgsVec
end higgsVec

namespace higgsField
open  Complex Real

/-- Given a `higgsField`, the corresponding map from `spaceTime` to `higgsVec`. -/
def toHiggsVec (Ï† : higgsField) : spaceTime â†’ higgsVec := Ï†


lemma toHiggsVec_smooth (Ï† : higgsField) : Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, higgsVec) Ï†.toHiggsVec := by
  intro x0
  have h1 := Ï†.contMDiff x0
  rw [Bundle.contMDiffAt_section] at h1
  have h2 :
    (fun x => ((trivializationAt higgsVec (Bundle.Trivial spaceTime higgsVec) x0)
    { proj := x, snd := Ï† x }).2) = Ï† := by
    rfl
  simp only [h2] at h1
  exact h1

lemma toField_toHiggsVec_apply (Ï† : higgsField) (x : spaceTime) :
    (Ï†.toHiggsVec x).toField x = Ï† x := by
  rfl

lemma higgsVecToFin2â„‚_toHiggsVec (Ï† : higgsField) : higgsVecToFin2â„‚ âˆ˜ Ï†.toHiggsVec = Ï† := by
  ext x
  rfl

lemma toVec_smooth (Ï† : higgsField) : Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, Fin 2 â†’ â„‚) Ï† := by
  rw [â† Ï†.higgsVecToFin2â„‚_toHiggsVec]
  exact Smooth.comp smooth_higgsVecToFin2â„‚ (Ï†.toHiggsVec_smooth)

lemma apply_smooth (Ï† : higgsField) :
    âˆ€ i, Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, â„‚) (fun (x : spaceTime) => (Ï† x i)) := by
  rw [â† smooth_pi_space]
  exact Ï†.toVec_smooth

lemma apply_re_smooth (Ï† : higgsField) (i : Fin 2):
    Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, â„) (reCLM âˆ˜ (fun (x : spaceTime) =>  (Ï† x i))) :=
  Smooth.comp (ContinuousLinearMap.smooth reCLM) (Ï†.apply_smooth i)

lemma apply_im_smooth (Ï† : higgsField) (i : Fin 2):
    Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, â„) (imCLM âˆ˜ (fun (x : spaceTime) =>  (Ï† x i))) :=
  Smooth.comp (ContinuousLinearMap.smooth imCLM) (Ï†.apply_smooth i)

/-- Given a `higgsField`, the map `spaceTime â†’ â„` obtained by taking the square norm of the
 higgs vector.  -/
@[simp]
def normSq (Ï† : higgsField) : spaceTime â†’ â„ := fun x => ( â€–Ï† xâ€– ^ 2)

lemma toHiggsVec_norm (Ï† : higgsField) (x : spaceTime) :
    â€–Ï† xâ€–  = â€–Ï†.toHiggsVec xâ€– := rfl

lemma normSq_expand (Ï† : higgsField)  :
    Ï†.normSq  = fun x => (conj (Ï† x 0) * (Ï† x 0) + conj (Ï† x 1) * (Ï† x 1) ).re := by
  funext x
  simp only [normSq, add_re, mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
  rw [@norm_sq_eq_inner â„‚]
  simp

lemma normSq_smooth (Ï† : higgsField) : Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, â„) Ï†.normSq := by
  rw [normSq_expand]
  refine Smooth.add ?_ ?_
  simp only [mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
  refine Smooth.add ?_ ?_
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_re_smooth 0
  exact Ï†.apply_re_smooth 0
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_im_smooth 0
  exact Ï†.apply_im_smooth 0
  simp only [mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
  refine Smooth.add ?_ ?_
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_re_smooth 1
  exact Ï†.apply_re_smooth 1
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_im_smooth 1
  exact Ï†.apply_im_smooth 1

lemma normSq_nonneg (Ï† : higgsField) (x : spaceTime) : 0 â‰¤ Ï†.normSq x := by
  simp only [normSq, ge_iff_le, norm_nonneg, pow_nonneg]

lemma normSq_zero (Ï† : higgsField) (x : spaceTime) : Ï†.normSq x = 0 â†” Ï† x = 0 := by
  simp only [normSq, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff, norm_eq_zero]

/-- The Higgs potential of the form `- Î¼Â² * |Ï†|Â² + Î» * |Ï†|â´`. -/
@[simp]
def potential (Ï† : higgsField) (Î¼Sq lambda : â„ ) (x : spaceTime) :  â„ :=
  - Î¼Sq * Ï†.normSq x + lambda * Ï†.normSq x * Ï†.normSq x

lemma potential_smooth (Ï† : higgsField) (Î¼Sq lambda : â„) :
    Smooth ğ“˜(â„, spaceTime) ğ“˜(â„, â„) (fun x => Ï†.potential Î¼Sq lambda x) := by
  simp only [potential, normSq, neg_mul]
  exact Smooth.add
    (Smooth.neg (Smooth.smul smooth_const Ï†.normSq_smooth))
    (Smooth.smul (Smooth.smul smooth_const Ï†.normSq_smooth) Ï†.normSq_smooth)


lemma potential_apply (Ï† : higgsField) (Î¼Sq lambda : â„) (x : spaceTime) :
    (Ï†.potential Î¼Sq lambda) x = higgsVec.potential Î¼Sq lambda (Ï†.toHiggsVec x) := by
  simp [higgsVec.potential, toHiggsVec_norm]
  ring


/-- A higgs field is constant if it is equal for all `x` `y` in `spaceTime`. -/
def isConst (Î¦ : higgsField) : Prop := âˆ€ x y, Î¦ x = Î¦ y

lemma isConst_of_higgsVec (Ï† : higgsVec) : Ï†.toField.isConst := by
  intro x _
  simp [higgsVec.toField]

lemma isConst_iff_of_higgsVec (Î¦ : higgsField) : Î¦.isConst â†” âˆƒ (Ï† : higgsVec), Î¦ = Ï†.toField := by
  apply Iff.intro
  intro h
  use Î¦ 0
  ext x y
  rw [â† h x 0]
  rfl
  intro h
  intro x y
  obtain âŸ¨Ï†, hÏ†âŸ© := h
  subst hÏ†
  rfl

lemma normSq_of_higgsVec (Ï† : higgsVec) : Ï†.toField.normSq = fun x => (norm Ï†) ^ 2 := by
  simp only [normSq, higgsVec.toField]
  funext x
  simp

lemma potential_of_higgsVec (Ï† : higgsVec) (Î¼Sq lambda : â„) :
    Ï†.toField.potential Î¼Sq lambda = fun _ => higgsVec.potential Î¼Sq lambda Ï† := by
  simp [higgsVec.potential]
  unfold potential
  rw [normSq_of_higgsVec]
  funext x
  simp only [neg_mul, add_right_inj]
  ring_nf



end higgsField
end
end StandardModel
