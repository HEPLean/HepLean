/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license.
Authors: Joseph Tooby-Smith
-/
import HepLean.StandardModel.Basic
import HepLean.StandardModel.Representations
import Mathlib.Data.Complex.Exponential
import Mathlib.Tactic.Polyrith
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.RepresentationTheory.Basic
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Geometry.Manifold.ContMDiff.Product
import Mathlib.Algebra.QuadraticDiscriminant
/-!
# The Higgs vector space

This file defines the target vector space of the Higgs boson, the potential on it,
and the representation of the SM gauge group acting on it.

This file is a import of `SM.HiggsBoson.Basic`.

## References

- We use conventions given in: https://pdg.lbl.gov/2019/reviews/rpp2019-rev-higgs-boson.pdf

-/
universe v u
namespace StandardModel
noncomputable section

open Manifold
open Matrix
open Complex
open ComplexConjugate

/-- The complex vector space in which the Higgs field takes values. -/
abbrev higgsVec := EuclideanSpace â„‚ (Fin 2)

section higgsVec

/-- The continuous linear map from the vector space `higgsVec` to `(Fin 2 â†’ â„‚)` achieved by
casting vectors. -/
def higgsVecToFin2â„‚ : higgsVec â†’L[â„] (Fin 2 â†’ â„‚) where
  toFun x := x
  map_add' x y := by simp
  map_smul' a x := by simp

lemma smooth_higgsVecToFin2â„‚ : Smooth ğ“˜(â„, higgsVec) ğ“˜(â„, Fin 2 â†’ â„‚) higgsVecToFin2â„‚ :=
  ContinuousLinearMap.smooth higgsVecToFin2â„‚

namespace higgsVec

/-- The Higgs representation as a homomorphism from the gauge group to unitary `2Ã—2`-matrices. -/
@[simps!]
noncomputable def higgsRepUnitary : gaugeGroup â†’* unitaryGroup (Fin 2) â„‚ where
  toFun g := repU1 g.2.2 * fundamentalSU2 g.2.1
  map_mul'  := by
    intro âŸ¨_, a2, a3âŸ© âŸ¨_, b2, b3âŸ©
    change repU1 (a3 * b3) *  fundamentalSU2 (a2 * b2) = _
    rw [repU1.map_mul, fundamentalSU2.map_mul, mul_assoc, mul_assoc,
      â† mul_assoc (repU1 b3) _ _, repU1_fundamentalSU2_commute]
    repeat rw [mul_assoc]
  map_one' := by simp

/-- An orthonormal basis of higgsVec. -/
noncomputable def orthonormBasis : OrthonormalBasis (Fin 2) â„‚ higgsVec :=
  EuclideanSpace.basisFun (Fin 2) â„‚

/-- Takes in a `2Ã—2`-matrix and returns a linear map of `higgsVec`. -/
noncomputable def matrixToLin : Matrix (Fin 2) (Fin 2) â„‚ â†’* (higgsVec â†’L[â„‚] higgsVec) where
  toFun g := LinearMap.toContinuousLinearMap
    $ Matrix.toLin orthonormBasis.toBasis orthonormBasis.toBasis g
  map_mul' g h := ContinuousLinearMap.coe_inj.mp $
    Matrix.toLin_mul orthonormBasis.toBasis orthonormBasis.toBasis orthonormBasis.toBasis g h
  map_one' := ContinuousLinearMap.coe_inj.mp $ Matrix.toLin_one orthonormBasis.toBasis

lemma matrixToLin_star (g : Matrix (Fin 2) (Fin 2) â„‚) :
    matrixToLin (star g) = star (matrixToLin g) :=
  ContinuousLinearMap.coe_inj.mp $ Matrix.toLin_conjTranspose orthonormBasis orthonormBasis g

lemma matrixToLin_unitary (g : unitaryGroup (Fin 2) â„‚) :
    matrixToLin g âˆˆ unitary (higgsVec â†’L[â„‚] higgsVec) := by
  rw [@unitary.mem_iff, â† matrixToLin_star, â† matrixToLin.map_mul, â† matrixToLin.map_mul,
    mem_unitaryGroup_iff.mp g.prop, mem_unitaryGroup_iff'.mp g.prop, matrixToLin.map_one]
  simp

/-- The natural homomorphism from unitary `2Ã—2` complex matrices to unitary transformations
of `higgsVec`. -/
noncomputable def unitaryToLin : unitaryGroup (Fin 2) â„‚ â†’* unitary (higgsVec â†’L[â„‚] higgsVec) where
  toFun g := âŸ¨matrixToLin g, matrixToLin_unitary gâŸ©
  map_mul' g h := by simp
  map_one' := by simp

/-- The inclusion of unitary transformations on `higgsVec` into all linear transformations. -/
@[simps!]
def unitToLinear : unitary (higgsVec â†’L[â„‚] higgsVec) â†’* higgsVec â†’â‚—[â„‚] higgsVec :=
  DistribMulAction.toModuleEnd â„‚ higgsVec

/-- The representation of the gauge group acting on `higgsVec`. -/
@[simps!]
def rep : Representation â„‚ gaugeGroup higgsVec :=
   unitToLinear.comp (unitaryToLin.comp higgsRepUnitary)

lemma higgsRepUnitary_mul (g : gaugeGroup) (Ï† : higgsVec) :
    (higgsRepUnitary g).1 *áµ¥ Ï† = g.2.2 ^ 3 â€¢ (g.2.1.1 *áµ¥ Ï†) := by
  simp [higgsRepUnitary_apply_coe, smul_mulVec_assoc]

lemma rep_apply (g : gaugeGroup) (Ï† : higgsVec) : rep g Ï† = g.2.2 ^ 3 â€¢ (g.2.1.1 *áµ¥ Ï†) :=
  higgsRepUnitary_mul g Ï†

lemma norm_invariant (g : gaugeGroup) (Ï† : higgsVec) : â€–rep g Ï†â€– = â€–Ï†â€– :=
  ContinuousLinearMap.norm_map_of_mem_unitary (unitaryToLin (higgsRepUnitary g)).2 Ï†

section potentialDefn

variable (Î¼Sq lambda : â„)
local notation "Î»" => lambda

/-- The higgs potential for `higgsVec`, i.e. for constant higgs fields. -/
def potential (Ï† : higgsVec) : â„ := - Î¼Sq  * â€–Ï†â€– ^ 2 + Î» * â€–Ï†â€– ^ 4

lemma potential_invariant (Ï† : higgsVec)  (g : gaugeGroup) :
    potential Î¼Sq (Î») (rep g Ï†) = potential Î¼Sq (Î») Ï† := by
  simp only [potential, neg_mul, norm_invariant]

lemma potential_as_quad (Ï† : higgsVec) :
    Î»  * â€–Ï†â€– ^ 2 * â€–Ï†â€– ^ 2 + (- Î¼Sq ) * â€–Ï†â€– ^ 2 + (- potential Î¼Sq (Î») Ï†) = 0 := by
  simp [potential]; ring

end potentialDefn
section potentialProp

variable {lambda : â„}
variable (Î¼Sq : â„)
variable (hLam : 0 < lambda)
local notation "Î»" => lambda

lemma potential_snd_term_nonneg (Ï† : higgsVec) :
    0 â‰¤ Î» * â€–Ï†â€– ^ 4 := by
  rw [mul_nonneg_iff]
  apply Or.inl
  simp_all only [ge_iff_le, norm_nonneg, pow_nonneg, and_true]
  exact le_of_lt hLam


lemma zero_le_potential_discrim  (Ï† : higgsVec) :
    0 â‰¤ discrim (Î») (- Î¼Sq ) (- potential Î¼Sq (Î») Ï†) := by
  have h1 := potential_as_quad Î¼Sq (Î») Ï†
  rw [quadratic_eq_zero_iff_discrim_eq_sq] at h1
  Â· simp only [h1, ne_eq, div_eq_zero_iff, OfNat.ofNat_ne_zero, or_false]
    exact sq_nonneg (2 * lambda * â€–Ï†â€– ^ 2 + -Î¼Sq)
  Â· exact ne_of_gt hLam

lemma potential_eq_zero_sol (Ï† : higgsVec)
    (hV : potential Î¼Sq (Î») Ï† = 0) : Ï† = 0 âˆ¨ â€–Ï†â€– ^ 2 = Î¼Sq / Î» := by
  have h1 := potential_as_quad Î¼Sq (Î») Ï†
  rw [hV] at h1
  have h2 : â€–Ï†â€– ^ 2 * (lambda  * â€–Ï†â€– ^ 2  + -Î¼Sq ) = 0 := by
    linear_combination h1
  simp at h2
  cases' h2 with h2 h2
  simp_all
  apply Or.inr
  field_simp at h2 âŠ¢
  ring_nf
  linear_combination h2

lemma potential_eq_zero_sol_of_Î¼Sq_nonpos  (hÎ¼Sq : Î¼Sq â‰¤ 0)
    (Ï† : higgsVec)  (hV : potential Î¼Sq (Î») Ï† = 0) : Ï† = 0 := by
  cases' (potential_eq_zero_sol Î¼Sq hLam Ï† hV) with h1 h1
  exact h1
  by_cases hÎ¼SqZ : Î¼Sq = 0
  simpa [hÎ¼SqZ] using h1
  refine ((?_ : Â¬ 0 â‰¤  Î¼Sq / Î») (?_)).elim
  Â· simp_all [div_nonneg_iff]
    intro h
    exact lt_imp_lt_of_le_imp_le (fun _ => h) (lt_of_le_of_ne hÎ¼Sq hÎ¼SqZ)
  Â· rw [â† h1]
    exact sq_nonneg â€–Ï†â€–

lemma potential_bounded_below (Ï† : higgsVec) :
    - Î¼Sq ^ 2 / (4 * (Î»)) â‰¤ potential Î¼Sq (Î») Ï†  := by
  have h1 := zero_le_potential_discrim Î¼Sq hLam Ï†
  simp [discrim] at h1
  ring_nf at h1
  rw [â† neg_le_iff_add_nonneg'] at h1
  have h3 : (Î») * potential Î¼Sq (Î») Ï† * 4 = (4 * Î») * potential Î¼Sq (Î») Ï† := by
    ring
  rw [h3] at h1
  have h2 :=  (div_le_iff' (by simp [hLam] : 0 < 4 * Î» )).mpr h1
  ring_nf at h2 âŠ¢
  exact h2

lemma potential_bounded_below_of_Î¼Sq_nonpos  {Î¼Sq : â„}
    (hÎ¼Sq : Î¼Sq â‰¤ 0) (Ï† : higgsVec) : 0 â‰¤ potential Î¼Sq (Î») Ï† := by
  refine add_nonneg ?_ (potential_snd_term_nonneg hLam Ï†)
  field_simp [mul_nonpos_iff]
  simp_all [ge_iff_le, norm_nonneg, pow_nonneg, and_self, or_true]

lemma potential_eq_bound_discrim_zero (Ï† : higgsVec)
    (hV : potential Î¼Sq (Î») Ï† = - Î¼Sq ^ 2 / (4  * Î»)) :
    discrim (Î») (- Î¼Sq) (- potential Î¼Sq (Î») Ï†) = 0 := by
  field_simp [discrim, hV]

lemma potential_eq_bound_higgsVec_sq (Ï† : higgsVec)
    (hV : potential Î¼Sq (Î») Ï† = - Î¼Sq ^ 2 / (4  * (Î»))) :
    â€–Ï†â€– ^ 2 = Î¼Sq / (2 * Î») := by
  have h1 := potential_as_quad Î¼Sq (Î») Ï†
  rw [quadratic_eq_zero_iff_of_discrim_eq_zero _
    (potential_eq_bound_discrim_zero Î¼Sq hLam Ï† hV)] at h1
  simp_rw [h1, neg_neg]
  exact ne_of_gt hLam

lemma potential_eq_bound_iff (Ï† : higgsVec) :
    potential Î¼Sq (Î») Ï† = - Î¼Sq ^ 2 / (4  * (Î»)) â†” â€–Ï†â€– ^ 2 = Î¼Sq / (2 * (Î»)) :=
  Iff.intro (potential_eq_bound_higgsVec_sq Î¼Sq hLam Ï†)
    (fun h â†¦ by
      have hv : â€–Ï†â€– ^ 4 = â€–Ï†â€– ^ 2 * â€–Ï†â€– ^ 2 := by ring_nf
      field_simp [potential, hv, h]
      ring_nf)

lemma potential_eq_bound_iff_of_Î¼Sq_nonpos  {Î¼Sq : â„}
    (hÎ¼Sq : Î¼Sq â‰¤ 0) (Ï† : higgsVec) : potential Î¼Sq (Î») Ï† = 0 â†” Ï† = 0 :=
  Iff.intro (fun h â†¦ potential_eq_zero_sol_of_Î¼Sq_nonpos Î¼Sq hLam hÎ¼Sq Ï† h)
  (fun h â†¦ by simp [potential, h])

lemma potential_eq_bound_IsMinOn (Ï† : higgsVec)
    (hv : potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda)) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† := by
  rw [isMinOn_univ_iff, hv]
  exact fun x â†¦ potential_bounded_below Î¼Sq hLam x

lemma potential_eq_bound_IsMinOn_of_Î¼Sq_nonpos  {Î¼Sq : â„}
    (hÎ¼Sq : Î¼Sq â‰¤ 0) (Ï† : higgsVec) (hv : potential Î¼Sq lambda Ï† = 0) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† := by
  rw [isMinOn_univ_iff, hv]
  exact fun x â†¦ potential_bounded_below_of_Î¼Sq_nonpos hLam hÎ¼Sq x

lemma potential_bound_reached_of_Î¼Sq_nonneg  {Î¼Sq : â„} (hÎ¼Sq : 0 â‰¤ Î¼Sq) :
    âˆƒ (Ï† : higgsVec), potential Î¼Sq lambda Ï† = - Î¼Sq ^ 2 / (4  * lambda) := by
  use ![âˆš(Î¼Sq/(2 * lambda)), 0]
  refine (potential_eq_bound_iff Î¼Sq hLam _).mpr ?_
  simp [PiLp.norm_sq_eq_of_L2]
  field_simp [mul_pow]

lemma IsMinOn_potential_iff_of_Î¼Sq_nonneg  {Î¼Sq : â„} (hÎ¼Sq : 0 â‰¤ Î¼Sq) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† â†” â€–Ï†â€– ^ 2 = Î¼Sq /(2 * lambda) := by
  apply Iff.intro <;> rw [â† potential_eq_bound_iff Î¼Sq hLam Ï†]
  Â· intro h
    obtain âŸ¨Ï†m, hÏ†âŸ© := potential_bound_reached_of_Î¼Sq_nonneg hLam hÎ¼Sq
    have hm := isMinOn_univ_iff.mp h Ï†m
    rw [hÏ†] at hm
    exact (Real.partialOrder.le_antisymm _ _ (potential_bounded_below Î¼Sq hLam Ï†) hm).symm
  Â· exact potential_eq_bound_IsMinOn Î¼Sq hLam Ï†

lemma IsMinOn_potential_iff_of_Î¼Sq_nonpos {Î¼Sq : â„} (hÎ¼Sq : Î¼Sq â‰¤ 0) :
    IsMinOn (potential Î¼Sq lambda) Set.univ Ï† â†” Ï† = 0 := by
  apply Iff.intro <;> rw [â† potential_eq_bound_iff_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï†]
  Â· intro h
    have h0 := isMinOn_univ_iff.mp h 0
    have h1 := potential_bounded_below_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï†
    rw [(potential_eq_bound_iff_of_Î¼Sq_nonpos hLam hÎ¼Sq 0).mpr (by rfl)] at h0
    exact (Real.partialOrder.le_antisymm _ _ h1 h0).symm
  Â· exact potential_eq_bound_IsMinOn_of_Î¼Sq_nonpos hLam hÎ¼Sq Ï†

end potentialProp
/-- Given a Higgs vector, a rotation matrix which puts the first component of the
vector to zero, and the second component to a real -/
def rotateMatrix (Ï† : higgsVec) : Matrix (Fin 2) (Fin 2) â„‚ :=
  ![![Ï† 1 /â€–Ï†â€– , - Ï† 0 /â€–Ï†â€–], ![conj (Ï† 0) / â€–Ï†â€– , conj (Ï† 1) / â€–Ï†â€–] ]

lemma rotateMatrix_star (Ï† : higgsVec) :
    star Ï†.rotateMatrix =
    ![![conj (Ï† 1) /â€–Ï†â€– ,  Ï† 0 /â€–Ï†â€–], ![- conj (Ï† 0) / â€–Ï†â€– , Ï† 1 / â€–Ï†â€–] ] := by
  simp_rw [star, rotateMatrix, conjTranspose]
  ext i j
  fin_cases i <;> fin_cases j <;> simp [conj_ofReal]

lemma rotateMatrix_det {Ï† : higgsVec} (hÏ† : Ï† â‰  0) : (rotateMatrix Ï†).det = 1 := by
  have h1 : (â€–Ï†â€– : â„‚)  â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
  field_simp [rotateMatrix, det_fin_two]
  rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
  simp [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
    Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm, add_comm]

lemma rotateMatrix_unitary {Ï† : higgsVec} (hÏ† : Ï† â‰  0) :
    (rotateMatrix Ï†) âˆˆ unitaryGroup (Fin 2) â„‚ := by
  rw [mem_unitaryGroup_iff', rotateMatrix_star, rotateMatrix]
  erw [mul_fin_two, one_fin_two]
  have : (â€–Ï†â€– : â„‚)  â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
  ext i j
  fin_cases i <;> fin_cases j <;> field_simp
  <;> rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
  Â· simp [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
      Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm, add_comm]
  Â· ring_nf
  Â· ring_nf
  Â· simp [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
      Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm]

lemma rotateMatrix_specialUnitary {Ï† : higgsVec} (hÏ† : Ï† â‰  0) :
    (rotateMatrix Ï†) âˆˆ specialUnitaryGroup (Fin 2) â„‚ :=
  mem_specialUnitaryGroup_iff.mpr âŸ¨rotateMatrix_unitary hÏ†, rotateMatrix_det hÏ†âŸ©

/-- Given a Higgs vector, an element of the gauge group which puts the first component of the
vector to zero, and the second component to a real -/
def rotateGuageGroup {Ï† : higgsVec} (hÏ† : Ï† â‰  0) : gaugeGroup :=
    âŸ¨1, âŸ¨(rotateMatrix Ï†), rotateMatrix_specialUnitary hÏ†âŸ©, 1âŸ©

lemma rotateGuageGroup_apply {Ï† : higgsVec} (hÏ† : Ï† â‰  0) :
    rep (rotateGuageGroup hÏ†) Ï† = ![0, ofReal â€–Ï†â€–] := by
  rw [rep_apply]
  simp only [rotateGuageGroup, rotateMatrix, one_pow, one_smul,
     Nat.succ_eq_add_one, Nat.reduceAdd, ofReal_eq_coe]
  ext i
  fin_cases i
  Â· simp only [mulVec, Fin.zero_eta, Fin.isValue, cons_val', empty_val', cons_val_fin_one,
    cons_val_zero, cons_dotProduct, vecHead, vecTail, Nat.succ_eq_add_one, Nat.reduceAdd,
    Function.comp_apply, Fin.succ_zero_eq_one, dotProduct_empty, add_zero]
    ring_nf
  Â· simp only [Fin.mk_one, Fin.isValue, cons_val_one, head_cons, mulVec, Fin.isValue,
    cons_val', empty_val', cons_val_fin_one, vecHead, cons_dotProduct, vecTail, Nat.succ_eq_add_one,
    Nat.reduceAdd, Function.comp_apply, Fin.succ_zero_eq_one, dotProduct_empty, add_zero]
    have : (â€–Ï†â€– : â„‚)  â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
    field_simp
    rw [â† ofReal_mul, â† sq, â† @real_inner_self_eq_norm_sq]
    simp [PiLp.inner_apply, Complex.inner,  neg_mul, sub_neg_eq_add,
      Fin.sum_univ_two, ofReal_add, ofReal_mul, mul_conj, mul_comm]

theorem rotate_fst_zero_snd_real (Ï† : higgsVec) :
    âˆƒ (g : gaugeGroup), rep g Ï† = ![0, ofReal â€–Ï†â€–] := by
  by_cases h : Ï† = 0
  Â· use âŸ¨1, 1, 1âŸ©
    simp [h]
    ext i
    fin_cases i <;> rfl
  Â· use rotateGuageGroup h
    exact rotateGuageGroup_apply h

end higgsVec
end higgsVec

end
end StandardModel
