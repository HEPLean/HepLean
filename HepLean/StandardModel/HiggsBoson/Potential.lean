/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.StandardModel.HiggsBoson.PointwiseInnerProd
/-!
# The potential of the Higgs field

We define the potential of the Higgs field.

We show that the potential is a smooth function on spacetime.

-/

noncomputable section

namespace StandardModel

namespace HiggsField

open Manifold
open Matrix
open Complex
open ComplexConjugate
open SpaceTime

/-!

## The Higgs potential

-/

/-- The parameters of the Higgs potential. -/
structure Potential where
  /-- The mass-squared of the Higgs boson. -/
  Î¼2 : â„
  /-- The quartic coupling of the Higgs boson. Usually denoted Î». -/
  ğ“µ : â„

namespace Potential

variable (P : Potential)

/-- The function corresponding to the Higgs potential. -/
def toFun (Ï† : HiggsField) (x : SpaceTime) : â„ :=
  - P.Î¼2 * â€–Ï†â€–_H ^ 2 x + P.ğ“µ * â€–Ï†â€–_H ^ 2 x * â€–Ï†â€–_H ^ 2 x

/-- The potential is smooth. -/
lemma toFun_smooth (Ï† : HiggsField) :
    ContMDiff ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) âŠ¤ (fun x => P.toFun Ï† x) := by
  simp only [toFun, normSq, neg_mul]
  exact (contMDiff_const.smul Ï†.normSq_smooth).neg.add
    ((contMDiff_const.smul Ï†.normSq_smooth).smul Ï†.normSq_smooth)

/-- The Higgs potential formed by negating the mass squared and the quartic coupling. -/
def neg : Potential where
  Î¼2 := - P.Î¼2
  ğ“µ := - P.ğ“µ

@[simp]
lemma toFun_neg (Ï† : HiggsField) (x : SpaceTime) : P.neg.toFun Ï† x = - P.toFun Ï† x := by
  simp only [toFun, neg, neg_neg, normSq, neg_mul, neg_add_rev]
  ring

@[simp]
lemma Î¼2_neg : P.neg.Î¼2 = - P.Î¼2 := by rfl

@[simp]
lemma ğ“µ_neg : P.neg.ğ“µ = - P.ğ“µ := by rfl

/-!

## Basic properties

-/

@[simp]
lemma toFun_zero (x : SpaceTime) : P.toFun HiggsField.zero x = 0 := by
  simp [toFun, zero, ofReal]

lemma complete_square (h : P.ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    P.toFun Ï† x = P.ğ“µ * (â€–Ï†â€–_H ^ 2 x - P.Î¼2 / (2 * P.ğ“µ)) ^ 2 - P.Î¼2 ^ 2 / (4 * P.ğ“µ) := by
  simp only [toFun]
  field_simp
  ring

/-- The quadratic equation satisfied by the Higgs potential at a spacetime point `x`. -/
lemma as_quad (Ï† : HiggsField) (x : SpaceTime) :
    P.ğ“µ * â€–Ï†â€–_H ^ 2 x * â€–Ï†â€–_H ^ 2 x + (- P.Î¼2) * â€–Ï†â€–_H ^ 2 x + (- P.toFun Ï† x) = 0 := by
  simp only [normSq, neg_mul, toFun, neg_add_rev, neg_neg]
  ring

/-- The Higgs potential is zero iff and only if the higgs field is zero, or the
  higgs field has norm-squared `P.Î¼2 / P.ğ“µ`, assuming `P.ğ“ = 0`. -/
lemma toFun_eq_zero_iff (h : P.ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    P.toFun Ï† x = 0 â†” Ï† x = 0 âˆ¨ â€–Ï†â€–_H ^ 2 x = P.Î¼2 / P.ğ“µ := by
  refine Iff.intro (fun hV => ?_) (fun hD => ?_)
  Â· have h1 := P.as_quad Ï† x
    rw [hV] at h1
    have h2 : â€–Ï†â€–_H ^ 2 x * (P.ğ“µ * â€–Ï†â€–_H ^ 2 x + - P.Î¼2) = 0 := by
      linear_combination h1
    simp only [normSq, mul_eq_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff,
      norm_eq_zero] at h2
    cases' h2 with h2 h2
    Â· simp_all
    Â· apply Or.inr
      field_simp at h2 âŠ¢
      ring_nf
      linear_combination h2
  Â· cases' hD with hD hD
    Â· simp [toFun, hD]
    Â· simp only [toFun, neg_mul]
      rw [hD]
      field_simp

/-!

## The descriminant

-/

/-- The discrimiant of the quadratic equation formed by the Higgs potential. -/
def quadDiscrim (Ï† : HiggsField) (x : SpaceTime) : â„ := discrim P.ğ“µ (- P.Î¼2) (- P.toFun Ï† x)

/-- The discriminant of the quadratic formed by the potential is non-negative. -/
lemma quadDiscrim_nonneg (h : P.ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    0 â‰¤ P.quadDiscrim Ï† x := by
  have h1 := P.as_quad Ï† x
  rw [mul_assoc, quadratic_eq_zero_iff_discrim_eq_sq] at h1
  Â· simp only [h1, ne_eq, quadDiscrim, div_eq_zero_iff, OfNat.ofNat_ne_zero, or_false]
    exact sq_nonneg (2 * P.ğ“µ * â€–Ï†â€–_H ^ 2 x + - P.Î¼2)
  Â· exact h

lemma quadDiscrim_eq_sqrt_mul_sqrt (h : P.ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    P.quadDiscrim Ï† x = Real.sqrt (P.quadDiscrim Ï† x) * Real.sqrt (P.quadDiscrim Ï† x) :=
  (Real.mul_self_sqrt (P.quadDiscrim_nonneg h Ï† x)).symm

lemma quadDiscrim_eq_zero_iff (h : P.ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    P.quadDiscrim Ï† x = 0 â†” P.toFun Ï† x = - P.Î¼2 ^ 2 / (4 * P.ğ“µ) := by
  rw [quadDiscrim, discrim]
  refine Iff.intro (fun hD => ?_) (fun hV => ?_)
  Â· field_simp
    linear_combination hD
  Â· field_simp [hV]

lemma quadDiscrim_eq_zero_iff_normSq (h : P.ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    P.quadDiscrim Ï† x = 0 â†” â€–Ï†â€–_H ^ 2 x = P.Î¼2 / (2 * P.ğ“µ) := by
  rw [P.quadDiscrim_eq_zero_iff h]
  refine Iff.intro (fun hV => ?_) (fun hF => ?_)
  Â· have h1 := P.as_quad Ï† x
    rw [mul_assoc, quadratic_eq_zero_iff_of_discrim_eq_zero h
      ((P.quadDiscrim_eq_zero_iff h Ï† x).mpr hV)] at h1
    simp_rw [h1, neg_neg]
  Â· rw [toFun, hF]
    field_simp
    ring

lemma neg_ğ“µ_quadDiscrim_zero_bound (h : P.ğ“µ < 0) (Ï† : HiggsField) (x : SpaceTime) :
    P.toFun Ï† x â‰¤ - P.Î¼2 ^ 2 / (4 * P.ğ“µ) := by
  have h1 := P.quadDiscrim_nonneg (ne_of_lt h) Ï† x
  simp only [quadDiscrim, discrim, even_two, Even.neg_pow] at h1
  ring_nf at h1
  rw [â† neg_le_iff_add_nonneg',
    show P.ğ“µ * P.toFun Ï† x * 4 = (- 4 * P.ğ“µ) * (- P.toFun Ï† x) by ring] at h1
  have h2 := le_neg_of_le_neg <| (div_le_iffâ‚€' (by linarith : 0 < - 4 * P.ğ“µ)).mpr h1
  ring_nf at h2 âŠ¢
  exact h2

lemma pos_ğ“µ_quadDiscrim_zero_bound (h : 0 < P.ğ“µ) (Ï† : HiggsField) (x : SpaceTime) :
    - P.Î¼2 ^ 2 / (4 * P.ğ“µ) â‰¤ P.toFun Ï† x := by
  have h1 := P.neg.neg_ğ“µ_quadDiscrim_zero_bound (by simpa [neg] using h) Ï† x
  simp only [toFun_neg, Î¼2_neg, even_two, Even.neg_pow, ğ“µ_neg, mul_neg, neg_div_neg_eq] at h1
  rw [neg_le, neg_div'] at h1
  exact h1

/-- If `P.ğ“µ` is negative, then if `P.Î¼2` is greater then zero, for all space-time points,
  the potential is negative `P.toFun Ï† x â‰¤ 0`. -/
lemma neg_ğ“µ_toFun_neg (h : P.ğ“µ < 0) (Ï† : HiggsField) (x : SpaceTime) :
    (0 < P.Î¼2 âˆ§ P.toFun Ï† x â‰¤ 0) âˆ¨ P.Î¼2 â‰¤ 0 := by
  by_cases hÎ¼2 : P.Î¼2 â‰¤ 0
  Â· simp [hÎ¼2]
  simp only [toFun, normSq, neg_mul, neg_add_le_iff_le_add, add_zero, hÎ¼2, or_false]
  apply And.intro (lt_of_not_ge hÎ¼2)
  have h1 : 0 â‰¤ P.Î¼2 * â€–Ï† xâ€– ^ 2 := by
    refine Left.mul_nonneg ?ha ?hb
    Â· exact le_of_not_ge hÎ¼2
    Â· exact sq_nonneg â€–Ï† xâ€–
  refine le_trans ?_ h1
  exact mul_nonpos_of_nonpos_of_nonneg (mul_nonpos_of_nonpos_of_nonneg (le_of_lt h)
    (sq_nonneg â€–Ï† xâ€–)) (sq_nonneg â€–Ï† xâ€–)

/-- If `P.ğ“µ` is bigger then zero, then if `P.Î¼2` is less then zero, for all space-time points,
  the potential is positive `0 â‰¤ P.toFun Ï† x`. -/
lemma pos_ğ“µ_toFun_pos (h : 0 < P.ğ“µ) (Ï† : HiggsField) (x : SpaceTime) :
    (P.Î¼2 < 0 âˆ§ 0 â‰¤ P.toFun Ï† x) âˆ¨ 0 â‰¤ P.Î¼2 := by
  simpa using P.neg.neg_ğ“µ_toFun_neg (by simpa using h) Ï† x

lemma neg_ğ“µ_sol_exists_iff (hğ“µ : P.ğ“µ < 0) (c : â„) : (âˆƒ Ï† x, P.toFun Ï† x = c) â†” (0 < P.Î¼2 âˆ§ c â‰¤ 0) âˆ¨
    (P.Î¼2 â‰¤ 0 âˆ§ c â‰¤ - P.Î¼2 ^ 2 / (4 * P.ğ“µ)) := by
  refine Iff.intro (fun âŸ¨Ï†, x, hVâŸ© => ?_) (fun h => ?_)
  Â· rcases P.neg_ğ“µ_toFun_neg hğ“µ Ï† x with hr | hr
    Â· rw [â† hV]
      exact Or.inl hr
    Â· rw [â† hV]
      exact Or.inr (And.intro hr (P.neg_ğ“µ_quadDiscrim_zero_bound hğ“µ Ï† x))
  Â· simp only [toFun, neg_mul]
    simp only [â† sub_eq_zero, sub_zero]
    ring_nf
    let a := (P.Î¼2 - Real.sqrt (discrim P.ğ“µ (- P.Î¼2) (- c))) / (2 * P.ğ“µ)
    have ha : 0 â‰¤ a := by
      simp only [discrim, even_two, Even.neg_pow, mul_neg, sub_neg_eq_add, a]
      rw [div_nonneg_iff]
      refine Or.inr (And.intro ?_ ?_)
      Â· rw [sub_nonpos]
        by_cases hÎ¼ : P.Î¼2 < 0
        Â· have h1 : 0 â‰¤ âˆš(P.Î¼2 ^ 2 + 4 * P.ğ“µ * c) := Real.sqrt_nonneg (P.Î¼2 ^ 2 + 4 * P.ğ“µ * c)
          linarith
        Â· refine Real.le_sqrt_of_sq_le ?_
          rw [le_add_iff_nonneg_right]
          refine mul_nonneg_of_nonpos_of_nonpos ?_ ?_
          Â· refine mul_nonpos_of_nonneg_of_nonpos ?_ ?_
            Â· linarith
            Â· linarith
          Â· rcases h with h | h
            Â· linarith
            Â· have h1 : P.Î¼2 = 0 := by linarith
              rw [h1] at h
              simpa using h.2
      Â· linarith
    use (ofReal a)
    use 0
    rw [ofReal_normSq ha]
    trans P.ğ“µ * a * a + (- P.Î¼2) * a + (- c)
    Â· ring
    have hd : 0 â‰¤ (discrim P.ğ“µ (- P.Î¼2) (-c)) := by
      simp only [discrim, even_two, Even.neg_pow, mul_neg, sub_neg_eq_add]
      rcases h with h | h
      Â· refine Left.add_nonneg (sq_nonneg P.Î¼2) ?_
        refine mul_nonneg_of_nonpos_of_nonpos ?_ h.2
        linarith
      Â· rw [â† @neg_le_iff_add_nonneg']
        rw [â† le_div_iff_of_neg']
        Â· exact h.2
        Â· linarith
    have hdd : discrim P.ğ“µ (- P.Î¼2) (-c) = Real.sqrt (discrim P.ğ“µ (- P.Î¼2) (-c))
        * Real.sqrt (discrim P.ğ“µ (- P.Î¼2) (-c)) := by
      exact (Real.mul_self_sqrt hd).symm
    rw [mul_assoc]
    refine (quadratic_eq_zero_iff (ne_of_gt hğ“µ).symm hdd _).mpr ?_
    simp only [neg_neg, or_true, a]

lemma pos_ğ“µ_sol_exists_iff (hğ“µ : 0 < P.ğ“µ) (c : â„) : (âˆƒ Ï† x, P.toFun Ï† x = c) â†” (P.Î¼2 < 0 âˆ§ 0 â‰¤ c) âˆ¨
    (0 â‰¤ P.Î¼2 âˆ§ - P.Î¼2 ^ 2 / (4 * P.ğ“µ) â‰¤ c) := by
  have h1 := P.neg.neg_ğ“µ_sol_exists_iff (by simpa using hğ“µ) (- c)
  simp only [toFun_neg, neg_inj, Î¼2_neg, Left.neg_pos_iff, Left.neg_nonpos_iff, even_two,
    Even.neg_pow, ğ“µ_neg, mul_neg, neg_div_neg_eq] at h1
  rw [neg_le, neg_div'] at h1
  exact h1

/-!

## Boundness of the potential

-/

/-- The proposition on the coefficents for a potential to be bounded. -/
def IsBounded : Prop :=
  âˆƒ c, âˆ€ Î¦ x, c â‰¤ P.toFun Î¦ x

/-- If the potential is bounded, then `P.ğ“µ` is non-negative. -/
lemma isBounded_ğ“µ_nonneg (h : P.IsBounded) : 0 â‰¤ P.ğ“µ := by
  by_contra hl
  rw [not_le] at hl
  obtain âŸ¨c, hcâŸ© := h
  by_cases hÎ¼ : P.Î¼2 â‰¤ 0
  Â· by_cases hcz : c â‰¤ - P.Î¼2 ^ 2 / (4 * P.ğ“µ)
    Â· have hcm1 : âˆƒ Ï† x, P.toFun Ï† x = c - 1 := by
        rw [P.neg_ğ“µ_sol_exists_iff hl (c - 1)]
        apply Or.inr
        simp_all
        linarith
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith
    Â· rw [not_le] at hcz
      have hcm1 : âˆƒ Ï† x, P.toFun Ï† x = - P.Î¼2 ^ 2 / (4 * P.ğ“µ) - 1 := by
        rw [P.neg_ğ“µ_sol_exists_iff hl _]
        apply Or.inr
        simp_all
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith
  Â· rw [not_le] at hÎ¼
    by_cases hcz : c â‰¤ 0
    Â· have hcm1 : âˆƒ Ï† x, P.toFun Ï† x = c - 1 := by
        rw [P.neg_ğ“µ_sol_exists_iff hl (c - 1)]
        apply Or.inl
        simp_all
        linarith
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith
    Â· have hcm1 : âˆƒ Ï† x, P.toFun Ï† x = 0 := by
        rw [P.neg_ğ“µ_sol_exists_iff hl 0]
        apply Or.inl
        simp_all
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith

/-- If `P.ğ“µ` is positive, then the potential is bounded. -/
lemma isBounded_of_ğ“µ_pos (h : 0 < P.ğ“µ) : P.IsBounded := by
  simp only [IsBounded]
  have h2 := P.pos_ğ“µ_quadDiscrim_zero_bound h
  by_contra hn
  simp only [not_exists, not_forall, not_le] at hn
  obtain âŸ¨Ï†, x, hxâŸ© := hn (-P.Î¼2 ^ 2 / (4 * P.ğ“µ))
  have h2' := h2 Ï† x
  linarith

informal_lemma isBounded_iff_of_ğ“µ_zero where
  physics :â‰ˆ "When there is no quartic coupling, the potential is bounded iff the mass squared is
    non-positive."
  math :â‰ˆ "For `P : Potential` then P.IsBounded if and only if P.Î¼2 â‰¤ 0.
    That is to say `- P.Î¼2 * â€–Ï†â€–_H ^ 2 x` is bounded below if and only if `P.Î¼2 â‰¤ 0`."
  deps :â‰ˆ [`StandardModel.HiggsField.Potential.IsBounded, `StandardModel.HiggsField.Potential]
/-!

## Minimum and maximum

-/

lemma eq_zero_iff_of_Î¼Sq_nonpos_ğ“µ_pos (hğ“µ : 0 < P.ğ“µ) (hÎ¼2 : P.Î¼2 â‰¤ 0) (Ï† : HiggsField)
    (x : SpaceTime) : P.toFun Ï† x = 0 â†” Ï† x = 0 := by
  rw [P.toFun_eq_zero_iff (ne_of_lt hğ“µ).symm]
  simp only [or_iff_left_iff_imp]
  intro h
  have h1 := div_nonpos_of_nonpos_of_nonneg hÎ¼2 (le_of_lt hğ“µ)
  rw [â† h] at h1
  have hx := normSq_nonneg Ï† x
  have hx' : â€–Ï†â€–_H ^ 2 x = 0 := by linarith
  simpa using hx'

lemma isMinOn_iff_of_Î¼Sq_nonpos_ğ“µ_pos (hğ“µ : 0 < P.ğ“µ) (hÎ¼2 : P.Î¼2 â‰¤ 0) (Ï† : HiggsField)
    (x : SpaceTime) : IsMinOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x)
    â†” P.toFun Ï† x = 0 := by
  have h1 := P.pos_ğ“µ_sol_exists_iff hğ“µ
  rw [isMinOn_univ_iff]
  simp only [Prod.forall]
  refine Iff.intro (fun h => ?_) (fun h => ?_)
  Â· have h1' : P.toFun Ï† x â‰¤ 0 := by
      simpa using h HiggsField.zero 0
    have h1'' : 0 â‰¤ P.toFun Ï† x := by
      have hx := (h1 (P.toFun Ï† x)).mp âŸ¨Ï†, x, rflâŸ©
      rcases hx with hx | hx
      Â· exact hx.2
      Â· have hÎ¼2' : P.Î¼2 = 0 := by
          linarith
        simpa [hÎ¼2'] using hx.2
    linarith
  Â· rw [h]
    intro Ï†' x'
    have h1' := (h1 (P.toFun Ï†' x')).mp âŸ¨Ï†', x', rflâŸ©
    rcases h1' with h1' | h1'
    Â· exact h1'.2
    Â· have hÎ¼2' : P.Î¼2 = 0 := by
        linarith
      simpa [hÎ¼2'] using h1'.2

lemma isMinOn_iff_field_of_Î¼Sq_nonpos_ğ“µ_pos (hğ“µ : 0 < P.ğ“µ) (hÎ¼2 : P.Î¼2 â‰¤ 0) (Ï† : HiggsField)
    (x : SpaceTime) : IsMinOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x)
    â†” Ï† x = 0 := by
  rw [P.isMinOn_iff_of_Î¼Sq_nonpos_ğ“µ_pos hğ“µ hÎ¼2 Ï† x]
  exact P.eq_zero_iff_of_Î¼Sq_nonpos_ğ“µ_pos hğ“µ hÎ¼2 Ï† x

lemma isMinOn_iff_of_Î¼Sq_nonneg_ğ“µ_pos (hğ“µ : 0 < P.ğ“µ) (hÎ¼2 : 0 â‰¤ P.Î¼2) (Ï† : HiggsField)
    (x : SpaceTime) : IsMinOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x) â†”
    P.toFun Ï† x = - P.Î¼2 ^ 2 / (4 * P.ğ“µ) := by
  have h1 := P.pos_ğ“µ_sol_exists_iff hğ“µ
  simp only [not_lt.mpr hÎ¼2, false_and, hÎ¼2, true_and, false_or] at h1
  rw [isMinOn_univ_iff]
  simp only [Prod.forall]
  refine Iff.intro (fun h => ?_) (fun h => ?_)
  Â· obtain âŸ¨Ï†', x', hÏ†'âŸ© := (h1 (- P.Î¼2 ^ 2 / (4 * P.ğ“µ))).mpr (by rfl)
    have h' := h Ï†' x'
    rw [hÏ†'] at h'
    have hÏ† := (h1 (P.toFun Ï† x)).mp âŸ¨Ï†, x, rflâŸ©
    linarith
  Â· intro Ï†' x'
    rw [h]
    exact (h1 (P.toFun Ï†' x')).mp âŸ¨Ï†', x', rflâŸ©

lemma isMinOn_iff_field_of_Î¼Sq_nonneg_ğ“µ_pos (hğ“µ : 0 < P.ğ“µ) (hÎ¼2 : 0 â‰¤ P.Î¼2) (Ï† : HiggsField)
    (x : SpaceTime) : IsMinOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x) â†”
    â€–Ï†â€–_H ^ 2 x = P.Î¼2 /(2 * P.ğ“µ) := by
  rw [P.isMinOn_iff_of_Î¼Sq_nonneg_ğ“µ_pos hğ“µ hÎ¼2 Ï† x, â† P.quadDiscrim_eq_zero_iff_normSq
    (Ne.symm (ne_of_lt hğ“µ)), P.quadDiscrim_eq_zero_iff (Ne.symm (ne_of_lt hğ“µ))]

theorem isMinOn_iff_field_of_ğ“µ_pos (hğ“µ : 0 < P.ğ“µ) (Ï† : HiggsField) (x : SpaceTime) :
    IsMinOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x) â†”
    (0 â‰¤ P.Î¼2 âˆ§ â€–Ï†â€–_H ^ 2 x = P.Î¼2 /(2 * P.ğ“µ)) âˆ¨ (P.Î¼2 < 0 âˆ§ Ï† x = 0) := by
  by_cases hÎ¼2 : 0 â‰¤ P.Î¼2
  Â· simpa [not_lt.mpr hÎ¼2, hÎ¼2] using P.isMinOn_iff_field_of_Î¼Sq_nonneg_ğ“µ_pos hğ“µ hÎ¼2 Ï† x
  Â· simpa [hÎ¼2, lt_of_not_ge hÎ¼2] using P.isMinOn_iff_field_of_Î¼Sq_nonpos_ğ“µ_pos hğ“µ (by linarith) Ï† x

lemma isMaxOn_iff_isMinOn_neg (Ï† : HiggsField) (x : SpaceTime) :
    IsMaxOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x) â†”
    IsMinOn (fun (Ï†, x) => P.neg.toFun Ï† x) Set.univ (Ï†, x) := by
  simp only [toFun_neg]
  rw [isMaxOn_univ_iff, isMinOn_univ_iff]
  simp_all only [Prod.forall, neg_le_neg_iff]

lemma isMaxOn_iff_field_of_ğ“µ_neg (hğ“µ : P.ğ“µ < 0) (Ï† : HiggsField) (x : SpaceTime) :
    IsMaxOn (fun (Ï†, x) => P.toFun Ï† x) Set.univ (Ï†, x) â†”
    (P.Î¼2 â‰¤ 0 âˆ§ â€–Ï†â€–_H ^ 2 x = P.Î¼2 /(2 * P.ğ“µ)) âˆ¨ (0 < P.Î¼2 âˆ§ Ï† x = 0) := by
  rw [P.isMaxOn_iff_isMinOn_neg,
    P.neg.isMinOn_iff_field_of_ğ“µ_pos (by simpa using hğ“µ)]
  simp

end Potential

end HiggsField

end StandardModel
end
