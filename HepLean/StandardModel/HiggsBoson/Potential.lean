/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.Algebra.QuadraticDiscriminant
import HepLean.StandardModel.HiggsBoson.PointwiseInnerProd
/-!
# The potential of the Higgs field

We define the potential of the Higgs field.

We show that the potential is a smooth function on spacetime.

-/

noncomputable section

namespace StandardModel

namespace HiggsField

open Manifold
open Matrix
open Complex
open ComplexConjugate
open SpaceTime

/-!

## The Higgs potential

-/

/-- The Higgs potential of the form `- Î¼Â² * |Ï†|Â² + ğ“µ * |Ï†|â´`. -/
@[simp]
def potential (Î¼2 ğ“µ : â„) (Ï† : HiggsField) (x : SpaceTime) : â„ :=
  - Î¼2 * â€–Ï†â€–_H ^ 2 x + ğ“µ * â€–Ï†â€–_H ^ 2 x * â€–Ï†â€–_H ^ 2 x

/-!

## Smoothness of the potential

-/

lemma potential_smooth (Î¼Sq lambda : â„) (Ï† : HiggsField) :
    Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) (fun x => Ï†.potential Î¼Sq lambda x) := by
  simp only [potential, normSq, neg_mul]
  exact (smooth_const.smul Ï†.normSq_smooth).neg.add
    ((smooth_const.smul Ï†.normSq_smooth).smul Ï†.normSq_smooth)

namespace potential
/-!

## Basic properties

-/

lemma complete_square (Î¼2 ğ“µ : â„) (h : ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    potential Î¼2 ğ“µ Ï† x = ğ“µ * (â€–Ï†â€–_H ^ 2 x - Î¼2 / (2 * ğ“µ)) ^ 2 - Î¼2 ^ 2 / (4 * ğ“µ) := by
  simp only [potential]
  field_simp
  ring

lemma as_quad (Î¼2 ğ“µ : â„) (Ï† : HiggsField) (x : SpaceTime) :
    ğ“µ * â€–Ï†â€–_H ^ 2 x * â€–Ï†â€–_H ^ 2 x + (- Î¼2) * â€–Ï†â€–_H ^ 2 x + (- potential Î¼2 ğ“µ Ï† x) = 0 := by
  simp only [normSq, neg_mul, potential, neg_add_rev, neg_neg]
  ring

/-- The discriminant of the quadratic formed by the potential is non-negative. -/
lemma discrim_nonneg (Î¼2 : â„) {ğ“µ : â„} (h : ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    0 â‰¤ discrim ğ“µ (- Î¼2) (- potential Î¼2 ğ“µ Ï† x) := by
  have h1 := as_quad Î¼2 ğ“µ Ï† x
  rw [quadratic_eq_zero_iff_discrim_eq_sq] at h1
  Â· simp only [h1, ne_eq, div_eq_zero_iff, OfNat.ofNat_ne_zero, or_false]
    exact sq_nonneg (2 * ğ“µ * â€–Ï†â€–_H ^ 2 x + - Î¼2)
  Â· exact h

lemma discrim_eq_sqrt_discrim_mul_self (Î¼2 : â„) {ğ“µ : â„} (h : ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime) :
    discrim ğ“µ (- Î¼2) (- potential Î¼2 ğ“µ Ï† x) = Real.sqrt (discrim ğ“µ (- Î¼2) (- potential Î¼2 ğ“µ Ï† x)) *
      Real.sqrt (discrim ğ“µ (- Î¼2) (- potential Î¼2 ğ“µ Ï† x)) := by
  refine Eq.symm (Real.mul_self_sqrt ?h)
  exact discrim_nonneg Î¼2 h Ï† x

lemma eq_zero_at (Î¼2 : â„) {ğ“µ : â„} (h : ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime)
    (hV : potential Î¼2 ğ“µ Ï† x = 0) : Ï† x = 0 âˆ¨ â€–Ï†â€–_H ^ 2 x = Î¼2 / ğ“µ := by
  have h1 := as_quad Î¼2 ğ“µ Ï† x
  rw [hV] at h1
  have h2 : â€–Ï†â€–_H ^ 2 x * (ğ“µ * â€–Ï†â€–_H ^ 2 x + - Î¼2) = 0 := by
    linear_combination h1
  simp at h2
  cases' h2 with h2 h2
  Â· simp_all
  Â· apply Or.inr
    field_simp at h2 âŠ¢
    ring_nf
    linear_combination h2

/-- When `0 < ğ“µ`, the potential has a lower bound. -/
lemma bounded_below (Î¼2 : â„) {ğ“µ : â„} (hğ“µ : 0 < ğ“µ) (Ï† : HiggsField) (x : SpaceTime) :
    - Î¼2 ^ 2 / (4 * ğ“µ) â‰¤ potential Î¼2 ğ“µ Ï† x := by
  have h1 := discrim_nonneg Î¼2 (ne_of_lt hğ“µ).symm Ï† x
  simp only [discrim, even_two, Even.neg_pow, normSq, neg_mul, neg_add_rev, neg_neg] at h1
  ring_nf at h1
  rw [â† neg_le_iff_add_nonneg'] at h1
  rw [show ğ“µ * potential Î¼2 ğ“µ Ï† x * 4 = (4 * ğ“µ) * potential Î¼2 ğ“µ Ï† x by ring] at h1
  have h2 := (div_le_iffâ‚€' (by simp [hğ“µ] : 0 < 4 * ğ“µ)).mpr h1
  ring_nf at h2 âŠ¢
  exact h2

/-- When `ğ“µ < 0`, the potential has an upper bound. -/
lemma bounded_above (Î¼2 : â„) {ğ“µ : â„} (hğ“µ : ğ“µ < 0) (Ï† : HiggsField) (x : SpaceTime) :
    potential Î¼2 ğ“µ Ï† x â‰¤ - Î¼2 ^ 2 / (4 * ğ“µ) := by
  have h1 := discrim_nonneg Î¼2 (ne_of_lt hğ“µ) Ï† x
  simp only [discrim, even_two, Even.neg_pow, normSq, neg_mul, neg_add_rev, neg_neg] at h1
  ring_nf at h1
  rw [â† neg_le_iff_add_nonneg'] at h1
  rw [show ğ“µ * potential Î¼2 ğ“µ Ï† x * 4 = (- 4 * ğ“µ) * (- potential Î¼2 ğ“µ Ï† x) by ring] at h1
  have h2 := le_neg_of_le_neg <| (div_le_iffâ‚€' (by linarith : 0 < - 4 * ğ“µ)).mpr h1
  ring_nf at h2 âŠ¢
  exact h2

lemma discrim_eq_zero_of_eq_bound (Î¼2 : â„) {ğ“µ : â„} (h : ğ“µ â‰  0) (Ï† : HiggsField) (x : SpaceTime)
    (hV : potential Î¼2 ğ“µ Ï† x = - Î¼2 ^ 2 / (4 * ğ“µ)) :
    discrim ğ“µ (- Î¼2) (- potential Î¼2 ğ“µ Ï† x) = 0 := by
  rw [discrim, hV]
  field_simp

lemma discrim_ge_zero_of_neg_ğ“µ (Î¼2 : â„) {ğ“µ : â„} (h : ğ“µ < 0) (c : â„) :
    0 â‰¤ discrim ğ“µ (- Î¼2) (- c) â†” c â‰¤ - Î¼2 ^ 2 / (4 * ğ“µ) := by
  rw [discrim]
  simp only [even_two, Even.neg_pow, mul_neg, sub_neg_eq_add]
  rw [â† neg_le_iff_add_nonneg', show 4 * ğ“µ * c = (- 4 * ğ“µ) * (- c) by ring,
    â† (div_le_iffâ‚€' (by linarith : 0 < - 4 * ğ“µ)), le_neg]
  ring_nf

example (a b c : â„ ) (hc : c < 0) :  a â‰¤ b / c â†” b â‰¤ c * a := by
  exact le_div_iff_of_neg' hc
lemma pot_le_zero_of_neg_ğ“µ (Î¼2 : â„) {ğ“µ : â„} (h : ğ“µ < 0) (Ï† : HiggsField) (x : SpaceTime) :
    (0 < Î¼2 âˆ§ potential Î¼2 ğ“µ Ï† x â‰¤ 0) âˆ¨ Î¼2 â‰¤ 0 := by
  by_cases hÎ¼2 : Î¼2 â‰¤ 0
  Â· simp [hÎ¼2]
  simp [potential, hÎ¼2]
  apply And.intro (lt_of_not_ge hÎ¼2)
  have h1 : 0 â‰¤  Î¼2 * â€–Ï† xâ€– ^ 2 := by
    refine Left.mul_nonneg ?ha ?hb
    Â· exact le_of_not_ge hÎ¼2
    Â· exact sq_nonneg â€–Ï† xâ€–
  refine le_trans ?_ h1
  exact mul_nonpos_of_nonpos_of_nonneg (mul_nonpos_of_nonpos_of_nonneg (le_of_lt h)
    (sq_nonneg â€–Ï† xâ€–)) (sq_nonneg â€–Ï† xâ€–)

lemma exist_sol_iff_of_neg_ğ“µ (Î¼2 : â„) {ğ“µ : â„} (hğ“µ : ğ“µ < 0) (c : â„) :
    (âˆƒ Ï† x, potential Î¼2 ğ“µ Ï† x = c) â†” (0 < Î¼2 âˆ§ c â‰¤ 0) âˆ¨
    (Î¼2 â‰¤ 0 âˆ§ c â‰¤ - Î¼2 ^ 2 / (4 * ğ“µ)) := by
  refine Iff.intro (fun âŸ¨Ï†, x, hVâŸ© => ?_) (fun h => ?_)
  Â· rcases pot_le_zero_of_neg_ğ“µ Î¼2 hğ“µ Ï† x with hr | hr
    Â· rw [â† hV]
      exact Or.inl hr
    Â· rw [â† hV]
      exact Or.inr (And.intro hr (bounded_above Î¼2 hğ“µ Ï† x))
  Â· simp only [potential, neg_mul]
    simp only [â† sub_eq_zero, sub_zero]
    ring_nf
    let a := (Î¼2 - Real.sqrt (discrim ğ“µ (- Î¼2) (- c))) / (2 * ğ“µ)
    have ha : 0 â‰¤ a := by
      simp [a, discrim]
      rw [div_nonneg_iff]
      refine Or.inr (And.intro ?_ ?_)
      Â· rw [sub_nonpos]
        by_cases hÎ¼ : Î¼2 < 0
        Â· have h1 : 0 â‰¤ âˆš(Î¼2 ^ 2 + 4 * ğ“µ * c) := Real.sqrt_nonneg (Î¼2 ^ 2 + 4 * ğ“µ * c)
          linarith
        Â· refine Real.le_sqrt_of_sq_le ?_
          rw [le_add_iff_nonneg_right]
          refine mul_nonneg_of_nonpos_of_nonpos ?_ ?_
          Â· refine mul_nonpos_of_nonneg_of_nonpos ?_ ?_
            Â· linarith
            Â· linarith
          Â· rcases h with h | h
            Â· linarith
            Â· have h1 : Î¼2 = 0 := by linarith
              rw [h1] at h
              simpa using h.2
      Â· linarith
    use (ofReal a)
    use 0
    rw [ofReal_normSq ha]
    trans ğ“µ * a * a + (- Î¼2) * a + (- c)
    Â· ring
    have hd : 0 â‰¤ (discrim ğ“µ (-Î¼2) (-c)) := by
      simp [discrim]
      rcases h with h | h
      Â· refine Left.add_nonneg (sq_nonneg Î¼2) ?_
        refine mul_nonneg_of_nonpos_of_nonpos ?_ h.2
        linarith
      Â· rw [â† @neg_le_iff_add_nonneg']
        rw [â† le_div_iff_of_neg']
        Â· exact h.2
        Â· linarith
    have hdd : discrim ğ“µ (-Î¼2) (-c) = Real.sqrt (discrim ğ“µ (-Î¼2) (-c)) * Real.sqrt (discrim ğ“µ (-Î¼2) (-c)) := by
      exact (Real.mul_self_sqrt hd).symm
    refine (quadratic_eq_zero_iff (ne_of_gt hğ“µ).symm hdd _).mpr ?_
    simp only [neg_neg, or_true, a]

/-!

## Boundness of the potential

-/

/-- The proposition on the coefficents for a potential to be bounded. -/
def IsBounded (Î¼2 ğ“µ : â„) : Prop :=
  âˆƒ c, âˆ€ Î¦ x, c â‰¤ potential Î¼2 ğ“µ Î¦ x

lemma isBounded_ğ“µ_nonneg {Î¼2 ğ“µ : â„} (h : IsBounded Î¼2 ğ“µ) :
    0 â‰¤ ğ“µ := by
  by_contra hl
  simp at hl
  obtain âŸ¨c, hcâŸ© := h
  by_cases hÎ¼ : Î¼2 â‰¤ 0
  Â· by_cases hcz : c â‰¤ -Î¼2 ^ 2 / (4 * ğ“µ)
    Â· have hcm1 : âˆƒ Ï† x, potential Î¼2 ğ“µ Ï† x = c - 1 := by
        rw [propext (exist_sol_iff_of_neg_ğ“µ Î¼2 hl (c - 1))]
        apply Or.inr
        simp_all
        linarith
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith
    Â· simp at hcz
      have hcm1 : âˆƒ Ï† x, potential Î¼2 ğ“µ Ï† x = -Î¼2 ^ 2 / (4 * ğ“µ) - 1 := by
        rw [propext (exist_sol_iff_of_neg_ğ“µ Î¼2 hl _)]
        apply Or.inr
        simp_all
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith
  Â· simp at hÎ¼
    by_cases hcz : c â‰¤ 0
    Â· have hcm1 : âˆƒ Ï† x, potential Î¼2 ğ“µ Ï† x = c - 1 := by
        rw [propext (exist_sol_iff_of_neg_ğ“µ Î¼2 hl (c - 1))]
        apply Or.inl
        simp_all
        linarith
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith
    Â· have hcm1 : âˆƒ Ï† x, potential Î¼2 ğ“µ Ï† x = 0 := by
        rw [propext (exist_sol_iff_of_neg_ğ“µ Î¼2 hl 0)]
        apply Or.inl
        simp_all
      obtain âŸ¨Ï†, x, hÏ†âŸ© := hcm1
      have hc2 := hc Ï† x
      rw [hÏ†] at hc2
      linarith



section lowerBound
/-!

## Lower bound on potential

-/

variable (Î¼2 : â„) {ğ“µ : â„} (hğ“µ : 0 < ğ“µ)

include hğ“µ
/-- The second term of the potential is non-negative. -/
lemma snd_term_nonneg (Ï† : HiggsField) (x : SpaceTime) :
    0 â‰¤ ğ“µ * â€–Ï†â€–_H ^ 2 x * â€–Ï†â€–_H ^ 2 x := by
  rw [mul_nonneg_iff]
  apply Or.inl
  simp_all only [normSq, gt_iff_lt, mul_nonneg_iff_of_pos_left, ge_iff_le, norm_nonneg, pow_nonneg,
    and_self]

lemma eq_zero_at_of_Î¼Sq_nonpos {Î¼2 : â„} (hÎ¼2 : Î¼2 â‰¤ 0)
    (Ï† : HiggsField) (x : SpaceTime) (hV : potential Î¼2 ğ“µ Ï† x = 0) : Ï† x = 0 := by
  cases' (eq_zero_at Î¼2 (ne_of_lt hğ“µ).symm Ï† x hV) with h1 h1
  Â· exact h1
  Â· by_cases hÎ¼SqZ : Î¼2 = 0
    Â· simpa [hÎ¼SqZ] using h1
    Â· refine ((?_ : Â¬ 0 â‰¤ Î¼2 / ğ“µ) (?_)).elim
      Â· simp_all [div_nonneg_iff]
        intro h
        exact lt_imp_lt_of_le_imp_le (fun _ => h) (lt_of_le_of_ne hÎ¼2 hÎ¼SqZ)
      Â· rw [â† h1]
        exact normSq_nonneg Ï† x

lemma bounded_below_of_Î¼Sq_nonpos {Î¼2 : â„}
    (hÎ¼Sq : Î¼2 â‰¤ 0) (Ï† : HiggsField) (x : SpaceTime) : 0 â‰¤ potential Î¼2 ğ“µ Ï† x := by
  refine add_nonneg ?_ (snd_term_nonneg hğ“µ Ï† x)
  field_simp [mul_nonpos_iff]
  simp_all [ge_iff_le, norm_nonneg, pow_nonneg, and_self, or_true]

end lowerBound

section MinimumOfPotential
variable {ğ“µ : â„}
variable (Î¼2 : â„)
variable (hğ“µ : 0 < ğ“µ)

/-!

## Minima of potential

-/


include hğ“µ
lemma normSq_of_eq_bound (Ï† : HiggsField) (x : SpaceTime)
    (hV : potential Î¼2 ğ“µ Ï† x = - Î¼2 ^ 2 / (4 * ğ“µ)) :
    â€–Ï†â€–_H ^ 2 x = Î¼2 / (2 * ğ“µ) := by
  have h1 := as_quad Î¼2 ğ“µ Ï† x
  rw [quadratic_eq_zero_iff_of_discrim_eq_zero _
    (discrim_eq_zero_of_eq_bound Î¼2 (ne_of_lt hğ“µ).symm Ï† x hV)] at h1
  Â· simp_rw [h1, neg_neg]
  Â· exact ne_of_gt hğ“µ

lemma eq_bound_iff (Ï† : HiggsField) (x : SpaceTime) :
    potential Î¼2 ğ“µ Ï† x = - Î¼2 ^ 2 / (4 * ğ“µ) â†” â€–Ï†â€–_H ^ 2 x = Î¼2 / (2 * ğ“µ) :=
  Iff.intro (normSq_of_eq_bound Î¼2 hğ“µ Ï† x)
    (fun h â†¦ by
      rw [potential, h]
      field_simp
      ring_nf)

lemma eq_bound_iff_of_Î¼Sq_nonpos {Î¼2 : â„}
    (hÎ¼2 : Î¼2 â‰¤ 0) (Ï† : HiggsField) (x : SpaceTime) :
    potential Î¼2 ğ“µ Ï† x = 0 â†” Ï† x = 0 :=
  Iff.intro (fun h â†¦ eq_zero_at_of_Î¼Sq_nonpos hğ“µ hÎ¼2 Ï† x h)
  (fun h â†¦ by simp [potential, h])

lemma eq_bound_IsMinOn (Ï† : HiggsField) (x : SpaceTime)
    (hv : potential Î¼2 ğ“µ Ï† x = - Î¼2 ^ 2 / (4 * ğ“µ)) :
    IsMinOn (fun (Ï†, x) => potential Î¼2 ğ“µ Ï† x) Set.univ (Ï†, x) := by
  rw [isMinOn_univ_iff]
  simp only [normSq, neg_mul, le_neg_add_iff_add_le, ge_iff_le, hv]
  exact fun (Ï†', x') â†¦ bounded_below Î¼2 hğ“µ Ï†' x'

lemma eq_bound_IsMinOn_of_Î¼Sq_nonpos {Î¼2 : â„}
    (hÎ¼2 : Î¼2 â‰¤ 0) (Ï† : HiggsField) (x : SpaceTime) (hv : potential Î¼2 ğ“µ Ï† x = 0) :
    IsMinOn (fun (Ï†, x) => potential Î¼2 ğ“µ Ï† x) Set.univ (Ï†, x) := by
  rw [isMinOn_univ_iff]
  simp only [normSq, neg_mul, le_neg_add_iff_add_le, ge_iff_le, hv]
  exact fun (Ï†', x') â†¦ bounded_below_of_Î¼Sq_nonpos hğ“µ hÎ¼2 Ï†' x'

lemma bound_reached_of_Î¼Sq_nonneg {Î¼2 : â„} (hÎ¼2 : 0 â‰¤ Î¼2) :
    âˆƒ (Ï† : HiggsField) (x : SpaceTime),
    potential Î¼2 ğ“µ Ï† x = - Î¼2 ^ 2 / (4 * ğ“µ) := by
  use HiggsVec.toField ![âˆš(Î¼2/(2 * ğ“µ)), 0], 0
  refine (eq_bound_iff Î¼2 hğ“µ (HiggsVec.toField ![âˆš(Î¼2/(2 * ğ“µ)), 0]) 0).mpr ?_
  simp only [normSq, HiggsVec.toField, ContMDiffSection.coeFn_mk, PiLp.norm_sq_eq_of_L2,
    Nat.succ_eq_add_one, Nat.reduceAdd, norm_eq_abs, Fin.sum_univ_two, Fin.isValue, cons_val_zero,
    abs_ofReal, _root_.sq_abs, cons_val_one, head_cons, map_zero, ne_eq, OfNat.ofNat_ne_zero,
    not_false_eq_true, zero_pow, add_zero]
  field_simp [mul_pow]

lemma IsMinOn_iff_of_Î¼Sq_nonneg {Î¼2 : â„} (hÎ¼2 : 0 â‰¤ Î¼2) :
    IsMinOn (fun (Ï†, x) => potential Î¼2 ğ“µ Ï† x) Set.univ (Ï†, x) â†”
    â€–Ï†â€–_H ^ 2 x = Î¼2 /(2 * ğ“µ) := by
  apply Iff.intro <;> rw [â† eq_bound_iff Î¼2 hğ“µ Ï†]
  Â· intro h
    obtain âŸ¨Ï†m, xm, hÏ†âŸ© := bound_reached_of_Î¼Sq_nonneg hğ“µ hÎ¼2
    have hm := isMinOn_univ_iff.mp h (Ï†m, xm)
    simp only at hm
    rw [hÏ†] at hm
    exact (Real.partialOrder.le_antisymm _ _ (bounded_below Î¼2 hğ“µ Ï† x) hm).symm
  Â· exact eq_bound_IsMinOn Î¼2 hğ“µ Ï† x

lemma IsMinOn_iff_of_Î¼Sq_nonpos {Î¼2 : â„} (hÎ¼2 : Î¼2 â‰¤ 0) :
    IsMinOn (fun (Ï†, x) => potential Î¼2 ğ“µ Ï† x) Set.univ (Ï†, x) â†” Ï† x = 0 := by
  apply Iff.intro <;> rw [â† eq_bound_iff_of_Î¼Sq_nonpos hğ“µ hÎ¼2 Ï†]
  Â· intro h
    have h0 := isMinOn_univ_iff.mp h 0
    have h1 := bounded_below_of_Î¼Sq_nonpos hğ“µ hÎ¼2 Ï† x
    simp only at h0
    rw [(eq_bound_iff_of_Î¼Sq_nonpos hğ“µ hÎ¼2 0 0).mpr (by rfl)] at h0
    exact (Real.partialOrder.le_antisymm _ _ h1 h0).symm
  Â· exact eq_bound_IsMinOn_of_Î¼Sq_nonpos hğ“µ hÎ¼2 Ï† x

end MinimumOfPotential

end potential

end HiggsField

end StandardModel
end
