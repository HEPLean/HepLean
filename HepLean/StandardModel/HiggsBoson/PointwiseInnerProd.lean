/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.StandardModel.HiggsBoson.Basic
/-!
# The pointwise inner product

We define the pointwise inner product of two Higgs fields.

The notation for the inner product is `âŸªÏ†, ÏˆâŸ«_H`.

We also define the pointwise norm squared of a Higgs field `âˆ¥Ï†âˆ¥_H ^ 2`.

-/

noncomputable section

namespace StandardModel

namespace HiggsField

open Manifold
open Matrix
open Complex
open ComplexConjugate
open SpaceTime
open InnerProductSpace

/-!

## The pointwise inner product

-/

/-- Given two `HiggsField`, the map `SpaceTime â†’ â„‚` obtained by taking their pointwise
  inner product. -/
def innerProd (Ï†1 Ï†2 : HiggsField) : (SpaceTime â†’ â„‚) := fun x => âŸªÏ†1 x, Ï†2 xâŸ«_â„‚

/-- Notation for the inner product of two Higgs fields. -/
scoped[StandardModel.HiggsField] notation "âŸª" Ï†1 "," Ï†2 "âŸ«_H" => innerProd Ï†1 Ï†2

/-!

## Properties of the inner product

-/

@[simp]
lemma innerProd_neg_left (Ï†1 Ï†2 : HiggsField) : âŸª- Ï†1, Ï†2âŸ«_H = -âŸªÏ†1, Ï†2âŸ«_H := by
  funext x
  simp [innerProd]

@[simp]
lemma innerProd_neg_right (Ï†1 Ï†2 : HiggsField) : âŸªÏ†1, - Ï†2âŸ«_H = -âŸªÏ†1, Ï†2âŸ«_H := by
  funext x
  simp [innerProd]

@[simp]
lemma innerProd_left_zero (Ï† : HiggsField) : âŸª0, Ï†âŸ«_H = 0 := by
  funext x
  simp [innerProd]

@[simp]
lemma innerProd_right_zero (Ï† : HiggsField) : âŸªÏ†, 0âŸ«_H = 0 := by
  funext x
  simp [innerProd]

/-- Expands the inner product on Higgs fields in terms of complex components of the
  Higgs fields. -/
lemma innerProd_expand' (Ï†1 Ï†2 : HiggsField) (x : SpaceTime) :
    âŸªÏ†1, Ï†2âŸ«_H x = conj (Ï†1 x 0) * Ï†2 x 0 + conj (Ï†1 x 1) * Ï†2 x 1 := by
  simp [innerProd]

/-- Expands the inner product on Higgs fields in terms of real components of the
  Higgs fields. -/
lemma innerProd_expand (Ï†1 Ï†2 : HiggsField) :
    âŸªÏ†1, Ï†2âŸ«_H = fun x => equivRealProdCLM.symm (((Ï†1 x 0).re * (Ï†2 x 0).re
    + (Ï†1 x 1).re * (Ï†2 x 1).re+ (Ï†1 x 0).im * (Ï†2 x 0).im + (Ï†1 x 1).im * (Ï†2 x 1).im),
    ((Ï†1 x 0).re * (Ï†2 x 0).im + (Ï†1 x 1).re * (Ï†2 x 1).im
    - (Ï†1 x 0).im * (Ï†2 x 0).re - (Ï†1 x 1).im * (Ï†2 x 1).re)) := by
  funext x
  simp only [innerProd, PiLp.inner_apply, RCLike.inner_apply, Fin.sum_univ_two,
    equivRealProdCLM_symm_apply, ofReal_add, ofReal_mul, ofReal_sub]
  rw [RCLike.conj_eq_re_sub_im, RCLike.conj_eq_re_sub_im]
  nth_rewrite 1 [â† RCLike.re_add_im (Ï†2 x 0)]
  nth_rewrite 1 [â† RCLike.re_add_im (Ï†2 x 1)]
  ring_nf
  repeat rw [show RCLike.ofReal _ = ofReal' _ by rfl]
  simp only [Algebra.id.map_eq_id, RCLike.re_to_complex, RingHom.id_apply, RCLike.I_to_complex,
    RCLike.im_to_complex, I_sq, mul_neg, mul_one, neg_mul, sub_neg_eq_add, one_mul]
  ring

lemma smooth_innerProd (Ï†1 Ï†2 : HiggsField) : Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, â„‚) âŸªÏ†1, Ï†2âŸ«_H := by
  rw [innerProd_expand]
  exact (ContinuousLinearMap.smooth (equivRealProdCLM.symm : â„ Ã— â„ â†’L[â„] â„‚)).comp $
    (((((Ï†1.apply_re_smooth 0).smul (Ï†2.apply_re_smooth 0)).add
    ((Ï†1.apply_re_smooth 1).smul (Ï†2.apply_re_smooth 1))).add
    ((Ï†1.apply_im_smooth 0).smul (Ï†2.apply_im_smooth 0))).add
    ((Ï†1.apply_im_smooth 1).smul (Ï†2.apply_im_smooth 1))).prod_mk_space $
    ((((Ï†1.apply_re_smooth 0).smul (Ï†2.apply_im_smooth 0)).add
    ((Ï†1.apply_re_smooth 1).smul (Ï†2.apply_im_smooth 1))).sub
    ((Ï†1.apply_im_smooth 0).smul (Ï†2.apply_re_smooth 0))).sub
    ((Ï†1.apply_im_smooth 1).smul (Ï†2.apply_re_smooth 1))

/-!

## The pointwise norm squared

-/

/-- Given a `HiggsField`, the map `SpaceTime â†’ â„` obtained by taking the square norm of the
  pointwise Higgs vector. -/
@[simp]
def normSq (Ï† : HiggsField) : SpaceTime â†’ â„ := fun x => â€–Ï† xâ€– ^ 2

/-- Notation for the norm squared of a Higgs field. -/
scoped[StandardModel.HiggsField] notation "â€–" Ï†1 "â€–_H ^ 2" => normSq Ï†1

/-!

## Relation between inner prod and norm squared

-/

@[simp]
lemma innerProd_self_eq_normSq (Ï† : HiggsField) (x : SpaceTime) :
    âŸªÏ†, Ï†âŸ«_H x = â€–Ï†â€–_H ^ 2 x := by
  erw [normSq, @PiLp.norm_sq_eq_of_L2, Fin.sum_univ_two]
  simp only [innerProd, PiLp.inner_apply, RCLike.inner_apply, conj_mul', norm_eq_abs,
    Fin.sum_univ_two, ofReal_add, ofReal_pow]

lemma normSq_eq_innerProd_self (Ï† : HiggsField) (x : SpaceTime) :
    â€–Ï† xâ€– ^ 2 = (âŸªÏ†, Ï†âŸ«_H x).re := by
  rw [innerProd_self_eq_normSq]
  rfl

/-!

# Properties of the norm squared

-/

@[simp]
lemma normSq_apply_im_zero (Ï† : HiggsField) (x : SpaceTime) :
    ((Complex.ofReal' â€–Ï† xâ€–) ^ 2).im = 0 := by
  rw [sq]
  simp only [Complex.ofReal_eq_coe, Complex.mul_im, Complex.ofReal_re, Complex.ofReal_im,
    mul_zero, zero_mul, add_zero]

@[simp]
lemma normSq_apply_re_self (Ï† : HiggsField) (x : SpaceTime) :
    ((Complex.ofReal' â€–Ï† xâ€–) ^ 2).re = Ï†.normSq x := by
  rw [sq]
  simp only [mul_re, ofReal_re, ofReal_im, mul_zero, sub_zero, normSq]
  exact Eq.symm (pow_two â€–Ï† xâ€–)

lemma toHiggsVec_norm (Ï† : HiggsField) (x : SpaceTime) :
    â€–Ï† xâ€– = â€–Ï†.toHiggsVec xâ€– := rfl

lemma normSq_expand (Ï† : HiggsField) :
    Ï†.normSq = fun x => (conj (Ï† x 0) * (Ï† x 0) + conj (Ï† x 1) * (Ï† x 1)).re := by
  funext x
  simp [normSq, add_re, mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add, @norm_sq_eq_inner â„‚]

lemma normSq_nonneg (Ï† : HiggsField) (x : SpaceTime) : 0 â‰¤ Ï†.normSq x := by
  simp [normSq, ge_iff_le, norm_nonneg, pow_nonneg]

lemma normSq_zero (Ï† : HiggsField) (x : SpaceTime) : Ï†.normSq x = 0 â†” Ï† x = 0 := by
  simp [normSq, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff, norm_eq_zero]

lemma normSq_smooth (Ï† : HiggsField) : Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) Ï†.normSq := by
  rw [normSq_expand]
  refine Smooth.add ?_ ?_
  Â· simp only [mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
    exact ((Ï†.apply_re_smooth 0).smul (Ï†.apply_re_smooth 0)).add $
      (Ï†.apply_im_smooth 0).smul (Ï†.apply_im_smooth 0)
  Â· simp only [mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
    exact ((Ï†.apply_re_smooth 1).smul (Ï†.apply_re_smooth 1)).add $
      (Ï†.apply_im_smooth 1).smul (Ï†.apply_im_smooth 1)

lemma ofReal_normSq {a : â„} (ha : 0 â‰¤ a) (x : SpaceTime) : (ofReal a).normSq x = a := by
  simp only [normSq, ofReal, HiggsVec.toField_apply, ha, HiggsVec.ofReal_normSq]

end HiggsField

end StandardModel
end
