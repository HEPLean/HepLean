/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.Basic
import Mathlib.Data.Complex.Exponential
import Mathlib.Tactic.Polyrith
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Geometry.Manifold.ContMDiff.Product
/-!

# The Higgs field

This file defines the basic properties for the Higgs field in the standard model.

## References

- We use conventions given in: [Review of Particle Physics, PDG][ParticleDataGroup:2018ovx]

-/

namespace StandardModel
noncomputable section

open Manifold
open Matrix
open Complex
open ComplexConjugate
open SpaceTime

/-!

## The definition of the Higgs vector space.

In other words, the target space of the Higgs field.
-/

/-- The complex vector space in which the Higgs field takes values. -/
abbrev HiggsVec := EuclideanSpace â„‚ (Fin 2)

namespace HiggsVec

/-- The continuous linear map from the vector space `HiggsVec` to `(Fin 2 â†’ â„‚)` achieved by
casting vectors. -/
def toFin2â„‚ : HiggsVec â†’L[â„] (Fin 2 â†’ â„‚) where
  toFun x := x
  map_add' x y := rfl
  map_smul' a x := rfl

/-- The map `toFin2â„‚` is smooth. -/
lemma smooth_toFin2â„‚ : Smooth ğ“˜(â„, HiggsVec) ğ“˜(â„, Fin 2 â†’ â„‚) toFin2â„‚ :=
  ContinuousLinearMap.smooth toFin2â„‚

/-- An orthonormal basis of `HiggsVec`. -/
def orthonormBasis : OrthonormalBasis (Fin 2) â„‚ HiggsVec :=
  EuclideanSpace.basisFun (Fin 2) â„‚

/-- Generating a Higgs vector from a real number, such that the norm-squared of that Higgs vector
  is the given real number. -/
def ofReal (a : â„) : HiggsVec :=
  ![Real.sqrt a, 0]

@[simp]
lemma ofReal_normSq {a : â„} (ha : 0 â‰¤ a) : â€–ofReal aâ€– ^ 2 = a := by
  simp only [ofReal]
  rw [PiLp.norm_sq_eq_of_L2]
  rw [@Fin.sum_univ_two]
  simp only [Fin.isValue, cons_val_zero, norm_real, Real.norm_eq_abs, _root_.sq_abs, cons_val_one,
    head_cons, norm_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, add_zero]
  exact Real.sq_sqrt ha

end HiggsVec

/-!

## Definition of the Higgs Field

We also define the Higgs bundle.
-/

/-! TODO: Make `HiggsBundle` an associated bundle. -/
/-- The trivial vector bundle ğ“¡Â² Ã— â„‚Â². -/
abbrev HiggsBundle := Bundle.Trivial SpaceTime HiggsVec

instance : SmoothVectorBundle HiggsVec HiggsBundle SpaceTime.asSmoothManifold :=
  Bundle.Trivial.smoothVectorBundle HiggsVec ğ“˜(â„, SpaceTime)

/-- A Higgs field is a smooth section of the Higgs bundle. -/
abbrev HiggsField : Type := SmoothSection SpaceTime.asSmoothManifold HiggsVec HiggsBundle

instance : NormedAddCommGroup (Fin 2 â†’ â„‚) := by
  exact Pi.normedAddCommGroup

/-- Given a vector in `HiggsVec` the constant Higgs field with value equal to that
section. -/
def HiggsVec.toField (Ï† : HiggsVec) : HiggsField where
  toFun := fun _ â†¦ Ï†
  contMDiff_toFun := by
    intro x
    rw [Bundle.contMDiffAt_section]
    exact smoothAt_const

@[simp]
lemma HiggsVec.toField_apply (Ï† : HiggsVec) (x : SpaceTime) : (Ï†.toField x) = Ï† := rfl

namespace HiggsField
open HiggsVec

/-!

## Relation to `HiggsVec`

-/

/-- Given a `HiggsField`, the corresponding map from `SpaceTime` to `HiggsVec`. -/
def toHiggsVec (Ï† : HiggsField) : SpaceTime â†’ HiggsVec := Ï†

lemma toHiggsVec_smooth (Ï† : HiggsField) : Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, HiggsVec) Ï†.toHiggsVec := by
  intro x0
  have h1 := Ï†.contMDiff x0
  rw [Bundle.contMDiffAt_section] at h1
  exact h1

lemma toField_toHiggsVec_apply (Ï† : HiggsField) (x : SpaceTime) :
    (Ï†.toHiggsVec x).toField x = Ï† x := rfl

lemma toFin2â„‚_comp_toHiggsVec (Ï† : HiggsField) :
    toFin2â„‚ âˆ˜ Ï†.toHiggsVec = Ï† := rfl

/-!

## Smoothness properties of components

-/

lemma toVec_smooth (Ï† : HiggsField) : Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, Fin 2 â†’ â„‚) Ï† :=
  smooth_toFin2â„‚.comp Ï†.toHiggsVec_smooth

lemma apply_smooth (Ï† : HiggsField) :
    âˆ€ i, Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, â„‚) (fun (x : SpaceTime) => (Ï† x i)) :=
  (smooth_pi_space).mp (Ï†.toVec_smooth)

lemma apply_re_smooth (Ï† : HiggsField) (i : Fin 2) :
    Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) (reCLM âˆ˜ (fun (x : SpaceTime) => (Ï† x i))) :=
  (ContinuousLinearMap.smooth reCLM).comp (Ï†.apply_smooth i)

lemma apply_im_smooth (Ï† : HiggsField) (i : Fin 2) :
    Smooth ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) (imCLM âˆ˜ (fun (x : SpaceTime) => (Ï† x i))) :=
  (ContinuousLinearMap.smooth imCLM).comp (Ï†.apply_smooth i)

/-!

## Constant Higgs fields

-/

/-- A Higgs field is constant if it is equal for all `x` `y` in `spaceTime`. -/
def IsConst (Î¦ : HiggsField) : Prop := âˆ€ x y, Î¦ x = Î¦ y

lemma isConst_of_higgsVec (Ï† : HiggsVec) : Ï†.toField.IsConst := fun _ => congrFun rfl

lemma isConst_iff_of_higgsVec (Î¦ : HiggsField) : Î¦.IsConst â†” âˆƒ (Ï† : HiggsVec), Î¦ = Ï†.toField :=
  Iff.intro (fun h â†¦ âŸ¨Î¦ 0, by ext x y; rw [â† h x 0]; rflâŸ©) (fun âŸ¨Ï†, hÏ†âŸ© x y â†¦ by subst hÏ†; rfl)

/-- Generating a constant Higgs field from a real number, such that the norm-squared of that Higgs
  vector is the given real number. -/
def ofReal (a : â„) : HiggsField := (HiggsVec.ofReal a).toField

end HiggsField

end
end StandardModel
