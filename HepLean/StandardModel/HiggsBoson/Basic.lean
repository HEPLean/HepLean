/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license.
Authors: Joseph Tooby-Smith
-/
import HepLean.SpaceTime.Basic
import HepLean.StandardModel.Basic
import HepLean.StandardModel.HiggsBoson.TargetSpace
import Mathlib.Data.Complex.Exponential
import Mathlib.Tactic.Polyrith
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.RepresentationTheory.Basic
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Geometry.Manifold.ContMDiff.Product
import Mathlib.Algebra.QuadraticDiscriminant
/-!
# The Higgs field

This file defines the basic properties for the higgs field in the standard model.

## References

- We use conventions given in: [Review of Particle Physics, PDG][ParticleDataGroup:2018ovx]

-/
universe v u
namespace StandardModel
noncomputable section

open Manifold
open Matrix
open Complex
open ComplexConjugate
open SpaceTime
/-!

## Definition of the Higgs bundle

-/

/-! TODO: Make `HiggsBundle` an associated bundle. -/
/-- The trivial vector bundle ð“¡Â² Ã— â„‚Â². -/
abbrev HiggsBundle := Bundle.Trivial SpaceTime HiggsVec

instance : SmoothVectorBundle HiggsVec HiggsBundle SpaceTime.asSmoothManifold  :=
  Bundle.Trivial.smoothVectorBundle HiggsVec ð“˜(â„, SpaceTime)

/-- A higgs field is a smooth section of the higgs bundle. -/
abbrev HiggsField : Type := SmoothSection SpaceTime.asSmoothManifold HiggsVec HiggsBundle

instance : NormedAddCommGroup (Fin 2 â†’ â„‚) := by
  exact Pi.normedAddCommGroup

/-- Given a vector `â„‚Â²` the constant higgs field with value equal to that
section. -/
noncomputable def HiggsVec.toField (Ï† : HiggsVec) : HiggsField where
  toFun := fun _ â†¦ Ï†
  contMDiff_toFun := by
    intro x
    rw [Bundle.contMDiffAt_section]
    exact smoothAt_const

namespace HiggsField
open  Complex Real

/-!

## Relation to `HiggsVec`

-/

/-- Given a `higgsField`, the corresponding map from `spaceTime` to `higgsVec`. -/
def toHiggsVec (Ï† : HiggsField) : SpaceTime â†’ HiggsVec := Ï†

lemma toHiggsVec_smooth (Ï† : HiggsField) : Smooth ð“˜(â„, SpaceTime) ð“˜(â„, HiggsVec) Ï†.toHiggsVec := by
  intro x0
  have h1 := Ï†.contMDiff x0
  rw [Bundle.contMDiffAt_section] at h1
  have h2 :
    (fun x => ((trivializationAt HiggsVec (Bundle.Trivial SpaceTime HiggsVec) x0)
    { proj := x, snd := Ï† x }).2) = Ï† := by
    rfl
  simp only [h2] at h1
  exact h1

lemma toField_toHiggsVec_apply (Ï† : HiggsField) (x : SpaceTime) :
    (Ï†.toHiggsVec x).toField x = Ï† x := rfl

lemma higgsVecToFin2â„‚_toHiggsVec (Ï† : HiggsField) :
    higgsVecToFin2â„‚ âˆ˜ Ï†.toHiggsVec = Ï† := rfl

/-!

## The inner product and norm of Higgs fields

-/

/-- Given two `HiggsField`, the map `spaceTime â†’ â„‚` obtained by taking their inner product. -/
def innerProd (Ï†1 Ï†2 : HiggsField) : SpaceTime â†’ â„‚ := fun x => âŸªÏ†1 x, Ï†2 xâŸ«_â„‚

/-- Notation for the inner product of two Higgs fields. -/
scoped[StandardModel.HiggsField] notation "âŸª" Ï†1 "," Ï†2 "âŸ«_H" => innerProd Ï†1 Ï†2

@[simp]
lemma innerProd_left_zero (Ï† : HiggsField) : âŸª0, Ï†âŸ«_H = 0 := by
  funext x
  simp [innerProd]

@[simp]
lemma innerProd_right_zero (Ï† : HiggsField) : âŸªÏ†, 0âŸ«_H = 0 := by
  funext x
  simp [innerProd]

lemma innerProd_expand (Ï†1 Ï†2 : HiggsField)  :
    âŸªÏ†1, Ï†2âŸ«_H  = fun x => (conj (Ï†1 x 0) * (Ï†2 x 0) + conj (Ï†1 x 1) * (Ï†2 x 1) ) := by
  funext x
  simp only [innerProd, PiLp.inner_apply, RCLike.inner_apply, Fin.sum_univ_two]

/-- Given a `higgsField`, the map `spaceTime â†’ â„` obtained by taking the square norm of the
 higgs vector.  -/
@[simp]
def normSq (Ï† : HiggsField) : SpaceTime â†’ â„ := fun x => ( â€–Ï† xâ€– ^ 2)

/-- Notation for the norm squared of a Higgs field. -/
scoped[StandardModel.HiggsField] notation "â€–" Ï†1 "â€–_H ^ 2" => normSq Ï†1

lemma innerProd_self_eq_normSq (Ï† : HiggsField) (x : SpaceTime) :
    âŸªÏ†, Ï†âŸ«_H x  = â€–Ï†â€–_H ^ 2 x := by
  erw [normSq, @PiLp.norm_sq_eq_of_L2, Fin.sum_univ_two]
  simp only [ ofReal_add, ofReal_pow, innerProd, PiLp.inner_apply,
    RCLike.inner_apply, Fin.sum_univ_two, conj_mul']

lemma normSq_eq_innerProd_self (Ï† : HiggsField) (x : SpaceTime) :
    â€–Ï† xâ€– ^ 2 = (âŸªÏ†, Ï†âŸ«_H x).re := by
  rw [innerProd_self_eq_normSq]
  rfl

lemma toHiggsVec_norm (Ï† : HiggsField) (x : SpaceTime) :
    â€–Ï† xâ€–  = â€–Ï†.toHiggsVec xâ€– := rfl

lemma normSq_expand (Ï† : HiggsField)  :
    Ï†.normSq  = fun x => (conj (Ï† x 0) * (Ï† x 0) + conj (Ï† x 1) * (Ï† x 1) ).re := by
  funext x
  simp [normSq, add_re, mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add, @norm_sq_eq_inner â„‚]

lemma normSq_nonneg (Ï† : HiggsField) (x : SpaceTime) : 0 â‰¤ Ï†.normSq x := by
  simp [normSq, ge_iff_le, norm_nonneg, pow_nonneg]

lemma normSq_zero (Ï† : HiggsField) (x : SpaceTime) : Ï†.normSq x = 0 â†” Ï† x = 0 := by
  simp [normSq, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff, norm_eq_zero]

/-!

## The Higgs potential

-/

/-- The Higgs potential of the form `- Î¼Â² * |Ï†|Â² + Î» * |Ï†|â´`. -/
@[simp]
def potential (Ï† : HiggsField) (Î¼Sq lambda : â„ ) (x : SpaceTime) :  â„ :=
  - Î¼Sq * Ï†.normSq x + lambda * Ï†.normSq x * Ï†.normSq x

lemma potential_apply (Ï† : HiggsField) (Î¼Sq lambda : â„) (x : SpaceTime) :
    (Ï†.potential Î¼Sq lambda) x = HiggsVec.potential Î¼Sq lambda (Ï†.toHiggsVec x) := by
  simp [HiggsVec.potential, toHiggsVec_norm]
  ring

/-!

## Smoothness

-/

lemma toVec_smooth (Ï† : HiggsField) : Smooth ð“˜(â„, SpaceTime) ð“˜(â„, Fin 2 â†’ â„‚) Ï† :=
  smooth_higgsVecToFin2â„‚.comp Ï†.toHiggsVec_smooth

lemma apply_smooth (Ï† : HiggsField) :
    âˆ€ i, Smooth ð“˜(â„, SpaceTime) ð“˜(â„, â„‚) (fun (x : SpaceTime) => (Ï† x i)) :=
  (smooth_pi_space).mp (Ï†.toVec_smooth)

lemma apply_re_smooth (Ï† : HiggsField) (i : Fin 2):
    Smooth ð“˜(â„, SpaceTime) ð“˜(â„, â„) (reCLM âˆ˜ (fun (x : SpaceTime) =>  (Ï† x i))) :=
  (ContinuousLinearMap.smooth reCLM).comp (Ï†.apply_smooth i)

lemma apply_im_smooth (Ï† : HiggsField) (i : Fin 2):
    Smooth ð“˜(â„, SpaceTime) ð“˜(â„, â„) (imCLM âˆ˜ (fun (x : SpaceTime) =>  (Ï† x i))) :=
  (ContinuousLinearMap.smooth imCLM).comp (Ï†.apply_smooth i)

lemma normSq_smooth (Ï† : HiggsField) : Smooth ð“˜(â„, SpaceTime) ð“˜(â„, â„) Ï†.normSq := by
  rw [normSq_expand]
  refine Smooth.add ?_ ?_
  simp only [mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
  refine Smooth.add ?_ ?_
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_re_smooth 0
  exact Ï†.apply_re_smooth 0
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_im_smooth 0
  exact Ï†.apply_im_smooth 0
  simp only [mul_re, conj_re, conj_im, neg_mul, sub_neg_eq_add]
  refine Smooth.add ?_ ?_
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_re_smooth 1
  exact Ï†.apply_re_smooth 1
  refine Smooth.smul ?_ ?_
  exact Ï†.apply_im_smooth 1
  exact Ï†.apply_im_smooth 1

lemma potential_smooth (Ï† : HiggsField) (Î¼Sq lambda : â„) :
    Smooth ð“˜(â„, SpaceTime) ð“˜(â„, â„) (fun x => Ï†.potential Î¼Sq lambda x) := by
  simp only [potential, normSq, neg_mul]
  exact (smooth_const.smul Ï†.normSq_smooth).neg.add
    ((smooth_const.smul Ï†.normSq_smooth).smul Ï†.normSq_smooth)

/-!

## Constant higgs fields

-/

/-- A higgs field is constant if it is equal for all `x` `y` in `spaceTime`. -/
def IsConst (Î¦ : HiggsField) : Prop := âˆ€ x y, Î¦ x = Î¦ y

lemma isConst_of_higgsVec (Ï† : HiggsVec) : Ï†.toField.IsConst := by
  intro x _
  simp [HiggsVec.toField]

lemma isConst_iff_of_higgsVec (Î¦ : HiggsField) : Î¦.IsConst â†” âˆƒ (Ï† : HiggsVec), Î¦ = Ï†.toField :=
  Iff.intro (fun h â†¦ âŸ¨Î¦ 0, by ext x y; rw [â† h x 0]; rflâŸ©) (fun âŸ¨Ï†, hÏ†âŸ© x y â†¦ by subst hÏ†; rfl)

lemma normSq_of_higgsVec (Ï† : HiggsVec) : Ï†.toField.normSq = fun x => (norm Ï†) ^ 2 := by
  funext x
  simp [normSq, HiggsVec.toField]

lemma potential_of_higgsVec (Ï† : HiggsVec) (Î¼Sq lambda : â„) :
    Ï†.toField.potential Î¼Sq lambda = fun _ => HiggsVec.potential Î¼Sq lambda Ï† := by
  simp [HiggsVec.potential]
  unfold potential
  rw [normSq_of_higgsVec]
  ring_nf

end HiggsField
end
end StandardModel
