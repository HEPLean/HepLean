/-
Copyright (c) 2025 Gordon Hsu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gordon Hsu
-/
import Mathlib.LinearAlgebra.Eigenspace.Triangularizable
import Mathlib.LinearAlgebra.Matrix.Spectrum
/-! # Schur triangulation

Schur triangulation is more commonly known as Schur decomposition or Schur triangularization, but
"triangulation" makes the API more readable. It states that a square matrix over an algebraically
closed field, e.g., `‚ÑÇ`, is unitarily similar to an upper triangular matrix.

## Main definitions

- `Matrix.schur_triangulation` : a matrix `A : Matrix n n ùïú` with `ùïú` being algebraically closed can
be decomposed as `A = U * T * star U` where `U` is unitary and `T` is upper triangular.
- `Matrix.schurTriangulationUnitary` : the unitary matrix `U` as previously stated.
- `Matrix.schurTriangulation` : the upper triangular matrix `T` as previously stated.
- Some auxilary definitions are not meant to be used directly, but
`LinearMap.SchurTriangulationAux.of` contains the main algorithm for the triangulation procedure.

-/

open scoped InnerProductSpace

/-- `subNat' i h` subtracts `m` from `i`. This is an alternative form of `Fin.subNat`. -/
@[inline] def Fin.subNat' (i : Fin (m + n)) (h : ¬¨ i < m) : Fin n :=
  subNat m (Fin.cast (m.add_comm n) i) (Nat.ge_of_not_lt h)

namespace Equiv

/-- An alternative form of `Equiv.sumEquivSigmaBool` where `Bool.casesOn` is replaced by `cond`. -/
def sumEquivSigmalCond : Fin m ‚äï Fin n ‚âÉ Œ£ b, cond b (Fin m) (Fin n) :=
  calc Fin m ‚äï Fin n
    _ ‚âÉ Fin n ‚äï Fin m := sumComm ..
    _ ‚âÉ Œ£ b, Bool.casesOn b (Fin n) (Fin m) := sumEquivSigmaBool ..
    _ ‚âÉ Œ£ b, cond b (Fin m) (Fin n) := sigmaCongrRight fun | true | false => Equiv.refl _

/-- The composition of `finSumFinEquiv` and `Equiv.sumEquivSigmalCond` used by
`LinearMap.SchurTriangulationAux.of`. -/
def finAddEquivSigmaCond : Fin (m + n) ‚âÉ Œ£ b, cond b (Fin m) (Fin n) :=
  finSumFinEquiv.symm.trans sumEquivSigmalCond

variable {i : Fin (m + n)}

lemma finAddEquivSigmaCond_true (h : i < m) : finAddEquivSigmaCond i = ‚ü®true, i, h‚ü© :=
  congrArg sumEquivSigmalCond <| finSumFinEquiv_symm_apply_castAdd ‚ü®i, h‚ü©

lemma finAddEquivSigmaCond_false (h : ¬¨ i < m) : finAddEquivSigmaCond i = ‚ü®false, i.subNat' h‚ü© :=
  let j : Fin n := i.subNat' h
  calc finAddEquivSigmaCond i
    _ = finAddEquivSigmaCond (Fin.natAdd m j) :=
      suffices m + (i - m) = i from congrArg _ (Fin.ext this.symm)
      Nat.add_sub_of_le (Nat.le_of_not_gt h)
    _ = ‚ü®false, i.subNat' h‚ü© := congrArg sumEquivSigmalCond <| finSumFinEquiv_symm_apply_natAdd j

end Equiv

/-- The type family parameterized by `Bool` is finite if each type variant is finite. -/
instance [M : Fintype m] [N : Fintype n] (b : Bool) : Fintype (cond b m n) := b.rec N M

/-- The type family parameterized by `Bool` has decidable equality if each type variant is
decidable. -/
instance [DecidableEq m] [DecidableEq n] : DecidableEq (Œ£ b, cond b m n)
  | ‚ü®true, _‚ü©, ‚ü®false, _‚ü©
  | ‚ü®false, _‚ü©, ‚ü®true, _‚ü© => isFalse nofun
  | ‚ü®false, i‚ü©, ‚ü®false, j‚ü©
  | ‚ü®true, i‚ü©, ‚ü®true, j‚ü© =>
    if h : i = j then isTrue (Sigma.eq rfl h) else isFalse fun | rfl => h rfl

namespace Matrix

/-- The property of a matrix being upper triangular. See also `Matrix.det_of_upperTriangular`. -/
abbrev IsUpperTriangular [LT n] [CommRing R] (A : Matrix n n R) := A.BlockTriangular id

/-- The subtype of upper triangular matrices. -/
abbrev UpperTriangular (n R) [LT n] [CommRing R] := { A : Matrix n n R // A.IsUpperTriangular }

end Matrix

namespace LinearMap
variable [RCLike ùïú]

section
variable [NormedAddCommGroup E] [InnerProductSpace ùïú E]

section
variable [FiniteDimensional ùïú E] [Fintype n] [DecidableEq n]

lemma toMatrixOrthonormal_apply_apply (b : OrthonormalBasis n ùïú E) (f : Module.End ùïú E)
    (i j : n) :
    toMatrixOrthonormal b f i j = ‚ü™b i, f (b j)‚ü´_ùïú :=
  calc
    _ = b.repr (f (b j)) i := f.toMatrix_apply ..
    _ = ‚ü™b i, f (b j)‚ü´_ùïú := b.repr_apply_apply ..

lemma toMatrixOrthonormal_reindex [Fintype m] [DecidableEq m]
    (b : OrthonormalBasis m ùïú E) (e : m ‚âÉ n) (f : Module.End ùïú E) :
    toMatrixOrthonormal (b.reindex e) f = Matrix.reindex e e (toMatrixOrthonormal b f) :=
  Matrix.ext fun i j =>
    calc toMatrixOrthonormal (b.reindex e) f i j
      _ = (b.reindex e).repr (f (b.reindex e j)) i := f.toMatrix_apply ..
      _ = b.repr (f (b (e.symm j))) (e.symm i) := by simp
      _ = toMatrixOrthonormal b f (e.symm i) (e.symm j) := Eq.symm <| f.toMatrix_apply ..

end

/-- **Don't use this definition directly.** Instead, use `Matrix.schurTriangulationBasis`,
`Matrix.schurTriangulationUnitary`, and `Matrix.schurTriangulation`. See also
`LinearMap.SchurTriangulationAux.of` and `Matrix.schurTriangulationAux`. -/
structure SchurTriangulationAux (f : Module.End ùïú E) where
  /-- The dimension of the inner product space `E`. -/
  dim : ‚Ñï
  hdim : Module.finrank ùïú E = dim
  /-- An orthonormal basis of `E` that induces an upper triangular form for `f`. -/
  basis : OrthonormalBasis (Fin dim) ùïú E
  upperTriangular : (toMatrix basis.toBasis basis.toBasis f).IsUpperTriangular

end

/-! ## Schur's recursive triangulation procedure

Given a linear endomorphism `f` on a non-trivial finite-dimensional vector space `E` over an
algebraically closed field `ùïú`, one can always pick an eigenvalue `Œº` of `f` whose corresponding
eigenspace `V` is non-trivial. Given that `E` is also an inner product space, let `bV` and `bW` be
othonormal bases for `V` and `V·óÆ` respectively. Then, the collection of vectors in `bV` and `bW`
forms an othornomal basis `bE` for `E`, as the direct sum of `V` and `V·óÆ` is an internal
decomposition of `E`. The matrix representation of `f` with respect to `bE` satisfies
$$
\sideset{_\mathrm{bE}}{_\mathrm{bE}}{[f]} =
\begin{bmatrix}
  \sideset{_\mathrm{bV}}{_\mathrm{bV}}{[f]} &
  \sideset{_\mathrm{bW}}{_\mathrm{bV}}{[f]} \\
  \sideset{_\mathrm{bV}}{_\mathrm{bW}}{[f]} &
  \sideset{_\mathrm{bW}}{_\mathrm{bW}}{[f]}
\end{bmatrix} =
\begin{bmatrix} \mu I & ‚ñ° \\ 0 & \sideset{_\mathrm{bW}}{_\mathrm{bW}}{[f]} \end{bmatrix},
$$
which is upper triangular as long as $\sideset{_\mathrm{bW}}{_\mathrm{bW}}{[f]}$ is. Finally, one
observes that the recursion from $\sideset{_\mathrm{bE}}{_\mathrm{bE}}{[f]}$ to
$\sideset{_\mathrm{bW}}{_\mathrm{bW}}{[f]}$ is well-founded, as the dimension of `bW` is smaller
than that of `bE` because `bV` is non-trivial.

However, in order to leverage `DirectSum.IsInternal.collectedOrthonormalBasis`, the type
`Œ£ b, cond b (Fin m) (Fin n)` has to be used instead of the more natural `Fin m ‚äï Fin n` while their
equivalence is propositionally established by `Equiv.sumEquivSigmalCond`.

-/

variable [IsAlgClosed ùïú]

/-- **Don't use this definition directly.** This is the key algorithm behind
`Matrix.schur_triangulation`. -/
protected noncomputable def SchurTriangulationAux.of
    [NormedAddCommGroup E] [InnerProductSpace ùïú E] [FiniteDimensional ùïú E] (f : Module.End ùïú E) :
    SchurTriangulationAux f :=
  haveI : Decidable (Nontrivial E) := Classical.propDecidable _
  if hE : Nontrivial E then
    let Œº : f.Eigenvalues := default
    let V : Submodule ùïú E := f.eigenspace Œº
    let W : Submodule ùïú E := V·óÆ
    let m := Module.finrank ùïú V
    have hdim : m + Module.finrank ùïú W = Module.finrank ùïú E := V.finrank_add_finrank_orthogonal
    let g : Module.End ùïú W := orthogonalProjection W ‚àò‚Çó f.domRestrict W
    let ‚ü®n, hn, bW, hg‚ü© := SchurTriangulationAux.of g

    have bV : OrthonormalBasis (Fin m) ùïú V := stdOrthonormalBasis ùïú V
    have hV := V.orthogonalFamily_self
    have int : DirectSum.IsInternal (cond ¬∑ V W) :=
      suffices ‚®Ü b, cond b V W = ‚ä§ from (hV.decomposition this).isInternal _
      (sup_eq_iSup V W).symm.trans Submodule.sup_orthogonal_of_completeSpace
    let B (b : Bool) : OrthonormalBasis (cond b (Fin m) (Fin n)) ùïú ‚Ü•(cond b V W) := b.rec bW bV
    let bE : OrthonormalBasis (Œ£ b, cond b (Fin m) (Fin n)) ùïú E :=
      int.collectedOrthonormalBasis hV B
    let e := Equiv.finAddEquivSigmaCond
    let basis := bE.reindex e.symm
    {
      basis
      dim := m + n
      hdim := hn ‚ñ∏ hdim.symm
      upperTriangular := fun i j (hji : j < i) => show toMatrixOrthonormal basis f i j = 0 from
        have hB : ‚àÄ s, bE s = B s.1 s.2
          | ‚ü®true, i‚ü© => show bE ‚ü®true, i‚ü© = bV i from
            show (int.collectedBasis fun b => (B b).toBasis).toOrthonormalBasis _ ‚ü®true, i‚ü© = bV i
            by simp
          | ‚ü®false, j‚ü© => show bE ‚ü®false, j‚ü© = bW j from
            show (int.collectedBasis fun b => (B b).toBasis).toOrthonormalBasis _ ‚ü®false, j‚ü© = bW j
            by simp
        have hf {bi i' bj j'} (hi : e i = ‚ü®bi, i'‚ü©) (hj : e j = ‚ü®bj, j'‚ü©) :=
          calc toMatrixOrthonormal basis f i j
            _ = toMatrixOrthonormal bE f (e i) (e j) := by
              rw [f.toMatrixOrthonormal_reindex]
              rfl
            _ = ‚ü™bE (e i), f (bE (e j))‚ü´_ùïú := f.toMatrixOrthonormal_apply_apply ..
            _ = ‚ü™(B bi i' : E), f (B bj j')‚ü´_ùïú := by rw [hB, hB, hi, hj]

        if hj : j < m then
          let j' : Fin m := ‚ü®j, hj‚ü©
          have hf' {bi i'} (hi : e i = ‚ü®bi, i'‚ü©) (h0 : ‚ü™(B bi i' : E), bV j'‚ü´_ùïú = 0) :=
            calc toMatrixOrthonormal basis f i j
              _ = ‚ü™(B bi i' : E), f _‚ü´_ùïú := hf hi (Equiv.finAddEquivSigmaCond_true hj)
              _ = ‚ü™_, f (bV j')‚ü´_ùïú := rfl
              _ = 0 :=
                suffices f (bV j') = Œº.val ‚Ä¢ bV j' by rw [this, inner_smul_right, h0, mul_zero]
                suffices f.HasEigenvector Œº (bV j') from this.apply_eq_smul
                ‚ü®(bV j').property, fun h => bV.toBasis.ne_zero j' (Subtype.ext h)‚ü©

          if hi : i < m then
            let i' : Fin m := ‚ü®i, hi‚ü©
            suffices ‚ü™(bV i' : E), bV j'‚ü´_ùïú = 0 from hf' (Equiv.finAddEquivSigmaCond_true hi) this
            bV.orthonormal.right (Fin.ne_of_gt hji)
          else
            let i' : Fin n := i.subNat' hi
            suffices ‚ü™(bW i' : E), bV j'‚ü´_ùïú = 0 from hf' (Equiv.finAddEquivSigmaCond_false hi) this
            V.inner_left_of_mem_orthogonal (bV j').property (bW i').property
        else
          have hi (h : i < m) : False := hj (Nat.lt_trans hji h)
          let i' : Fin n := i.subNat' hi
          let j' : Fin n := j.subNat' hj
          calc toMatrixOrthonormal basis f i j
            _ = ‚ü™(bW i' : E), f (bW j')‚ü´_ùïú :=
              hf (Equiv.finAddEquivSigmaCond_false hi) (Equiv.finAddEquivSigmaCond_false hj)
            _ = ‚ü™bW i', g (bW j')‚ü´_ùïú := by simp [g]
            _ = toMatrixOrthonormal bW g i' j' := (g.toMatrixOrthonormal_apply_apply ..).symm
            _ = 0 := hg (Nat.sub_lt_sub_right (Nat.le_of_not_lt hj) hji)
    }
  else
    haveI : Subsingleton E := not_nontrivial_iff_subsingleton.mp hE
    {
      dim := 0
      hdim := Module.finrank_zero_of_subsingleton
      basis := (Basis.empty E).toOrthonormalBasis ‚ü®nofun, nofun‚ü©
      upperTriangular := nofun
    }
termination_by Module.finrank ùïú E
decreasing_by exact
  calc Module.finrank ùïú W
    _ < m + Module.finrank ùïú W :=
      suffices 0 < m from Nat.lt_add_of_pos_left this
      Submodule.one_le_finrank_iff.mpr Œº.property
    _ = Module.finrank ùïú E := hdim

end LinearMap

namespace Matrix
/- IMPORTANT: existing `DecidableEq n` should take precedence over `LinearOrder.decidableEq`,
a.k.a., `instDecidableEq_mathlib`. -/
variable [RCLike ùïú] [IsAlgClosed ùïú] [Fintype n] [DecidableEq n] [LinearOrder n] (A : Matrix n n ùïú)

/-- **Don't use this definition directly.** Instead, use `Matrix.schurTriangulationBasis`,
`Matrix.schurTriangulationUnitary`, and `Matrix.schurTriangulation` for which this is their
simultaneous definition. This is `LinearMap.SchurTriangulationAux` adapted for matrices in the
Euclidean space. -/
noncomputable def schurTriangulationAux :
    OrthonormalBasis n ùïú (EuclideanSpace ùïú n) √ó UpperTriangular n ùïú :=
  let f := toEuclideanLin A
  let ‚ü®d, hd, b, hut‚ü© := LinearMap.SchurTriangulationAux.of f
  let e : Fin d ‚âÉo n := Fintype.orderIsoFinOfCardEq n (finrank_euclideanSpace.symm.trans hd)
  let b' := b.reindex e
  let B := LinearMap.toMatrixOrthonormal b' f
  suffices B.IsUpperTriangular from ‚ü®b', B, this‚ü©
  fun i j (hji : j < i) =>
    calc LinearMap.toMatrixOrthonormal b' f i j
      _ = LinearMap.toMatrixOrthonormal b f (e.symm i) (e.symm j) := by
        rw [f.toMatrixOrthonormal_reindex]
        rfl
      _ = 0 := hut (e.symm.lt_iff_lt.mpr hji)

/-- The change of basis that induces the upper triangular form `A.schurTriangulation` of a matrix
`A` over an algebraically closed field. -/
noncomputable def schurTriangulationBasis : OrthonormalBasis n ùïú (EuclideanSpace ùïú n) :=
  A.schurTriangulationAux.1

/-- The unitary matrix that induces the upper triangular form `A.schurTriangulation` to which a
matrix `A` over an algebraically closed field is unitarily similar. -/
noncomputable def schurTriangulationUnitary : unitaryGroup n ùïú where
  val := (EuclideanSpace.basisFun n ùïú).toBasis.toMatrix A.schurTriangulationBasis
  property := OrthonormalBasis.toMatrix_orthonormalBasis_mem_unitary ..

/-- The upper triangular form induced by `A.schurTriangulationUnitary` to which a matrix `A` over an
algebraically closed field is unitarily similar. -/
noncomputable def schurTriangulation : UpperTriangular n ùïú :=
  A.schurTriangulationAux.2

/-- **Schur triangulation**, **Schur decomposition** for matrices over an algebraically closed
field. In particular, a complex matrix can be converted to upper-triangular form by a change of
basis. In other words, any complex matrix is unitarily similar to an upper triangular matrix. -/
lemma schur_triangulation :
    A = A.schurTriangulationUnitary * A.schurTriangulation * star A.schurTriangulationUnitary :=
  let U := A.schurTriangulationUnitary
  have h : U * A.schurTriangulation.val = A * U :=
    let b := A.schurTriangulationBasis.toBasis
    let c := (EuclideanSpace.basisFun n ùïú).toBasis
    calc c.toMatrix b * LinearMap.toMatrix b b (toEuclideanLin A)
      _ = LinearMap.toMatrix c c (toEuclideanLin A) * c.toMatrix b := by simp
      _ = LinearMap.toMatrix c c (toLin c c A) * U := rfl
      _ = A * U := by simp
  calc A
    _ = A * U * star U := by simp [mul_assoc]
    _ = U * A.schurTriangulation * star U := by rw [‚Üê h]

end Matrix
