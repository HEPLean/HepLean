/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.Wick.CreateAnnilateSection
import HepLean.PerturbationTheory.Wick.KoszulOrder
/-!

# Koszul signs and ordering for lists and algebras

-/

namespace Wick
open HepLean.List
open FieldStatistic

noncomputable section

variable {ð“• : Type} (q : ð“• â†’ FieldStatistic) (le : ð“• â†’ ð“• â†’ Prop) [DecidableRel le]

/-- The element of the free algebra `FreeAlgebra â„‚ I` associated with a `List I`. -/
def ofList (l : List ð“•) (x : â„‚) : FreeAlgebra â„‚ ð“• :=
  FreeAlgebra.equivMonoidAlgebraFreeMonoid.symm (MonoidAlgebra.single l x)

lemma ofList_pair (l r : List ð“•) (x y : â„‚) :
    ofList (l ++ r) (x * y) = ofList l x * ofList r y := by
  simp only [ofList, â† map_mul, MonoidAlgebra.single_mul_single, EmbeddingLike.apply_eq_iff_eq]
  rfl

lemma ofList_triple (la lb lc : List ð“•) (xa xb xc : â„‚) :
    ofList (la ++ lb ++ lc) (xa * xb * xc) = ofList la xa * ofList lb xb * ofList lc xc := by
  rw [ofList_pair, ofList_pair]

lemma ofList_triple_assoc (la lb lc : List ð“•) (xa xb xc : â„‚) :
    ofList (la ++ (lb ++ lc)) (xa * (xb * xc)) = ofList la xa * ofList lb xb * ofList lc xc := by
  rw [ofList_pair, ofList_pair]
  exact Eq.symm (mul_assoc (ofList la xa) (ofList lb xb) (ofList lc xc))

lemma ofList_cons_eq_ofList (l : List ð“•) (i : ð“•) (x : â„‚) :
    ofList (i :: l) x = ofList [i] 1 * ofList l x := by
  simp only [ofList, â† map_mul, MonoidAlgebra.single_mul_single, one_mul,
    EmbeddingLike.apply_eq_iff_eq]
  rfl

lemma ofList_singleton (i : ð“•) :
    ofList [i] 1 = FreeAlgebra.Î¹ â„‚ i := by
  simp only [ofList, FreeAlgebra.equivMonoidAlgebraFreeMonoid, MonoidAlgebra.of_apply,
    MonoidAlgebra.single, AlgEquiv.ofAlgHom_symm_apply, MonoidAlgebra.lift_single, one_smul]
  rfl

lemma ofList_eq_smul_one (l : List ð“•) (x : â„‚) :
    ofList l x = x â€¢ ofList l 1 := by
  simp only [ofList]
  rw [â† map_smul]
  simp

lemma ofList_empty : ofList [] 1 = (1 : FreeAlgebra â„‚ ð“•) := by
  simp only [ofList, EmbeddingLike.map_eq_one_iff]
  rfl

lemma ofList_empty' : ofList [] x = x â€¢ (1 : FreeAlgebra â„‚ ð“•) := by
  rw [ofList_eq_smul_one, ofList_empty]

lemma koszulOrder_ofList
    (l : List ð“•) (x : â„‚) :
    koszulOrder q le (ofList l x) = (koszulSign q le l) â€¢ ofList (List.insertionSort le l) x := by
  rw [ofList]
  rw [koszulOrder_single]
  change ofList (List.insertionSort le l) _ = _
  rw [ofList_eq_smul_one]
  conv_rhs => rw [ofList_eq_smul_one]
  rw [smul_smul]

lemma ofList_insertionSort_eq_koszulOrder (l : List ð“•) (x : â„‚) :
    ofList (List.insertionSort le l) x = (koszulSign q le l) â€¢ koszulOrder q le (ofList l x) := by
  rw [koszulOrder_ofList]
  rw [smul_smul]
  rw [koszulSign_mul_self]
  simp

/-- The map of algebras from `FreeAlgebra â„‚ I` to `FreeAlgebra â„‚ (Î£ i, f i)` taking
  the monomial `i` to the sum of elements in `(Î£ i, f i)` above `i`, i.e. the sum of the fiber
  above `i`. -/
def sumFiber (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] :
    FreeAlgebra â„‚ ð“• â†’â‚[â„‚] FreeAlgebra â„‚ (Î£ i, f i) :=
  FreeAlgebra.lift â„‚ fun i => âˆ‘ (j : f i), FreeAlgebra.Î¹ â„‚ âŸ¨i, jâŸ©

lemma sumFiber_Î¹ (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (i : ð“•) :
    sumFiber f (FreeAlgebra.Î¹ â„‚ i) = âˆ‘ (b : f i), FreeAlgebra.Î¹ â„‚ âŸ¨i, bâŸ© := by
  simp [sumFiber]

/-- Given a list `l : List I` the corresponding element of `FreeAlgebra â„‚ (Î£ i, f i)`
  by mapping each `i : I` to the sum of it's fiber in `Î£ i, f i` and taking the product of the
  result.
  For example, in terms of creation and annihlation opperators,
  `[Ï†â‚, Ï†â‚‚, Ï†â‚ƒ]` gets taken to `(Ï†â‚â° + Ï†â‚Â¹)(Ï†â‚‚â° + Ï†â‚‚Â¹)(Ï†â‚ƒâ° + Ï†â‚ƒÂ¹)`.
  -/
def ofListLift (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (l : List ð“•) (x : â„‚) :
    FreeAlgebra â„‚ (Î£ i, f i) :=
  sumFiber f (ofList l x)

lemma ofListLift_empty (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] :
    ofListLift f [] 1 = 1 := by
  simp only [ofListLift, EmbeddingLike.map_eq_one_iff]
  rw [ofList_empty]
  exact map_one (sumFiber f)

lemma ofListLift_empty_smul (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (x : â„‚) :
    ofListLift f [] x = x â€¢ 1 := by
  simp only [ofListLift, EmbeddingLike.map_eq_one_iff]
  rw [ofList_eq_smul_one]
  rw [ofList_empty]
  simp

lemma ofListLift_cons (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (i : ð“•) (r : List ð“•) (x : â„‚) :
    ofListLift f (i :: r) x = (âˆ‘ j : f i, FreeAlgebra.Î¹ â„‚ âŸ¨i, jâŸ©) * (ofListLift f r x) := by
  rw [ofListLift, ofList_cons_eq_ofList, ofList_singleton, map_mul]
  conv_lhs => lhs; rw [sumFiber]
  rw [ofListLift]
  simp

lemma ofListLift_singleton (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (i : ð“•) (x : â„‚) :
    ofListLift f [i] x = âˆ‘ j : f i, x â€¢ FreeAlgebra.Î¹ â„‚ âŸ¨i, jâŸ© := by
  simp only [ofListLift]
  rw [ofList_eq_smul_one, ofList_singleton, map_smul]
  rw [sumFiber_Î¹]
  rw [Finset.smul_sum]

lemma ofListLift_singleton_one (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (i : ð“•) :
    ofListLift f [i] 1 = âˆ‘ j : f i, FreeAlgebra.Î¹ â„‚ âŸ¨i, jâŸ© := by
  simp only [ofListLift]
  rw [ofList_eq_smul_one, ofList_singleton, map_smul]
  rw [sumFiber_Î¹]
  rw [Finset.smul_sum]
  simp

lemma ofListLift_cons_eq_ofListLift (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (i : ð“•)
    (r : List ð“•) (x : â„‚) :
    ofListLift f (i :: r) x = ofListLift f [i] 1 * ofListLift f r x := by
  rw [ofListLift_cons, ofListLift_singleton]
  simp only [one_smul]

lemma ofListLift_expand (f : ð“• â†’ Type) [âˆ€ i, Fintype (f i)] (x : â„‚) :
    (l : List ð“•) â†’ ofListLift f l x = âˆ‘ (a : CreateAnnilateSect f l), ofList a.toList x
  | [] => by
    simp only [ofListLift, CreateAnnilateSect, List.length_nil, List.get_eq_getElem,
      Finset.univ_unique, CreateAnnilateSect.toList, Finset.sum_const, Finset.card_singleton,
      one_smul]
    rw [ofList_eq_smul_one, map_smul, ofList_empty, ofList_eq_smul_one, ofList_empty, map_one]
  | i :: l => by
    rw [ofListLift_cons, ofListLift_expand f x l]
    conv_rhs => rw [â† (CreateAnnilateSect.extractEquiv
        âŸ¨0, by exact Nat.zero_lt_succ l.lengthâŸ©).symm.sum_comp (Î± := FreeAlgebra â„‚ _)]
    erw [Finset.sum_product]
    rw [Finset.sum_mul]
    conv_lhs =>
      rhs
      intro n
      rw [Finset.mul_sum]
    congr
    funext j
    congr
    funext n
    rw [â† ofList_singleton, â† ofList_pair, one_mul]
    rfl

lemma koszulOrder_ofListLift {f : ð“• â†’ Type} [âˆ€ i, Fintype (f i)]
    (l : List ð“•) (x : â„‚) :
    koszulOrder (fun i => q i.fst) (fun i j => le i.1 j.1) (ofListLift f l x) =
    sumFiber f (koszulOrder q le (ofList l x)) := by
  rw [koszulOrder_ofList]
  rw [map_smul]
  change _ = _ â€¢ ofListLift _ _ _
  rw [ofListLift_expand]
  rw [map_sum]
  conv_lhs =>
    rhs
    intro a
    rw [koszulOrder_ofList]
    rw [CreateAnnilateSect.toList_koszulSign]
  rw [â† Finset.smul_sum]
  apply congrArg
  conv_lhs =>
    rhs
    intro n
    rw [â† CreateAnnilateSect.sort_toList]
  rw [ofListLift_expand]
  refine Fintype.sum_equiv
    ((HepLean.List.insertionSortEquiv le l).piCongr fun i => Equiv.cast ?_) _ _ ?_
  congr 1
  Â· rw [â† HepLean.List.insertionSortEquiv_get]
    simp
  Â· intro x
    rfl

lemma koszulOrder_ofListLift_eq_ofListLift {f : ð“• â†’ Type} [âˆ€ i, Fintype (f i)]
    (l : List ð“•) (x : â„‚) : koszulOrder (fun i => q i.fst) (fun i j => le i.1 j.1)
    (ofListLift f l x) =
    koszulSign q le l â€¢ ofListLift f (List.insertionSort le l) x := by
  rw [koszulOrder_ofListLift, koszulOrder_ofList, map_smul]
  rfl

end
end Wick
