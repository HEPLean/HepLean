/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.Mathematics.List
import HepLean.PerturbationTheory.Wick.Signs.SuperCommuteCoef
/-!

# Insert sign

-/

namespace Wick
open HepLean.List
open FieldStatistic

section
/-!

## Basic properties of lists

To be replaced with Mathlib or Lean definitions when/where appropraite.
-/

lemma take_insert_same {I : Type} (i : I) :
    (n : â„•) â†’ (r : List I) â†’
    List.take n (List.insertIdx n i r) = List.take n r
  | 0, _ => by simp
  | _+1, [] => by simp
  | n+1, a::as => by
    simp only [List.insertIdx_succ_cons, List.take_succ_cons, List.cons.injEq, true_and]
    exact take_insert_same i n as

lemma take_eraseIdx_same {I : Type} :
    (n : â„•) â†’ (r : List I) â†’
    List.take n (List.eraseIdx r n) = List.take n r
  | 0, _ => by simp
  | _+1, [] => by simp
  | n+1, a::as => by
    simp only [List.eraseIdx_cons_succ, List.take_succ_cons, List.cons.injEq, true_and]
    exact take_eraseIdx_same n as

lemma take_insert_gt {I : Type} (i : I) :
    (n m : â„•) â†’ (h : n < m) â†’ (r : List I) â†’
    List.take n (List.insertIdx m i r) = List.take n r
  | 0, 0, _, _ => by simp
  | 0, m + 1, _, _ => by simp
  | n+1, m + 1, _, [] => by simp
  | n+1, m + 1, h, a::as => by
    simp only [List.insertIdx_succ_cons, List.take_succ_cons, List.cons.injEq, true_and]
    refine take_insert_gt i n m (Nat.succ_lt_succ_iff.mp h) as

lemma take_insert_let {I : Type} (i : I) :
    (n m : â„•) â†’ (h : m â‰¤ n) â†’ (r : List I) â†’ (hm : m â‰¤ r.length) â†’
    (List.take (n + 1) (List.insertIdx m i r)).Perm (i :: List.take n r)
  | 0, 0, h, _, _ => by simp
  | m + 1, 0, h, r, _ => by simp
  | n + 1, m + 1, h, [], hm => by
    simp at hm
  | n + 1, m + 1, h, a::as, hm => by
    simp only [List.insertIdx_succ_cons, List.take_succ_cons]
    have hp : (i :: a :: List.take n as).Perm (a :: i :: List.take n as) := by
      exact List.Perm.swap a i (List.take n as)
    refine List.Perm.trans ?_ hp.symm
    refine List.Perm.cons a ?_
    exact take_insert_let i n m (Nat.le_of_succ_le_succ h) as (Nat.le_of_succ_le_succ hm)

end

/-!

## Insert sign

-/

section InsertSign

variable {ğ“• : Type} (q : ğ“• â†’ FieldStatistic)

/-- The sign associated with inserting a field `Ï†` into a list of fields `Ï†s` at
  the `n`th position. -/
def insertSign (n : â„•) (Ï† : ğ“•) (Ï†s : List ğ“•) : â„‚ :=
  superCommuteCoef q [Ï†] (List.take n Ï†s)

/-- If `Ï†` is bosonic, there is no sign associated with inserting it into a list of fields. -/
lemma insertSign_bosonic (n : â„•)  (Ï† : ğ“•) (Ï†s : List ğ“•) (hÏ† : q Ï† = bosonic) :
    insertSign q n Ï† Ï†s = 1 := by
  simp only [insertSign, superCommuteCoef, ofList_singleton, hÏ†, reduceCtorEq, false_and,
    â†“reduceIte]

lemma insertSign_insert (n : â„•) (Ï† : ğ“•) (Ï†s : List ğ“•) :
    insertSign q n Ï† Ï†s = insertSign q n Ï† (List.insertIdx n Ï† Ï†s) := by
  simp only [insertSign]
  congr 1
  rw [take_insert_same]

lemma insertSign_eraseIdx (n : â„•) (Ï† : ğ“•) (Ï†s : List ğ“•) :
    insertSign q n Ï† (Ï†s.eraseIdx n) = insertSign q n Ï† Ï†s := by
  simp only [insertSign]
  congr 1
  rw [take_eraseIdx_same]

lemma insertSign_zero (Ï† : ğ“•) (Ï†s : List ğ“•) : insertSign q 0 Ï† Ï†s = 1 := by
  simp [insertSign, superCommuteCoef]

lemma insertSign_succ_cons (n : â„•) (Ï†0 Ï†1 : ğ“•) (Ï†s : List ğ“•) : insertSign q (n + 1) Ï†0 (Ï†1 :: Ï†s) =
    superCommuteCoef q [Ï†0] [Ï†1] * insertSign q n Ï†0 Ï†s := by
  simp only [insertSign, List.take_succ_cons]
  rw [superCommuteCoef_cons]

lemma insertSign_insert_gt (n m : â„•) (Ï†0 Ï†1 : ğ“•) (Ï†s : List ğ“•) (hn : n < m) :
    insertSign q n Ï†0 (List.insertIdx m Ï†1 Ï†s) = insertSign q n Ï†0 Ï†s := by
  rw [insertSign, insertSign]
  congr 1
  exact take_insert_gt Ï†1 n m hn Ï†s

lemma insertSign_insert_lt_eq_insertSort (n m : â„•) (Ï†0 Ï†1 : ğ“•) (Ï†s : List ğ“•) (hn : m â‰¤ n)
    (hm : m â‰¤ Ï†s.length) :
    insertSign q (n + 1) Ï†0 (List.insertIdx m Ï†1 Ï†s) = insertSign q (n + 1) Ï†0 (Ï†1 :: Ï†s) := by
  rw [insertSign, insertSign]
  apply superCommuteCoef_perm_snd
  simp only [List.take_succ_cons]
  refine take_insert_let Ï†1 n m hn Ï†s hm

lemma insertSign_insert_lt (n m : â„•) (Ï†0 Ï†1 : ğ“•) (Ï†s : List ğ“•) (hn : m â‰¤ n) (hm : m â‰¤ Ï†s.length) :
    insertSign q (n + 1) Ï†0 (List.insertIdx m Ï†1 Ï†s) = superCommuteCoef q [Ï†0] [Ï†1] *
    insertSign q n Ï†0 Ï†s := by
  rw [insertSign_insert_lt_eq_insertSort, insertSign_succ_cons]
  Â· exact hn
  Â· exact hm

end InsertSign

end Wick
