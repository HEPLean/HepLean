/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldSpecification.NormalOrder
import HepLean.PerturbationTheory.Algebras.FieldOpFreeAlgebra.SuperCommute
import HepLean.PerturbationTheory.Koszul.KoszulSign
/-!

# Normal Ordering in the FieldOpFreeAlgebra

In the module
`HepLean.PerturbationTheory.FieldSpecification.NormalOrder`
we defined the normal ordering of a list of `CrAnFieldOp`.
In this module we extend the normal ordering to a linear map on `FieldOpFreeAlgebra`.

We derive properties of this normal ordering.

-/

namespace FieldSpecification
variable {ð“• : FieldSpecification}
open FieldStatistic

namespace FieldOpFreeAlgebra

noncomputable section

/-- The linear map on the free creation and annihlation
  algebra defined as the map taking
  a list of CrAnFieldOp to the normal-ordered list of states multiplied by
  the sign corresponding to the number of fermionic-fermionic
  exchanges done in ordering. -/
def normalOrderF : FieldOpFreeAlgebra ð“• â†’â‚—[â„‚] FieldOpFreeAlgebra ð“• :=
  Basis.constr ofCrAnListFBasis â„‚ fun Ï†s =>
  normalOrderSign Ï†s â€¢ ofCrAnListF (normalOrderList Ï†s)

@[inherit_doc normalOrderF]
scoped[FieldSpecification.FieldOpFreeAlgebra] notation "ð“á¶ (" a ")" => normalOrderF a

lemma normalOrderF_ofCrAnListF (Ï†s : List ð“•.CrAnFieldOp) :
    ð“á¶ (ofCrAnListF Ï†s) = normalOrderSign Ï†s â€¢ ofCrAnListF (normalOrderList Ï†s) := by
  rw [â† ofListBasis_eq_ofList, normalOrderF, Basis.constr_basis]

lemma ofCrAnListF_eq_normalOrderF (Ï†s : List ð“•.CrAnFieldOp) :
    ofCrAnListF (normalOrderList Ï†s) = normalOrderSign Ï†s â€¢ ð“á¶ (ofCrAnListF Ï†s) := by
  rw [normalOrderF_ofCrAnListF, normalOrderList, smul_smul, normalOrderSign,
    Wick.koszulSign_mul_self, one_smul]

lemma normalOrderF_one : normalOrderF (ð“• := ð“•) 1 = 1 := by
  rw [â† ofCrAnListF_nil, normalOrderF_ofCrAnListF, normalOrderSign_nil, normalOrderList_nil,
    ofCrAnListF_nil, one_smul]

lemma normalOrderF_normalOrderF_mid (a b c : ð“•.FieldOpFreeAlgebra) :
    ð“á¶ (a * b * c) = ð“á¶ (a * ð“á¶ (b) * c) := by
  let pc (c : ð“•.FieldOpFreeAlgebra) (hc : c âˆˆ Submodule.span â„‚ (Set.range ofCrAnListFBasis)) :
    Prop := ð“á¶ (a * b * c) = ð“á¶ (a * ð“á¶ (b) * c)
  change pc c (Basis.mem_span _ c)
  apply Submodule.span_induction
  Â· intro x hx
    obtain âŸ¨Ï†s, rflâŸ© := hx
    simp only [ofListBasis_eq_ofList, pc]
    let pb (b : ð“•.FieldOpFreeAlgebra) (hb : b âˆˆ Submodule.span â„‚ (Set.range ofCrAnListFBasis)) :
      Prop := ð“á¶ (a * b * ofCrAnListF Ï†s) = ð“á¶ (a * ð“á¶ (b) * ofCrAnListF Ï†s)
    change pb b (Basis.mem_span _ b)
    apply Submodule.span_induction
    Â· intro x hx
      obtain âŸ¨Ï†s', rflâŸ© := hx
      simp only [ofListBasis_eq_ofList, pb]
      let pa (a : ð“•.FieldOpFreeAlgebra) (ha : a âˆˆ Submodule.span â„‚ (Set.range ofCrAnListFBasis)) :
        Prop := ð“á¶ (a * ofCrAnListF Ï†s' * ofCrAnListF Ï†s) = ð“á¶ (a * ð“á¶ (ofCrAnListF Ï†s') * ofCrAnListF Ï†s)
      change pa a (Basis.mem_span _ a)
      apply Submodule.span_induction
      Â· intro x hx
        obtain âŸ¨Ï†s'', rflâŸ© := hx
        simp only [ofListBasis_eq_ofList, pa]
        rw [normalOrderF_ofCrAnListF]
        simp only [â† ofCrAnListF_append, Algebra.mul_smul_comm,
          Algebra.smul_mul_assoc, map_smul]
        rw [normalOrderF_ofCrAnListF, normalOrderF_ofCrAnListF, smul_smul]
        congr 1
        Â· simp only [normalOrderSign, normalOrderList]
          rw [Wick.koszulSign_of_append_eq_insertionSort, mul_comm]
        Â· congr 1
          simp only [normalOrderList]
          rw [HepLean.List.insertionSort_append_insertionSort_append]
      Â· simp [pa]
      Â· intro x y hx hy h1 h2
        simp_all [pa, add_mul]
      Â· intro x hx h
        simp_all [pa]
    Â· simp [pb]
    Â· intro x y hx hy h1 h2
      simp_all [pb, mul_add, add_mul]
    Â· intro x hx h
      simp_all [pb]
  Â· simp [pc]
  Â· intro x y hx hy h1 h2
    simp_all [pc, mul_add]
  Â· intro x hx h hp
    simp_all [pc]

lemma normalOrderF_normalOrderF_right (a b : ð“•.FieldOpFreeAlgebra) : ð“á¶ (a * b) = ð“á¶ (a * ð“á¶ (b)) := by
  trans ð“á¶ (a * b * 1)
  Â· simp
  Â· rw [normalOrderF_normalOrderF_mid]
    simp

lemma normalOrderF_normalOrderF_left (a b : ð“•.FieldOpFreeAlgebra) : ð“á¶ (a * b) = ð“á¶ (ð“á¶ (a) * b) := by
  trans ð“á¶ (1 * a * b)
  Â· simp
  Â· rw [normalOrderF_normalOrderF_mid]
    simp

/-!

## Normal ordering with a creation operator on the left or annihilation on the right

-/

lemma normalOrderF_ofCrAnListF_cons_create (Ï† : ð“•.CrAnFieldOp)
    (hÏ† : ð“• |>á¶œ Ï† = CreateAnnihilate.create) (Ï†s : List ð“•.CrAnFieldOp) :
    ð“á¶ (ofCrAnListF (Ï† :: Ï†s)) = ofCrAnOpF Ï† * ð“á¶ (ofCrAnListF Ï†s) := by
  rw [normalOrderF_ofCrAnListF, normalOrderSign_cons_create Ï† hÏ†,
    normalOrderList_cons_create Ï† hÏ† Ï†s]
  rw [ofCrAnListF_cons, normalOrderF_ofCrAnListF, mul_smul_comm]

lemma normalOrderF_create_mul (Ï† : ð“•.CrAnFieldOp)
    (hÏ† : ð“• |>á¶œ Ï† = CreateAnnihilate.create) (a : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (ofCrAnOpF Ï† * a) = ofCrAnOpF Ï† * ð“á¶ (a) := by
  change (normalOrderF âˆ˜â‚— mulLinearMap (ofCrAnOpF Ï†)) a =
    (mulLinearMap (ofCrAnOpF Ï†) âˆ˜â‚— normalOrderF) a
  refine LinearMap.congr_fun (ofCrAnListFBasis.ext fun l â†¦ ?_) a
  simp only [mulLinearMap, LinearMap.coe_mk, AddHom.coe_mk, ofListBasis_eq_ofList,
    LinearMap.coe_comp, Function.comp_apply]
  rw [â† ofCrAnListF_cons, normalOrderF_ofCrAnListF_cons_create Ï† hÏ†]

lemma normalOrderF_ofCrAnListF_append_annihilate (Ï† : ð“•.CrAnFieldOp)
    (hÏ† : ð“• |>á¶œ Ï† = CreateAnnihilate.annihilate) (Ï†s : List ð“•.CrAnFieldOp) :
    ð“á¶ (ofCrAnListF (Ï†s ++ [Ï†])) = ð“á¶ (ofCrAnListF Ï†s) * ofCrAnOpF Ï† := by
  rw [normalOrderF_ofCrAnListF, normalOrderSign_append_annihlate Ï† hÏ† Ï†s,
    normalOrderList_append_annihilate Ï† hÏ† Ï†s, ofCrAnListF_append, ofCrAnListF_singleton,
      normalOrderF_ofCrAnListF, smul_mul_assoc]

lemma normalOrderF_mul_annihilate (Ï† : ð“•.CrAnFieldOp)
    (hÏ† : ð“• |>á¶œ Ï† = CreateAnnihilate.annihilate)
    (a : FieldOpFreeAlgebra ð“•) : ð“á¶ (a * ofCrAnOpF Ï†) = ð“á¶ (a) * ofCrAnOpF Ï† := by
  change (normalOrderF âˆ˜â‚— mulLinearMap.flip (ofCrAnOpF Ï†)) a =
    (mulLinearMap.flip (ofCrAnOpF Ï†) âˆ˜â‚— normalOrderF) a
  refine LinearMap.congr_fun (ofCrAnListFBasis.ext fun l â†¦ ?_) a
  simp only [mulLinearMap, ofListBasis_eq_ofList, LinearMap.coe_comp, Function.comp_apply,
    LinearMap.flip_apply, LinearMap.coe_mk, AddHom.coe_mk]
  rw [â† ofCrAnListF_singleton, â† ofCrAnListF_append, ofCrAnListF_singleton,
    normalOrderF_ofCrAnListF_append_annihilate Ï† hÏ†]

lemma normalOrderF_crPartF_mul (Ï† : ð“•.FieldOp) (a : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (crPartF Ï† * a) =
    crPartF Ï† * ð“á¶ (a) := by
  match Ï† with
  | .inAsymp Ï† =>
    rw [crPartF]
    exact normalOrderF_create_mul âŸ¨FieldOp.inAsymp Ï†, ()âŸ© rfl a
  | .position Ï† =>
    rw [crPartF]
    exact normalOrderF_create_mul _ rfl _
  | .outAsymp Ï† => simp

lemma normalOrderF_mul_anPartF (Ï† : ð“•.FieldOp) (a : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (a * anPartF Ï†) =
    ð“á¶ (a) * anPartF Ï† := by
  match Ï† with
  | .inAsymp Ï† => simp
  | .position Ï† =>
    rw [anPartF]
    exact normalOrderF_mul_annihilate _ rfl _
  | .outAsymp Ï† =>
    rw [anPartF]
    refine normalOrderF_mul_annihilate _ rfl _

/-!

## Normal ordering for an adjacent creation and annihliation state

The main result of this section is `normalOrderF_superCommuteF_annihilate_create`.
-/

lemma normalOrderF_swap_create_annihlate_ofCrAnListF_ofCrAnListF (Ï†c Ï†a : ð“•.CrAnFieldOp)
    (hÏ†c : ð“• |>á¶œ Ï†c = CreateAnnihilate.create) (hÏ†a : ð“• |>á¶œ Ï†a = CreateAnnihilate.annihilate)
    (Ï†s Ï†s' : List ð“•.CrAnFieldOp) :
    ð“á¶ (ofCrAnListF Ï†s' * ofCrAnOpF Ï†c * ofCrAnOpF Ï†a * ofCrAnListF Ï†s) = ð“¢(ð“• |>â‚› Ï†c, ð“• |>â‚› Ï†a) â€¢
    ð“á¶ (ofCrAnListF Ï†s' * ofCrAnOpF Ï†a * ofCrAnOpF Ï†c * ofCrAnListF Ï†s) := by
  rw [mul_assoc, mul_assoc, â† ofCrAnListF_cons, â† ofCrAnListF_cons, â† ofCrAnListF_append]
  rw [normalOrderF_ofCrAnListF, normalOrderSign_swap_create_annihlate Ï†c Ï†a hÏ†c hÏ†a]
  rw [normalOrderList_swap_create_annihlate Ï†c Ï†a hÏ†c hÏ†a, â† smul_smul, â† normalOrderF_ofCrAnListF]
  rw [ofCrAnListF_append, ofCrAnListF_cons, ofCrAnListF_cons]
  noncomm_ring

lemma normalOrderF_swap_create_annihlate_ofCrAnListF (Ï†c Ï†a : ð“•.CrAnFieldOp)
    (hÏ†c : ð“• |>á¶œ Ï†c = CreateAnnihilate.create) (hÏ†a : ð“• |>á¶œ Ï†a = CreateAnnihilate.annihilate)
    (Ï†s : List ð“•.CrAnFieldOp) (a : ð“•.FieldOpFreeAlgebra) :
    ð“á¶ (ofCrAnListF Ï†s * ofCrAnOpF Ï†c * ofCrAnOpF Ï†a * a) = ð“¢(ð“• |>â‚› Ï†c, ð“• |>â‚› Ï†a) â€¢
    ð“á¶ (ofCrAnListF Ï†s * ofCrAnOpF Ï†a * ofCrAnOpF Ï†c * a) := by
  change (normalOrderF âˆ˜â‚— mulLinearMap (ofCrAnListF Ï†s * ofCrAnOpF Ï†c * ofCrAnOpF Ï†a)) a =
    (smulLinearMap _ âˆ˜â‚— normalOrderF âˆ˜â‚—
    mulLinearMap (ofCrAnListF Ï†s * ofCrAnOpF Ï†a * ofCrAnOpF Ï†c)) a
  refine LinearMap.congr_fun (ofCrAnListFBasis.ext fun l â†¦ ?_) a
  simp only [mulLinearMap, LinearMap.coe_mk, AddHom.coe_mk, ofListBasis_eq_ofList,
    LinearMap.coe_comp, Function.comp_apply, instCommGroup.eq_1]
  rw [normalOrderF_swap_create_annihlate_ofCrAnListF_ofCrAnListF Ï†c Ï†a hÏ†c hÏ†a]
  rfl

lemma normalOrderF_swap_create_annihlate (Ï†c Ï†a : ð“•.CrAnFieldOp)
    (hÏ†c : ð“• |>á¶œ Ï†c = CreateAnnihilate.create) (hÏ†a : ð“• |>á¶œ Ï†a = CreateAnnihilate.annihilate)
    (a b : ð“•.FieldOpFreeAlgebra) :
    ð“á¶ (a * ofCrAnOpF Ï†c * ofCrAnOpF Ï†a * b) = ð“¢(ð“• |>â‚› Ï†c, ð“• |>â‚› Ï†a) â€¢
    ð“á¶ (a * ofCrAnOpF Ï†a * ofCrAnOpF Ï†c * b) := by
  rw [mul_assoc, mul_assoc, mul_assoc, mul_assoc]
  change (normalOrderF âˆ˜â‚— mulLinearMap.flip (ofCrAnOpF Ï†c * (ofCrAnOpF Ï†a * b))) a =
    (smulLinearMap (ð“¢(ð“• |>â‚› Ï†c, ð“• |>â‚› Ï†a)) âˆ˜â‚—
    normalOrderF âˆ˜â‚— mulLinearMap.flip (ofCrAnOpF Ï†a * (ofCrAnOpF Ï†c * b))) a
  refine LinearMap.congr_fun (ofCrAnListFBasis.ext fun l â†¦ ?_) _
  simp only [mulLinearMap, ofListBasis_eq_ofList, LinearMap.coe_comp, Function.comp_apply,
    LinearMap.flip_apply, LinearMap.coe_mk, AddHom.coe_mk, instCommGroup.eq_1, â† mul_assoc,
      normalOrderF_swap_create_annihlate_ofCrAnListF Ï†c Ï†a hÏ†c hÏ†a]
  rfl

lemma normalOrderF_superCommuteF_create_annihilate (Ï†c Ï†a : ð“•.CrAnFieldOp)
    (hÏ†c : ð“• |>á¶œ Ï†c = CreateAnnihilate.create) (hÏ†a : ð“• |>á¶œ Ï†a = CreateAnnihilate.annihilate)
    (a b : ð“•.FieldOpFreeAlgebra) :
    ð“á¶ (a * [ofCrAnOpF Ï†c, ofCrAnOpF Ï†a]â‚›ca * b) = 0 := by
  simp only [superCommuteF_ofCrAnOpF_ofCrAnOpF, instCommGroup.eq_1, Algebra.smul_mul_assoc]
  rw [mul_sub, sub_mul, map_sub, â† smul_mul_assoc, â† mul_assoc, â† mul_assoc,
    normalOrderF_swap_create_annihlate Ï†c Ï†a hÏ†c hÏ†a]
  simp

lemma normalOrderF_superCommuteF_annihilate_create (Ï†c Ï†a : ð“•.CrAnFieldOp)
    (hÏ†c : ð“• |>á¶œ Ï†c = CreateAnnihilate.create) (hÏ†a : ð“• |>á¶œ Ï†a = CreateAnnihilate.annihilate)
    (a b : ð“•.FieldOpFreeAlgebra) :
    ð“á¶ (a * [ofCrAnOpF Ï†a, ofCrAnOpF Ï†c]â‚›ca * b) = 0 := by
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF_symm]
  simp only [instCommGroup.eq_1, neg_smul, mul_neg, Algebra.mul_smul_comm, neg_mul,
    Algebra.smul_mul_assoc, map_neg, map_smul, neg_eq_zero, smul_eq_zero]
  exact Or.inr (normalOrderF_superCommuteF_create_annihilate Ï†c Ï†a hÏ†c hÏ†a ..)

lemma normalOrderF_swap_crPartF_anPartF (Ï† Ï†' : ð“•.FieldOp) (a b : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (a * (crPartF Ï†) * (anPartF Ï†') * b) =
    ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ï†') â€¢
    ð“á¶ (a * (anPartF Ï†') * (crPartF Ï†) * b) := by
  match Ï†, Ï†' with
  | _, .inAsymp Ï†' => simp
  | .outAsymp Ï†, _ => simp
  | .position Ï†, .position Ï†' =>
    simp only [crPartF_position, anPartF_position, instCommGroup.eq_1]
    rw [normalOrderF_swap_create_annihlate]
    simp only [instCommGroup.eq_1, crAnStatistics, Function.comp_apply, crAnFieldOpToFieldOp_prod]
    rfl; rfl
  | .inAsymp Ï†, .outAsymp Ï†' =>
    simp only [crPartF_negAsymp, anPartF_posAsymp, instCommGroup.eq_1]
    rw [normalOrderF_swap_create_annihlate]
    simp only [instCommGroup.eq_1, crAnStatistics, Function.comp_apply, crAnFieldOpToFieldOp_prod]
    rfl; rfl
  | .inAsymp Ï†, .position Ï†' =>
    simp only [crPartF_negAsymp, anPartF_position, instCommGroup.eq_1]
    rw [normalOrderF_swap_create_annihlate]
    simp only [instCommGroup.eq_1, crAnStatistics, Function.comp_apply, crAnFieldOpToFieldOp_prod]
    rfl; rfl
  | .position Ï†, .outAsymp Ï†' =>
    simp only [crPartF_position, anPartF_posAsymp, instCommGroup.eq_1]
    rw [normalOrderF_swap_create_annihlate]
    simp only [instCommGroup.eq_1, crAnStatistics, Function.comp_apply, crAnFieldOpToFieldOp_prod]
    rfl; rfl

/-!

## Normal ordering for an anPartF and crPartF

Using the results from above.

-/

lemma normalOrderF_swap_anPartF_crPartF (Ï† Ï†' : ð“•.FieldOp) (a b : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (a * (anPartF Ï†) * (crPartF Ï†') * b) =
    ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ï†') â€¢ ð“á¶ (a * (crPartF Ï†') *
      (anPartF Ï†) * b) := by
  simp [normalOrderF_swap_crPartF_anPartF, smul_smul]

lemma normalOrderF_superCommuteF_crPartF_anPartF (Ï† Ï†' : ð“•.FieldOp) (a b : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (a * superCommuteF
      (crPartF Ï†) (anPartF Ï†') * b) = 0 := by
  match Ï†, Ï†' with
  | _, .inAsymp Ï†' => simp
  | .outAsymp Ï†', _ => simp
  | .position Ï†, .position Ï†' =>
    rw [crPartF_position, anPartF_position]
    exact normalOrderF_superCommuteF_create_annihilate _ _ rfl rfl ..
  | .inAsymp Ï†, .outAsymp Ï†' =>
    rw [crPartF_negAsymp, anPartF_posAsymp]
    exact normalOrderF_superCommuteF_create_annihilate _ _ rfl rfl ..
  | .inAsymp Ï†, .position Ï†' =>
    rw [crPartF_negAsymp, anPartF_position]
    exact normalOrderF_superCommuteF_create_annihilate _ _ rfl rfl ..
  | .position Ï†, .outAsymp Ï†' =>
    rw [crPartF_position, anPartF_posAsymp]
    exact normalOrderF_superCommuteF_create_annihilate _ _ rfl rfl ..

lemma normalOrderF_superCommuteF_anPartF_crPartF (Ï† Ï†' : ð“•.FieldOp) (a b : FieldOpFreeAlgebra ð“•) :
    ð“á¶ (a * superCommuteF
    (anPartF Ï†) (crPartF Ï†') * b) = 0 := by
  match Ï†, Ï†' with
  | .inAsymp Ï†', _ => simp
  | _, .outAsymp Ï†' => simp
  | .position Ï†, .position Ï†' =>
    rw [anPartF_position, crPartF_position]
    exact normalOrderF_superCommuteF_annihilate_create _ _ rfl rfl ..
  | .outAsymp Ï†', .inAsymp Ï† =>
    simp only [anPartF_posAsymp, crPartF_negAsymp]
    exact normalOrderF_superCommuteF_annihilate_create _ _ rfl rfl ..
  | .position Ï†', .inAsymp Ï† =>
    simp only [anPartF_position, crPartF_negAsymp]
    exact normalOrderF_superCommuteF_annihilate_create _ _ rfl rfl ..
  | .outAsymp Ï†, .position Ï†' =>
    simp only [anPartF_posAsymp, crPartF_position]
    exact normalOrderF_superCommuteF_annihilate_create _ _ rfl rfl ..

/-!

## The normal ordering of a product of two states

-/

@[simp]
lemma normalOrderF_crPartF_mul_crPartF (Ï† Ï†' : ð“•.FieldOp) :
    ð“á¶ (crPartF Ï† * crPartF Ï†') =
    crPartF Ï† * crPartF Ï†' := by
  rw [normalOrderF_crPartF_mul]
  conv_lhs => rw [â† mul_one (crPartF Ï†')]
  rw [normalOrderF_crPartF_mul, normalOrderF_one]
  simp

@[simp]
lemma normalOrderF_anPartF_mul_anPartF (Ï† Ï†' : ð“•.FieldOp) :
    ð“á¶ (anPartF Ï† * anPartF Ï†') =
    anPartF Ï† * anPartF Ï†' := by
  rw [normalOrderF_mul_anPartF]
  conv_lhs => rw [â† one_mul (anPartF Ï†)]
  rw [normalOrderF_mul_anPartF, normalOrderF_one]
  simp

@[simp]
lemma normalOrderF_crPartF_mul_anPartF (Ï† Ï†' : ð“•.FieldOp) :
    ð“á¶ (crPartF Ï† * anPartF Ï†') =
    crPartF Ï† * anPartF Ï†' := by
  rw [normalOrderF_crPartF_mul]
  conv_lhs => rw [â† one_mul (anPartF Ï†')]
  rw [normalOrderF_mul_anPartF, normalOrderF_one]
  simp

@[simp]
lemma normalOrderF_anPartF_mul_crPartF (Ï† Ï†' : ð“•.FieldOp) :
    ð“á¶ (anPartF Ï† * crPartF Ï†') =
    ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ï†') â€¢
    (crPartF Ï†' * anPartF Ï†) := by
  conv_lhs => rw [â† one_mul (anPartF Ï† * crPartF Ï†')]
  conv_lhs => rw [â† mul_one (1 * (anPartF Ï† *
    crPartF Ï†'))]
  rw [â† mul_assoc, normalOrderF_swap_anPartF_crPartF]
  simp

lemma normalOrderF_ofFieldOpF_mul_ofFieldOpF (Ï† Ï†' : ð“•.FieldOp) :
    ð“á¶ (ofFieldOpF Ï† * ofFieldOpF Ï†') =
    crPartF Ï† * crPartF Ï†' +
    ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ï†') â€¢
    (crPartF Ï†' * anPartF Ï†) +
    crPartF Ï† * anPartF Ï†' +
    anPartF Ï† * anPartF Ï†' := by
  rw [ofFieldOpF_eq_crPartF_add_anPartF, ofFieldOpF_eq_crPartF_add_anPartF, mul_add, add_mul, add_mul]
  simp only [map_add, normalOrderF_crPartF_mul_crPartF, normalOrderF_anPartF_mul_crPartF,
    instCommGroup.eq_1, normalOrderF_crPartF_mul_anPartF, normalOrderF_anPartF_mul_anPartF]
  abel

/-!

## Normal order with super commutors

-/

TODO "Split the following two lemmas up into smaller parts."

lemma normalOrderF_superCommuteF_ofCrAnListF_create_create_ofCrAnListF
    (Ï†c Ï†c' : ð“•.CrAnFieldOp) (hÏ†c : ð“• |>á¶œ Ï†c = CreateAnnihilate.create)
    (hÏ†c' : ð“• |>á¶œ Ï†c' = CreateAnnihilate.create) (Ï†s Ï†s' : List ð“•.CrAnFieldOp) :
    (ð“á¶ (ofCrAnListF Ï†s * [ofCrAnOpF Ï†c, ofCrAnOpF Ï†c']â‚›ca * ofCrAnListF Ï†s')) =
      normalOrderSign (Ï†s ++ Ï†c' :: Ï†c :: Ï†s') â€¢
    (ofCrAnListF (createFilter Ï†s) * [ofCrAnOpF Ï†c, ofCrAnOpF Ï†c']â‚›ca *
      ofCrAnListF (createFilter Ï†s') * ofCrAnListF (annihilateFilter (Ï†s ++ Ï†s'))) := by
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF, mul_sub, sub_mul, map_sub]
  conv_lhs =>
    lhs; rhs
    rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton, â† ofCrAnListF_append, â† ofCrAnListF_append,
      â† ofCrAnListF_append]
  conv_lhs =>
    lhs
    rw [normalOrderF_ofCrAnListF, normalOrderList_eq_createFilter_append_annihilateFilter]
    rw [createFilter_append, createFilter_append, createFilter_append,
      createFilter_singleton_create _ hÏ†c, createFilter_singleton_create _ hÏ†c']
    rw [annihilateFilter_append, annihilateFilter_append, annihilateFilter_append,
      annihilateFilter_singleton_create _ hÏ†c, annihilateFilter_singleton_create _ hÏ†c']
    enter [2, 1, 2]
    simp only [List.singleton_append, List.append_assoc, List.cons_append, List.append_nil,
      instCommGroup.eq_1, Algebra.smul_mul_assoc, Algebra.mul_smul_comm, map_smul]
    rw [â† annihilateFilter_append]
  conv_lhs =>
    rhs; rhs
    rw [smul_mul_assoc, Algebra.mul_smul_comm, smul_mul_assoc]
    rhs
    rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton, â† ofCrAnListF_append, â† ofCrAnListF_append,
      â† ofCrAnListF_append]
  conv_lhs =>
    rhs
    rw [map_smul]
    rhs
    rw [normalOrderF_ofCrAnListF, normalOrderList_eq_createFilter_append_annihilateFilter]
    rw [createFilter_append, createFilter_append, createFilter_append,
      createFilter_singleton_create _ hÏ†c, createFilter_singleton_create _ hÏ†c']
    rw [annihilateFilter_append, annihilateFilter_append, annihilateFilter_append,
      annihilateFilter_singleton_create _ hÏ†c, annihilateFilter_singleton_create _ hÏ†c']
    enter [2, 1, 2]
    simp only [List.singleton_append, List.append_assoc, List.cons_append, instCommGroup.eq_1,
      List.append_nil, Algebra.smul_mul_assoc]
    rw [â† annihilateFilter_append]
  conv_lhs =>
    lhs; lhs
    simp
  conv_lhs =>
    rhs; rhs; lhs
    simp
  rw [normalOrderSign_swap_create_create Ï†c Ï†c' hÏ†c hÏ†c']
  rw [smul_smul, mul_comm, â† smul_smul]
  rw [â† smul_sub, ofCrAnListF_append, ofCrAnListF_append, ofCrAnListF_append]
  conv_lhs =>
    rhs; rhs
    rw [ofCrAnListF_append, ofCrAnListF_append, ofCrAnListF_append]
    rw [â† smul_mul_assoc, â† smul_mul_assoc, â† Algebra.mul_smul_comm]
  rw [â† sub_mul, â† sub_mul, â† mul_sub, ofCrAnListF_append, ofCrAnListF_singleton,
    ofCrAnListF_singleton]
  rw [ofCrAnListF_append, ofCrAnListF_singleton, ofCrAnListF_singleton, smul_mul_assoc]

lemma normalOrderF_superCommuteF_ofCrAnListF_annihilate_annihilate_ofCrAnListF
    (Ï†a Ï†a' : ð“•.CrAnFieldOp)
    (hÏ†a : ð“• |>á¶œ Ï†a = CreateAnnihilate.annihilate)
    (hÏ†a' : ð“• |>á¶œ Ï†a' = CreateAnnihilate.annihilate)
    (Ï†s Ï†s' : List ð“•.CrAnFieldOp) :
    ð“á¶ (ofCrAnListF Ï†s * [ofCrAnOpF Ï†a, ofCrAnOpF Ï†a']â‚›ca * ofCrAnListF Ï†s') =
      normalOrderSign (Ï†s ++ Ï†a' :: Ï†a :: Ï†s') â€¢
    (ofCrAnListF (createFilter (Ï†s ++ Ï†s'))
      * ofCrAnListF (annihilateFilter Ï†s) * [ofCrAnOpF Ï†a, ofCrAnOpF Ï†a']â‚›ca
      * ofCrAnListF (annihilateFilter Ï†s')) := by
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF, mul_sub, sub_mul, map_sub]
  conv_lhs =>
    lhs; rhs
    rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton, â† ofCrAnListF_append, â† ofCrAnListF_append,
      â† ofCrAnListF_append]
  conv_lhs =>
    lhs
    rw [normalOrderF_ofCrAnListF, normalOrderList_eq_createFilter_append_annihilateFilter]
    rw [createFilter_append, createFilter_append, createFilter_append,
      createFilter_singleton_annihilate _ hÏ†a, createFilter_singleton_annihilate _ hÏ†a']
    rw [annihilateFilter_append, annihilateFilter_append, annihilateFilter_append,
      annihilateFilter_singleton_annihilate _ hÏ†a, annihilateFilter_singleton_annihilate _ hÏ†a']
    enter [2, 1, 1]
    simp only [List.singleton_append, List.append_assoc, List.cons_append, List.append_nil,
      instCommGroup.eq_1, Algebra.smul_mul_assoc, Algebra.mul_smul_comm, map_smul]
    rw [â† createFilter_append]
  conv_lhs =>
    rhs; rhs
    rw [smul_mul_assoc]
    rw [Algebra.mul_smul_comm, smul_mul_assoc]
    rhs
    rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton, â† ofCrAnListF_append, â† ofCrAnListF_append,
      â† ofCrAnListF_append]
  conv_lhs =>
    rhs
    rw [map_smul]
    rhs
    rw [normalOrderF_ofCrAnListF, normalOrderList_eq_createFilter_append_annihilateFilter]
    rw [createFilter_append, createFilter_append, createFilter_append,
      createFilter_singleton_annihilate _ hÏ†a, createFilter_singleton_annihilate _ hÏ†a']
    rw [annihilateFilter_append, annihilateFilter_append, annihilateFilter_append,
      annihilateFilter_singleton_annihilate _ hÏ†a, annihilateFilter_singleton_annihilate _ hÏ†a']
    enter [2, 1, 1]
    simp only [List.singleton_append, List.append_assoc, List.cons_append, instCommGroup.eq_1,
      List.append_nil, Algebra.smul_mul_assoc]
    rw [â† createFilter_append]
  conv_lhs =>
    lhs; lhs
    simp
  conv_lhs =>
    rhs; rhs; lhs
    simp
  rw [normalOrderSign_swap_annihilate_annihilate Ï†a Ï†a' hÏ†a hÏ†a']
  rw [smul_smul, mul_comm, â† smul_smul]
  rw [â† smul_sub, ofCrAnListF_append, ofCrAnListF_append, ofCrAnListF_append]
  conv_lhs =>
    rhs; rhs
    rw [ofCrAnListF_append, ofCrAnListF_append, ofCrAnListF_append]
    rw [â† Algebra.mul_smul_comm, â† smul_mul_assoc, â† Algebra.mul_smul_comm]
  rw [â† mul_sub, â† sub_mul, â† mul_sub]
  apply congrArg
  conv_rhs => rw [mul_assoc, mul_assoc]
  apply congrArg
  rw [mul_assoc]
  apply congrArg
  rw [ofCrAnListF_append, ofCrAnListF_singleton, ofCrAnListF_singleton]
  rw [ofCrAnListF_append, ofCrAnListF_singleton, ofCrAnListF_singleton, smul_mul_assoc]

/-!

## Super commututators involving a normal order.

-/

lemma ofCrAnListF_superCommuteF_normalOrderF_ofCrAnListF (Ï†s Ï†s' : List ð“•.CrAnFieldOp) :
    [ofCrAnListF Ï†s, ð“á¶ (ofCrAnListF Ï†s')]â‚›ca =
    ofCrAnListF Ï†s * ð“á¶ (ofCrAnListF Ï†s') -
    ð“¢(ð“• |>â‚› Ï†s, ð“• |>â‚› Ï†s') â€¢ ð“á¶ (ofCrAnListF Ï†s') * ofCrAnListF Ï†s := by
  simp [normalOrderF_ofCrAnListF, map_smul, superCommuteF_ofCrAnListF_ofCrAnListF, ofCrAnListF_append,
    smul_sub, smul_smul, mul_comm]

lemma ofCrAnListF_superCommuteF_normalOrderF_ofFieldOpListF (Ï†s : List ð“•.CrAnFieldOp)
    (Ï†s' : List ð“•.FieldOp) : [ofCrAnListF Ï†s, ð“á¶ (ofFieldOpListF Ï†s')]â‚›ca =
    ofCrAnListF Ï†s * ð“á¶ (ofFieldOpListF Ï†s') -
    ð“¢(ð“• |>â‚› Ï†s, ð“• |>â‚› Ï†s') â€¢ ð“á¶ (ofFieldOpListF Ï†s') * ofCrAnListF Ï†s := by
  rw [ofFieldOpListF_sum, map_sum, Finset.mul_sum, Finset.smul_sum, Finset.sum_mul,
    â† Finset.sum_sub_distrib, map_sum]
  congr
  funext n
  rw [ofCrAnListF_superCommuteF_normalOrderF_ofCrAnListF,
    CrAnSection.statistics_eq_state_statistics]

/-!

## Multiplications with normal order written in terms of super commute.

-/

lemma ofCrAnListF_mul_normalOrderF_ofFieldOpListF_eq_superCommuteF (Ï†s : List ð“•.CrAnFieldOp)
    (Ï†s' : List ð“•.FieldOp) :
    ofCrAnListF Ï†s * ð“á¶ (ofFieldOpListF Ï†s') =
    ð“¢(ð“• |>â‚› Ï†s, ð“• |>â‚› Ï†s') â€¢ ð“á¶ (ofFieldOpListF Ï†s') * ofCrAnListF Ï†s
    + [ofCrAnListF Ï†s, ð“á¶ (ofFieldOpListF Ï†s')]â‚›ca := by
  simp [ofCrAnListF_superCommuteF_normalOrderF_ofFieldOpListF]

lemma ofCrAnOpF_mul_normalOrderF_ofFieldOpListF_eq_superCommuteF (Ï† : ð“•.CrAnFieldOp)
    (Ï†s' : List ð“•.FieldOp) : ofCrAnOpF Ï† * ð“á¶ (ofFieldOpListF Ï†s') =
    ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ï†s') â€¢ ð“á¶ (ofFieldOpListF Ï†s') * ofCrAnOpF Ï†
    + [ofCrAnOpF Ï†, ð“á¶ (ofFieldOpListF Ï†s')]â‚›ca := by
  simp [â† ofCrAnListF_singleton, ofCrAnListF_mul_normalOrderF_ofFieldOpListF_eq_superCommuteF]

lemma anPartF_mul_normalOrderF_ofFieldOpListF_eq_superCommuteF (Ï† : ð“•.FieldOp)
    (Ï†s' : List ð“•.FieldOp) :
    anPartF Ï† * ð“á¶ (ofFieldOpListF Ï†s') =
    ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ï†s') â€¢ ð“á¶ (ofFieldOpListF Ï†s' * anPartF Ï†)
    + [anPartF Ï†, ð“á¶ (ofFieldOpListF Ï†s')]â‚›ca := by
  rw [normalOrderF_mul_anPartF]
  match Ï† with
  | .inAsymp Ï† => simp
  | .position Ï† => simp [ofCrAnOpF_mul_normalOrderF_ofFieldOpListF_eq_superCommuteF, crAnStatistics]
  | .outAsymp Ï† => simp [ofCrAnOpF_mul_normalOrderF_ofFieldOpListF_eq_superCommuteF, crAnStatistics]

end

end FieldOpFreeAlgebra

end FieldSpecification
