/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldOpAlgebra.NormalOrder.WickContractions
import HepLean.PerturbationTheory.WickContraction.Sign.InsertNone
import HepLean.PerturbationTheory.WickContraction.Sign.InsertSome
import HepLean.PerturbationTheory.WickContraction.StaticContract
/-!

# Static Wick's terms

-/
open FieldSpecification
variable {ð“• : FieldSpecification}

namespace WickContraction
variable {n : â„•} (c : WickContraction n)
open HepLean.List
open FieldOpAlgebra
open FieldStatistic
noncomputable section

/-- For a list `Ï†s` of `ð“•.FieldOp`, and a Wick contraction `Ï†sÎ›` of `Ï†s`, the element
  of `ð“•.FieldOpAlgebra`, `Ï†sÎ›.staticWickTerm` is defined as

  `Ï†sÎ›.sign â€¢ Ï†sÎ›.staticContract * ð“([Ï†sÎ›]áµ˜á¶œ)`.

  This is term which appears in the static version Wick's theorem. -/
def staticWickTerm {Ï†s : List ð“•.FieldOp} (Ï†sÎ› : WickContraction Ï†s.length) : ð“•.FieldOpAlgebra :=
  Ï†sÎ›.sign â€¢ Ï†sÎ›.staticContract * ð“(ofFieldOpList [Ï†sÎ›]áµ˜á¶œ)

/-- For the empty list `[]` of `ð“•.FieldOp`, the `staticWickTerm` of the empty Wick contraction
  `empty` of `[]` (its only Wick contraction) is `1`. -/
@[simp]
lemma staticWickTerm_empty_nil :
    staticWickTerm (empty (n := ([] : List ð“•.FieldOp).length)) = 1 := by
  rw [staticWickTerm, uncontractedListGet, nil_zero_uncontractedList]
  simp [sign, empty, staticContract]

/--
For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ð“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, and an element `Ï†` of
  `ð“•.FieldOp`, the following relation holds

`(Ï†sÎ› â†©Î› Ï† 0 none).staticWickTerm = Ï†sÎ›.sign â€¢ Ï†sÎ›.staticWickTerm * ð“(Ï† :: [Ï†sÎ›]áµ˜á¶œ)`

The proof of this result relies on
- `staticContract_insert_none` to rewrite the static contract.
- `sign_insert_none_zero` to rewrite the sign.
-/
lemma staticWickTerm_insert_zero_none (Ï† : ð“•.FieldOp) (Ï†s : List ð“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) :
    (Ï†sÎ› â†©Î› Ï† 0 none).staticWickTerm =
    Ï†sÎ›.sign â€¢ Ï†sÎ›.staticContract * ð“(ofFieldOpList (Ï† :: [Ï†sÎ›]áµ˜á¶œ)) := by
  symm
  erw [staticWickTerm, sign_insert_none_zero]
  simp only [staticContract_insert_none, insertAndContract_uncontractedList_none_zero,
    Algebra.smul_mul_assoc]

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ð“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ð“•.FieldOp`, and a `k` in `Ï†sÎ›.uncontracted`, `(Ï†sÎ› â†©Î› Ï† 0 (some k)).wickTerm` is equal
  to the product of
- the sign `ð“¢(Ï†, Ï†â‚€â€¦Ï†áµ¢â‚‹â‚) `
- the sign `Ï†sÎ›.sign`
- `Ï†sÎ›.staticContract`
- `s â€¢ [anPart Ï†, ofFieldOp Ï†s[k]]â‚›` where `s` is the sign associated with moving `Ï†` through
  uncontracted fields in `Ï†â‚€â€¦Ï†â‚–â‚‹â‚`
- the normal ordering `ð“([Ï†sÎ›]áµ˜á¶œ.erase (uncontractedFieldOpEquiv Ï†s Ï†sÎ› k))`.

The proof of this result ultimitley relies on
- `staticContract_insert_some` to rewrite static contractions.
- `normalOrder_uncontracted_some` to rewrite normal orderings.
- `sign_insert_some_zero` to rewrite signs.
-/
lemma staticWickTerm_insert_zero_some (Ï† : ð“•.FieldOp) (Ï†s : List ð“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (k : { x // x âˆˆ Ï†sÎ›.uncontracted }) :
    (Ï†sÎ› â†©Î› Ï† 0 k).staticWickTerm =
    sign Ï†s Ï†sÎ› â€¢ (â†‘Ï†sÎ›.staticContract *
    (contractStateAtIndex Ï† [Ï†sÎ›]áµ˜á¶œ ((uncontractedFieldOpEquiv Ï†s Ï†sÎ›) (some k)) *
    ð“(ofFieldOpList (optionEraseZ [Ï†sÎ›]áµ˜á¶œ Ï† (uncontractedFieldOpEquiv Ï†s Ï†sÎ› k))))) := by
  symm
  rw [staticWickTerm, normalOrder_uncontracted_some]
  simp only [â† mul_assoc]
  rw [â† smul_mul_assoc]
  congr 1
  rw [staticContract_insert_some_of_lt]
  swap
  Â· simp
  rw [smul_smul]
  by_cases hn : GradingCompliant Ï†s Ï†sÎ› âˆ§ (ð“•|>â‚›Ï†) = (ð“•|>â‚› Ï†s[k.1])
  Â· congr 1
    swap
    Â· rw [Subalgebra.mem_center_iff.mp Ï†sÎ›.staticContract.2]
    Â· rw [sign_insert_some_zero _ _ _ _ hn, mul_comm, â† mul_assoc]
      simp
  Â· simp only [Fin.getElem_fin, not_and] at hn
    by_cases h0 : Â¬ GradingCompliant Ï†s Ï†sÎ›
    Â· rw [staticContract_of_not_gradingCompliant]
      simp only [ZeroMemClass.coe_zero, zero_mul, smul_zero, instCommGroup.eq_1, mul_zero]
      exact h0
    Â· simp_all only [Finset.mem_univ, not_not, instCommGroup.eq_1, forall_const]
      have h1 : contractStateAtIndex Ï† [Ï†sÎ›]áµ˜á¶œ (uncontractedFieldOpEquiv Ï†s Ï†sÎ› k) = 0 := by
        simp only [contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
          Equiv.coe_trans, Option.map_some', Function.comp_apply, finCongr_apply,
          instCommGroup.eq_1, Fin.coe_cast, Fin.getElem_fin, smul_eq_zero]
        right
        simp only [uncontractedListGet, List.getElem_map,
          uncontractedList_getElem_uncontractedIndexEquiv_symm, List.get_eq_getElem]
        rw [superCommute_anPart_ofFieldOpF_diff_grade_zero]
        exact hn
      rw [h1]
      simp

/--
For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ð“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, the following relation
holds

`Ï† * Ï†sÎ›.staticWickTerm = âˆ‘ k, (Ï†sÎ› â†©Î› Ï† i k).wickTerm`

where the sum is over all `k` in `Option Ï†sÎ›.uncontracted` (so either `none` or `some k`).

The proof of proceeds as follows:
- `ofFieldOp_mul_normalOrder_ofFieldOpList_eq_sum` is used to expand `Ï† ð“([Ï†sÎ›]áµ˜á¶œ)` as
  a sum over `k` in `Option Ï†sÎ›.uncontracted` of terms involving `[anPart Ï†, Ï†s[k]]â‚›`.
- Then `staticWickTerm_insert_zero_none` and `staticWickTerm_insert_zero_some` are
  used to equate terms.
-/
lemma mul_staticWickTerm_eq_sum (Ï† : ð“•.FieldOp) (Ï†s : List ð“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) :
    ofFieldOp Ï† * Ï†sÎ›.staticWickTerm =
    âˆ‘ (k : Option Ï†sÎ›.uncontracted), (Ï†sÎ› â†©Î› Ï† 0 k).staticWickTerm := by
  trans (Ï†sÎ›.sign â€¢ Ï†sÎ›.staticContract * (ofFieldOp Ï† * normalOrder (ofFieldOpList [Ï†sÎ›]áµ˜á¶œ)))
  Â· have ht := Subalgebra.mem_center_iff.mp (Subalgebra.smul_mem (Subalgebra.center â„‚ _)
      (Ï†sÎ›.staticContract).2 Ï†sÎ›.sign)
    conv_rhs => rw [â† mul_assoc, â† ht]
    simp [mul_assoc, staticWickTerm]
  rw [ofFieldOp_mul_normalOrder_ofFieldOpList_eq_sum]
  rw [Finset.mul_sum]
  rw [uncontractedFieldOpEquiv_list_sum]
  refine Finset.sum_congr rfl (fun n _ => ?_)
  match n with
  | none =>
    simp only [contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
      Equiv.coe_trans, Option.map_none', one_mul, Algebra.smul_mul_assoc, Nat.succ_eq_add_one,
      Fin.zero_eta, Fin.val_zero, List.insertIdx_zero, staticContract_insert_none,
      insertAndContract_uncontractedList_none_zero]
    rw [staticWickTerm_insert_zero_none]
    simp only [Algebra.smul_mul_assoc]
    rfl
  | some n =>
    simp only [Algebra.smul_mul_assoc, Nat.succ_eq_add_one, Fin.zero_eta, Fin.val_zero,
      List.insertIdx_zero]
    rw [staticWickTerm_insert_zero_some]

end
end WickContraction
