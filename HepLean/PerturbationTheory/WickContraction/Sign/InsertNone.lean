/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.WickContraction.Sign.Basic

/-!

# Sign on inserting and not contracting

-/

open FieldSpecification
variable {ğ“• : FieldSpecification}

namespace WickContraction
variable {n : â„•} (c : WickContraction n)
open HepLean.List
open FieldStatistic

lemma signFinset_insertAndContract_none (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (i1 i2 : Fin Ï†s.length) :
    (Ï†sÎ› â†©Î› Ï† i none).signFinset (finCongr (insertIdx_length_fin Ï† Ï†s i).symm
    (i.succAbove i1)) (finCongr (insertIdx_length_fin Ï† Ï†s i).symm (i.succAbove i2)) =
    if i.succAbove i1 < i âˆ§ i < i.succAbove i2 then
      Insert.insert (finCongr (insertIdx_length_fin Ï† Ï†s i).symm i)
      (insertAndContractLiftFinset Ï† i (Ï†sÎ›.signFinset i1 i2))
    else
      (insertAndContractLiftFinset Ï† i (Ï†sÎ›.signFinset i1 i2)) := by
  ext k
  rcases insert_fin_eq_self Ï† i k with hk | hk
  Â· subst hk
    conv_lhs => simp only [Nat.succ_eq_add_one, signFinset, finCongr_apply, Finset.mem_filter,
      Finset.mem_univ, insertAndContract_none_getDual?_self, Option.isSome_none, Bool.false_eq_true,
      IsEmpty.forall_iff, or_self, and_true, true_and]
    by_cases h : i.succAbove i1 < i âˆ§ i < i.succAbove i2
    Â· simp [h, Fin.lt_def]
    Â· simp only [Nat.succ_eq_add_one, h, â†“reduceIte, self_not_mem_insertAndContractLiftFinset,
      iff_false]
      rw [Fin.lt_def, Fin.lt_def] at h âŠ¢
      simp_all
  Â· obtain âŸ¨k, hkâŸ© := hk
    subst hk
    have h1 : Fin.cast (insertIdx_length_fin Ï† Ï†s i).symm (i.succAbove k) âˆˆ
      (if i.succAbove i1 < i âˆ§ i < i.succAbove i2 then
        Insert.insert ((finCongr (insertIdx_length_fin Ï† Ï†s i).symm) i)
        (insertAndContractLiftFinset Ï† i (Ï†sÎ›.signFinset i1 i2))
      else insertAndContractLiftFinset Ï† i (Ï†sÎ›.signFinset i1 i2)) â†”
      Fin.cast (insertIdx_length_fin Ï† Ï†s i).symm (i.succAbove k) âˆˆ
        insertAndContractLiftFinset Ï† i (Ï†sÎ›.signFinset i1 i2) := by
      split
      Â· simp only [Nat.succ_eq_add_one, finCongr_apply, Finset.mem_insert, Fin.ext_iff,
        Fin.coe_cast, or_iff_right_iff_imp]
        intro h
        have h1 : i.succAbove k â‰  i := by
          exact Fin.succAbove_ne i k
        omega
      Â· simp
    rw [h1]
    rw [succAbove_mem_insertAndContractLiftFinset]
    simp only [Nat.succ_eq_add_one, signFinset, finCongr_apply, Finset.mem_filter, Finset.mem_univ,
      insertAndContract_none_succAbove_getDual?_eq_none_iff, true_and,
      insertAndContract_none_succAbove_getDual?_isSome_iff, insertAndContract_none_getDual?_get_eq]
    rw [Fin.lt_def, Fin.lt_def, Fin.lt_def, Fin.lt_def]
    simp only [Fin.coe_cast, Fin.val_fin_lt]
    rw [Fin.succAbove_lt_succAbove_iff, Fin.succAbove_lt_succAbove_iff]
    simp only [and_congr_right_iff]
    intro h1 h2
    conv_lhs =>
      rhs
      enter [h]
      rw [Fin.lt_def]
      simp only [Fin.coe_cast, Fin.val_fin_lt]
      rw [Fin.succAbove_lt_succAbove_iff]

/-- Given a Wick contraction `Ï†sÎ›` associated with a list of states `Ï†s`
  and an `i : Fin Ï†s.length.succ`, the change in sign of the contraction associated with
  inserting `Ï†` into `Ï†s` at position `i` without contracting it.

  For each contracted pair `{a1, a2}` in `Ï†sÎ›` if `a1 < a2` such that `i` is within the range
  `a1 < i < a2` we pick up a sign equal to `ğ“¢(Ï†, Ï†s[a2])`. -/
def signInsertNone (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) (Ï†sÎ› : WickContraction Ï†s.length)
    (i : Fin Ï†s.length.succ) : â„‚ :=
  âˆ (a : Ï†sÎ›.1),
    if i.succAbove (Ï†sÎ›.fstFieldOfContract a) < i âˆ§ i < i.succAbove (Ï†sÎ›.sndFieldOfContract a) then
      ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ï†s[Ï†sÎ›.sndFieldOfContract a])
    else 1

lemma sign_insert_none_eq_signInsertNone_mul_sign (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) :
    (Ï†sÎ› â†©Î› Ï† i none).sign = (Ï†sÎ›.signInsertNone Ï† Ï†s i) * Ï†sÎ›.sign := by
  rw [sign]
  rw [signInsertNone, sign, â† Finset.prod_mul_distrib]
  rw [insertAndContract_none_prod_contractions]
  congr
  funext a
  simp only [instCommGroup, Nat.succ_eq_add_one, insertAndContract_sndFieldOfContract,
    finCongr_apply, Fin.getElem_fin, Fin.coe_cast, insertIdx_getElem_fin,
    insertAndContract_fstFieldOfContract, ite_mul, one_mul]
  erw [signFinset_insertAndContract_none]
  split
  Â· rw [ofFinset_insert]
    simp only [instCommGroup, Nat.succ_eq_add_one, finCongr_apply, Fin.getElem_fin, Fin.coe_cast,
      List.getElem_insertIdx_self, map_mul]
    rw [stat_ofFinset_of_insertAndContractLiftFinset]
    simp only [exchangeSign_symm, instCommGroup.eq_1]
    simp
  Â· rw [stat_ofFinset_of_insertAndContractLiftFinset]

lemma signInsertNone_eq_mul_fst_snd (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) :
  Ï†sÎ›.signInsertNone Ï† Ï†s i = âˆ (a : Ï†sÎ›.1),
    (if i.succAbove (Ï†sÎ›.fstFieldOfContract a) < i then
      ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ï†s[Ï†sÎ›.sndFieldOfContract a])
    else 1) *
    (if i.succAbove (Ï†sÎ›.sndFieldOfContract a) < i then
      ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ï†s[Ï†sÎ›.sndFieldOfContract a])
    else 1) := by
  rw [signInsertNone]
  congr
  funext a
  split
  Â· rename_i h
    simp only [instCommGroup.eq_1, Fin.getElem_fin, h.1, â†“reduceIte, mul_ite, exchangeSign_mul_self,
      mul_one]
    rw [if_neg]
    omega
  Â· rename_i h
    simp only [Nat.succ_eq_add_one, not_and, not_lt] at h
    split <;> rename_i h1
    Â· simp_all only [forall_const, instCommGroup.eq_1, Fin.getElem_fin, mul_ite,
      exchangeSign_mul_self, mul_one]
      rw [if_pos]
      have h1 :i.succAbove (Ï†sÎ›.sndFieldOfContract a) â‰  i :=
        Fin.succAbove_ne i (Ï†sÎ›.sndFieldOfContract a)
      omega
    Â· simp only [not_lt] at h1
      rw [if_neg]
      simp only [mul_one]
      have hn := fstFieldOfContract_lt_sndFieldOfContract Ï†sÎ› a
      have hx := (Fin.succAbove_lt_succAbove_iff (p := i)).mpr hn
      omega

lemma signInsertNone_eq_prod_prod (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (hG : GradingCompliant Ï†s Ï†sÎ›) :
    Ï†sÎ›.signInsertNone Ï† Ï†s i = âˆ (a : Ï†sÎ›.1), âˆ (x : a),
      (if i.succAbove x < i then ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ï†s[x.1]) else 1) := by
  rw [signInsertNone_eq_mul_fst_snd]
  congr
  funext a
  rw [prod_finset_eq_mul_fst_snd]
  congr 1
  congr 1
  congr 1
  simp only [Fin.getElem_fin]
  erw [hG a]
  rfl

lemma signInsertNone_eq_prod_getDual?_Some (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (hG : GradingCompliant Ï†s Ï†sÎ›) :
    Ï†sÎ›.signInsertNone Ï† Ï†s i = âˆ (x : Fin Ï†s.length),
      if (Ï†sÎ›.getDual? x).isSome then
        (if i.succAbove x < i then ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ï†s[x.1]) else 1)
      else 1 := by
  rw [signInsertNone_eq_prod_prod]
  trans âˆ (x : (a : Ï†sÎ›.1) Ã— a), (if i.succAbove x.2 < i then ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ï†s[x.2.1]) else 1)
  Â· rw [Finset.prod_sigma']
    rfl
  rw [â† Ï†sÎ›.sigmaContractedEquiv.symm.prod_comp]
  let e2 : Fin Ï†s.length â‰ƒ {x // (Ï†sÎ›.getDual? x).isSome} âŠ• {x // Â¬ (Ï†sÎ›.getDual? x).isSome} := by
    exact (Equiv.sumCompl fun a => (Ï†sÎ›.getDual? a).isSome = true).symm
  rw [â† e2.symm.prod_comp]
  simp only [instCommGroup.eq_1, Fin.getElem_fin, Fintype.prod_sum_type]
  conv_rhs =>
    rhs
    enter [2, a]
    rw [if_neg (by simpa [e2] using a.2)]
  conv_rhs =>
    lhs
    enter [2, a]
    rw [if_pos (by simpa [e2] using a.2)]
  simp only [Equiv.symm_symm, Equiv.sumCompl_apply_inl, Finset.prod_const_one, mul_one, e2]
  rfl
  exact hG

lemma signInsertNone_eq_filter_map (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (hG : GradingCompliant Ï†s Ï†sÎ›) :
    Ï†sÎ›.signInsertNone Ï† Ï†s i =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› ((List.filter (fun x => (Ï†sÎ›.getDual? x).isSome âˆ§ i.succAbove x < i)
    (List.finRange Ï†s.length)).map Ï†s.get)) := by
  rw [signInsertNone_eq_prod_getDual?_Some]
  rw [FieldStatistic.ofList_map_eq_finset_prod]
  rw [map_prod]
  congr
  funext a
  simp only [instCommGroup.eq_1, Bool.decide_and, Bool.decide_eq_true, List.mem_filter,
    List.mem_finRange, Bool.and_eq_true, decide_eq_true_eq, true_and, Fin.getElem_fin]
  split
  Â· rename_i h
    simp only [h, true_and]
    split
    Â· rfl
    Â· simp only [map_one]
  Â· rename_i h
    simp [h]
  Â· refine List.Nodup.filter _ ?_
    exact List.nodup_finRange Ï†s.length
  Â· exact hG

/-- The following signs for a grading compliant Wick contraction are equal:
- The sign `Ï†sÎ›.signInsertNone Ï† Ï†s i` which is given by the following: For each
  contracted pair `{a1, a2}` in `Ï†sÎ›` if `a1 < a2`
  such that `i` is within the range `a1 < i < a2` we pick up a sign equal to `ğ“¢(Ï†, Ï†s[a2])`.
- The sign got by moving `Ï†` through `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚` and only picking up a sign when `Ï†áµ¢` has a dual.
These are equal since: Both ignore uncontracted fields, and for a contracted pair `{a1, a2}`
with `a1 < a2`
- if `i < a1 < a2` then we don't pick up a sign from either `Ï†â‚â‚` or `Ï†â‚â‚‚`.
- if `a1 < i < a2` then we pick up a sign from `Ï†â‚â‚` cases which is equal to `ğ“¢(Ï†, Ï†s[a2])`
(since `Ï†sÎ›` is grading compliant).
- if `a1 < a2 < i` then we pick up a sign from both `Ï†â‚â‚` and `Ï†â‚â‚‚` which cancel each other out.
-/
lemma signInsertNone_eq_filterset (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (hG : GradingCompliant Ï†s Ï†sÎ›) :
    Ï†sÎ›.signInsertNone Ï† Ï†s i = ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, Finset.univ.filter
    (fun x => (Ï†sÎ›.getDual? x).isSome âˆ§ i.succAbove x < i)âŸ©) := by
  rw [ofFinset_eq_prod, signInsertNone_eq_prod_getDual?_Some, map_prod]
  congr
  funext a
  simp only [instCommGroup.eq_1, Finset.mem_filter, Finset.mem_univ, true_and, Fin.getElem_fin]
  split
  Â· rename_i h
    simp only [h, true_and]
    split
    Â· rfl
    Â· simp only [map_one]
  Â· rename_i h
    simp [h]
  Â· exact hG

/-- For `Ï†sÎ›` a grading compliant Wick contraction, and `i : Fin Ï†s.length.succ` we have
  `(Ï†sÎ› â†©Î› Ï† i none).sign = s * Ï†sÎ›.sign`
  where `s` is the sign got by moving `Ï†` through the elements of `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚` which
  are contracted. -/
lemma sign_insert_none (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (hG : GradingCompliant Ï†s Ï†sÎ›) :
    (Ï†sÎ› â†©Î› Ï† i none).sign = ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, Finset.univ.filter
    (fun x => (Ï†sÎ›.getDual? x).isSome âˆ§ i.succAbove x < i)âŸ©) * Ï†sÎ›.sign := by
  rw [sign_insert_none_eq_signInsertNone_mul_sign]
  rw [signInsertNone_eq_filterset]
  exact hG

lemma sign_insert_none_zero (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) : (Ï†sÎ› â†©Î› Ï† 0 none).sign = Ï†sÎ›.sign := by
  rw [sign_insert_none_eq_signInsertNone_mul_sign]
  simp [signInsertNone]

end WickContraction
