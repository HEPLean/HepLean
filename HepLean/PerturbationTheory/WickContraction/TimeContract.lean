/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.WickContraction.Sign.Basic
import HepLean.PerturbationTheory.FieldOpAlgebra.TimeContraction
/-!

# Time contractions

-/

open FieldSpecification
variable {ğ“• : FieldSpecification}

namespace WickContraction
variable {n : â„•} (c : WickContraction n)
open HepLean.List
open FieldOpAlgebra

/-- For a list `Ï†s` of `ğ“•.FieldOp` and a Wick contraction `Ï†sÎ›` the
  element of the center of `ğ“•.FieldOpAlgebra`, `Ï†sÎ›.timeContract` is defined as the product
  of `timeContract Ï†s[j] Ï†s[k]` over contracted pairs `{j, k}` (both indices of `Ï†s`) in `Ï†sÎ›`
  with `j < k`. -/
noncomputable def timeContract {Ï†s : List ğ“•.FieldOp}
    (Ï†sÎ› : WickContraction Ï†s.length) :
    Subalgebra.center â„‚ ğ“•.FieldOpAlgebra :=
  âˆ (a : Ï†sÎ›.1), âŸ¨FieldOpAlgebra.timeContract
    (Ï†s.get (Ï†sÎ›.fstFieldOfContract a)) (Ï†s.get (Ï†sÎ›.sndFieldOfContract a)),
    timeContract_mem_center _ _âŸ©

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, and a `i â‰¤ Ï†s.length` the following relation holds

  `(Ï†sÎ› â†©Î› Ï† i none).timeContract = Ï†sÎ›.timeContract`

  The prove of this result ultimately a consequence of definitions. -/
@[simp]
lemma timeContract_insert_none (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) :
    (Ï†sÎ› â†©Î› Ï† i none).timeContract = Ï†sÎ›.timeContract := by
  rw [timeContract, insertAndContract_none_prod_contractions]
  congr
  ext a
  simp

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, a `i â‰¤ Ï†s.length` and a `k` in `Ï†sÎ›.uncontracted`, then
  `(Ï†sÎ› â†©Î› Ï† i (some k)).timeContract` is equal to the product of
  - `timeContract Ï† Ï†s[k]` if `i â‰¤ k` or `timeContract Ï†s[k] Ï†` if `k < i`
  - `Ï†sÎ›.timeContract`.

  The proof of this result ultimately a consequence of definitions. -/
lemma timeContract_insertAndContract_some
    (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (j : Ï†sÎ›.uncontracted) :
    (Ï†sÎ› â†©Î› Ï† i (some j)).timeContract =
    (if i < i.succAbove j then
      âŸ¨FieldOpAlgebra.timeContract Ï† Ï†s[j.1], timeContract_mem_center _ _âŸ©
    else âŸ¨FieldOpAlgebra.timeContract Ï†s[j.1] Ï†, timeContract_mem_center _ _âŸ©) *
    Ï†sÎ›.timeContract := by
  rw [timeContract, insertAndContract_some_prod_contractions]
  congr 1
  Â· simp only [Nat.succ_eq_add_one, insertAndContract_fstFieldOfContract_some_incl, finCongr_apply,
    List.get_eq_getElem, insertAndContract_sndFieldOfContract_some_incl, Fin.getElem_fin]
    split
    Â· simp
    Â· simp
  Â· congr
    ext a
    simp

@[simp]
lemma timeContract_empty (Ï†s : List ğ“•.FieldOp) :
    (@empty Ï†s.length).timeContract = 1 := by
  rw [timeContract, empty]
  simp

open FieldStatistic

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, a `i â‰¤ Ï†s.length` and a `k` in `Ï†sÎ›.uncontracted` such that `i â‰¤ k`, with the
  condition that `Ï†` has greater or equal time to `Ï†s[k]`, then
  `(Ï†sÎ› â†©Î› Ï† i (some k)).timeContract` is equal to the product of
  - `[anPart Ï†, Ï†s[k]]â‚›`
  - `Ï†sÎ›.timeContract`
  - two copies of the exchange sign of `Ï†` with the uncontracted fields in `Ï†â‚€â€¦Ï†â‚–â‚‹â‚`.
    These two exchange signs cancle each other out but are included for convenience.

  The proof of this result ultimately a consequence of definitions and
  `timeContract_of_timeOrderRel`. -/
lemma timeContract_insert_some_of_lt
    (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (k : Ï†sÎ›.uncontracted)
    (ht : ğ“•.timeOrderRel Ï† Ï†s[k.1]) (hik : i < i.succAbove k) :
    (Ï†sÎ› â†©Î› Ï† i (some k)).timeContract =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, (Ï†sÎ›.uncontracted.filter (fun x => x < k))âŸ©)
    â€¢ (contractStateAtIndex Ï† [Ï†sÎ›]áµ˜á¶œ ((uncontractedFieldOpEquiv Ï†s Ï†sÎ›) (some k)) *
      Ï†sÎ›.timeContract) := by
  rw [timeContract_insertAndContract_some]
  simp only [Nat.succ_eq_add_one, Fin.getElem_fin, ite_mul, instCommGroup.eq_1,
    contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
    Equiv.coe_trans, Option.map_some', Function.comp_apply, finCongr_apply, Fin.coe_cast,
    List.getElem_map, uncontractedList_getElem_uncontractedIndexEquiv_symm, List.get_eq_getElem,
    Algebra.smul_mul_assoc, uncontractedListGet]
  Â· simp only [hik, â†“reduceIte, MulMemClass.coe_mul]
    rw [timeContract_of_timeOrderRel]
    trans (1 : â„‚) â€¢ ((superCommute (anPart Ï†)) (ofFieldOp Ï†s[k.1]) * â†‘Ï†sÎ›.timeContract)
    Â· simp
    simp only [smul_smul]
    congr 1
    have h1 : ofList ğ“•.fieldOpStatistic (List.take (â†‘(Ï†sÎ›.uncontractedIndexEquiv.symm k))
        (List.map Ï†s.get Ï†sÎ›.uncontractedList))
        = (ğ“• |>â‚› âŸ¨Ï†s.get, (Finset.filter (fun x => x < k) Ï†sÎ›.uncontracted)âŸ©) := by
      simp only [ofFinset]
      congr
      rw [â† List.map_take]
      congr
      rw [take_uncontractedIndexEquiv_symm]
      rw [filter_uncontractedList]
    rw [h1]
    simp only [exchangeSign_mul_self]
    Â· exact ht

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, a `i â‰¤ Ï†s.length` and a `k` in `Ï†sÎ›.uncontracted` such that `k < i`, with the
  condition that `Ï†s[k]` does not have has greater or equal time to `Ï†`, then
  `(Ï†sÎ› â†©Î› Ï† i (some k)).timeContract` is equal to the product of
  - `[anPart Ï†, Ï†s[k]]â‚›`
  - `Ï†sÎ›.timeContract`
  - the exchange sign of `Ï†` with the uncontracted fields in `Ï†â‚€â€¦Ï†â‚–â‚‹â‚`.
  - the exchange sign of `Ï†` with the uncontracted fields in `Ï†â‚€â€¦Ï†â‚–`.

  Most of the contributes to the exchange signs cancle.

  The proof of this result ultimately a consequence of definitions and
  `timeContract_of_not_timeOrderRel_expand`. -/
lemma timeContract_insert_some_of_not_lt
    (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (k : Ï†sÎ›.uncontracted)
    (ht : Â¬ ğ“•.timeOrderRel Ï†s[k.1] Ï†) (hik : Â¬ i < i.succAbove k) :
    (Ï†sÎ› â†©Î› Ï† i (some k)).timeContract =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, (Ï†sÎ›.uncontracted.filter (fun x => x â‰¤ k))âŸ©)
    â€¢ (contractStateAtIndex Ï† [Ï†sÎ›]áµ˜á¶œ
      ((uncontractedFieldOpEquiv Ï†s Ï†sÎ›) (some k)) * Ï†sÎ›.timeContract) := by
  rw [timeContract_insertAndContract_some]
  simp only [Nat.succ_eq_add_one, Fin.getElem_fin, ite_mul, instCommGroup.eq_1,
    contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
    Equiv.coe_trans, Option.map_some', Function.comp_apply, finCongr_apply, Fin.coe_cast,
    List.getElem_map, uncontractedList_getElem_uncontractedIndexEquiv_symm, List.get_eq_getElem,
    Algebra.smul_mul_assoc, uncontractedListGet]
  simp only [hik, â†“reduceIte, MulMemClass.coe_mul]
  rw [timeContract_of_not_timeOrderRel, timeContract_of_timeOrderRel]
  simp only [instCommGroup.eq_1, Algebra.smul_mul_assoc, smul_smul]
  congr
  have h1 : ofList ğ“•.fieldOpStatistic (List.take (â†‘(Ï†sÎ›.uncontractedIndexEquiv.symm k))
      (List.map Ï†s.get Ï†sÎ›.uncontractedList))
      = (ğ“• |>â‚› âŸ¨Ï†s.get, (Finset.filter (fun x => x < k) Ï†sÎ›.uncontracted)âŸ©) := by
    simp only [ofFinset]
    congr
    rw [â† List.map_take]
    congr
    rw [take_uncontractedIndexEquiv_symm, filter_uncontractedList]
  rw [h1]
  trans ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, {k.1}âŸ©)
  Â· rw [exchangeSign_symm, ofFinset_singleton]
    simp
  rw [â† map_mul]
  congr
  rw [ofFinset_union]
  congr
  ext a
  simp only [Finset.mem_singleton, Finset.mem_sdiff, Finset.mem_union, Finset.mem_filter,
    Finset.mem_inter, not_and, not_lt, and_imp]
  apply Iff.intro
  Â· intro h
    subst h
    simp
  Â· intro h
    have h1 := h.1
    rcases h1 with h1 | h1
    Â· have h2' := h.2 h1.1 h1.2 h1.1
      omega
    Â· have h2' := h.2 h1.1 (by omega) h1.1
      omega
  have ht := IsTotal.total (r := timeOrderRel) Ï†s[k.1] Ï†
  simp_all only [Fin.getElem_fin, Nat.succ_eq_add_one, not_lt, false_or]
  exact ht

lemma timeContract_of_not_gradingCompliant (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (h : Â¬ GradingCompliant Ï†s Ï†sÎ›) :
    Ï†sÎ›.timeContract = 0 := by
  rw [timeContract]
  simp only [GradingCompliant, Fin.getElem_fin, Subtype.forall, not_forall] at h
  obtain âŸ¨a, haâŸ© := h
  obtain âŸ¨ha, ha2âŸ© := ha
  apply Finset.prod_eq_zero (i := âŸ¨a, haâŸ©)
  simp only [Finset.univ_eq_attach, Finset.mem_attach]
  apply Subtype.eq
  simp only [List.get_eq_getElem, ZeroMemClass.coe_zero]
  rw [timeContract_zero_of_diff_grade]
  simp [ha2]

end WickContraction
