/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldSpecification.CrAnFieldOp
import HepLean.PerturbationTheory.FieldSpecification.CrAnSection
/-!

# Creation and annihilation free-algebra

This module defines the creation and annihilation algebra for a field structure.

The creation and annihilation algebra extends from the state algebra by adding information about
whether a state is a creation or annihilation operator.

The algebra is spanned by lists of creation/annihilation states.

The main structures defined in this module are:

* `FieldOpFreeAlgebra` - The creation and annihilation algebra
* `ofCrAnOpF` - Maps a creation/annihilation state to the algebra
* `ofCrAnListF` - Maps a list of creation/annihilation states to the algebra
* `ofFieldOpF` - Maps a state to a sum of creation and annihilation operators
* `crPartF` - The creation part of a state in the algebra
* `anPartF` - The annihilation part of a state in the algebra
* `superCommuteF` - The super commutator on the algebra

The key lemmas show how these operators interact, particularly focusing on the
super commutation relations between creation and annihilation operators.

-/

namespace FieldSpecification
variable {ğ“• : FieldSpecification}

/-- For a field specification `ğ“•`, the algebra `ğ“•.FieldOpFreeAlgebra` is
  the free algebra generated by `ğ“•.CrAnFieldOp`. -/
abbrev FieldOpFreeAlgebra (ğ“• : FieldSpecification) : Type := FreeAlgebra â„‚ ğ“•.CrAnFieldOp

namespace FieldOpFreeAlgebra

remark naming_convention := "
  For mathematicial objects defined in relation to `FieldOpFreeAlgebra` we will often postfix
  their names with an `F` to indicate that they are related to the free algebra.
  This is to avoid confusion when working within the context of `FieldOpAlgebra` which is defined
  as a quotient of `FieldOpFreeAlgebra`."

/-- For a field specification `ğ“•`, the element of `ğ“•.FieldOpFreeAlgebra` formed by a
  single `ğ“•.CrAnFieldOp`. -/
def ofCrAnOpF (Ï† : ğ“•.CrAnFieldOp) : FieldOpFreeAlgebra ğ“• :=
  FreeAlgebra.Î¹ â„‚ Ï†

/--
The algebra `ğ“•.FieldOpFreeAlgebra` satisfies the universal property that for any other algebra
  `A` (e.g. the operator algebra of the theory) with a map `f : ğ“•.CrAnFieldOp â†’ A` (e.g.
  the inclusion of the creation and annihilation parts of field operators into the
  operator algebra) there is a unqiue algebra map `g : ğ“•.FieldOpFreeAlgebra â†’ A`
  such that `g âˆ˜ ofCrAnOpF = f`.

  The unique `g` is given by `FreeAlgebra.lift â„‚ f`.
-/
lemma universality {A : Type} [Semiring A] [Algebra â„‚ A] (f : ğ“•.CrAnFieldOp â†’ A) :
    âˆƒ! g : FieldOpFreeAlgebra ğ“• â†’â‚[â„‚] A, g âˆ˜ ofCrAnOpF = f := by
  use FreeAlgebra.lift â„‚ f
  apply And.intro
  Â· funext x
    simp [ofCrAnOpF]
  Â· intro g hg
    ext x
    simpa using congrFun hg x

/-- For a field specification `ğ“•`, `ofCrAnListF Ï†s` of `ğ“•.FieldOpFreeAlgebra` formed by a
  list `Ï†s` of `ğ“•.CrAnFieldOp`. For example for the list `[Ï†â‚á¶œ, Ï†â‚‚áµƒ, Ï†â‚ƒá¶œ]` we schematically
  get `Ï†â‚á¶œÏ†â‚‚áµƒÏ†â‚ƒá¶œ`. The set of all `ofCrAnListF Ï†s` forms a basis of `FieldOpFreeAlgebra ğ“•`. -/
def ofCrAnListF (Ï†s : List ğ“•.CrAnFieldOp) : FieldOpFreeAlgebra ğ“• := (List.map ofCrAnOpF Ï†s).prod

@[simp]
lemma ofCrAnListF_nil : ofCrAnListF ([] : List ğ“•.CrAnFieldOp) = 1 := rfl

lemma ofCrAnListF_cons (Ï† : ğ“•.CrAnFieldOp) (Ï†s : List ğ“•.CrAnFieldOp) :
    ofCrAnListF (Ï† :: Ï†s) = ofCrAnOpF Ï† * ofCrAnListF Ï†s := rfl

lemma ofCrAnListF_append (Ï†s Ï†s' : List ğ“•.CrAnFieldOp) :
    ofCrAnListF (Ï†s ++ Ï†s') = ofCrAnListF Ï†s * ofCrAnListF Ï†s' := by
  simp [ofCrAnListF, List.map_append]

lemma ofCrAnListF_singleton (Ï† : ğ“•.CrAnFieldOp) :
    ofCrAnListF [Ï†] = ofCrAnOpF Ï† := by simp [ofCrAnListF]

/-- For a field specification `ğ“•`, the element of `ğ“•.FieldOpFreeAlgebra` formed by a
  `ğ“•.FieldOp` by summing over the creation and annihilation components of `ğ“•.FieldOp`.
  For example for `Ï†â‚` an incoming asymptotic field operator we get
  `ofCrAnOpF Ï†â‚á¶œ`, and for `Ï†â‚` a
  position field operator we get `ofCrAnOpF Ï†â‚á¶œ + ofCrAnOpF Ï†â‚áµƒ`. -/
def ofFieldOpF (Ï† : ğ“•.FieldOp) : FieldOpFreeAlgebra ğ“• :=
  âˆ‘ (i : ğ“•.fieldOpToCrAnType Ï†), ofCrAnOpF âŸ¨Ï†, iâŸ©

/-- For a field specification `ğ“•`, the element of `ğ“•.FieldOpFreeAlgebra` formed by a
  list of `ğ“•.FieldOp` by summing over the creation and annihilation components.
  For example, `Ï†â‚` and `Ï†â‚‚` position states `[Ï†1, Ï†2]` gets sent to
  `(ofCrAnOpF Ï†1á¶œ + ofCrAnOpF Ï†1áµƒ) * (ofCrAnOpF Ï†2á¶œ + ofCrAnOpF Ï†2áµƒ)`. -/
def ofFieldOpListF (Ï†s : List ğ“•.FieldOp) : FieldOpFreeAlgebra ğ“• := (List.map ofFieldOpF Ï†s).prod

remark notation_drop := "In doc-strings we will often drop explicit applications of `ofCrAnOpF`,
`ofCrAnListF`, `ofFieldOpF`, and `ofFieldOpListF`"

/-- Coercion from `List ğ“•.FieldOp` to `FieldOpFreeAlgebra ğ“•` through `ofFieldOpListF`. -/
instance : Coe (List ğ“•.FieldOp) (FieldOpFreeAlgebra ğ“•) := âŸ¨ofFieldOpListFâŸ©

@[simp]
lemma ofFieldOpListF_nil : ofFieldOpListF ([] : List ğ“•.FieldOp) = 1 := rfl

lemma ofFieldOpListF_cons (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    ofFieldOpListF (Ï† :: Ï†s) = ofFieldOpF Ï† * ofFieldOpListF Ï†s := rfl

lemma ofFieldOpListF_singleton (Ï† : ğ“•.FieldOp) :
    ofFieldOpListF [Ï†] = ofFieldOpF Ï† := by simp [ofFieldOpListF]

lemma ofFieldOpListF_append (Ï†s Ï†s' : List ğ“•.FieldOp) :
    ofFieldOpListF (Ï†s ++ Ï†s') = ofFieldOpListF Ï†s * ofFieldOpListF Ï†s' := by
  dsimp only [ofFieldOpListF]
  rw [List.map_append, List.prod_append]

lemma ofFieldOpListF_sum (Ï†s : List ğ“•.FieldOp) :
    ofFieldOpListF Ï†s = âˆ‘ (s : CrAnSection Ï†s), ofCrAnListF s.1 := by
  induction Ï†s with
  | nil => simp
  | cons Ï† Ï†s ih =>
    rw [CrAnSection.sum_cons]
    dsimp only [CrAnSection.cons, ofCrAnListF_cons]
    conv_rhs =>
      enter [2, x]
      rw [â† Finset.mul_sum]
    rw [â† Finset.sum_mul, ofFieldOpListF_cons, â† ih]
    rfl

/-!

## Creation and annihilation parts of a state

-/

/-- The algebra map taking an element of the free-state algbra to
  the part of it in the creation and annihilation free algebra
  spanned by creation operators. -/
def crPartF : ğ“•.FieldOp â†’ ğ“•.FieldOpFreeAlgebra := fun Ï† =>
  match Ï† with
  | FieldOp.inAsymp Ï† => ofCrAnOpF âŸ¨FieldOp.inAsymp Ï†, ()âŸ©
  | FieldOp.position Ï† => ofCrAnOpF âŸ¨FieldOp.position Ï†, CreateAnnihilate.createâŸ©
  | FieldOp.outAsymp _ => 0

@[simp]
lemma crPartF_negAsymp (Ï† : (Î£ f, ğ“•.AsymptoticLabel f) Ã— (Fin 3 â†’ â„)) :
    crPartF (FieldOp.inAsymp Ï†) = ofCrAnOpF âŸ¨FieldOp.inAsymp Ï†, ()âŸ© := by
  simp [crPartF]

@[simp]
lemma crPartF_position (Ï† : (Î£ f, ğ“•.PositionLabel f) Ã— SpaceTime) :
    crPartF (FieldOp.position Ï†) =
    ofCrAnOpF âŸ¨FieldOp.position Ï†, CreateAnnihilate.createâŸ© := by
  simp [crPartF]

@[simp]
lemma crPartF_posAsymp (Ï† : (Î£ f, ğ“•.AsymptoticLabel f) Ã— (Fin 3 â†’ â„)) :
    crPartF (FieldOp.outAsymp Ï†) = 0 := by
  simp [crPartF]

/-- The algebra map taking an element of the free-state algbra to
  the part of it in the creation and annihilation free algebra
  spanned by annihilation operators. -/
def anPartF : ğ“•.FieldOp â†’ ğ“•.FieldOpFreeAlgebra := fun Ï† =>
  match Ï† with
  | FieldOp.inAsymp _ => 0
  | FieldOp.position Ï† => ofCrAnOpF âŸ¨FieldOp.position Ï†, CreateAnnihilate.annihilateâŸ©
  | FieldOp.outAsymp Ï† => ofCrAnOpF âŸ¨FieldOp.outAsymp Ï†, ()âŸ©

@[simp]
lemma anPartF_negAsymp (Ï† : (Î£ f, ğ“•.AsymptoticLabel f) Ã— (Fin 3 â†’ â„)) :
    anPartF (FieldOp.inAsymp Ï†) = 0 := by
  simp [anPartF]

@[simp]
lemma anPartF_position (Ï† : (Î£ f, ğ“•.PositionLabel f) Ã— SpaceTime) :
    anPartF (FieldOp.position Ï†) =
    ofCrAnOpF âŸ¨FieldOp.position Ï†, CreateAnnihilate.annihilateâŸ© := by
  simp [anPartF]

@[simp]
lemma anPartF_posAsymp (Ï† : (Î£ f, ğ“•.AsymptoticLabel f) Ã— (Fin 3 â†’ â„)) :
    anPartF (FieldOp.outAsymp Ï†) = ofCrAnOpF âŸ¨FieldOp.outAsymp Ï†, ()âŸ© := by
  simp [anPartF]

lemma ofFieldOpF_eq_crPartF_add_anPartF (Ï† : ğ“•.FieldOp) :
    ofFieldOpF Ï† = crPartF Ï† + anPartF Ï† := by
  rw [ofFieldOpF]
  cases Ï† with
  | inAsymp Ï† => simp [fieldOpToCrAnType]
  | position Ï† => simp [fieldOpToCrAnType, CreateAnnihilate.sum_eq]
  | outAsymp Ï† => simp [fieldOpToCrAnType]

/-!

## The basis of the creation and annihilation free-algebra.

-/

/-- The basis of the free creation and annihilation algebra formed by lists of CrAnFieldOp. -/
noncomputable def ofCrAnListFBasis : Basis (List ğ“•.CrAnFieldOp) â„‚ (FieldOpFreeAlgebra ğ“•) where
  repr := FreeAlgebra.equivMonoidAlgebraFreeMonoid.toLinearEquiv

@[simp]
lemma ofListBasis_eq_ofList (Ï†s : List ğ“•.CrAnFieldOp) :
    ofCrAnListFBasis Ï†s = ofCrAnListF Ï†s := by
  simp only [ofCrAnListFBasis, FreeAlgebra.equivMonoidAlgebraFreeMonoid, MonoidAlgebra.of_apply,
    Basis.coe_ofRepr, AlgEquiv.toLinearEquiv_symm, AlgEquiv.toLinearEquiv_apply,
    AlgEquiv.ofAlgHom_symm_apply, ofCrAnListF]
  erw [MonoidAlgebra.lift_apply]
  simp only [zero_smul, Finsupp.sum_single_index, one_smul]
  rw [@FreeMonoid.lift_apply]
  match Ï†s with
  | [] => rfl
  | Ï† :: Ï†s => erw [List.map_cons]

/-!

## Some useful multi-linear maps.

-/

/-- The bi-linear map associated with multiplication in `FieldOpFreeAlgebra`. -/
noncomputable def mulLinearMap : FieldOpFreeAlgebra ğ“• â†’â‚—[â„‚] FieldOpFreeAlgebra ğ“• â†’â‚—[â„‚]
    FieldOpFreeAlgebra ğ“• where
  toFun a := {
    toFun := fun b => a * b,
    map_add' := fun c d => by simp [mul_add]
    map_smul' := by simp}
  map_add' := fun a b => by
    ext c
    simp [add_mul]
  map_smul' := by
    intros
    ext c
    simp [smul_mul']

lemma mulLinearMap_apply (a b : FieldOpFreeAlgebra ğ“•) :
    mulLinearMap a b = a * b := rfl

/-- The linear map associated with scalar-multiplication in `FieldOpFreeAlgebra`. -/
noncomputable def smulLinearMap (c : â„‚) : FieldOpFreeAlgebra ğ“• â†’â‚—[â„‚] FieldOpFreeAlgebra ğ“• where
  toFun a := c â€¢ a
  map_add' := by simp
  map_smul' m x := by simp [smul_smul, RingHom.id_apply, NonUnitalNormedCommRing.mul_comm]

end FieldOpFreeAlgebra

end FieldSpecification
