/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldSpecification.TimeOrder
import HepLean.PerturbationTheory.FieldOpFreeAlgebra.SuperCommute
import HepLean.PerturbationTheory.Koszul.KoszulSign
/-!

# Time Ordering in the FieldOpFreeAlgebra

-/

namespace FieldSpecification
variable {ð“• : FieldSpecification}
open FieldStatistic

namespace FieldOpFreeAlgebra

noncomputable section
open HepLean.List
/-!

## Time order

-/

/-- Time ordering for the `FieldOpFreeAlgebra`. -/
def timeOrderF : FieldOpFreeAlgebra ð“• â†’â‚—[â„‚] FieldOpFreeAlgebra ð“• :=
  Basis.constr ofCrAnListFBasis â„‚ fun Ï†s =>
  crAnTimeOrderSign Ï†s â€¢ ofCrAnListF (crAnTimeOrderList Ï†s)

@[inherit_doc timeOrderF]
scoped[FieldSpecification.FieldOpFreeAlgebra] notation "ð“£á¶ (" a ")" => timeOrderF a

lemma timeOrderF_ofCrAnListF (Ï†s : List ð“•.CrAnFieldOp) :
    ð“£á¶ (ofCrAnListF Ï†s) = crAnTimeOrderSign Ï†s â€¢ ofCrAnListF (crAnTimeOrderList Ï†s) := by
  rw [â† ofListBasis_eq_ofList]
  simp only [timeOrderF, Basis.constr_basis]

lemma timeOrderF_timeOrderF_mid (a b c : ð“•.FieldOpFreeAlgebra) :
    ð“£á¶ (a * b * c) = ð“£á¶ (a * ð“£á¶ (b) * c) := by
  let pc (c : ð“•.FieldOpFreeAlgebra) (hc : c âˆˆ Submodule.span â„‚ (Set.range ofCrAnListFBasis)) :
    Prop := ð“£á¶ (a * b * c) = ð“£á¶ (a * ð“£á¶ (b) * c)
  change pc c (Basis.mem_span _ c)
  apply Submodule.span_induction
  Â· intro x hx
    obtain âŸ¨Ï†s, rflâŸ© := hx
    simp only [ofListBasis_eq_ofList, pc]
    let pb (b : ð“•.FieldOpFreeAlgebra) (hb : b âˆˆ Submodule.span â„‚ (Set.range ofCrAnListFBasis)) :
      Prop := ð“£á¶ (a * b * ofCrAnListF Ï†s) = ð“£á¶ (a * ð“£á¶ (b) * ofCrAnListF Ï†s)
    change pb b (Basis.mem_span _ b)
    apply Submodule.span_induction
    Â· intro x hx
      obtain âŸ¨Ï†s', rflâŸ© := hx
      simp only [ofListBasis_eq_ofList, pb]
      let pa (a : ð“•.FieldOpFreeAlgebra) (ha : a âˆˆ Submodule.span â„‚ (Set.range ofCrAnListFBasis)) :
        Prop := ð“£á¶ (a * ofCrAnListF Ï†s' * ofCrAnListF Ï†s) =
          ð“£á¶ (a * ð“£á¶ (ofCrAnListF Ï†s') * ofCrAnListF Ï†s)
      change pa a (Basis.mem_span _ a)
      apply Submodule.span_induction
      Â· intro x hx
        obtain âŸ¨Ï†s'', rflâŸ© := hx
        simp only [ofListBasis_eq_ofList, pa]
        rw [timeOrderF_ofCrAnListF]
        simp only [â† ofCrAnListF_append, Algebra.mul_smul_comm,
          Algebra.smul_mul_assoc, map_smul]
        rw [timeOrderF_ofCrAnListF, timeOrderF_ofCrAnListF, smul_smul]
        congr 1
        Â· simp only [crAnTimeOrderSign, crAnTimeOrderList]
          rw [Wick.koszulSign_of_append_eq_insertionSort, mul_comm]
        Â· congr 1
          simp only [crAnTimeOrderList]
          rw [insertionSort_append_insertionSort_append]
      Â· simp [pa]
      Â· intro x y hx hy h1 h2
        simp_all [pa, add_mul]
      Â· intro x hx h
        simp_all [pa]
    Â· simp [pb]
    Â· intro x y hx hy h1 h2
      simp_all [pb, mul_add, add_mul]
    Â· intro x hx h
      simp_all [pb]
  Â· simp [pc]
  Â· intro x y hx hy h1 h2
    simp_all [pc, mul_add]
  Â· intro x hx h hp
    simp_all [pc]

lemma timeOrderF_timeOrderF_right (a b : ð“•.FieldOpFreeAlgebra) : ð“£á¶ (a * b) = ð“£á¶ (a * ð“£á¶ (b)) := by
  trans ð“£á¶ (a * b * 1)
  Â· simp
  Â· rw [timeOrderF_timeOrderF_mid]
    simp

lemma timeOrderF_timeOrderF_left (a b : ð“•.FieldOpFreeAlgebra) : ð“£á¶ (a * b) = ð“£á¶ (ð“£á¶ (a) * b) := by
  trans ð“£á¶ (1 * a * b)
  Â· simp
  Â· rw [timeOrderF_timeOrderF_mid]
    simp

lemma timeOrderF_ofFieldOpListF (Ï†s : List ð“•.FieldOp) :
    ð“£á¶ (ofFieldOpListF Ï†s) = timeOrderSign Ï†s â€¢ ofFieldOpListF (timeOrderList Ï†s) := by
  conv_lhs =>
    rw [ofFieldOpListF_sum, map_sum]
    enter [2, x]
    rw [timeOrderF_ofCrAnListF]
  simp only [crAnTimeOrderSign_crAnSection]
  rw [â† Finset.smul_sum]
  congr
  rw [ofFieldOpListF_sum, sum_crAnSections_timeOrder]
  rfl

lemma timeOrderF_ofFieldOpListF_nil : timeOrderF (ð“• := ð“•) (ofFieldOpListF []) = 1 := by
  rw [timeOrderF_ofFieldOpListF]
  simp [timeOrderSign, Wick.koszulSign, timeOrderList]

@[simp]
lemma timeOrderF_ofFieldOpListF_singleton (Ï† : ð“•.FieldOp) :
    ð“£á¶ (ofFieldOpListF [Ï†]) = ofFieldOpListF [Ï†] := by
  simp [timeOrderF_ofFieldOpListF, timeOrderSign, timeOrderList]

lemma timeOrderF_ofFieldOpF_ofFieldOpF_ordered {Ï† Ïˆ : ð“•.FieldOp} (h : timeOrderRel Ï† Ïˆ) :
    ð“£á¶ (ofFieldOpF Ï† * ofFieldOpF Ïˆ) = ofFieldOpF Ï† * ofFieldOpF Ïˆ := by
  rw [â† ofFieldOpListF_singleton, â† ofFieldOpListF_singleton, â† ofFieldOpListF_append,
    timeOrderF_ofFieldOpListF]
  simp only [List.singleton_append]
  rw [timeOrderSign_pair_ordered h, timeOrderList_pair_ordered h]
  simp

lemma timeOrderF_ofFieldOpF_ofFieldOpF_not_ordered {Ï† Ïˆ : ð“•.FieldOp} (h : Â¬ timeOrderRel Ï† Ïˆ) :
    ð“£á¶ (ofFieldOpF Ï† * ofFieldOpF Ïˆ) = ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ïˆ) â€¢ ofFieldOpF Ïˆ * ofFieldOpF Ï† := by
  rw [â† ofFieldOpListF_singleton, â† ofFieldOpListF_singleton,
    â† ofFieldOpListF_append, timeOrderF_ofFieldOpListF]
  simp only [List.singleton_append, instCommGroup.eq_1, Algebra.smul_mul_assoc]
  rw [timeOrderSign_pair_not_ordered h, timeOrderList_pair_not_ordered h]
  simp [â† ofFieldOpListF_append]

lemma timeOrderF_ofFieldOpF_ofFieldOpF_not_ordered_eq_timeOrderF {Ï† Ïˆ : ð“•.FieldOp}
    (h : Â¬ timeOrderRel Ï† Ïˆ) :
    ð“£á¶ (ofFieldOpF Ï† * ofFieldOpF Ïˆ) = ð“¢(ð“• |>â‚› Ï†, ð“• |>â‚› Ïˆ) â€¢ ð“£á¶ (ofFieldOpF Ïˆ * ofFieldOpF Ï†) := by
  rw [timeOrderF_ofFieldOpF_ofFieldOpF_not_ordered h]
  rw [timeOrderF_ofFieldOpF_ofFieldOpF_ordered]
  simp only [instCommGroup.eq_1, Algebra.smul_mul_assoc]
  have hx := IsTotal.total (r := timeOrderRel) Ïˆ Ï†
  simp_all

lemma timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel
    {Ï† Ïˆ : ð“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï† Ïˆ) :
    ð“£á¶ ([ofCrAnOpF Ï†, ofCrAnOpF Ïˆ]â‚›ca) = 0 := by
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF]
  simp only [instCommGroup.eq_1, Algebra.smul_mul_assoc, map_sub, map_smul]
  rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton,
    â† ofCrAnListF_append, â† ofCrAnListF_append, timeOrderF_ofCrAnListF, timeOrderF_ofCrAnListF]
  simp only [List.singleton_append]
  rw [crAnTimeOrderSign_pair_not_ordered h, crAnTimeOrderList_pair_not_ordered h]
  rw [sub_eq_zero, smul_smul]
  have h1 := IsTotal.total (r := crAnTimeOrderRel) Ï† Ïˆ
  congr
  Â· rw [crAnTimeOrderSign_pair_ordered, exchangeSign_symm]
    simp only [instCommGroup.eq_1, mul_one]
    simp_all
  Â· rw [crAnTimeOrderList_pair_ordered]
    simp_all

lemma timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_right
    {Ï† Ïˆ : ð“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï† Ïˆ) (a : ð“•.FieldOpFreeAlgebra) :
    ð“£á¶ (a * [ofCrAnOpF Ï†, ofCrAnOpF Ïˆ]â‚›ca) = 0 := by
  rw [timeOrderF_timeOrderF_right,
    timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel h]
  simp

lemma timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_left
    {Ï† Ïˆ : ð“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï† Ïˆ) (a : ð“•.FieldOpFreeAlgebra) :
    ð“£á¶ ([ofCrAnOpF Ï†, ofCrAnOpF Ïˆ]â‚›ca * a) = 0 := by
  rw [timeOrderF_timeOrderF_left,
    timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel h]
  simp

lemma timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_mid
    {Ï† Ïˆ : ð“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï† Ïˆ) (a b : ð“•.FieldOpFreeAlgebra) :
    ð“£á¶ (a * [ofCrAnOpF Ï†, ofCrAnOpF Ïˆ]â‚›ca * b) = 0 := by
  rw [timeOrderF_timeOrderF_mid,
    timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel h]
  simp

lemma timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel
    {Ï†1 Ï†2 : ð“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï†1 Ï†2) (a : ð“•.FieldOpFreeAlgebra) :
    ð“£á¶ ([a, [ofCrAnOpF Ï†1, ofCrAnOpF Ï†2]â‚›ca]â‚›ca) = 0 := by
  rw [â† bosonicProj_add_fermionicProj a]
  simp only [map_add, LinearMap.add_apply]
  rw [bosonic_superCommuteF (Submodule.coe_mem (bosonicProj a))]
  simp only [map_sub]
  rw [timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_left h]
  rw [timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_right h]
  simp only [sub_self, zero_add]
  rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton]
  rcases superCommuteF_ofCrAnListF_ofCrAnListF_bosonic_or_fermionic [Ï†1] [Ï†2] with h' | h'
  Â· rw [superCommuteF_bonsonic h']
    simp only [ofCrAnListF_singleton, map_sub]
    rw [timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_left h]
    rw [timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_right h]
    simp
  Â· rw [superCommuteF_fermionic_fermionic (Submodule.coe_mem (fermionicProj a)) h']
    simp only [ofCrAnListF_singleton, map_add]
    rw [timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_left h]
    rw [timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_not_crAnTimeOrderRel_right h]
    simp

lemma timeOrderF_superCommuteF_ofCrAnOpF_superCommuteF_not_crAnTimeOrderRel
    {Ï†1 Ï†2 Ï†3 : ð“•.CrAnFieldOp} (h12 : Â¬ crAnTimeOrderRel Ï†1 Ï†2)
    (h13 : Â¬ crAnTimeOrderRel Ï†1 Ï†3) :
    ð“£á¶ ([ofCrAnOpF Ï†1, [ofCrAnOpF Ï†2, ofCrAnOpF Ï†3]â‚›ca]â‚›ca) = 0 := by
  rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton, â† ofCrAnListF_singleton]
  rw [summerCommute_jacobi_ofCrAnListF]
  simp only [instCommGroup.eq_1, ofList_singleton, ofCrAnListF_singleton, neg_smul, map_smul,
    map_sub, map_neg, smul_eq_zero]
  right
  rw [timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel h12]
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF_symm Ï†3]
  simp only [smul_zero, neg_zero, instCommGroup.eq_1, neg_smul, map_neg, map_smul, smul_neg,
    sub_neg_eq_add, zero_add, smul_eq_zero]
  rw [timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel h13]
  simp

lemma timeOrderF_superCommuteF_ofCrAnOpF_superCommuteF_not_crAnTimeOrderRel'
    {Ï†1 Ï†2 Ï†3 : ð“•.CrAnFieldOp} (h12 : Â¬ crAnTimeOrderRel Ï†2 Ï†1)
    (h13 : Â¬ crAnTimeOrderRel Ï†3 Ï†1) :
    ð“£á¶ ([ofCrAnOpF Ï†1, [ofCrAnOpF Ï†2, ofCrAnOpF Ï†3]â‚›ca]â‚›ca) = 0 := by
  rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton, â† ofCrAnListF_singleton]
  rw [summerCommute_jacobi_ofCrAnListF]
  simp only [instCommGroup.eq_1, ofList_singleton, ofCrAnListF_singleton, neg_smul, map_smul,
    map_sub, map_neg, smul_eq_zero]
  right
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF_symm Ï†1]
  simp only [instCommGroup.eq_1, neg_smul, map_neg, map_smul, smul_neg, neg_neg]
  rw [timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel h12]
  simp only [smul_zero, zero_sub, neg_eq_zero, smul_eq_zero]
  rw [timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel h13]
  simp

lemma timeOrderF_superCommuteF_ofCrAnOpF_superCommuteF_all_not_crAnTimeOrderRel
    (Ï†1 Ï†2 Ï†3 : ð“•.CrAnFieldOp) (h : Â¬
      (crAnTimeOrderRel Ï†1 Ï†2 âˆ§ crAnTimeOrderRel Ï†1 Ï†3 âˆ§
      crAnTimeOrderRel Ï†2 Ï†1 âˆ§ crAnTimeOrderRel Ï†2 Ï†3 âˆ§
      crAnTimeOrderRel Ï†3 Ï†1 âˆ§ crAnTimeOrderRel Ï†3 Ï†2)) :
    ð“£á¶ ([ofCrAnOpF Ï†1, [ofCrAnOpF Ï†2, ofCrAnOpF Ï†3]â‚›ca]â‚›ca) = 0 := by
  simp only [not_and] at h
  by_cases h23 : Â¬ crAnTimeOrderRel Ï†2 Ï†3
  Â· simp_all only [IsEmpty.forall_iff, implies_true]
    rw [timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel h23]
  simp_all only [Decidable.not_not, forall_const]
  by_cases h32 : Â¬ crAnTimeOrderRel Ï†3 Ï†2
  Â· simp_all only [not_false_eq_true, implies_true]
    rw [superCommuteF_ofCrAnOpF_ofCrAnOpF_symm]
    simp only [instCommGroup.eq_1, neg_smul, map_neg, map_smul, neg_eq_zero, smul_eq_zero]
    rw [timeOrderF_superCommuteF_superCommuteF_ofCrAnOpF_not_crAnTimeOrderRel h32]
    simp
  simp_all only [imp_false, Decidable.not_not]
  by_cases h12 : Â¬ crAnTimeOrderRel Ï†1 Ï†2
  Â· have h13 : Â¬ crAnTimeOrderRel Ï†1 Ï†3 := by
      intro h13
      apply h12
      exact IsTrans.trans Ï†1 Ï†3 Ï†2 h13 h32
    rw [timeOrderF_superCommuteF_ofCrAnOpF_superCommuteF_not_crAnTimeOrderRel h12 h13]
  simp_all only [Decidable.not_not, forall_const]
  have h13 : crAnTimeOrderRel Ï†1 Ï†3 := IsTrans.trans Ï†1 Ï†2 Ï†3 h12 h23
  simp_all only [forall_const]
  by_cases h21 : Â¬ crAnTimeOrderRel Ï†2 Ï†1
  Â· simp_all only [IsEmpty.forall_iff]
    have h31 : Â¬ crAnTimeOrderRel Ï†3 Ï†1 := by
      intro h31
      apply h21
      exact IsTrans.trans Ï†2 Ï†3 Ï†1 h23 h31
    rw [timeOrderF_superCommuteF_ofCrAnOpF_superCommuteF_not_crAnTimeOrderRel' h21 h31]
  simp_all only [Decidable.not_not, forall_const]
  refine False.elim (h ?_)
  exact IsTrans.trans Ï†3 Ï†2 Ï†1 h32 h21

lemma timeOrderF_superCommuteF_ofCrAnOpF_ofCrAnOpF_eq_time
    {Ï† Ïˆ : ð“•.CrAnFieldOp} (h1 : crAnTimeOrderRel Ï† Ïˆ) (h2 : crAnTimeOrderRel Ïˆ Ï†) :
    ð“£á¶ ([ofCrAnOpF Ï†, ofCrAnOpF Ïˆ]â‚›ca) = [ofCrAnOpF Ï†, ofCrAnOpF Ïˆ]â‚›ca := by
  rw [superCommuteF_ofCrAnOpF_ofCrAnOpF]
  simp only [instCommGroup.eq_1, Algebra.smul_mul_assoc, map_sub, map_smul]
  rw [â† ofCrAnListF_singleton, â† ofCrAnListF_singleton,
    â† ofCrAnListF_append, â† ofCrAnListF_append, timeOrderF_ofCrAnListF, timeOrderF_ofCrAnListF]
  simp only [List.singleton_append]
  rw [crAnTimeOrderSign_pair_ordered h1, crAnTimeOrderList_pair_ordered h1,
    crAnTimeOrderSign_pair_ordered h2, crAnTimeOrderList_pair_ordered h2]
  simp

/-!

## Interaction with maxTimeField

-/

/-- In the state algebra time, ordering obeys `T(Ï†â‚€Ï†â‚â€¦Ï†â‚™) = s * Ï†áµ¢ * T(Ï†â‚€Ï†â‚â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚â€¦Ï†â‚™)`
  where `Ï†áµ¢` is the state
  which has maximum time and `s` is the exchange sign of `Ï†áµ¢` and `Ï†â‚€Ï†â‚â€¦Ï†áµ¢â‚‹â‚`. -/
lemma timeOrderF_eq_maxTimeField_mul (Ï† : ð“•.FieldOp) (Ï†s : List ð“•.FieldOp) :
    ð“£á¶ (ofFieldOpListF (Ï† :: Ï†s)) =
    ð“¢(ð“• |>â‚› maxTimeField Ï† Ï†s, ð“• |>â‚› (Ï† :: Ï†s).take (maxTimeFieldPos Ï† Ï†s)) â€¢
    ofFieldOpF (maxTimeField Ï† Ï†s) * ð“£á¶ (ofFieldOpListF (eraseMaxTimeField Ï† Ï†s)) := by
  rw [timeOrderF_ofFieldOpListF, timeOrderList_eq_maxTimeField_timeOrderList]
  rw [ofFieldOpListF_cons, timeOrderF_ofFieldOpListF]
  simp only [instCommGroup.eq_1, Algebra.mul_smul_comm, Algebra.smul_mul_assoc, smul_smul]
  congr
  rw [timerOrderSign_of_eraseMaxTimeField, mul_assoc]
  simp

/-- In the state algebra time, ordering obeys `T(Ï†â‚€Ï†â‚â€¦Ï†â‚™) = s * Ï†áµ¢ * T(Ï†â‚€Ï†â‚â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚â€¦Ï†â‚™)`
  where `Ï†áµ¢` is the state
  which has maximum time and `s` is the exchange sign of `Ï†áµ¢` and `Ï†â‚€Ï†â‚â€¦Ï†áµ¢â‚‹â‚`.
  Here `s` is written using finite sets. -/
lemma timeOrderF_eq_maxTimeField_mul_finset (Ï† : ð“•.FieldOp) (Ï†s : List ð“•.FieldOp) :
    ð“£á¶ (ofFieldOpListF (Ï† :: Ï†s)) = ð“¢(ð“• |>â‚› maxTimeField Ï† Ï†s, ð“• |>â‚› âŸ¨(eraseMaxTimeField Ï† Ï†s).get,
      (Finset.filter (fun x =>
        (maxTimeFieldPosFin Ï† Ï†s).succAbove x < maxTimeFieldPosFin Ï† Ï†s) Finset.univ)âŸ©) â€¢
      ofFieldOpF (maxTimeField Ï† Ï†s) * ð“£á¶ (ofFieldOpListF (eraseMaxTimeField Ï† Ï†s)) := by
  rw [timeOrderF_eq_maxTimeField_mul]
  congr 3
  apply FieldStatistic.ofList_perm
  nth_rewrite 1 [â† List.finRange_map_get (Ï† :: Ï†s)]
  simp only [List.length_cons, eraseMaxTimeField, insertionSortDropMinPos]
  rw [eraseIdx_get, â† List.map_take, â† List.map_map]
  refine List.Perm.map (Ï† :: Ï†s).get ?_
  apply (List.perm_ext_iff_of_nodup _ _).mpr
  Â· intro i
    simp only [List.length_cons, maxTimeFieldPos, mem_take_finrange, Fin.val_fin_lt, List.mem_map,
      Finset.mem_sort, Finset.mem_filter, Finset.mem_univ, true_and, Function.comp_apply]
    refine Iff.intro (fun hi => ?_) (fun h => ?_)
    Â· have h2 := (maxTimeFieldPosFin Ï† Ï†s).2
      simp only [eraseMaxTimeField, insertionSortDropMinPos, List.length_cons, Nat.succ_eq_add_one,
        maxTimeFieldPosFin, insertionSortMinPosFin] at h2
      use âŸ¨i, by omegaâŸ©
      apply And.intro
      Â· simp only [Fin.succAbove, List.length_cons, Fin.castSucc_mk, maxTimeFieldPosFin,
        insertionSortMinPosFin, Nat.succ_eq_add_one, Fin.mk_lt_mk, Fin.val_fin_lt, Fin.succ_mk]
        rw [Fin.lt_def]
        split
        Â· simp only [Fin.val_fin_lt]
          omega
        Â· omega
      Â· simp only [Fin.succAbove, List.length_cons, Fin.castSucc_mk, Fin.succ_mk, Fin.ext_iff,
        Fin.coe_cast]
        split
        Â· simp
        Â· simp_all [Fin.lt_def]
    Â· obtain âŸ¨j, h1, h2âŸ© := h
      subst h2
      simp only [Fin.lt_def, Fin.coe_cast]
      exact h1
  Â· exact List.Sublist.nodup (List.take_sublist _ _) <|
      List.nodup_finRange (Ï†s.length + 1)
  Â· refine List.Nodup.map ?_ ?_
    Â· refine Function.Injective.comp ?hf.hg Fin.succAbove_right_injective
      exact Fin.cast_injective (eraseIdx_length (Ï† :: Ï†s) (insertionSortMinPos timeOrderRel Ï† Ï†s))
    Â· exact Finset.sort_nodup (fun x1 x2 => x1 â‰¤ x2)
        (Finset.filter (fun x => (maxTimeFieldPosFin Ï† Ï†s).succAbove x < maxTimeFieldPosFin Ï† Ï†s)
          Finset.univ)

end

end FieldOpFreeAlgebra

end FieldSpecification
