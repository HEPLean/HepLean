/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.Algebra.FreeAlgebra
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.Analysis.Complex.Basic
import HepLean.Mathematics.List.InsertIdx
/-!

# Field statistics

Basic properties related to whether a field, or list of fields, is bosonic or fermionic.

-/

/-- A field can either be bosonic or fermionic in nature.
  That is to say, they can either have Bose-Einstein statistics or
  Fermi-Dirac statistics. -/
inductive FieldStatistic : Type where
  | bosonic : FieldStatistic
  | fermionic : FieldStatistic
deriving DecidableEq

namespace FieldStatistic

variable {ğ“• : Type}

@[simp]
instance : CommGroup FieldStatistic where
  one := bosonic
  mul a b :=
    match a, b with
    | bosonic, bosonic => bosonic
    | bosonic, fermionic => fermionic
    | fermionic, bosonic => fermionic
    | fermionic, fermionic => bosonic
  inv a := a
  mul_assoc a b c := by
    cases a <;> cases b <;> cases c <;>
    dsimp [HMul.hMul]
  one_mul a := by
    cases a <;> dsimp [HMul.hMul]
  mul_one a := by
    cases a <;> dsimp [HMul.hMul]
  inv_mul_cancel a := by
    cases a <;> dsimp only [HMul.hMul, Nat.succ_eq_add_one] <;> rfl
  mul_comm a b := by
    cases a <;> cases b <;> rfl

@[simp]
lemma bosonic_mul_bosonic : bosonic * bosonic = bosonic := rfl

@[simp]
lemma bosonic_mul_fermionic : bosonic * fermionic = fermionic := rfl

@[simp]
lemma fermionic_mul_bosonic : fermionic * bosonic = fermionic := rfl

@[simp]
lemma fermionic_mul_fermionic : fermionic * fermionic = bosonic := rfl

@[simp]
lemma mul_self (a : FieldStatistic) : a * a = 1 := by
  cases a <;> rfl

/-- Field statics form a finite type. -/
instance : Fintype FieldStatistic where
  elems := {bosonic, fermionic}
  complete := by
    intro c
    cases c
    Â· exact Finset.mem_insert_self bosonic {fermionic}
    Â· refine Finset.insert_eq_self.mp ?_
      exact rfl

@[simp]
lemma fermionic_not_eq_bonsic : Â¬ fermionic = bosonic := by
  intro h
  exact FieldStatistic.noConfusion h

lemma bonsic_eq_fermionic_false : bosonic = fermionic â†” false := by
  simp only [reduceCtorEq, Bool.false_eq_true]

@[simp]
lemma neq_fermionic_iff_eq_bosonic (a : FieldStatistic) : Â¬ a = fermionic â†” a = bosonic := by
  fin_cases a
  Â· simp
  Â· simp

@[simp]
lemma bosonic_neq_iff_fermionic_eq (a : FieldStatistic) : Â¬ bosonic = a â†” fermionic = a := by
  fin_cases a
  Â· simp
  Â· simp

@[simp]
lemma fermionic_neq_iff_bosonic_eq (a : FieldStatistic) : Â¬ fermionic = a â†” bosonic = a := by
  fin_cases a
  Â· simp
  Â· simp

lemma eq_self_if_eq_bosonic {a : FieldStatistic} :
    (if a = bosonic then bosonic else fermionic) = a := by
  fin_cases a <;> rfl

lemma eq_self_if_bosonic_eq {a : FieldStatistic} :
    (if bosonic = a then bosonic else fermionic) = a := by
  fin_cases a <;> rfl

lemma mul_eq_one_iff (a b : FieldStatistic) : a * b = 1 â†” a = b := by
  fin_cases a <;> fin_cases b <;> simp

lemma one_eq_mul_iff (a b : FieldStatistic) : 1 = a * b â†” a = b := by
  fin_cases a <;> fin_cases b <;> simp

lemma mul_eq_iff_eq_mul (a b c : FieldStatistic) : a * b = c â†” a = b * c := by
  fin_cases a <;> fin_cases b <;> fin_cases c <;>
    simp only [bosonic_mul_fermionic, fermionic_not_eq_bonsic, mul_self,
      reduceCtorEq, fermionic_mul_bosonic, true_iff, iff_true]
  all_goals rfl

lemma mul_eq_iff_eq_mul' (a b c : FieldStatistic) : a * b = c â†” b = a * c := by
  fin_cases a <;> fin_cases b <;> fin_cases c <;>
    simp only [bosonic_mul_fermionic, fermionic_not_eq_bonsic, mul_self,
      reduceCtorEq, fermionic_mul_bosonic, true_iff, iff_true]
  all_goals rfl

/-- The field statistics of a list of fields is fermionic if ther is an odd number of fermions,
  otherwise it is bosonic. -/
def ofList (s : ğ“• â†’ FieldStatistic) : (Ï†s : List ğ“•) â†’ FieldStatistic
  | [] => bosonic
  | Ï† :: Ï†s => if s Ï† = ofList s Ï†s then bosonic else fermionic

lemma ofList_cons_eq_mul (s : ğ“• â†’ FieldStatistic) (Ï† : ğ“•) (Ï†s : List ğ“•) :
    ofList s (Ï† :: Ï†s) = s Ï† * ofList s Ï†s := by
  have ha (a b : FieldStatistic) : (if a = b then bosonic else fermionic) = a * b := by
    fin_cases a <;> fin_cases b <;> rfl
  exact ha (s Ï†) (ofList s Ï†s)

lemma ofList_eq_prod (s : ğ“• â†’ FieldStatistic) : (Ï†s : List ğ“•) â†’
    ofList s Ï†s = (List.map s Ï†s).prod
  | [] => rfl
  | Ï† :: Ï†s => by
    rw [ofList_cons_eq_mul, List.map_cons, List.prod_cons, ofList_eq_prod]

@[simp]
lemma ofList_singleton (s : ğ“• â†’ FieldStatistic) (Ï† : ğ“•) : ofList s [Ï†] = s Ï† := by
  simp only [ofList, Fin.isValue]
  rw [eq_self_if_eq_bosonic]

@[simp]
lemma ofList_freeMonoid (s : ğ“• â†’ FieldStatistic) (Ï† : ğ“•) : ofList s (FreeMonoid.of Ï†) = s Ï† :=
  ofList_singleton s Ï†

@[simp]
lemma ofList_empty (s : ğ“• â†’ FieldStatistic) : ofList s [] = bosonic := rfl

@[simp]
lemma ofList_append (s : ğ“• â†’ FieldStatistic) (Ï†s Ï†s' : List ğ“•) :
    ofList s (Ï†s ++ Ï†s') = if ofList s Ï†s = ofList s Ï†s' then bosonic else fermionic := by
  induction Ï†s with
  | nil =>
    simp only [List.nil_append, ofList_empty, Fin.isValue, eq_self_if_bosonic_eq]
  | cons a l ih =>
    have hab (a b c : FieldStatistic) :
        (if a = (if b = c then bosonic else fermionic) then bosonic else fermionic) =
        if (if a = b then bosonic else fermionic) = c then bosonic else fermionic := by
      fin_cases a <;> fin_cases b <;> fin_cases c <;> rfl
    simp only [ofList, List.append_eq, Fin.isValue, ih, hab]

lemma ofList_append_eq_mul (s : ğ“• â†’ FieldStatistic) (Ï†s Ï†s' : List ğ“•) :
    ofList s (Ï†s ++ Ï†s') = ofList s Ï†s * ofList s Ï†s' := by
  rw [ofList_append]
  have ha (a b : FieldStatistic) : (if a = b then bosonic else fermionic) = a * b := by
    fin_cases a <;> fin_cases b <;> rfl
  exact ha _ _

lemma ofList_perm (s : ğ“• â†’ FieldStatistic) {l l' : List ğ“•} (h : l.Perm l') :
    ofList s l = ofList s l' := by
  rw [ofList_eq_prod, ofList_eq_prod]
  exact List.Perm.prod_eq (List.Perm.map s h)

lemma ofList_orderedInsert (s : ğ“• â†’ FieldStatistic) (le1 : ğ“• â†’ ğ“• â†’ Prop) [DecidableRel le1]
    (Ï†s : List ğ“•) (Ï† : ğ“•) : ofList s (List.orderedInsert le1 Ï† Ï†s) = ofList s (Ï† :: Ï†s) :=
  ofList_perm s (List.perm_orderedInsert le1 Ï† Ï†s)

@[simp]
lemma ofList_insertionSort (s : ğ“• â†’ FieldStatistic) (le1 : ğ“• â†’ ğ“• â†’ Prop) [DecidableRel le1]
    (Ï†s : List ğ“•) : ofList s (List.insertionSort le1 Ï†s) = ofList s Ï†s :=
  ofList_perm s (List.perm_insertionSort le1 Ï†s)

lemma ofList_map_eq_finset_prod (s : ğ“• â†’ FieldStatistic) :
    (Ï†s : List ğ“•) â†’ (l : List (Fin Ï†s.length)) â†’ (hl : l.Nodup) â†’
    ofList s (l.map Ï†s.get) = âˆ (i : Fin Ï†s.length), if i âˆˆ l then s Ï†s[i] else 1
  | [], [], _ => rfl
  | [], i :: l, hl => Fin.elim0 i
  | Ï† :: Ï†s, [], hl => by
    simp only [List.length_cons, List.map_nil, ofList_empty, List.not_mem_nil, â†“reduceIte,
      Finset.prod_const_one]
    rfl
  | Ï† :: Ï†s, i :: l, hl => by
    simp only [List.length_cons, List.map_cons, List.get_eq_getElem, List.mem_cons, instCommGroup,
      Fin.getElem_fin]
    rw [ofList_cons_eq_mul]
    rw [ofList_map_eq_finset_prod s (Ï† :: Ï†s) l]
    have h1 : s (Ï† :: Ï†s)[â†‘i] = âˆ (j : Fin (Ï† :: Ï†s).length),
      if j = i then s (Ï† :: Ï†s)[â†‘i] else 1 := by
      rw [Fintype.prod_ite_eq']
    erw [h1]
    rw [â† Finset.prod_mul_distrib]
    congr
    funext a
    simp only [instCommGroup, List.length_cons, mul_ite, ite_mul, one_mul, mul_one]
    by_cases ha : a = i
    Â· simp only [ha, â†“reduceIte, mul_self, true_or]
      rw [if_neg]
      rfl
      simp only [List.length_cons, List.nodup_cons] at hl
      exact hl.1
    Â· simp only [ha, â†“reduceIte, false_or]
      rfl
    simp only [List.length_cons, List.nodup_cons] at hl
    exact hl.2

/-!

## ofList and take

-/

section ofListTake
open HepLean.List
variable (q : ğ“• â†’ FieldStatistic)
lemma ofList_take_insert (n : â„•) (Ï† : ğ“•) (Ï†s : List ğ“•) :
    ofList q (List.take n Ï†s) = ofList q (List.take n (List.insertIdx n Ï† Ï†s)) := by
  congr 1
  rw [take_insert_same]

lemma ofList_take_eraseIdx (n : â„•) (Ï†s : List ğ“•) :
    ofList q (List.take n (Ï†s.eraseIdx n)) = ofList q (List.take n Ï†s) := by
  congr 1
  rw [take_eraseIdx_same]

lemma ofList_take_zero (Ï†s : List ğ“•) :
    ofList q (List.take 0 Ï†s) = 1 := by
  simp only [List.take_zero, ofList_empty]
  rfl

lemma ofList_take_succ_cons (n : â„•) (Ï†1 : ğ“•) (Ï†s : List ğ“•) :
    ofList q ((Ï†1 :: Ï†s).take (n + 1)) = q Ï†1 * ofList q (Ï†s.take n) := by
  simp only [List.take_succ_cons, instCommGroup]
  rw [ofList_cons_eq_mul]

lemma ofList_take_insertIdx_gt (n m : â„•) (Ï†1 : ğ“•) (Ï†s : List ğ“•) (hn : n < m) :
    ofList q ((List.insertIdx m Ï†1 Ï†s).take n) = ofList q (Ï†s.take n) := by
  rw [take_insert_gt Ï†1 n m hn Ï†s]

lemma ofList_insert_lt_eq (n m : â„•) (Ï†1 : ğ“•) (Ï†s : List ğ“•) (hn : m â‰¤ n)
    (hm : m â‰¤ Ï†s.length) :
    ofList q ((List.insertIdx m Ï†1 Ï†s).take (n + 1)) =
    ofList q ((Ï†1 :: Ï†s).take (n + 1)) := by
  apply ofList_perm
  simp only [List.take_succ_cons]
  refine take_insert_let Ï†1 n m hn Ï†s hm

lemma ofList_take_insertIdx_le (n m : â„•) (Ï†1 : ğ“•) (Ï†s : List ğ“•) (hn : m â‰¤ n) (hm : m â‰¤ Ï†s.length) :
    ofList q ((List.insertIdx m Ï†1 Ï†s).take (n + 1)) = q Ï†1 * ofList q (Ï†s.take n) := by
  rw [ofList_insert_lt_eq, ofList_take_succ_cons]
  Â· exact hn
  Â· exact hm

end ofListTake
end FieldStatistic
