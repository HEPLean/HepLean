/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldStatistics.Basic
/-!

# Exchange sign for field statistics

Suppose we have two fields `Ï†` and `Ïˆ`, and the term `Ï†Ïˆ`, if we swap them
`ÏˆÏ†`, we may pick up a sign. This sign is called the exchange sign.
This sign is `-1` if both fields `Ïˆ` and `Ï†` are fermionic and `1` otherwise.

In this module we define the exchange sign for general field statistics,
and prove some properties of it. Importantly:
- It is symmetric `exchangeSign_symm`.
- When multiplied with itself it is `1` `exchangeSign_mul_self`.
- It is a cocycle `exchangeSign_cocycle`.

-/

namespace FieldStatistic

variable {ğ“• : Type}

/-- The exchange sign of two field statistics is defined to be
  `-1` if both field statistics are `fermionic` and `1` otherwise.
  It is a group homomorphism from `FieldStatistic` to the group of homomorphisms from
  `FieldStatistic` to `â„‚`. -/
def exchangeSign : FieldStatistic â†’* FieldStatistic â†’* â„‚ where
  toFun a :=
    {
      toFun := fun b =>
        match a, b with
        | bosonic, _ => 1
        | _, bosonic => 1
        | fermionic, fermionic => -1
      map_one' := by
        fin_cases a
        <;> simp
      map_mul' := fun c b => by
        fin_cases a <;>
          fin_cases b <;>
          fin_cases c <;>
          simp
    }
  map_one' := by
    ext b
    fin_cases b
    <;> simp
  map_mul' c b := by
    ext a
    fin_cases a
    <;> fin_cases b <;> fin_cases c
    <;> simp

/-- The echange sign of two field statistics.
  Defined to be `-1` if both field statistics are `fermionic` and `1` otherwise. -/
scoped[FieldStatistic] notation "ğ“¢(" a "," b ")" => exchangeSign a b

/-- The exchange sign is symmetric. -/
lemma exchangeSign_symm (a b : FieldStatistic) : ğ“¢(a, b) = ğ“¢(b, a) := by
  fin_cases a <;> fin_cases b <;> rfl

@[simp]
lemma exchangeSign_bosonic (a : FieldStatistic) : ğ“¢(a, bosonic) = 1 := by
  fin_cases a <;> rfl

@[simp]
lemma bosonic_exchangeSign (a : FieldStatistic) : ğ“¢(bosonic, a) = 1 := by
  rw [exchangeSign_symm, exchangeSign_bosonic]

lemma exchangeSign_eq_if (a b : FieldStatistic) :
    ğ“¢(a, b) = if a = fermionic âˆ§ b = fermionic then - 1 else 1 := by
  fin_cases a <;> fin_cases b <;> rfl

@[simp]
lemma exchangeSign_mul_self (a b : FieldStatistic) : ğ“¢(a, b) * ğ“¢(a, b) = 1 := by
  fin_cases a <;> fin_cases b <;> simp [exchangeSign]

@[simp]
lemma exchangeSign_mul_self_swap (a b : FieldStatistic) : ğ“¢(a, b) * ğ“¢(b, a) = 1 := by
  fin_cases a <;> fin_cases b <;> simp [exchangeSign]

lemma exchangeSign_ofList_cons (a : FieldStatistic)
    (s : ğ“• â†’ FieldStatistic) (Ï† : ğ“•) (Ï†s : List ğ“•) :
    ğ“¢(a, ofList s (Ï† :: Ï†s)) = ğ“¢(a, s Ï†) * ğ“¢(a, ofList s Ï†s) := by
  rw [ofList_cons_eq_mul, map_mul]

/-- The exchange sign is a cocycle. -/
lemma exchangeSign_cocycle (a b c : FieldStatistic) :
    ğ“¢(a, b * c) * ğ“¢(b, c) = ğ“¢(a, b) * ğ“¢(a * b, c) := by
  fin_cases a <;> fin_cases b <;> fin_cases c <;> simp

end FieldStatistic
