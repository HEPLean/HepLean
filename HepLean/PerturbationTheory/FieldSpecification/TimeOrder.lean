/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldSpecification.CrAnSection
import HepLean.PerturbationTheory.FieldSpecification.NormalOrder
/-!

# Time ordering of states

-/

namespace FieldSpecification
variable {ğ“• : FieldSpecification}

/-!

## Time ordering for states

-/

/-- The time ordering relation on states. We have that `timeOrderRel Ï†0 Ï†1` is true
  if and only if `Ï†1` has a time less-then or equal to `Ï†0`, or `Ï†1` is a negative
  asymptotic state, or `Ï†0` is a positive asymptotic state. -/
def timeOrderRel : ğ“•.FieldOp â†’ ğ“•.FieldOp â†’ Prop
  | FieldOp.outAsymp _, _ => True
  | FieldOp.position Ï†0, FieldOp.position Ï†1 => Ï†1.2 0 â‰¤ Ï†0.2 0
  | FieldOp.position _, FieldOp.inAsymp _ => True
  | FieldOp.position _, FieldOp.outAsymp _ => False
  | FieldOp.inAsymp _, FieldOp.outAsymp _ => False
  | FieldOp.inAsymp _, FieldOp.position _ => False
  | FieldOp.inAsymp _, FieldOp.inAsymp _ => True

/-- The relation `timeOrderRel` is decidable, but not computablly so due to
  `Real.decidableLE`. -/
noncomputable instance : (Ï† Ï†' : ğ“•.FieldOp) â†’ Decidable (timeOrderRel Ï† Ï†')
  | FieldOp.outAsymp _, _ => isTrue True.intro
  | FieldOp.position Ï†0, FieldOp.position Ï†1 => inferInstanceAs (Decidable (Ï†1.2 0 â‰¤ Ï†0.2 0))
  | FieldOp.position _, FieldOp.inAsymp _ => isTrue True.intro
  | FieldOp.position _, FieldOp.outAsymp _ => isFalse (fun a => a)
  | FieldOp.inAsymp _, FieldOp.outAsymp _ => isFalse (fun a => a)
  | FieldOp.inAsymp _, FieldOp.position _ => isFalse (fun a => a)
  | FieldOp.inAsymp _, FieldOp.inAsymp _ => isTrue True.intro

/-- Time ordering is total. -/
instance : IsTotal ğ“•.FieldOp ğ“•.timeOrderRel where
  total a b := by
    cases a <;> cases b <;>
      simp only [or_self, or_false, or_true, timeOrderRel, Fin.isValue, implies_true, imp_self,
        IsEmpty.forall_iff]
    exact LinearOrder.le_total _ _

/-- Time ordering is transitive. -/
instance : IsTrans ğ“•.FieldOp ğ“•.timeOrderRel where
  trans a b c := by
    cases a <;> cases b <;> cases c <;>
      simp only [timeOrderRel, Fin.isValue, implies_true, imp_self, IsEmpty.forall_iff]
    exact fun h1 h2 => Preorder.le_trans _ _ _ h2 h1

noncomputable section

open FieldStatistic
open HepLean.List

/-- Given a list `Ï† :: Ï†s` of states, the (zero-based) position of the state which is
  of maximum time. For example
  - for the list `[Ï†1(t = 4), Ï†2(t = 5), Ï†3(t = 3), Ï†4(t = 5)]` this would return `1`.
  This is defined for a list `Ï† :: Ï†s` instead of `Ï†s` to ensure that such a position exists.
-/
def maxTimeFieldPos (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) : â„• :=
  insertionSortMinPos timeOrderRel Ï† Ï†s

lemma maxTimeFieldPos_lt_length (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    maxTimeFieldPos Ï† Ï†s < (Ï† :: Ï†s).length := by
  simp [maxTimeFieldPos]

/-- Given a list `Ï† :: Ï†s` of states, the left-most state of maximum time, if there are more.
  As an example:
  - for the list `[Ï†1(t = 4), Ï†2(t = 5), Ï†3(t = 3), Ï†4(t = 5)]` this would return `Ï†2(t = 5)`.
  It is the state at the position `maxTimeFieldPos Ï† Ï†s`.
-/
def maxTimeField (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) : ğ“•.FieldOp :=
  insertionSortMin timeOrderRel Ï† Ï†s

/-- Given a list `Ï† :: Ï†s` of states, the list with the left-most state of maximum
  time removed.
  As an example:
  - for the list `[Ï†1(t = 4), Ï†2(t = 5), Ï†3(t = 3), Ï†4(t = 5)]` this would return
    `[Ï†1(t = 4), Ï†3(t = 3), Ï†4(t = 5)]`.
-/
def eraseMaxTimeField (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) : List ğ“•.FieldOp :=
  insertionSortDropMinPos timeOrderRel Ï† Ï†s

@[simp]
lemma eraseMaxTimeField_length (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    (eraseMaxTimeField Ï† Ï†s).length = Ï†s.length := by
  simp [eraseMaxTimeField, insertionSortDropMinPos, eraseIdx_length']

lemma maxTimeFieldPos_lt_eraseMaxTimeField_length_succ (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    maxTimeFieldPos Ï† Ï†s < (eraseMaxTimeField Ï† Ï†s).length.succ := by
  simp only [eraseMaxTimeField_length, Nat.succ_eq_add_one]
  exact maxTimeFieldPos_lt_length Ï† Ï†s

/-- Given a list `Ï† :: Ï†s` of states, the position of the left-most state of maximum
  time as an eement of `Fin (eraseMaxTimeField Ï† Ï†s).length.succ`.
  As an example:
  - for the list `[Ï†1(t = 4), Ï†2(t = 5), Ï†3(t = 3), Ï†4(t = 5)]` this would return `âŸ¨1,...âŸ©`.
-/
def maxTimeFieldPosFin (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    Fin (eraseMaxTimeField Ï† Ï†s).length.succ :=
  insertionSortMinPosFin timeOrderRel Ï† Ï†s

lemma lt_maxTimeFieldPosFin_not_timeOrder (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (i : Fin (eraseMaxTimeField Ï† Ï†s).length)
    (hi : (maxTimeFieldPosFin Ï† Ï†s).succAbove i < maxTimeFieldPosFin Ï† Ï†s) :
    Â¬ timeOrderRel ((eraseMaxTimeField Ï† Ï†s)[i.val]) (maxTimeField Ï† Ï†s) := by
  exact insertionSortMin_lt_mem_insertionSortDropMinPos_of_lt timeOrderRel Ï† Ï†s i hi

lemma timeOrder_maxTimeField (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (i : Fin (eraseMaxTimeField Ï† Ï†s).length) :
    timeOrderRel (maxTimeField Ï† Ï†s) ((eraseMaxTimeField Ï† Ï†s)[i.val]) := by
  exact insertionSortMin_lt_mem_insertionSortDropMinPos timeOrderRel Ï† Ï†s _

/-- The sign associated with putting a list of states into time order (with
  the state of greatest time to the left).
  We pick up a minus sign for every fermion paired crossed. -/
def timeOrderSign (Ï†s : List ğ“•.FieldOp) : â„‚ :=
  Wick.koszulSign ğ“•.fieldOpStatistic ğ“•.timeOrderRel Ï†s

@[simp]
lemma timeOrderSign_nil : timeOrderSign (ğ“• := ğ“•) [] = 1 := by
  simp only [timeOrderSign]
  rfl

lemma timeOrderSign_pair_ordered {Ï† Ïˆ : ğ“•.FieldOp} (h : timeOrderRel Ï† Ïˆ) :
    timeOrderSign [Ï†, Ïˆ] = 1 := by
  simp only [timeOrderSign, Wick.koszulSign, Wick.koszulSignInsert, mul_one, ite_eq_left_iff,
    ite_eq_right_iff, and_imp]
  exact fun h' => False.elim (h' h)

lemma timeOrderSign_pair_not_ordered {Ï† Ïˆ : ğ“•.FieldOp} (h : Â¬ timeOrderRel Ï† Ïˆ) :
    timeOrderSign [Ï†, Ïˆ] = ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ïˆ) := by
  simp only [timeOrderSign, Wick.koszulSign, Wick.koszulSignInsert, mul_one, instCommGroup.eq_1]
  rw [if_neg h]
  simp [FieldStatistic.exchangeSign_eq_if]

lemma timerOrderSign_of_eraseMaxTimeField (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    timeOrderSign (eraseMaxTimeField Ï† Ï†s) = timeOrderSign (Ï† :: Ï†s) *
    ğ“¢(ğ“• |>â‚› maxTimeField Ï† Ï†s, ğ“• |>â‚› (Ï† :: Ï†s).take (maxTimeFieldPos Ï† Ï†s)) := by
  rw [eraseMaxTimeField, insertionSortDropMinPos, timeOrderSign,
    Wick.koszulSign_eraseIdx_insertionSortMinPos]
  rw [â† timeOrderSign, â† maxTimeField]
  rfl

/-- The time ordering of a list of states. A schematic example is:
  - `normalOrderList [Ï†1(t = 4), Ï†2(t = 5), Ï†3(t = 3), Ï†4(t = 5)]` is equal to
    `[Ï†2(t = 5), Ï†4(t = 5), Ï†1(t = 4), Ï†3(t = 3)]` -/
def timeOrderList (Ï†s : List ğ“•.FieldOp) : List ğ“•.FieldOp :=
  List.insertionSort ğ“•.timeOrderRel Ï†s

lemma timeOrderList_pair_ordered {Ï† Ïˆ : ğ“•.FieldOp} (h : timeOrderRel Ï† Ïˆ) :
    timeOrderList [Ï†, Ïˆ] = [Ï†, Ïˆ] := by
  simp only [timeOrderList, List.insertionSort, List.orderedInsert, ite_eq_left_iff,
    List.cons.injEq, and_true]
  exact fun h' => False.elim (h' h)

lemma timeOrderList_pair_not_ordered {Ï† Ïˆ : ğ“•.FieldOp} (h : Â¬ timeOrderRel Ï† Ïˆ) :
    timeOrderList [Ï†, Ïˆ] = [Ïˆ, Ï†] := by
  simp only [timeOrderList, List.insertionSort, List.orderedInsert, ite_eq_right_iff,
    List.cons.injEq, and_true]
  exact fun h' => False.elim (h h')

@[simp]
lemma timeOrderList_nil : timeOrderList (ğ“• := ğ“•) [] = [] := by
  simp [timeOrderList]

lemma timeOrderList_eq_maxTimeField_timeOrderList (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) :
    timeOrderList (Ï† :: Ï†s) = maxTimeField Ï† Ï†s :: timeOrderList (eraseMaxTimeField Ï† Ï†s) := by
  exact insertionSort_eq_insertionSortMin_cons timeOrderRel Ï† Ï†s

/-!

## Time ordering for CrAnFieldOp

-/

/-!

## timeOrderRel

-/

/-- For a field specification `ğ“•`, `ğ“•.crAnTimeOrderRel` is a relation on
  `ğ“•.CrAnFieldOp` representing time ordering.
  It is defined as such that `ğ“•.crAnTimeOrderRel Ï†â‚€ Ï†â‚` is true if and only if one of the following
  holds
- `Ï†â‚€` is an *outgoing* asymptotic operator
- `Ï†â‚` is an *incoming* asymptotic field operator
- `Ï†â‚€` and `Ï†â‚` are both position field operators where
  the `SpaceTime` point of `Ï†â‚€` has a time *greater* then or equal to that of `Ï†â‚`.

Thus, colloquially `ğ“•.crAnTimeOrderRel Ï†â‚€ Ï†â‚` if `Ï†â‚€` has time *greater* then or equal to `Ï†â‚`.
-/
def crAnTimeOrderRel (a b : ğ“•.CrAnFieldOp) : Prop := ğ“•.timeOrderRel a.1 b.1

/-- The relation `crAnTimeOrderRel` is decidable, but not computablly so due to
  `Real.decidableLE`. -/
noncomputable instance (Ï† Ï†' : ğ“•.CrAnFieldOp) : Decidable (crAnTimeOrderRel Ï† Ï†') :=
  inferInstanceAs (Decidable (ğ“•.timeOrderRel Ï†.1 Ï†'.1))

/-- Time ordering of `CrAnFieldOp` is total. -/
instance : IsTotal ğ“•.CrAnFieldOp ğ“•.crAnTimeOrderRel where
  total a b := IsTotal.total (r := ğ“•.timeOrderRel) a.1 b.1

/-- Time ordering of `CrAnFieldOp` is transitive. -/
instance : IsTrans ğ“•.CrAnFieldOp ğ“•.crAnTimeOrderRel where
  trans a b c := IsTrans.trans (r := ğ“•.timeOrderRel) a.1 b.1 c.1

@[simp]
lemma crAnTimeOrderRel_refl (Ï† : ğ“•.CrAnFieldOp) : crAnTimeOrderRel Ï† Ï† := by
  exact (IsTotal.to_isRefl (r := ğ“•.crAnTimeOrderRel)).refl Ï†

/-- For a field specification `ğ“•`, and a list `Ï†s` of `ğ“•.CrAnFieldOp`,
  `ğ“•.crAnTimeOrderSign Ï†s` is the sign corresponding to the number of `ferimionic`-`fermionic`
  undertaken to time-order (i.e. order with respect to `ğ“•.crAnTimeOrderRel`) `Ï†s` using the
  insertion sort algorithm. -/
def crAnTimeOrderSign (Ï†s : List ğ“•.CrAnFieldOp) : â„‚ :=
  Wick.koszulSign ğ“•.crAnStatistics ğ“•.crAnTimeOrderRel Ï†s

@[simp]
lemma crAnTimeOrderSign_nil : crAnTimeOrderSign (ğ“• := ğ“•) [] = 1 := by
  simp only [crAnTimeOrderSign]
  rfl

lemma crAnTimeOrderSign_pair_ordered {Ï† Ïˆ : ğ“•.CrAnFieldOp} (h : crAnTimeOrderRel Ï† Ïˆ) :
    crAnTimeOrderSign [Ï†, Ïˆ] = 1 := by
  simp only [crAnTimeOrderSign, Wick.koszulSign, Wick.koszulSignInsert, mul_one, ite_eq_left_iff,
    ite_eq_right_iff, and_imp]
  exact fun h' => False.elim (h' h)

lemma crAnTimeOrderSign_pair_not_ordered {Ï† Ïˆ : ğ“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï† Ïˆ) :
    crAnTimeOrderSign [Ï†, Ïˆ] = ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› Ïˆ) := by
  simp only [crAnTimeOrderSign, Wick.koszulSign, Wick.koszulSignInsert, mul_one, instCommGroup.eq_1]
  rw [if_neg h]
  simp [FieldStatistic.exchangeSign_eq_if]

lemma crAnTimeOrderSign_swap_eq_time {Ï† Ïˆ : ğ“•.CrAnFieldOp}
    (h1 : crAnTimeOrderRel Ï† Ïˆ) (h2 : crAnTimeOrderRel Ïˆ Ï†) (Ï†s Ï†s' : List ğ“•.CrAnFieldOp) :
    crAnTimeOrderSign (Ï†s ++ Ï† :: Ïˆ :: Ï†s') = crAnTimeOrderSign (Ï†s ++ Ïˆ :: Ï† :: Ï†s') := by
  exact Wick.koszulSign_swap_eq_rel _ _ h1 h2 _ _

/-- For a field specification `ğ“•`, and a list `Ï†s` of `ğ“•.CrAnFieldOp`,
  `ğ“•.crAnTimeOrderList Ï†s` is the list `Ï†s` time-ordered using the insertion sort algorithm. -/
def crAnTimeOrderList (Ï†s : List ğ“•.CrAnFieldOp) : List ğ“•.CrAnFieldOp :=
  List.insertionSort ğ“•.crAnTimeOrderRel Ï†s

@[simp]
lemma crAnTimeOrderList_nil : crAnTimeOrderList (ğ“• := ğ“•) [] = [] := by
  simp [crAnTimeOrderList]

lemma crAnTimeOrderList_pair_ordered {Ï† Ïˆ : ğ“•.CrAnFieldOp} (h : crAnTimeOrderRel Ï† Ïˆ) :
    crAnTimeOrderList [Ï†, Ïˆ] = [Ï†, Ïˆ] := by
  simp only [crAnTimeOrderList, List.insertionSort, List.orderedInsert, ite_eq_left_iff,
    List.cons.injEq, and_true]
  exact fun h' => False.elim (h' h)

lemma crAnTimeOrderList_pair_not_ordered {Ï† Ïˆ : ğ“•.CrAnFieldOp} (h : Â¬ crAnTimeOrderRel Ï† Ïˆ) :
    crAnTimeOrderList [Ï†, Ïˆ] = [Ïˆ, Ï†] := by
  simp only [crAnTimeOrderList, List.insertionSort, List.orderedInsert, ite_eq_right_iff,
    List.cons.injEq, and_true]
  exact fun h' => False.elim (h h')

lemma orderedInsert_swap_eq_time {Ï† Ïˆ : ğ“•.CrAnFieldOp}
    (h1 : crAnTimeOrderRel Ï† Ïˆ) (h2 : crAnTimeOrderRel Ïˆ Ï†) (Ï†s : List ğ“•.CrAnFieldOp) :
    List.orderedInsert crAnTimeOrderRel Ï† (List.orderedInsert crAnTimeOrderRel Ïˆ Ï†s) =
    List.takeWhile (fun b => Â¬ crAnTimeOrderRel Ïˆ b) Ï†s ++ Ï† :: Ïˆ ::
    List.dropWhile (fun b => Â¬ crAnTimeOrderRel Ïˆ b) Ï†s := by
  rw [List.orderedInsert_eq_take_drop crAnTimeOrderRel Ïˆ Ï†s]
  simp only [decide_not]
  rw [List.orderedInsert_eq_take_drop]
  simp only [decide_not]
  have h1 (b : ğ“•.CrAnFieldOp) : (crAnTimeOrderRel Ï† b) â†” (crAnTimeOrderRel Ïˆ b) :=
    Iff.intro (fun h => IsTrans.trans _ _ _ h2 h) (fun h => IsTrans.trans _ _ _ h1 h)
  simp only [h1]
  rw [List.takeWhile_append]
  rw [List.takeWhile_takeWhile]
  simp only [Bool.not_eq_eq_eq_not, Bool.not_true, decide_eq_false_iff_not, and_self, decide_not,
    â†“reduceIte, crAnTimeOrderRel_refl, decide_true, Bool.false_eq_true, not_false_eq_true,
    List.takeWhile_cons_of_neg, List.append_nil, List.append_cancel_left_eq, List.cons.injEq,
    true_and]
  rw [List.dropWhile_append]
  simp only [List.isEmpty_eq_true, List.dropWhile_eq_nil_iff, Bool.not_eq_eq_eq_not, Bool.not_true,
    decide_eq_false_iff_not, crAnTimeOrderRel_refl, decide_true, Bool.false_eq_true,
    not_false_eq_true, List.dropWhile_cons_of_neg, ite_eq_left_iff, not_forall, Classical.not_imp,
    Decidable.not_not, List.append_left_eq_self, forall_exists_index, and_imp]
  intro x hx hxÏˆ
  intro y hy
  simpa using List.mem_takeWhile_imp hy

lemma orderedInsert_in_swap_eq_time {Ï† Ïˆ Ï†': ğ“•.CrAnFieldOp} (h1 : crAnTimeOrderRel Ï† Ïˆ)
    (h2 : crAnTimeOrderRel Ïˆ Ï†) : (Ï†s Ï†s' : List ğ“•.CrAnFieldOp) â†’ âˆƒ l1 l2,
    List.orderedInsert crAnTimeOrderRel Ï†' (Ï†s ++ Ï† :: Ïˆ :: Ï†s') = l1 ++ Ï† :: Ïˆ :: l2 âˆ§
    List.orderedInsert crAnTimeOrderRel Ï†' (Ï†s ++ Ïˆ :: Ï† :: Ï†s') = l1 ++ Ïˆ :: Ï† :: l2
  | [], Ï†s' => by
    have h1 (b : ğ“•.CrAnFieldOp) : (crAnTimeOrderRel b Ï†) â†” (crAnTimeOrderRel b Ïˆ) :=
      Iff.intro (fun h => IsTrans.trans _ _ _ h h1) (fun h => IsTrans.trans _ _ _ h h2)
    by_cases h : crAnTimeOrderRel Ï†' Ï†
    Â· simp only [List.orderedInsert, h, â†“reduceIte, â† h1 Ï†']
      use [Ï†'], Ï†s'
      simp
    Â· simp only [List.orderedInsert, h, â†“reduceIte, â† h1 Ï†']
      use [], List.orderedInsert crAnTimeOrderRel Ï†' Ï†s'
      simp
  | Ï†'' :: Ï†s, Ï†s' => by
    obtain âŸ¨l1, l2, hlâŸ© := orderedInsert_in_swap_eq_time (Ï†' := Ï†') h1 h2 Ï†s Ï†s'
    simp only [List.orderedInsert, List.append_eq]
    rw [hl.1, hl.2]
    by_cases h : crAnTimeOrderRel Ï†' Ï†''
    Â· simp only [h, â†“reduceIte]
      use (Ï†' :: Ï†'' :: Ï†s), Ï†s'
      simp
    Â· simp only [h, â†“reduceIte]
      use (Ï†'' :: l1), l2
      simp

lemma crAnTimeOrderList_swap_eq_time {Ï† Ïˆ : ğ“•.CrAnFieldOp}
    (h1 : crAnTimeOrderRel Ï† Ïˆ) (h2 : crAnTimeOrderRel Ïˆ Ï†) :
    (Ï†s Ï†s' : List ğ“•.CrAnFieldOp) â†’
    âˆƒ (l1 l2 : List ğ“•.CrAnFieldOp),
      crAnTimeOrderList (Ï†s ++ Ï† :: Ïˆ :: Ï†s') = l1 ++ Ï† :: Ïˆ :: l2 âˆ§
      crAnTimeOrderList (Ï†s ++ Ïˆ :: Ï† :: Ï†s') = l1 ++ Ïˆ :: Ï† :: l2
  | [], Ï†s' => by
    simp only [crAnTimeOrderList]
    simp only [List.insertionSort]
    use List.takeWhile (fun b => Â¬ crAnTimeOrderRel Ïˆ b) (List.insertionSort crAnTimeOrderRel Ï†s'),
      List.dropWhile (fun b => Â¬ crAnTimeOrderRel Ïˆ b) (List.insertionSort crAnTimeOrderRel Ï†s')
    apply And.intro
    Â· exact orderedInsert_swap_eq_time h1 h2 _
    Â· have h1' (b : ğ“•.CrAnFieldOp) : (crAnTimeOrderRel Ï† b) â†” (crAnTimeOrderRel Ïˆ b) :=
        Iff.intro (fun h => IsTrans.trans _ _ _ h2 h) (fun h => IsTrans.trans _ _ _ h1 h)
      simp only [â† h1', decide_not]
      simpa using orderedInsert_swap_eq_time h2 h1 _
  | Ï†'' :: Ï†s, Ï†s' => by
    rw [crAnTimeOrderList, crAnTimeOrderList]
    simp only [List.insertionSort, List.append_eq]
    obtain âŸ¨l1, l2, hlâŸ© := crAnTimeOrderList_swap_eq_time h1 h2 Ï†s Ï†s'
    simp only [crAnTimeOrderList] at hl
    rw [hl.1, hl.2]
    obtain âŸ¨l1', l2', hl'âŸ© := orderedInsert_in_swap_eq_time (Ï†' := Ï†'') h1 h2 l1 l2
    rw [hl'.1, hl'.2]
    use l1', l2'

/-!

## Relationship to sections
-/

lemma koszulSignInsert_crAnTimeOrderRel_crAnSection {Ï† : ğ“•.FieldOp} {Ïˆ : ğ“•.CrAnFieldOp}
    (h : Ïˆ.1 = Ï†) : {Ï†s : List ğ“•.FieldOp} â†’ (Ïˆs : CrAnSection Ï†s) â†’
    Wick.koszulSignInsert ğ“•.crAnStatistics ğ“•.crAnTimeOrderRel Ïˆ Ïˆs.1 =
    Wick.koszulSignInsert ğ“•.fieldOpStatistic ğ“•.timeOrderRel Ï† Ï†s
  | [], âŸ¨[], hâŸ© => by
    simp [Wick.koszulSignInsert]
  | Ï†' :: Ï†s, âŸ¨Ïˆ' :: Ïˆs, h1âŸ© => by
    simp only [Wick.koszulSignInsert, crAnTimeOrderRel, h]
    simp only [List.map_cons, List.cons.injEq] at h1
    have hi := koszulSignInsert_crAnTimeOrderRel_crAnSection h (Ï†s := Ï†s) âŸ¨Ïˆs, h1.2âŸ©
    rw [hi]
    congr
    Â· exact h1.1
    Â· simp only [crAnStatistics, crAnFieldOpToFieldOp, Function.comp_apply]
      congr
    Â· simp only [crAnStatistics, crAnFieldOpToFieldOp, Function.comp_apply]
      congr
      exact h1.1

@[simp]
lemma crAnTimeOrderSign_crAnSection : {Ï†s : List ğ“•.FieldOp} â†’ (Ïˆs : CrAnSection Ï†s) â†’
    crAnTimeOrderSign Ïˆs.1 = timeOrderSign Ï†s
  | [], âŸ¨[], hâŸ© => by
    simp
  | Ï† :: Ï†s, âŸ¨Ïˆ :: Ïˆs, hâŸ© => by
    simp only [crAnTimeOrderSign, Wick.koszulSign, timeOrderSign]
    simp only [List.map_cons, List.cons.injEq] at h
    congr 1
    Â· rw [koszulSignInsert_crAnTimeOrderRel_crAnSection h.1 âŸ¨Ïˆs, h.2âŸ©]
    Â· exact crAnTimeOrderSign_crAnSection âŸ¨Ïˆs, h.2âŸ©

lemma orderedInsert_crAnTimeOrderRel_crAnSection {Ï† : ğ“•.FieldOp} {Ïˆ : ğ“•.CrAnFieldOp}
    (h : Ïˆ.1 = Ï†) : {Ï†s : List ğ“•.FieldOp} â†’ (Ïˆs : CrAnSection Ï†s) â†’
    (List.orderedInsert ğ“•.crAnTimeOrderRel Ïˆ Ïˆs.1).map ğ“•.crAnFieldOpToFieldOp =
    List.orderedInsert ğ“•.timeOrderRel Ï† Ï†s
  | [], âŸ¨[], _âŸ© => by
    simp only [List.orderedInsert, List.map_cons, List.map_nil, List.cons.injEq, and_true]
    exact h
  | Ï†' :: Ï†s, âŸ¨Ïˆ' :: Ïˆs, h1âŸ© => by
    simp only [List.orderedInsert, crAnTimeOrderRel, h]
    simp only [List.map_cons, List.cons.injEq] at h1
    by_cases hr : timeOrderRel Ï† Ï†'
    Â· simp only [hr, â†“reduceIte]
      rw [â† h1.1] at hr
      simp only [crAnFieldOpToFieldOp] at hr
      simp only [hr, â†“reduceIte, List.map_cons, List.cons.injEq]
      exact And.intro h (And.intro h1.1 h1.2)
    Â· simp only [hr, â†“reduceIte]
      rw [â† h1.1] at hr
      simp only [crAnFieldOpToFieldOp] at hr
      simp only [hr, â†“reduceIte, List.map_cons, List.cons.injEq]
      apply And.intro h1.1
      exact orderedInsert_crAnTimeOrderRel_crAnSection h âŸ¨Ïˆs, h1.2âŸ©

lemma crAnTimeOrderList_crAnSection_is_crAnSection : {Ï†s : List ğ“•.FieldOp} â†’ (Ïˆs : CrAnSection Ï†s) â†’
    (crAnTimeOrderList Ïˆs.1).map ğ“•.crAnFieldOpToFieldOp = timeOrderList Ï†s
  | [], âŸ¨[], hâŸ© => by
    simp
  | Ï† :: Ï†s, âŸ¨Ïˆ :: Ïˆs, hâŸ© => by
    simp only [crAnTimeOrderList, List.insertionSort, timeOrderList]
    simp only [List.map_cons, List.cons.injEq] at h
    exact orderedInsert_crAnTimeOrderRel_crAnSection h.1 âŸ¨(List.insertionSort crAnTimeOrderRel Ïˆs),
      crAnTimeOrderList_crAnSection_is_crAnSection âŸ¨Ïˆs, h.2âŸ©âŸ©

/-- Time ordering of sections of a list of states. -/
def crAnSectionTimeOrder (Ï†s : List ğ“•.FieldOp) (Ïˆs : CrAnSection Ï†s) :
    CrAnSection (timeOrderList Ï†s) :=
  âŸ¨crAnTimeOrderList Ïˆs.1, crAnTimeOrderList_crAnSection_is_crAnSection ÏˆsâŸ©

lemma orderedInsert_crAnTimeOrderRel_injective {Ïˆ Ïˆ' : ğ“•.CrAnFieldOp} (h : Ïˆ.1 = Ïˆ'.1) :
    {Ï†s : List ğ“•.FieldOp} â†’ (Ïˆs Ïˆs' : ğ“•.CrAnSection Ï†s) â†’
    (ho : List.orderedInsert crAnTimeOrderRel Ïˆ Ïˆs.1 =
    List.orderedInsert crAnTimeOrderRel Ïˆ' Ïˆs'.1) â†’ Ïˆ = Ïˆ' âˆ§ Ïˆs = Ïˆs'
  | [], âŸ¨[], _âŸ©, âŸ¨[], _âŸ©, h => by
    simp only [List.orderedInsert, List.cons.injEq, and_true] at h
    simpa using h
  | Ï† :: Ï†s, âŸ¨Ïˆ1 :: Ïˆs, h1âŸ©, âŸ¨Ïˆ1' :: Ïˆs', h1'âŸ©, ho => by
    simp only [List.map_cons, List.cons.injEq] at h1 h1'
    have ih := orderedInsert_crAnTimeOrderRel_injective h âŸ¨Ïˆs, h1.2âŸ© âŸ¨Ïˆs', h1'.2âŸ©
    simp only [List.orderedInsert] at ho
    by_cases hr : crAnTimeOrderRel Ïˆ Ïˆ1
    Â· simp_all only [ite_true]
      by_cases hr2 : crAnTimeOrderRel Ïˆ' Ïˆ1'
      Â· simp_all
      Â· simp only [crAnTimeOrderRel] at hr hr2
        simp_all only
        rw [crAnFieldOpToFieldOp] at h1 h1'
        rw [h1.1] at hr
        rw [h1'.1] at hr2
        exact False.elim (hr2 hr)
    Â· simp_all only [ite_false]
      by_cases hr2 : crAnTimeOrderRel Ïˆ' Ïˆ1'
      Â· simp only [crAnTimeOrderRel] at hr hr2
        simp_all only
        rw [crAnFieldOpToFieldOp] at h1 h1'
        rw [h1.1] at hr
        rw [h1'.1] at hr2
        exact False.elim (hr hr2)
      Â· simp only [hr2, â†“reduceIte, List.cons.injEq] at ho
        have ih' := ih ho.2
        simp_all only [and_self, implies_true, not_false_eq_true, true_and]
        apply Subtype.ext
        simp only [List.cons.injEq, true_and]
        rw [Subtype.eq_iff] at ih'
        exact ih'.2

lemma crAnSectionTimeOrder_injective : {Ï†s : List ğ“•.FieldOp} â†’
    Function.Injective (ğ“•.crAnSectionTimeOrder Ï†s)
  | [], âŸ¨[], _âŸ©, âŸ¨[], _âŸ© => by
    simp
  | Ï† :: Ï†s, âŸ¨Ïˆ :: Ïˆs, hâŸ©, âŸ¨Ïˆ' :: Ïˆs', h'âŸ© => by
    intro h1
    apply Subtype.ext
    simp only [List.cons.injEq]
    simp only [crAnSectionTimeOrder] at h1
    rw [Subtype.eq_iff] at h1
    simp only [crAnTimeOrderList, List.insertionSort] at h1
    simp only [List.map_cons, List.cons.injEq] at h h'
    rw [crAnFieldOpToFieldOp] at h h'
    have hin := orderedInsert_crAnTimeOrderRel_injective (by rw [h.1, h'.1])
      (ğ“•.crAnSectionTimeOrder Ï†s âŸ¨Ïˆs, h.2âŸ©)
      (ğ“•.crAnSectionTimeOrder Ï†s âŸ¨Ïˆs', h'.2âŸ©) h1
    apply And.intro hin.1
    have hl := crAnSectionTimeOrder_injective hin.2
    rw [Subtype.ext_iff] at hl
    simpa using hl

lemma crAnSectionTimeOrder_bijective (Ï†s : List ğ“•.FieldOp) :
    Function.Bijective (ğ“•.crAnSectionTimeOrder Ï†s) := by
  rw [Fintype.bijective_iff_injective_and_card]
  apply And.intro crAnSectionTimeOrder_injective
  apply CrAnSection.card_perm_eq
  simp only [timeOrderList]
  exact List.Perm.symm (List.perm_insertionSort timeOrderRel Ï†s)

lemma sum_crAnSections_timeOrder {Ï†s : List ğ“•.FieldOp} [AddCommMonoid M]
    (f : CrAnSection (timeOrderList Ï†s) â†’ M) : âˆ‘ s, f s = âˆ‘ s, f (ğ“•.crAnSectionTimeOrder Ï†s s) := by
  erw [(Equiv.ofBijective _ (ğ“•.crAnSectionTimeOrder_bijective Ï†s)).sum_comp]

/-!

## normTimeOrderRel

-/

/-- The time ordering relation on `CrAnFieldOp` such that if two CrAnFieldOp have the same
  time, we normal order them. -/
def normTimeOrderRel (a b : ğ“•.CrAnFieldOp) : Prop :=
  crAnTimeOrderRel a b âˆ§ (crAnTimeOrderRel b a â†’ normalOrderRel a b)

/-- The relation `normTimeOrderRel` is decidable, but not computablly so due to
  `Real.decidableLE`. -/
noncomputable instance (Ï† Ï†' : ğ“•.CrAnFieldOp) : Decidable (normTimeOrderRel Ï† Ï†') :=
  instDecidableAnd

/-- Norm-Time ordering of `CrAnFieldOp` is total. -/
instance : IsTotal ğ“•.CrAnFieldOp ğ“•.normTimeOrderRel where
  total a b := by
    simp only [normTimeOrderRel]
    match IsTotal.total (r := ğ“•.crAnTimeOrderRel) a b,
      IsTotal.total (r := ğ“•.normalOrderRel) a b with
    | Or.inl h1, Or.inl h2 => simp [h1, h2]
    | Or.inr h1, Or.inl h2 =>
      simp only [h1, h2, imp_self, and_true, true_and]
      by_cases hn : crAnTimeOrderRel a b
      Â· simp [hn]
      Â· simp [hn]
    | Or.inl h1, Or.inr h2 =>
      simp only [h1, true_and, h2, imp_self, and_true]
      by_cases hn : crAnTimeOrderRel b a
      Â· simp [hn]
      Â· simp [hn]
    | Or.inr h1, Or.inr h2 => simp [h1, h2]

/-- Norm-Time ordering of `CrAnFieldOp` is transitive. -/
instance : IsTrans ğ“•.CrAnFieldOp ğ“•.normTimeOrderRel where
  trans a b c := by
    intro h1 h2
    simp_all only [normTimeOrderRel]
    apply And.intro
    Â· exact IsTrans.trans _ _ _ h1.1 h2.1
    Â· intro hc
      refine IsTrans.trans _ _ _ (h1.2 ?_) (h2.2 ?_)
      Â· exact IsTrans.trans _ _ _ h2.1 hc
      Â· exact IsTrans.trans _ _ _ hc h1.1

/-- The sign associated with putting a list of `CrAnFieldOp` into normal-time order (with
  the state of greatest time to the left).
  We pick up a minus sign for every fermion paired crossed. -/
def normTimeOrderSign (Ï†s : List ğ“•.CrAnFieldOp) : â„‚ :=
  Wick.koszulSign ğ“•.crAnStatistics ğ“•.normTimeOrderRel Ï†s

/-- Sort a list of `CrAnFieldOp` based on `normTimeOrderRel`. -/
def normTimeOrderList (Ï†s : List ğ“•.CrAnFieldOp) : List ğ“•.CrAnFieldOp :=
  List.insertionSort ğ“•.normTimeOrderRel Ï†s

end
end FieldSpecification
