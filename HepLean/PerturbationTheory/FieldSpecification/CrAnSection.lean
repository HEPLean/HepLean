/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import HepLean.PerturbationTheory.FieldSpecification.CrAnFieldOp
/-!

# Creation and annihilation sections

In the module
`HepLean.PerturbationTheory.FieldSpecification.Basic`
we defined states for a field specification, and in the module
`HepLean.PerturbationTheory.FieldStatistics.CrAnFieldOp`
we defined a refinement of states called `CrAnFieldOp` which distinquishes between the
creation and annihilation components of states.
There exists, in particular, a map from `CrAnFieldOp` to `FieldOp` called `crAnFieldOpToFieldOp`.

Given a list of `FieldOp`, `Ï†s`, in this module we define a section of `Ï†s` to be a list of
`CrAnFieldOp`, `Ïˆs`, such that under the map `crAnFieldOpToFieldOp`, `Ïˆs` is mapped to `Ï†s`.
That is to say, the states underlying `Ïˆs` are the states in `Ï†s`.
We denote these sections as `CrAnSection Ï†s`.

Looking forward the main consequence of this definition is the lemma
`FieldSpecification.FieldOpFreeAlgebra.ofFieldOpListF_sum`.

In this module we define various properties of `CrAnSection`.

-/

namespace FieldSpecification
variable {ğ“• : FieldSpecification}

/-- The sections in `ğ“•.CrAnFieldOp` over a list `Ï†s : List ğ“•.FieldOp`.
  In terms of physics, given some fields `Ï†â‚...Ï†â‚™`, the different ways one can associate
  each field as a `creation` or an `annilation` operator. E.g. the number of terms
  `Ï†â‚â°Ï†â‚‚Â¹...Ï†â‚™â°` `Ï†â‚Â¹Ï†â‚‚Â¹...Ï†â‚™â°` etc. If some fields are exclusively creation or annihilation
  operators at this point (e.g. asymptotic states) this is accounted for. -/
def CrAnSection (Ï†s : List ğ“•.FieldOp) : Type :=
  {Ïˆs : List ğ“•.CrAnFieldOp // Ïˆs.map ğ“•.crAnFieldOpToFieldOp = Ï†s}
  -- Î  i, ğ“•.fieldOpToCreateAnnihilateType (Ï†s.get i)

namespace CrAnSection
open FieldStatistic
variable {ğ“• : FieldSpecification} {Ï†s : List ğ“•.FieldOp}

@[simp]
lemma length_eq (Ïˆs : CrAnSection Ï†s) : Ïˆs.1.length = Ï†s.length := by
  simpa using congrArg List.length Ïˆs.2

/-- The tail of a section for `Ï†s`. -/
def tail : {Ï†s : List ğ“•.FieldOp} â†’ (Ïˆs : CrAnSection Ï†s) â†’ CrAnSection Ï†s.tail
  | [], âŸ¨[], hâŸ© => âŸ¨[], hâŸ©
  | Ï† :: Ï†s, âŸ¨[], hâŸ© => False.elim (by simp at h)
  | Ï† :: Ï†s, âŸ¨Ïˆ :: Ïˆs, hâŸ© => âŸ¨Ïˆs, by rw [List.map_cons, List.cons.injEq] at h; exact h.2âŸ©

lemma head_state_eq {Ï† : ğ“•.FieldOp} : (Ïˆs : CrAnSection (Ï† :: Ï†s)) â†’
    (Ïˆs.1.head (by simp [â† List.length_pos_iff_ne_nil])).1 = Ï†
  | âŸ¨[], hâŸ© => False.elim (by simp at h)
  | âŸ¨Ïˆ :: Ïˆs, hâŸ© => by
    simp only [List.map_cons, List.cons.injEq] at h
    exact h.1

lemma statistics_eq_state_statistics (Ïˆs : CrAnSection Ï†s) :
    (ğ“• |>â‚› Ïˆs.1) = ğ“• |>â‚› Ï†s := by
  erw [FieldStatistic.ofList_eq_prod, FieldStatistic.ofList_eq_prod, crAnStatistics]
  rw [â† List.map_comp_map, Function.comp_apply, Ïˆs.2]

lemma take_statistics_eq_take_state_statistics (Ïˆs : CrAnSection Ï†s) n :
    (ğ“• |>â‚› (Ïˆs.1.take n)) = ğ“• |>â‚› (Ï†s.take n) := by
  erw [FieldStatistic.ofList_eq_prod, FieldStatistic.ofList_eq_prod, crAnStatistics]
  simp only [instCommGroup, List.map_take]
  rw [â† List.map_comp_map, Function.comp_apply, Ïˆs.2]

/-- The head of a section for `Ï† :: Ï†s` as an element in `ğ“•.fieldOpToCreateAnnihilateType Ï†`. -/
def head : {Ï† : ğ“•.FieldOp} â†’ (Ïˆs : CrAnSection (Ï† :: Ï†s)) â†’
    ğ“•.fieldOpToCrAnType Ï†
  | Ï†, âŸ¨[], hâŸ© => False.elim (by simp at h)
  | Ï†, âŸ¨Ïˆ :: Ïˆs, hâŸ© => ğ“•.fieldOpToCreateAnnihilateTypeCongr (by
    simpa using head_state_eq âŸ¨Ïˆ :: Ïˆs, hâŸ©) Ïˆ.2

lemma eq_head_cons_tail {Ï† : ğ“•.FieldOp} {Ïˆs : CrAnSection (Ï† :: Ï†s)} :
    Ïˆs.1 = âŸ¨Ï†, head ÏˆsâŸ© :: Ïˆs.tail.1 := by
  match Ïˆs with
  | âŸ¨[], hâŸ© => exact False.elim (by simp at h)
  | âŸ¨Ïˆ :: Ïˆs, hâŸ© =>
    have h2 := head_state_eq âŸ¨Ïˆ :: Ïˆs, hâŸ©
    simp only [List.head_cons] at h2
    subst h2
    rfl

/-- The creation of a section from for `Ï† : Ï†s` from a section for `Ï†s` and a
  element of `ğ“•.fieldOpToCreateAnnihilateType Ï†`. -/
def cons {Ï† : ğ“•.FieldOp} (Ïˆ : ğ“•.fieldOpToCrAnType Ï†) (Ïˆs : CrAnSection Ï†s) :
    CrAnSection (Ï† :: Ï†s) := âŸ¨âŸ¨Ï†, ÏˆâŸ© :: Ïˆs.1, by
  simp [List.map_cons, Ïˆs.2]âŸ©

/-- For the empty list of states there is only one `CrAnSection`. Corresponding to the
  empty list of `CrAnFieldOp`. -/
def nilEquiv : CrAnSection (ğ“• := ğ“•) [] â‰ƒ Unit where
  toFun _ := ()
  invFun _ := âŸ¨[], rflâŸ©
  left_inv Ïˆs := Subtype.ext <| by
    have h2 := Ïˆs.2
    simp only [List.map_eq_nil_iff] at h2
    simp [h2]
  right_inv _ := by
    simp

/-- The creation and annihilation sections for a singleton list is given by
  a choice of `ğ“•.fieldOpToCreateAnnihilateType Ï†`. If `Ï†` is a asymptotic state
  there is no choice here, else there are two choices. -/
def singletonEquiv {Ï† : ğ“•.FieldOp} : CrAnSection [Ï†] â‰ƒ
    ğ“•.fieldOpToCrAnType Ï† where
  toFun Ïˆs := Ïˆs.head
  invFun Ïˆ := âŸ¨[âŸ¨Ï†, ÏˆâŸ©], by simpâŸ©
  left_inv Ïˆs := by
    apply Subtype.ext
    simp only
    have h1 := eq_head_cons_tail (Ïˆs := Ïˆs)
    rw [h1]
    have h2 := Ïˆs.tail.2
    simp only [List.tail_cons, List.map_eq_nil_iff] at h2
    simp [h2]
  right_inv Ïˆ := by
    simp only [head]
    rfl

/-- An equivalence separating the head of a creation and annihilation section
  from the tail. -/
def consEquiv {Ï† : ğ“•.FieldOp} {Ï†s : List ğ“•.FieldOp} : CrAnSection (Ï† :: Ï†s) â‰ƒ
    ğ“•.fieldOpToCrAnType Ï† Ã— CrAnSection Ï†s where
  toFun Ïˆs := âŸ¨Ïˆs.head, Ïˆs.tailâŸ©
  invFun ÏˆÏˆs :=
    match ÏˆÏˆs with
    | (Ïˆ, Ïˆs) => cons Ïˆ Ïˆs
  left_inv Ïˆs := by
    apply Subtype.ext
    exact Eq.symm eq_head_cons_tail
  right_inv ÏˆÏˆs := by
    match ÏˆÏˆs with
    | (Ïˆ, Ïˆs) => rfl

/-- The instance of a finite type on `CrAnSection`s defined recursively through
  `consEquiv`. -/
instance fintype : (Ï†s : List ğ“•.FieldOp) â†’ Fintype (CrAnSection Ï†s)
  | [] => Fintype.ofEquiv _ nilEquiv.symm
  | _ :: Ï†s =>
    haveI : Fintype (CrAnSection Ï†s) := fintype Ï†s
    Fintype.ofEquiv _ consEquiv.symm

@[simp]
lemma card_nil_eq : Fintype.card (CrAnSection (ğ“• := ğ“•) []) = 1 := by
  rw [Fintype.ofEquiv_card nilEquiv.symm]
  simp

lemma card_cons_eq {Ï† : ğ“•.FieldOp} {Ï†s : List ğ“•.FieldOp} :
    Fintype.card (CrAnSection (Ï† :: Ï†s)) = Fintype.card (ğ“•.fieldOpToCrAnType Ï†) *
    Fintype.card (CrAnSection Ï†s) := by
  rw [Fintype.ofEquiv_card consEquiv.symm]
  simp

lemma card_eq_mul : {Ï†s : List ğ“•.FieldOp} â†’ Fintype.card (CrAnSection Ï†s) =
    2 ^ (List.countP ğ“•.statesIsPosition Ï†s)
  | [] => by
    simp
  | FieldOp.position _ :: Ï†s => by
      simp only [statesIsPosition, List.countP_cons_of_pos]
      rw [card_cons_eq]
      rw [card_eq_mul]
      simp only [fieldOpToCrAnType, CreateAnnihilate.CreateAnnihilate_card_eq_two]
      ring
  | FieldOp.inAsymp x_ :: Ï†s => by
      simp only [statesIsPosition, Bool.false_eq_true, not_false_eq_true, List.countP_cons_of_neg]
      rw [card_cons_eq]
      rw [card_eq_mul]
      simp [fieldOpToCrAnType]
  | FieldOp.outAsymp _ :: Ï†s => by
      simp only [statesIsPosition, Bool.false_eq_true, not_false_eq_true, List.countP_cons_of_neg]
      rw [card_cons_eq]
      rw [card_eq_mul]
      simp [fieldOpToCrAnType]

lemma card_perm_eq {Ï†s Ï†s' : List ğ“•.FieldOp} (h : Ï†s.Perm Ï†s') :
    Fintype.card (CrAnSection Ï†s) = Fintype.card (CrAnSection Ï†s') := by
  rw [card_eq_mul, card_eq_mul]
  congr 1
  exact List.Perm.countP_congr h fun x => congrFun rfl

@[simp]
lemma sum_nil (f : CrAnSection (ğ“• := ğ“•) [] â†’ M) [AddCommMonoid M] :
    âˆ‘ (s : CrAnSection []), f s = f âŸ¨[], rflâŸ© := by
  rw [â† nilEquiv.symm.sum_comp]
  simp only [Finset.univ_unique, PUnit.default_eq_unit, Finset.sum_singleton]
  rfl

lemma sum_cons (f : CrAnSection (Ï† :: Ï†s) â†’ M) [AddCommMonoid M] :
    âˆ‘ (s : CrAnSection (Ï† :: Ï†s)), f s = âˆ‘ (a : ğ“•.fieldOpToCrAnType Ï†),
    âˆ‘ (s : CrAnSection Ï†s), f (cons a s) := by
  rw [â† consEquiv.symm.sum_comp, Fintype.sum_prod_type]
  rfl

lemma sum_over_length {s : CrAnSection Ï†s} (f : Fin s.1.length â†’ M)
    [AddCommMonoid M] : âˆ‘ (n : Fin s.1.length), f n =
    âˆ‘ (n : Fin Ï†s.length), f (Fin.cast (length_eq s).symm n) := by
  rw [â† (finCongr (length_eq s)).sum_comp]
  rfl

/-- The equivalence between `CrAnSection Ï†s` and
  `CrAnSection Ï†s'` induced by an equality `Ï†s = Ï†s'`. -/
def congr : {Ï†s Ï†s' : List ğ“•.FieldOp} â†’ (h : Ï†s = Ï†s') â†’
    CrAnSection Ï†s â‰ƒ CrAnSection Ï†s'
  | _, _, rfl => Equiv.refl _

@[simp]
lemma congr_fst {Ï†s Ï†s' : List ğ“•.FieldOp} (h : Ï†s = Ï†s') (Ïˆs : CrAnSection Ï†s) :
    (congr h Ïˆs).1 = Ïˆs.1 := by
  cases h
  rfl

@[simp]
lemma congr_symm {Ï†s Ï†s' : List ğ“•.FieldOp} (h : Ï†s = Ï†s') :
    (congr h).symm = congr h.symm := by
  cases h
  rfl

@[simp]
lemma congr_trans_apply {Ï†s Ï†s' Ï†s'' : List ğ“•.FieldOp} (h1 : Ï†s = Ï†s') (h2 : Ï†s' = Ï†s'')
    (Ïˆs : CrAnSection Ï†s) :
    (congr h2 (congr h1 Ïˆs)) = congr (by rw [h1, h2]) Ïˆs := by
  subst h1 h2
  rfl

/-- Returns the first `n` elements of a section and its underlying list. -/
def take (n : â„•) (Ïˆs : CrAnSection Ï†s) : CrAnSection (Ï†s.take n) :=
  âŸ¨Ïˆs.1.take n, by simp [Ïˆs.2]âŸ©

@[simp]
lemma take_congr {Ï†s Ï†s' : List ğ“•.FieldOp} (h : Ï†s = Ï†s') (n : â„•)
    (Ïˆs : CrAnSection Ï†s) :
    (take n (congr h Ïˆs)) = congr (by rw [h]) (take n Ïˆs) := by
  subst h
  rfl

/-- Removes the first `n` elements of a section and its underlying list. -/
def drop (n : â„•) (Ïˆs : CrAnSection Ï†s) : CrAnSection (Ï†s.drop n) :=
  âŸ¨Ïˆs.1.drop n, by simp [Ïˆs.2]âŸ©

@[simp]
lemma drop_congr {Ï†s Ï†s' : List ğ“•.FieldOp} (h : Ï†s = Ï†s') (n : â„•)
    (Ïˆs : CrAnSection Ï†s) :
    (drop n (congr h Ïˆs)) = congr (by rw [h]) (drop n Ïˆs) := by
  subst h
  rfl

/-- Appends two sections and their underlying lists. -/
def append {Ï†s Ï†s' : List ğ“•.FieldOp} (Ïˆs : CrAnSection Ï†s)
    (Ïˆs' : CrAnSection Ï†s') : CrAnSection (Ï†s ++ Ï†s') :=
  âŸ¨Ïˆs.1 ++ Ïˆs'.1, by simp [Ïˆs.2, Ïˆs'.2]âŸ©

lemma append_assoc {Ï†s Ï†s' Ï†s'' : List ğ“•.FieldOp} (Ïˆs : CrAnSection Ï†s)
    (Ïˆs' : CrAnSection Ï†s') (Ïˆs'' : CrAnSection Ï†s'') :
    append Ïˆs (append Ïˆs' Ïˆs'') = congr (by simp) (append (append Ïˆs Ïˆs') Ïˆs'') := by
  apply Subtype.ext
  simp [append]

lemma append_assoc' {Ï†s Ï†s' Ï†s'' : List ğ“•.FieldOp} (Ïˆs : CrAnSection Ï†s)
    (Ïˆs' : CrAnSection Ï†s') (Ïˆs'' : CrAnSection Ï†s'') :
    (append (append Ïˆs Ïˆs') Ïˆs'') = congr (by simp) (append Ïˆs (append Ïˆs' Ïˆs'')) := by
  apply Subtype.ext
  simp [append]

lemma singletonEquiv_append_eq_cons {Ï†s : List ğ“•.FieldOp} {Ï† : ğ“•.FieldOp}
    (Ïˆs : CrAnSection Ï†s) (Ïˆ : ğ“•.fieldOpToCrAnType Ï†) :
    append (singletonEquiv.symm Ïˆ) Ïˆs = cons Ïˆ Ïˆs := by
  apply Subtype.ext
  simp [append, cons, singletonEquiv]

@[simp]
lemma take_append_drop {n : â„•} (Ïˆs : CrAnSection Ï†s) :
    append (take n Ïˆs) (drop n Ïˆs) = congr (List.take_append_drop n Ï†s).symm Ïˆs := by
  apply Subtype.ext
  simp [take, drop, append]

lemma congr_append {Ï†s1 Ï†s1' Ï†s2 Ï†s2' : List ğ“•.FieldOp} (h1 : Ï†s1 = Ï†s1') (h2 : Ï†s2 = Ï†s2')
    (Ïˆs1 : CrAnSection Ï†s1) (Ïˆs2 : CrAnSection Ï†s2) :
    (append (congr h1 Ïˆs1) (congr h2 Ïˆs2)) = congr (by rw [h1, h2]) (append Ïˆs1 Ïˆs2) := by
  subst h1 h2
  rfl

@[simp]
lemma congr_fst_append {Ï†s1 Ï†s1' Ï†s2 : List ğ“•.FieldOp} (h1 : Ï†s1 = Ï†s1')
    (Ïˆs1 : CrAnSection Ï†s1) (Ïˆs2 : CrAnSection Ï†s2) :
    (append (congr h1 Ïˆs1) (Ïˆs2)) = congr (by rw [h1]) (append Ïˆs1 Ïˆs2) := by
  subst h1
  rfl

@[simp]
lemma congr_snd_append {Ï†s1 Ï†s2 Ï†s2' : List ğ“•.FieldOp} (h2 : Ï†s2 = Ï†s2')
    (Ïˆs1 : CrAnSection Ï†s1) (Ïˆs2 : CrAnSection Ï†s2) :
    (append Ïˆs1 (congr h2 Ïˆs2)) = congr (by rw [h2]) (append Ïˆs1 Ïˆs2) := by
  subst h2
  rfl

@[simp]
lemma take_left {Ï†s Ï†s' : List ğ“•.FieldOp} (Ïˆs : CrAnSection Ï†s)
    (Ïˆs' : CrAnSection Ï†s') :
    take Ï†s.length (Ïˆs.append Ïˆs') = congr (by simp) Ïˆs := by
  apply Subtype.ext
  simp [take, append]

@[simp]
lemma drop_left {Ï†s Ï†s' : List ğ“•.FieldOp} (Ïˆs : CrAnSection Ï†s)
    (Ïˆs' : CrAnSection Ï†s') :
    drop Ï†s.length (Ïˆs.append Ïˆs') = congr (by simp) Ïˆs' := by
  apply Subtype.ext
  simp [drop, append]

/-- The equivalence between `CrAnSection (Ï†s ++ Ï†s')` and
  `CrAnSection Ï†s Ã— CrAnSection Ï†s` formed by `append`, `take`
  and `drop` and their interrelationship. -/
def appendEquiv {Ï†s Ï†s' : List ğ“•.FieldOp} : CrAnSection (Ï†s ++ Ï†s') â‰ƒ
    CrAnSection Ï†s Ã— CrAnSection Ï†s' where
  toFun Ïˆs := (congr (List.take_left Ï†s Ï†s') (take Ï†s.length Ïˆs),
    congr (List.drop_left Ï†s Ï†s') (drop Ï†s.length Ïˆs))
  invFun ÏˆsÏˆs' := append ÏˆsÏˆs'.1 ÏˆsÏˆs'.2
  left_inv Ïˆs := by
    apply Subtype.ext
    simp
  right_inv ÏˆsÏˆs' := by
    match ÏˆsÏˆs' with
    | (Ïˆs, Ïˆs') =>
    simp only [take_left, drop_left, Prod.mk.injEq]
    refine And.intro (Subtype.ext ?_) (Subtype.ext ?_)
    Â· simp
    Â· simp

@[simp]
lemma _root_.List.map_eraseIdx {Î± Î² : Type} (f : Î± â†’ Î²) : (l : List Î±) â†’ (n : â„•) â†’
    List.map f (l.eraseIdx n) = (List.map f l).eraseIdx n
  | [], _ => rfl
  | a :: l, 0 => rfl
  | a :: l, n+1 => by
    simp only [List.eraseIdx, List.map_cons, List.cons.injEq, true_and]
    exact List.map_eraseIdx f l n

/-- Erasing an element from a section and it's underlying list. -/
def eraseIdx (n : â„•) (Ïˆs : CrAnSection Ï†s) : CrAnSection (Ï†s.eraseIdx n) :=
  âŸ¨Ïˆs.1.eraseIdx n, by simp [Ïˆs.2]âŸ©

/-- The equivalence formed by extracting an element from a section. -/
def eraseIdxEquiv (n : â„•) (Ï†s : List ğ“•.FieldOp) (hn : n < Ï†s.length) :
    CrAnSection Ï†s â‰ƒ ğ“•.fieldOpToCrAnType Ï†s[n] Ã—
    CrAnSection (Ï†s.eraseIdx n) :=
  (congr (by simp only [List.take_concat_get', List.take_append_drop])).trans <|
  appendEquiv.trans <|
  (Equiv.prodCongr (appendEquiv.trans (Equiv.prodComm _ _)) (Equiv.refl _)).trans <|
  (Equiv.prodAssoc _ _ _).trans <|
  Equiv.prodCongr singletonEquiv <|
  appendEquiv.symm.trans <|
  congr (List.eraseIdx_eq_take_drop_succ Ï†s n).symm

@[simp]
lemma eraseIdxEquiv_apply_snd {n : â„•} (Ïˆs : CrAnSection Ï†s) (hn : n < Ï†s.length) :
    (eraseIdxEquiv n Ï†s hn Ïˆs).snd = eraseIdx n Ïˆs := by
  apply Subtype.ext
  simp only [eraseIdxEquiv, appendEquiv, take, List.take_concat_get', List.length_take, drop,
    append, Equiv.trans_apply, Equiv.coe_fn_mk, congr_fst, Equiv.prodCongr_apply, Equiv.coe_trans,
    Equiv.coe_prodComm, Equiv.coe_refl, Prod.map_apply, Function.comp_apply, Prod.swap_prod_mk,
    id_eq, Equiv.prodAssoc_apply, Equiv.coe_fn_symm_mk, eraseIdx]
  rw [Nat.min_eq_left (Nat.le_of_succ_le hn), Nat.min_eq_left hn, List.take_take]
  simp only [Nat.succ_eq_add_one, le_add_iff_nonneg_right, zero_le, inf_of_le_left]
  exact Eq.symm (List.eraseIdx_eq_take_drop_succ Ïˆs.1 n)

lemma eraseIdxEquiv_symm_eq_take_cons_drop {n : â„•} (Ï†s : List ğ“•.FieldOp) (hn : n < Ï†s.length)
    (a : ğ“•.fieldOpToCrAnType Ï†s[n]) (s : CrAnSection (Ï†s.eraseIdx n)) :
    (eraseIdxEquiv n Ï†s hn).symm âŸ¨a, sâŸ© =
    congr (by
    rw [HepLean.List.take_eraseIdx_same, HepLean.List.drop_eraseIdx_succ]
    conv_rhs => rw [â† List.take_append_drop n Ï†s]) (append (take n s) (cons a (drop n s))) := by
  simp only [eraseIdxEquiv, appendEquiv, Equiv.symm_trans_apply, congr_symm, Equiv.prodCongr_symm,
    Equiv.refl_symm, Equiv.prodCongr_apply, Prod.map_apply, Equiv.symm_symm, Equiv.coe_fn_mk,
    take_congr, congr_trans_apply, drop_congr, Equiv.prodAssoc_symm_apply, Equiv.coe_refl,
    Equiv.prodComm_symm, Equiv.prodComm_apply, Prod.swap_prod_mk, Equiv.coe_fn_symm_mk,
    congr_fst_append, id_eq, congr_snd_append]
  rw [append_assoc', singletonEquiv_append_eq_cons]
  simp only [List.singleton_append, congr_trans_apply]
  apply Subtype.ext
  simp only [congr_fst]
  have hn : (List.take n Ï†s).length = n := by
    rw [@List.length_take]
    simp only [inf_eq_left]
    exact Nat.le_of_succ_le hn
  rw [hn]

@[simp]
lemma eraseIdxEquiv_symm_getElem {n : â„•} (Ï†s : List ğ“•.FieldOp) (hn : n < Ï†s.length)
    (a : ğ“•.fieldOpToCrAnType Ï†s[n]) (s : CrAnSection (Ï†s.eraseIdx n)) :
    getElem ((eraseIdxEquiv n Ï†s hn).symm âŸ¨a,sâŸ©).1 n
    (by rw [length_eq]; exact hn) = âŸ¨Ï†s[n], aâŸ© := by
  rw [eraseIdxEquiv_symm_eq_take_cons_drop]
  simp only [append, take, cons, drop, congr_fst]
  rw [List.getElem_append]
  simp only [List.length_take, length_eq, lt_inf_iff, lt_self_iff_false, false_and, â†“reduceDIte]
  have h0 : n âŠ“ (Ï†s.eraseIdx n).length = n := by
    simp only [inf_eq_left]
    rw [â† HepLean.List.eraseIdx_length _ âŸ¨n, hnâŸ©] at hn
    exact Nat.le_of_lt_succ hn
  simp [h0]

@[simp]
lemma eraseIdxEquiv_symm_eraseIdx {n : â„•} (Ï†s : List ğ“•.FieldOp) (hn : n < Ï†s.length)
    (a : ğ“•.fieldOpToCrAnType Ï†s[n]) (s : CrAnSection (Ï†s.eraseIdx n)) :
    ((eraseIdxEquiv n Ï†s hn).symm âŸ¨a, sâŸ©).1.eraseIdx n = s.1 := by
  change (((eraseIdxEquiv n Ï†s hn).symm âŸ¨a, sâŸ©).eraseIdx n).1 = _
  rw [â† eraseIdxEquiv_apply_snd _ hn]
  simp

lemma sum_eraseIdxEquiv (n : â„•) (Ï†s : List ğ“•.FieldOp) (hn : n < Ï†s.length)
    (f : CrAnSection Ï†s â†’ M) [AddCommMonoid M] : âˆ‘ (s : CrAnSection Ï†s), f s =
    âˆ‘ (a : ğ“•.fieldOpToCrAnType Ï†s[n]), âˆ‘ (s : CrAnSection (Ï†s.eraseIdx n)),
    f ((eraseIdxEquiv n Ï†s hn).symm âŸ¨a, sâŸ©) := by
  rw [â† (eraseIdxEquiv n Ï†s hn).symm.sum_comp]
  rw [Fintype.sum_prod_type]

end CrAnSection

end FieldSpecification
