/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.QFT.PerturbationTheory.WickContraction.Sign.Basic
import PhysLean.QFT.PerturbationTheory.FieldOpAlgebra.TimeContraction
/-!

# Time contractions

-/

open FieldSpecification
variable {ğ“• : FieldSpecification}

namespace WickContraction
variable {n : â„•} (c : WickContraction n)
open PhysLean.List
open FieldOpAlgebra

/-- For a list `Ï†s` of `ğ“•.FieldOp` and a Wick contraction `Ï†sÎ›`, the
  element of the center of `ğ“•.FieldOpAlgebra`, `Ï†sÎ›.staticContract` is defined as the product
  of `[anPart Ï†s[j], Ï†s[k]]â‚›` over contracted pairs `{j, k}` in `Ï†sÎ›`
  with `j < k`. -/
noncomputable def staticContract {Ï†s : List ğ“•.FieldOp}
    (Ï†sÎ› : WickContraction Ï†s.length) :
    Subalgebra.center â„‚ ğ“•.FieldOpAlgebra :=
  âˆ (a : Ï†sÎ›.1), âŸ¨[anPart (Ï†s.get (Ï†sÎ›.fstFieldOfContract a)),
    ofFieldOp (Ï†s.get (Ï†sÎ›.sndFieldOfContract a))]â‚›,
      superCommute_anPart_ofFieldOp_mem_center _ _âŸ©

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, and a `i â‰¤ Ï†s.length`, then the following relation holds:

  `(Ï†sÎ› â†©Î› Ï† i none).staticContract = Ï†sÎ›.staticContract`

  The proof of this result ultimately is a consequence of definitions.
-/
@[simp]
lemma staticContract_insert_none (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) :
    (Ï†sÎ› â†©Î› Ï† i none).staticContract = Ï†sÎ›.staticContract := by
  rw [staticContract, insertAndContract_none_prod_contractions]
  congr
  ext a
  simp

/--
  For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, a `i â‰¤ Ï†s.length` and a `k` in `Ï†sÎ›.uncontracted`, then
  `(Ï†sÎ› â†©Î› Ï† i (some k)).staticContract` is equal to the product of
  - `[anPart Ï†, Ï†s[k]]â‚›` if `i â‰¤ k` or `[anPart Ï†s[k], Ï†]â‚›` if `k < i`
  - `Ï†sÎ›.staticContract`.

  The proof of this result ultimately is a consequence of definitions.
-/
lemma staticContract_insert_some
    (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (j : Ï†sÎ›.uncontracted) :
    (Ï†sÎ› â†©Î› Ï† i (some j)).staticContract =
    (if i < i.succAbove j then
      âŸ¨[anPart Ï†, ofFieldOp Ï†s[j.1]]â‚›, superCommute_anPart_ofFieldOp_mem_center _ _âŸ©
    else âŸ¨[anPart Ï†s[j.1], ofFieldOp Ï†]â‚›, superCommute_anPart_ofFieldOp_mem_center _ _âŸ©) *
    Ï†sÎ›.staticContract := by
  rw [staticContract, insertAndContract_some_prod_contractions]
  congr 1
  Â· simp only [Nat.succ_eq_add_one, insertAndContract_fstFieldOfContract_some_incl, finCongr_apply,
    List.get_eq_getElem, insertAndContract_sndFieldOfContract_some_incl, Fin.getElem_fin]
    split
    Â· simp
    Â· simp
  Â· congr
    ext a
    simp

open FieldStatistic

lemma staticContract_insert_some_of_lt
    (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (i : Fin Ï†s.length.succ) (k : Ï†sÎ›.uncontracted)
    (hik : i < i.succAbove k) :
    (Ï†sÎ› â†©Î› Ï† i (some k)).staticContract =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, (Ï†sÎ›.uncontracted.filter (fun x => x < k))âŸ©)
    â€¢ (contractStateAtIndex Ï† [Ï†sÎ›]áµ˜á¶œ ((uncontractedFieldOpEquiv Ï†s Ï†sÎ›) (some k)) *
      Ï†sÎ›.staticContract) := by
  rw [staticContract_insert_some]
  simp only [Nat.succ_eq_add_one, Fin.getElem_fin, ite_mul, instCommGroup.eq_1,
    contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
    Equiv.coe_trans, Option.map_some', Function.comp_apply, finCongr_apply, Fin.coe_cast,
    List.getElem_map, uncontractedList_getElem_uncontractedIndexEquiv_symm, List.get_eq_getElem,
    Algebra.smul_mul_assoc, uncontractedListGet]
  Â· simp only [hik, â†“reduceIte, MulMemClass.coe_mul]
    trans (1 : â„‚) â€¢ ((superCommute (anPart Ï†)) (ofFieldOp Ï†s[k.1]) * â†‘Ï†sÎ›.staticContract)
    Â· simp
    simp only [smul_smul]
    congr 1
    have h1 : ofList ğ“•.fieldOpStatistic (List.take (â†‘(Ï†sÎ›.uncontractedIndexEquiv.symm k))
        (List.map Ï†s.get Ï†sÎ›.uncontractedList))
        = (ğ“• |>â‚› âŸ¨Ï†s.get, (Finset.filter (fun x => x < k) Ï†sÎ›.uncontracted)âŸ©) := by
      simp only [ofFinset]
      congr
      rw [â† List.map_take]
      congr
      rw [take_uncontractedIndexEquiv_symm]
      rw [filter_uncontractedList]
    rw [h1]
    simp only [exchangeSign_mul_self]

lemma staticContract_of_not_gradingCompliant (Ï†s : List ğ“•.FieldOp)
    (Ï†sÎ› : WickContraction Ï†s.length) (h : Â¬ GradingCompliant Ï†s Ï†sÎ›) :
    Ï†sÎ›.staticContract = 0 := by
  rw [staticContract]
  simp only [GradingCompliant, Fin.getElem_fin, Subtype.forall, not_forall] at h
  obtain âŸ¨a, haâŸ© := h
  obtain âŸ¨ha, ha2âŸ© := ha
  apply Finset.prod_eq_zero (i := âŸ¨a, haâŸ©)
  simp only [Finset.univ_eq_attach, Finset.mem_attach]
  apply Subtype.eq
  simp only [List.get_eq_getElem, ZeroMemClass.coe_zero]
  rw [superCommute_anPart_ofFieldOpF_diff_grade_zero]
  simp [ha2]

end WickContraction
