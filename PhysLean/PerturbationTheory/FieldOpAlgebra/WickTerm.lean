/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.PerturbationTheory.WickContraction.Sign.Basic
import PhysLean.PerturbationTheory.WickContraction.Sign.InsertNone
import PhysLean.PerturbationTheory.WickContraction.Sign.InsertSome
import PhysLean.PerturbationTheory.WickContraction.TimeContract
import PhysLean.PerturbationTheory.FieldOpAlgebra.NormalOrder.WickContractions
/-!

# Wick term

-/

open FieldSpecification
variable {ğ“• : FieldSpecification}

namespace WickContraction
variable {n : â„•} (c : WickContraction n)
open PhysLean.List
open FieldOpAlgebra
open FieldStatistic
noncomputable section

/-- For a list `Ï†s` of `ğ“•.FieldOp`, and a Wick contraction `Ï†sÎ›` of `Ï†s`, the element
  of `ğ“•.FieldOpAlgebra`, `Ï†sÎ›.wickTerm` is defined as

  `Ï†sÎ›.sign â€¢ Ï†sÎ›.timeContract * ğ“([Ï†sÎ›]áµ˜á¶œ)`.

  This is a term which appears in the Wick's theorem. -/
def wickTerm {Ï†s : List ğ“•.FieldOp} (Ï†sÎ› : WickContraction Ï†s.length) : ğ“•.FieldOpAlgebra :=
  Ï†sÎ›.sign â€¢ Ï†sÎ›.timeContract * ğ“(ofFieldOpList [Ï†sÎ›]áµ˜á¶œ)

/-- For the empty list `[]` of `ğ“•.FieldOp`, the `wickTerm` of the Wick contraction
  corresponding to the empty set `âˆ…` (the only Wick contraction of `[]`) is `1`. -/
@[simp]
lemma wickTerm_empty_nil :
    wickTerm (empty (n := ([] : List ğ“•.FieldOp).length)) = 1 := by
  rw [wickTerm]
  simp [sign_empty]

/--
For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, and `i â‰¤ Ï†s.length`, then `(Ï†sÎ› â†©Î› Ï† i none).wickTerm` is equal to

`ğ“¢(Ï†, Ï†â‚€â€¦Ï†áµ¢â‚‹â‚) Ï†sÎ›.sign â€¢ Ï†sÎ›.timeContract * ğ“(Ï† :: [Ï†sÎ›]áµ˜á¶œ)`

The proof of this result relies on
- `normalOrder_uncontracted_none` to rewrite normal orderings.
- `timeContract_insert_none` to rewrite the time contract.
- `sign_insert_none` to rewrite the sign.
-/
lemma wickTerm_insert_none (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (i : Fin Ï†s.length.succ) (Ï†sÎ› : WickContraction Ï†s.length) :
    (Ï†sÎ› â†©Î› Ï† i none).wickTerm =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, (Finset.univ.filter (fun k => i.succAbove k < i))âŸ©)
    â€¢ (Ï†sÎ›.sign â€¢ Ï†sÎ›.timeContract * ğ“(ofFieldOpList (Ï† :: [Ï†sÎ›]áµ˜á¶œ))) := by
  rw [wickTerm]
  by_cases hg : GradingCompliant Ï†s Ï†sÎ›
  Â· rw [normalOrder_uncontracted_none, sign_insert_none _ _ _ _ hg]
    simp only [Nat.succ_eq_add_one, timeContract_insert_none, instCommGroup.eq_1,
      Algebra.mul_smul_comm, Algebra.smul_mul_assoc, smul_smul]
    congr 1
    rw [â† mul_assoc]
    congr 1
    rw [â† map_mul]
    congr
    rw [ofFinset_union]
    congr
    ext a
    simp only [Finset.mem_sdiff, Finset.mem_union, Finset.mem_filter, Finset.mem_univ, true_and,
      Finset.mem_inter, not_and, not_lt, and_imp]
    apply Iff.intro
    Â· intro ha
      have ha1 := ha.1
      rcases ha1 with ha1 | ha1
      Â· exact ha1.2
      Â· exact ha1.2
    Â· intro ha
      simp only [uncontracted, Finset.mem_filter, Finset.mem_univ, true_and, ha, and_true,
        forall_const]
      have hx : Ï†sÎ›.getDual? a = none â†” Â¬ (Ï†sÎ›.getDual? a).isSome := by
        simp
      rw [hx]
      simp only [Bool.not_eq_true, Bool.eq_false_or_eq_true_self, true_and]
      intro h1 h2
      simp_all
  Â· simp only [Nat.succ_eq_add_one, timeContract_insert_none, Algebra.smul_mul_assoc,
    instCommGroup.eq_1]
    rw [timeContract_of_not_gradingCompliant]
    simp only [ZeroMemClass.coe_zero, zero_mul, smul_zero]
    exact hg

/-- For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, `i â‰¤ Ï†s.length` and a `k` in `Ï†sÎ›.uncontracted`,
  such that all `ğ“•.FieldOp` in `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚` have time strictly less than `Ï†` and
  `Ï†` has a time greater than or equal to all `FieldOp` in `Ï†â‚€â€¦Ï†â‚™`, then
  `(Ï†sÎ› â†©Î› Ï† i (some k)).staticWickTerm`
is equal to the product of
- the sign `ğ“¢(Ï†, Ï†â‚€â€¦Ï†áµ¢â‚‹â‚) `
- the sign `Ï†sÎ›.sign`
- `Ï†sÎ›.timeContract`
- `s â€¢ [anPart Ï†, ofFieldOp Ï†s[k]]â‚›` where `s` is the sign associated with moving `Ï†` through
  uncontracted fields in `Ï†â‚€â€¦Ï†â‚–â‚‹â‚`
- the normal ordering `[Ï†sÎ›]áµ˜á¶œ` with the field corresponding to `k` removed.

The proof of this result relies on
- `timeContract_insert_some_of_not_lt`
  and `timeContract_insert_some_of_lt` to rewrite time
  contractions.
- `normalOrder_uncontracted_some` to rewrite normal orderings.
- `sign_insert_some_of_not_lt` and `sign_insert_some_of_lt` to rewrite signs.
-/
lemma wickTerm_insert_some (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp)
    (i : Fin Ï†s.length.succ) (Ï†sÎ› : WickContraction Ï†s.length) (k : Ï†sÎ›.uncontracted)
    (hlt : âˆ€ (k : Fin Ï†s.length), timeOrderRel Ï† Ï†s[k])
    (hn : âˆ€ (k : Fin Ï†s.length), i.succAbove k < i â†’ Â¬ timeOrderRel Ï†s[k] Ï†) :
    (Ï†sÎ› â†©Î› Ï† i (some k)).wickTerm =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, (Finset.univ.filter (fun x => i.succAbove x < i))âŸ©)
    â€¢ (Ï†sÎ›.sign â€¢ (contractStateAtIndex Ï† [Ï†sÎ›]áµ˜á¶œ
      ((uncontractedFieldOpEquiv Ï†s Ï†sÎ›) (some k)) * Ï†sÎ›.timeContract)
    * ğ“(ofFieldOpList (optionEraseZ [Ï†sÎ›]áµ˜á¶œ Ï† (uncontractedFieldOpEquiv Ï†s Ï†sÎ› k)))) := by
  rw [wickTerm]
  by_cases hg : GradingCompliant Ï†s Ï†sÎ› âˆ§ (ğ“• |>â‚› Ï†) = (ğ“• |>â‚› Ï†s[k.1])
  Â· by_cases hk : i.succAbove k < i
    Â· rw [WickContraction.timeContract_insert_some_of_not_lt]
      swap
      Â· exact hn _ hk
      Â· rw [normalOrder_uncontracted_some, sign_insert_some_of_lt Ï† Ï†s Ï†sÎ› i k hk hg]
        simp only [instCommGroup.eq_1, smul_smul, Algebra.smul_mul_assoc]
        congr 1
        rw [mul_assoc, mul_assoc, mul_comm, mul_assoc, mul_assoc]
        simp
      Â· omega
    Â· have hik : i.succAbove â†‘k â‰  i := Fin.succAbove_ne i â†‘k
      rw [timeContract_insert_some_of_lt]
      swap
      Â· exact hlt _
      Â· rw [normalOrder_uncontracted_some]
        rw [sign_insert_some_of_not_lt Ï† Ï†s Ï†sÎ› i k hk hg]
        simp only [instCommGroup.eq_1, smul_smul, Algebra.smul_mul_assoc]
        congr 1
        rw [mul_assoc, mul_assoc, mul_comm, mul_assoc, mul_assoc]
        simp
      Â· omega
  Â· rw [timeContract_insertAndContract_some]
    simp only [Fin.getElem_fin, not_and] at hg
    by_cases hg' : GradingCompliant Ï†s Ï†sÎ›
    Â· have hg := hg hg'
      simp only [Nat.succ_eq_add_one, Fin.getElem_fin, ite_mul, Algebra.smul_mul_assoc,
        instCommGroup.eq_1, contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
        Equiv.coe_trans, Option.map_some', Function.comp_apply, finCongr_apply, Fin.coe_cast,
        List.getElem_map, uncontractedList_getElem_uncontractedIndexEquiv_symm, List.get_eq_getElem,
        uncontractedListGet]
      by_cases h1 : i < i.succAbove â†‘k
      Â· simp only [h1, â†“reduceIte, MulMemClass.coe_mul]
        rw [timeContract_zero_of_diff_grade]
        simp only [zero_mul, smul_zero]
        rw [superCommute_anPart_ofFieldOpF_diff_grade_zero]
        simp only [zero_mul, smul_zero]
        exact hg
        exact hg
      Â· simp only [h1, â†“reduceIte, MulMemClass.coe_mul]
        rw [timeContract_zero_of_diff_grade]
        simp only [zero_mul, smul_zero]
        rw [superCommute_anPart_ofFieldOpF_diff_grade_zero]
        simp only [zero_mul, smul_zero]
        exact hg
        exact fun a => hg (id (Eq.symm a))
    Â· rw [timeContract_of_not_gradingCompliant]
      simp only [Nat.succ_eq_add_one, Fin.getElem_fin, mul_zero, ZeroMemClass.coe_zero, smul_zero,
        zero_mul, instCommGroup.eq_1]
      exact hg'

/--
For a list `Ï†s = Ï†â‚€â€¦Ï†â‚™` of `ğ“•.FieldOp`, a Wick contraction `Ï†sÎ›` of `Ï†s`, an element `Ï†` of
  `ğ“•.FieldOp`, and `i â‰¤ Ï†s.length`
  such that all `ğ“•.FieldOp` in `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚` have time strictly less than `Ï†` and
  `Ï†` has a time greater than or equal to all `FieldOp` in `Ï†â‚€â€¦Ï†â‚™`, then

`Ï† * Ï†sÎ›.wickTerm = ğ“¢(Ï†, Ï†â‚€â€¦Ï†áµ¢â‚‹â‚) â€¢ âˆ‘ k, (Ï†sÎ› â†©Î› Ï† i k).wickTerm`

where the sum is over all `k` in `Option Ï†sÎ›.uncontracted`, so `k` is either `none` or `some k`.

The proof proceeds as follows:
- `ofFieldOp_mul_normalOrder_ofFieldOpList_eq_sum` is used to expand `Ï† ğ“([Ï†sÎ›]áµ˜á¶œ)` as
  a sum over `k` in `Option Ï†sÎ›.uncontracted` of terms involving `[anPart Ï†, Ï†s[k]]â‚›`.
- Then `wickTerm_insert_none` and `wickTerm_insert_some` are used to equate terms.
-/
lemma mul_wickTerm_eq_sum (Ï† : ğ“•.FieldOp) (Ï†s : List ğ“•.FieldOp) (i : Fin Ï†s.length.succ)
    (Ï†sÎ› : WickContraction Ï†s.length) (hlt : âˆ€ (k : Fin Ï†s.length), timeOrderRel Ï† Ï†s[k])
    (hn : âˆ€ (k : Fin Ï†s.length), i.succAbove k < i â†’ Â¬timeOrderRel Ï†s[k] Ï†) :
    ofFieldOp Ï† * Ï†sÎ›.wickTerm =
    ğ“¢(ğ“• |>â‚› Ï†, ğ“• |>â‚› âŸ¨Ï†s.get, (Finset.univ.filter (fun x => i.succAbove x < i))âŸ©) â€¢
    âˆ‘ (k : Option Ï†sÎ›.uncontracted), (Ï†sÎ› â†©Î› Ï† i k).wickTerm := by
  trans (Ï†sÎ›.sign â€¢ Ï†sÎ›.timeContract) * ((ofFieldOp Ï†) * ğ“(ofFieldOpList [Ï†sÎ›]áµ˜á¶œ))
  Â· have ht := Subalgebra.mem_center_iff.mp (Subalgebra.smul_mem (Subalgebra.center â„‚ _)
      (WickContraction.timeContract Ï†sÎ›).2 (Ï†sÎ›.sign))
    rw [wickTerm]
    rw [â† mul_assoc, ht, mul_assoc]
  rw [ofFieldOp_mul_normalOrder_ofFieldOpList_eq_sum, Finset.mul_sum,
    uncontractedFieldOpEquiv_list_sum, Finset.smul_sum]
  simp only [instCommGroup.eq_1, Nat.succ_eq_add_one]
  congr 1
  funext n
  match n with
  | none =>
    rw [wickTerm_insert_none]
    simp only [contractStateAtIndex, uncontractedFieldOpEquiv, Equiv.optionCongr_apply,
      Equiv.coe_trans, Option.map_none', one_mul, Algebra.smul_mul_assoc, instCommGroup.eq_1,
      smul_smul]
    congr 1
    rw [â† mul_assoc, exchangeSign_mul_self]
    simp
  | some n =>
    rw [wickTerm_insert_some _ _ _ _ _
      (fun k => hlt k) (fun k a => hn k a)]
    simp only [uncontractedFieldOpEquiv, Equiv.optionCongr_apply, Equiv.coe_trans, Option.map_some',
      Function.comp_apply, finCongr_apply, Algebra.smul_mul_assoc, instCommGroup.eq_1, smul_smul]
    congr 1
    Â· rw [â† mul_assoc, exchangeSign_mul_self]
      rw [one_mul]
    Â· rw [â† mul_assoc]
      congr 1
      have ht := (WickContraction.timeContract Ï†sÎ›).prop
      rw [@Subalgebra.mem_center_iff] at ht
      rw [ht]

end
end WickContraction
